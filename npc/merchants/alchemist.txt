//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//============================================================ 

alde_alche,24,188,3	script	行会 贩卖员	740,{
	if (checkweight(1201,1) == 0) {
		mes "- 等一下! -";
		mes "- 现在你身上带着 -";
		mes "- 太多东西了! -";
		mes "- 你先去卡普拉仓库 -";
		mes "- 寄存一些东西再来吧! -";
		close;
	}
	mes "[吉佛 阿尔 夏普]";
	mes "欢迎来到";
	mes "炼金术师公会. ";
	mes "有什么我能帮助您的吗? ";
	next;
	switch(select("- 购买材料:- 购买说明书:- 退出")) {
	case 1:
		mes "[吉佛 阿尔 夏普]";
		mes "您想要点什么? ";
		next;
		if (select("药钵 - 8 Zeny:取消. ") == 1) {
			mes "[吉佛 阿尔 夏普]";
			mes "您想要买多少个? ";
			mes "如果不想买就请输入 '0'";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[吉佛 阿尔 夏普]";
					mes "交易取消. ";
					mes "欢迎下次再来. ";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[吉佛 阿尔 夏普]";
					mes "您输入的数量";
					mes "必须小于2000. ";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[吉佛 阿尔 夏普]";
				mes "您似乎";
				mes "没有足够的zeny. ";
				mes "请先确认";
				mes "您所带的zeny数量. ";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[吉佛 阿尔 夏普]";
				mes "看来您似乎";
				mes "带不下这些东西. ";
				mes "请检查您的";
				mes "随身物品数量. ";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[吉佛 阿尔 夏普]";
			mes "谢谢. ";
			mes "欢迎再来. ";
			close;
		}
		mes "[吉佛 阿尔 夏普]";
		mes "好吧, ";
		mes "如果您想";
		mes "购买任何和炼金术师";
		mes "有关的物品";
		mes "记得来找我, 好吗? ";
		close;
	case 2:
		mes "[吉佛 阿尔 夏普]";
		mes "您想要点什么? ";
		mes "说明书价格一般为 100,000 zeny.  不过有部分特殊的说明书更贵一些. ";
		next;
		switch(select("- 药水制作说明书:- 酒精制作说明书:- 火烟瓶制作说明书:- 盐酸瓶制作说明书:- 植物瓶制作说明书:- 刺锤瓶制作说明书:- 护贝药制作说明书:- 纤细药水制作说明书:- 取消")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[吉佛 阿尔 夏普]";
			mes "好吧……";
			mes "如果您想要买";
			mes "任何的制作说明书";
			mes "请再来. ";
			close;
		}
	case 3:
		mes "[吉佛 阿尔 夏普]";
		mes "好吧, ";
		mes "祝您愉快. ";
		close;
	}

S_SellManual:
	.@item_id = getarg(0);
	.@zeny_req = getarg(1);

	if (Zeny < .@zeny_req) {
		mes "[吉佛 阿尔 夏普]";
		mes "您似乎没有";
		mes "足够的 Zeny. ";
		mes "请检查您是否有";
		mes "足够的 Zeny. ";
		close;
	}
	mes "[吉佛 阿尔 夏普]";
	mes getitemname(.@item_id) +"?";
	mes "总共是 "+ .@zeny_req +" zeny.";
	next;
	if (select("购买:退出") == 1) {
		Zeny = Zeny - .@zeny_req;
		getitem .@item_id,1;
		mes "[吉佛 阿尔 夏普]";
		mes "谢谢您的";
		mes "惠顾. ";
		close;
	}
	mes "[吉佛 阿尔 夏普]";
	mes "欢迎";
	mes "下次再来. ";
	close;
}
