//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Gemstone Trader
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Trade various colors of gemstones for other color gemstones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Any notes pertaining to the prior trader may be found
//=     in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=     Added a checkweight. 
//= 1.3 Fixed checks. [Euphy]
//============================================================ 

payon,173,238,5	script	杰依德#pay	754,{
	if (checkweight(1201,1) == 0) {
		mes "你好像携带了非常多的东西. 为何不放一些东西到仓库后再来找我呢?";
		close;
	}
	mes "[杰依德]";
	mes "您好~";
	mes "如果你拿两个相同色彩的";
	mes "魔力矿石来给我的话,";
	mes "我会马上帮你做成不同色彩的";
	mes "魔力矿石喔.";
	next;
	switch(select("蓝色魔力矿石换为红色魔力矿石!:红色魔力矿石换为黄色魔力矿石!:黄色魔力矿石换为蓝色魔力矿石!")) {
	case 1: callsub S_TradeGems,717,716;
	case 2: callsub S_TradeGems,716,715;
	case 3: callsub S_TradeGems,715,717;
	}

S_TradeGems:
	.@item_req = getarg(0);
	.@item_id = getarg(1);

	if (countitem(.@item_req) < 2) {
		mes "[杰依德]";
		mes "啊哈...!";
		mes "你在拿我开玩笑吧?";
		mes "如果没有2个相同色彩的";
		mes "魔力矿石的话,";
		mes "是无法进行兑换的！";
		mes "2 "+ getitemname(.@item_req) +"s!";
		close;
	}
	else {
		.@gems = countitem(.@item_req) /2;
		mes "[杰依德]";
		mes "我想我能制作";
		mes "总共 "+ .@gems +" "+ getitemname(.@item_id) +"s";
		mes "这些 "+ getitemname(.@item_req) +"材料";
		mes "就是你带来的.";
		mes "你想要现在就交换吗?";
		next;
		switch(select("我要把全部矿石的都换掉.:我想兑换一部分就可以了:忽然,不想兑换了.")) {
		case 1:
			delitem .@item_req,.@gems * 2;
			getitem .@item_id,.@gems;
			break;
		case 2:
			mes "[杰依德]";
			mes "你想兑换多少个呢?";
			mes "你只能输入不超过100的数值哈.";
			mes "如果不想兑换,";
			mes "就输入0好了.";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[杰依德]";
					mes "额?一个都不兑换?";
					mes "我想.";
					mes "你应该是改变主意了...";
					close;
				}
				else if (.@input > 100) {
					mes "[杰依德]";
					mes "呃,这...";
					mes "输入的数量";
					mes "请不要超过100个.";
					mes "请记住...";
					next;
				}
				else if (.@gems < .@input) {
					// Custom dialogue
					mes "[杰依德]";
					mes "呃...";
					mes "你没有足够的矿石.";
					mes "可以进行交易...";
					next;
				}
				else break;
			}
			delitem .@item_req,.@input * 2;
			getitem .@item_id,.@input;
			break;
		case 3:
			mes "[杰依德]";
			mes "好的,没问题.";
			mes "如果需要的话随时找我.";
			close;
		}
		mes "[杰依德]";
		mes "轻松搞定~";
		mes "欢迎你下次再来.";
		mes "唔,你在看什么呢?";
		mes "唔!";
		mes "是不是有什么东西在我脸上?";
		close;
	}
}
