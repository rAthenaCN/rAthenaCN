//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================
//= Magazine Dealers
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Turns bullets into magazines/packs.
//===== Additional Comments: =================================
//= 1.0 First version. [SinSloth]
//= 1.1 Optimized version - Reduced to only one function [SinSloth]
//= 1.2 Optimized^2, corrected npc's name [ultramage]
//= 1.2a Optimized. Please, ommit extra NPC names [Lupus]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.4 Updated to match AEGIS script again. [Masao]
//= 1.5 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//============================================================

alberta,118,157,3	script	弹药背包商人 凯尼::mdk	4_M_01,{
	if (BaseClass == Job_Gunslinger) {
		mes "[凯尼]";
		mes "欢迎来到我的弹药背包店. ";
		mes "正如你所知, 大量的子弹";
		mes "用弹夹来携带, ";
		mes "会十分的方便. 那么, 您对这里";
		mes "的什么感兴趣? ";
		next;
		switch(select("- 雷击弹夹:- 失明弹夹:- 毒弹夹:- 冰弹夹:- 火弹夹:- 普通弹夹:- 血波弹弹夹:- 银色弹夹:- 取消")) {
		case 1: callfunc "Func_Casing",13204,12144; break;
		case 2: callfunc "Func_Casing",13206,12145; break;
		case 3: callfunc "Func_Casing",13205,12146; break;
		case 4: callfunc "Func_Casing",13207,12147; break;
		case 5: callfunc "Func_Casing",13203,12148; break;
		case 6: callfunc "Func_Casing",13200,12149; break;
		case 7: callfunc "Func_Casing",13202,12150; break;
		case 8: callfunc "Func_Casing",13201,12151; break;
		case 9:
			mes "[凯尼]";
			mes "好吧, 如果您发觉";
			mes "您携带的子弹太多, ";
			mes "可以随时来找我. ";
			mes "把子弹存入弹夹中";
			mes "是个很好的主意. ";
			close;
		}
	}
	mes "[凯尼]";
	mes "欢迎来到我的商店. ";
	mes "这里我提供给枪手";
	mes "各种弹药夹. ";
	mes "对不起, 我提供的服务";
	mes "好像对您没有什么帮助, ";
	mes "冒险者. ";
	next;
	mes "[凯尼]";
	mes "呃, 如果您认识其他的枪手, ";
	mes "让他们来找我. ";
	mes "子弹带的再多, ";
	mes "也不为过. ";
	close;
}

function	script	Func_Casing	{
	mes "[凯尼]";
	mes "现在, 你可以用";
	mes "500 个"+getitemname(getarg(0));
	mes "以及 500 Zeny 换一个 "+getitemname(getarg(1))+", 所以请确定";
	mes "您带够了子弹";
	mes "和 Zeny 来兑换. ";
	next;
	mes "[凯尼]";
	mes "你可以用 500 个 "+getitemname(getarg(0))+"和 500 Zeny 来兑换";
	mes "一个 "+getitemname(getarg(1))+". ";
	next;
	mes "[凯尼]";
	mes "请记得我一次最多只能";
	mes "帮您兑换 50 个弹夹. ";
	mes "请输入您想要兑换";
	mes "的弹夹数量. ";
	mes "如果想取消兑换, 请输入 ''0. ''";
	next;
	input .@amount;
	if (.@amount > 50 || .@amount == 0) {
		mes "[凯尼]";
		mes "嘿, 我不能给您";
		mes "那么多弹夹. ";
		mes "请重试, 不要";
		mes "输入一个大于";
		mes "50 的数字. ";
		close;
	}
	if (countitem(getarg(0)) >= (500*.@amount)) {
		if (Zeny >= (500*.@amount)) {
			if (checkweight(getarg(1),.@amount) == 0) {
				mes "[凯尼]";
				mes "嘿, 您的背包里";
				mes "塞的东西太多了. ";
				mes "清理出一些空间, ";
				mes "然后再来用子弹";
				mes "和我换弹夹, ";
				mes "明白了吗? ";
				close;
			}else{
				mes "[凯尼]";
				mes "很好, 这里是您的";
				mes "弹夹. 谢谢光临我的";
				mes "商店, 同时我希望您";
				mes "能好好利用手里的这";
				mes "些弹药. ";
				set Zeny, Zeny - 500*.@amount;
				delitem getarg(0),500*.@amount;
				getitem getarg(1),.@amount;
				close;
			}
		} else {
			mes "[凯尼]";
			mes "对不起, 您没有足够的";
			mes "zeny 来换取这些弹夹. ";
			mes "等你存够了钱, ";
			mes "再回到我的商店, ";
			mes "那时候, ";
			mes "我再帮你换弹夹. ";
			close;
		}
	} else {
		mes "[凯尼]";
		mes "对不起, 但是您没有";
		mes "足够的弹药来兑换";
		mes "这个弹夹. ";
		mes "也许您需要再检查";
		mes "一下您的物品栏. ";
		mes "确定了再来找我. ";
		close;
	}
}
