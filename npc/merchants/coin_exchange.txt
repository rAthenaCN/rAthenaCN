//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================
//= Coin Merchants
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= [Official Conversion]
//= Coin redemption NPCs.
//===== Additional Comments: =================================
//= 1.0 First version 
//= 1.1 Fixed checkweight. [Gepard]
//= 1.2 Optimized and merged into one file. [Euphy]
//============================================================

// Manuk & Splendide
//============================================================
-	script	::merchant_13_2	-1,{
	set .@n$, "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "您携带了太多的物品.";
			mes "请稍微处理一下再来?";
			close;
		}
		mes "你好.";
		mes "你需要什么?";
		next;
		if (strnpcinfo(1) == "马努克商人") {
			setarray .@items[2],12342,12343,12348;
			set .@Book,11019; //Manuk_Selling_Item
			set .@coin,6080; //Manuk_Coin
			set .@i, select("- 查看物品介绍:- 马努克的机会:- 马努克的勇气:- 马努克的信心:- 取消");
		}
		else {
			setarray .@items[2],12344,12345,12349;
			set .@Book,11018; //Splendide_Selling_Item
			set .@coin,6081; //Splendide_Coin
			set .@i, select("- 查看物品介绍:- 购买 Pinguicula's 果酱:- 购买 Luciola's 蜂蜜果酱:- 购买山茱萸之泪:- 取消");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "这是物品介绍.";
			close2;
			readbook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "我可以买给你 3 "+getitemname(.@items[.@i])+" 个换 ^3131FF1 硬币^000000.";
			next;
			switch(select("- 购买:- 取消")) {
			case 1:
				if (countitem(.@coin)) {
					mes .@n$;
					mes "谢谢惠顾.";
					delitem .@coin,1;
					getitem .@items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "你的硬币不够.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "如果你改变主意了请来找我.";
		close;
	}
	else {
		if (strnpcinfo(1) == "马努克商人") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	马努克商人	4_MAN_PIOM
spl_in01,110,326,5	duplicate(merchant_13_2)	辉煌领域商人	4_F_FAIRYKID4
