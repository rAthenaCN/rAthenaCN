//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Dye Maker
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Quests to create the eight colored Dyestuffs.
//===== Additional Comments: ================================= 
//= 1.0 Fully working [kobra_k88]
//= 1.1 Spell Checked [Nexon] 
//= 1.2 Rescripted to Aegis 10.3 standard. [L0ne_W0lf]
//= 1.2b Fixed typos in variable names [ultramage]
//= 1.3 Made the checks more dynamic [ultramage]
//= 1.3b Fixed incorrect checking loop structure [ultramage]
//= 1.4 Added a checkweight. [L0ne_W0lf]
//============================================================ 

morocc_in,146,99,3	script	杰瓦杜拉罕	58,{
	if ((MaxWeight-Weight) < 200 || checkweight(1201,1) == 0) {
		mes "- 等下! -";
		mes "- 当前你携带了太多东西. -";
		mes "- 在你把一些物品放进卡普拉 -";
		mes "- 仓库后, 再回来吧. -";
		mes "- 我会在次等候. -";
		close;
	}
	mes "[染料制作师 杰瓦杜拉罕]";
	mes "哇喔...";
	mes "多棒的一天啊. 这天可是制作染料的完美时刻.";
	next;
	switch(select("谈话:制作染料:取消")) {
	case 1:
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "呃, 我还真没有什么可说的. 不过, 如果你希望我给你讲讲我的生活, 倒是可以. 很长很无聊, 你愿意听听吗?";
		next;
		if (select("听:不听") == 1) {
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "从我记事起, 我父亲就已经开始制作染料. 他通常要花去数小时来制造不同的颜色的染料. 即使当我母亲去世的时候, 他都不曾停止.";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "在我人生的某段时间, 我变得叛逆并且离家出走. 我不想被我家庭的事业牵制住而想要尝试其它事物.";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "无论如何...";
			mes "我最后还是回到家中继承了家庭的传统, 已经制造染料15 年了.我猜它是在我的血液流淌着的. ";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "我终于意识到一些事情, 这让我真正地能够了解我的父亲. 为什么我的父亲舍弃所有东西来制造染料...";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "在那些沉闷和辛苦的工作当中, 他或许感觉到人们的彩色的梦的魔力.";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "也许他是为了那些梦而尝试努力为他的客人制造染料...";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "呵... 我怎么能真正了解到呢? 好吧, 我现在也许明白了. 几年了, 我感觉我能够听到染料在说话...";
			next;
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "这样看来. 总有一天, 我将成功的制造出我父亲想要的染料, 但是总得不到一个制作的机会.";
			close;
		}
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "哈哈哈哈哈...";
		mes "好吧, 我猜想谁还会想呆在这里去听一个年迈的染料制作师的生活故事呢. 哈哈哈哈哈....";
		close;

	case 2:
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "很好! 如果你真的想要, 我就为你制作. 我保证我能制作出你想要的染料. 但你要什么染料呢? 这费用可是取决于染料的.";
		next;
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "为什么, 你问问? 要知道染料是很容易制作的, 只需要努力和工作. 但请不要认为费用会很贵.";
		next;
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "染料的颜色是很特别的, 我会用真心和实意去制作.";
		next;
		switch(select("深红色染料:柠檬色染料:天蓝色染料:墨绿色染料:橘黄色染料:紫罗兰色染料:白色染料:黑色染料:取消")) {
		case 1: callsub S_MakeDye,1,1; break;
		case 2: callsub S_MakeDye,2,1; break;
		case 3: callsub S_MakeDye,3,2; break;
		case 4: callsub S_MakeDye,4,3; break;
		case 5: callsub S_MakeDye,5,4; break;
		case 6: callsub S_MakeDye,6,4; break;
		case 7: callsub S_MakeDye,7,4; break;
		case 8: callsub S_MakeDye,8,4; break;
		case 9:
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "呃!! 怎么了? 你现在改变心意了吗!? 真扫兴...";
			close;
		}

	case 3:
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "我不是自夸啊. 我拥有很高超的制作染料的技术. 如果你需要染料, 请来找我. 我会以很合理的价格为你制造染料的.";
		close;
	}

S_MakeDye:
	mes "[染料制作师 杰瓦杜拉罕]";
	switch(getarg(0)) {
	case 1:
		mes "恩... 我需要30片红色药草, 1瓶中和剂, 和1个空瓶来制作红色染料. 只需3000Zeny的费用就可以制作.";
		setarray .@item[0], 507, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,975;
		break;
	case 2:
		mes "恩... 我需要30片黄色药草, 1瓶中和剂, 和1个空瓶来制作柠檬色染料. 只需3000Zeny的费用就可以制作.";
		setarray .@item[0], 508, 973, 713;
		setarray .@count[0], 30,   1,   1;
		set .@cost,3000;
		set .@dyestuff,976;
		break;
	case 3:
		mes "恩... 我需要20片蓝色药草, 1瓶中和剂, 和1个空瓶来制作天蓝色染料. 运用蓝色草药很复杂, 所以费用为3500Zeny.";
		setarray .@item[0], 510, 973, 713;
		setarray .@count[0], 20,   1,   1;
		set .@cost,3500;
		set .@dyestuff,978;
		break;
	case 4:
		mes "恩... 我需要5片蓝色药草, 20片绿色草药, 20片黄色药草, 1瓶中和剂, 1瓶混合剂, 和1个空瓶来制作墨绿色染料. 别弄混需要的材料. 费用仅为5000z.";
		setarray .@item[0], 510, 511, 508, 974, 973, 713;
		setarray .@count[0],  5,  20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,979;
		break;
	case 5:
		mes "恩... 我需要20片红色药草, 20片黄色药草, 1瓶中和剂, 1瓶混合剂, 和1个空瓶来制作橘黄色染料. 费用为5000Zeny.";
		setarray .@item[0], 507, 508, 974, 973, 713;
		setarray .@count[0], 20,  20,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,980;
		break;
	case 6:
		mes "恩... 我需要10片蓝色药草, 30片红色药草, 1瓶中和剂, 1瓶混合剂, 和1个空瓶来制作紫罗兰色染料. 费用为5000Zeny.";
		setarray .@item[0], 510, 507, 974, 973, 713;
		setarray .@count[0], 10,  30,   1,   1,   1;
		set .@cost,5000;
		set .@dyestuff,981;
		break;
	case 7:
		mes "恩... 我需要30片白色药草, 1瓶中和剂, 和1个空瓶来制作白色染料. 费用为3000Zeny.";
		setarray .@item[0], 509, 973, 713;
		setarray .@count[0], 30,   1,   1; 
		set .@cost,3000;
		set .@dyestuff,982;
		break;
	case 8:
		mes "恩... 我需要红色药草, 黄色药草, 绿色草药各30 片, 5片蓝色药草, 1瓶中和剂, 1瓶混合剂, 和1个空瓶. 这个将比其它需要更多时间和更多的努力, 所以费用为7000 Zeny.";
		setarray .@item[0], 507, 508, 511, 510, 974, 973, 713;
		setarray .@count[0], 30,  30,  30,   5,   1,   1,   1;
		set .@cost,7000;
		set .@dyestuff,983;
		break;
	}
	next;
	mes "[染料制作师 杰瓦杜拉罕]";
	switch(getarg(1)) {
	case 1: mes "啊! 我想你已经准备好所有的东西了. 你要我开始工作吗?"; break;
	case 2: mes "很好! 我相信你已经准备好所有的东西了. 你要我开始工作吗?"; break;
	case 3: mes "很好! 我相信已经准备好所有的东西了. 我可以开始工作了吗?"; break;
	case 4: mes "很好! 我相信已经准备了所有的东西了. 你要我开始工作吗?"; break;
	}
	next;
	if (select("制作染料:取消") == 1) {
		set .@size, getarraysize(.@item);
		// 检查材料
		for( set .@i,0 ; .@i < .@size ; set .@i,.@i+1 )
			if( countitem(.@item[.@i]) < .@count[.@i] )
				break;
		if( .@i < .@size || Zeny < .@cost ) {
			mes "[染料制作师 杰瓦杜拉罕]";
			mes "嗯. 不够啊...";
			mes "我想我不能用你带来的材料制造你想要的染料. 你为何不多拿点材料来...?";
			close;
		}
		// 删除物品
		for (set .@i, 0; .@i < .@size ; set .@i, .@i+1)
			delitem .@item[.@i], .@count[.@i];
		set zeny,zeny-.@cost;
		// 得到染料
		getitem .@dyestuff,1;
		
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "嗯... 做的不错哦. 一个非常棒的颜色. 当然我还会尝试制作更迷人的染料...";
		next;
		mes "[染料制作师 杰瓦杜拉罕]";
		mes "好的, 不错. 当你需要更多染料时, 就来找我.";
		close;
	}
	mes "[染料制作师 杰瓦杜拉罕]";
	mes "呃!! 怎么了? 你现在改变心意";
	mes "了吗!? 真扫兴...";
	return;
}
