//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//= 1.3 Fixed some typos and optimized a little more. [Daegaladh]
//= 1.3a Added 'disable_items' command. [Euphy]
//============================================================ 

prontera,165,70,2	script	实习附魔师	73,{
	disable_items;
	mes "[实习附魔师]";
	if (Zeny > 399999) {
		mes "长期以来我一直在研究如何改造盔甲,使盔甲的能力最大化";
		next;
		mes "[实习附魔师]";
		mes "所谓盔甲附魔,是指在盔甲内隐藏的插槽里注入神秘力量的一种新型技术";
		next;
		mes "[实习附魔师]";
		mes "当然需要注意,同样的盔甲如果拥有两件以上,会按物品在装备视窗中的顺序进行附魔";
		next;
		mes "[实习附魔师]";
		mes "在这种情况下,有可能会对不想附魔的盔甲进行附魔,因此请^5555ff只携带1件^000000需要附魔的盔甲过来即可";
		next;
		mes "[实习附魔师]";
		mes "如果带着2件以上的盔甲过来,结果出了意外,我是不负责任喔!";
		next;
		switch(select("无洞盔甲:一洞盔甲:高级盔甲:下次再说")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			mes "[实习附魔师]";
			mes "以后如果有兴趣的话,请务必再来找我";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	else {
		mes "我负责盔甲的附魔工作,也就是说我是专门研究如何提高既有盔甲力量的人";
		next;
		mes "[实习附魔师]";
		mes "如果你有兴趣的话,请带着40万金币和需要附魔的盔甲过来找我吧!";
		close;
	}

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[实习附魔师]";
	if ((countitem(.@itemid) > 0) && (countitem(.@itemid) < 2)) {
		mes "盔甲附魔需要40万金币,而且附魔效果是随机的,当然装备有一定几率会损坏";
		next;
		mes "[实习附魔师]";
		mes "最重要的是,如果损坏,那么";
		mes "^ff5555既有的精炼值";
		mes "和原来装备的卡片会全部消失^000000,";
		mes "你确定要附魔吗?";
		next;
		if(select("呃... 让我再考虑一下:我已经下定决心了，你下手吧！")==1) {
			mes "[实习附魔师]";
			mes "安全第一,祝你旅途愉快!";
			close;
		}
		mes "[实习附魔师]";
		mes "看来你很喜欢冒险,那我们开始吧?";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[实习附魔师]";
			mes "抱歉,你没有足够的金币";
			close;
		}
		progressbar "ffff00",7;
		set zeny,zeny-400000;
		delitem .@itemid,1;
		switch (rand(1,.@failrate)) {
			case 1: set .@addpart,4702;break;
			case 2: set .@addpart,4712;break;
			case 3: set .@addpart,4722;break;
			case 4: set .@addpart,4732;break;
			case 5: set .@addpart,4742;break;
			case 6: set .@addpart,4752;break;
			case 7:
			case 8: set .@addpart,4701;break;
			case 9:
			case 10: set .@addpart,4711;break;
			case 11:
			case 12: set .@addpart,4721;break;
			case 13:
			case 14: set .@addpart,4731;break;
			case 15:
			case 16: set .@addpart,4741;break;
			case 17:
			case 18: set .@addpart,4751;break;
			case 19:
			case 20:
			case 21: set .@addpart,4700;break;
			case 22:
			case 23:
			case 24: set .@addpart,4710;break;
			case 25:
			case 26:
			case 27: set .@addpart,4720;break;
			case 28:
			case 29:
			case 30: set .@addpart,4730;break;
			case 31:
			case 32:
			case 33: set .@addpart,4740;break;
			case 34:
			case 35:
			case 36: set .@addpart,4750;break;
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[实习附魔师]";
				mes "很遗憾,";
				mes "你委托我附魔的装备由于附魔失败而损坏了";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "嗯?好像什么都没有啊?";
		mes "请带着^5555ff1个^000000要附魔的装备再来找我吧!";
		close;
	}
}
