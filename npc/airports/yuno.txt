//============================================================
//= rAthenaCN [飞空艇]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Yuno Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Yuno Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

y_airport,143,43,5	script	机场职员#y_air1a::y_airport1	90,{
	mes "[机场职员]";
	mes "欢迎来到朱诺机场.我们公司提供";
	mes "抵达艾音布罗克,里希塔乐和毁葛的境内旅行航班";
	mes "以及抵达依斯鲁得岛和拉赫的境外旅行航班.";
	mes "请问需要帮忙吗?";
	next;
	if (select("我想搭乘飞空艇:取消") == 1) {
		mes "[机场职员]";
		mes "搭乘飞空艇的费用共计 1,200 zeny.";
		mes "如果您使用飞空艇免费搭乘券的话,就可以免费搭乘.";
		mes "请问您是否愿意搭乘飞空艇呢?";
		next;
		if (select("是的:不要") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "y_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "y_airport",148,51;
				end;
			}
			mes "[机场职员]";
			mes "很抱歉，您身上并没有 1,200 Zeny 可用于支付飞空艇的搭乘费用！";
			close;
		}
	}
	mes "[机场职员]";
	mes "等您有需要的时候请再来找我吧";
	mes "祝您有个美好的一天~";
	close;
}

y_airport,158,43,5	duplicate(y_airport1)	机场职员#y_air1b	90
y_airport,126,43,5	duplicate(y_airport1)	机场职员#y_air1c	90

y_airport,143,49,3	script	离站办理职员#y_air2a::y_airport2	90,{
	mes "[离站手续办理职员]";
	mes "欢迎来到朱诺机场.如果您刚从飞空艇上下来的话,那么就由我来带领您离站吧,";
	mes "如果您是想要搭乘飞空艇的话,那么就请搭乘飞空艇去您想去的目的地好了.";
	next;
	if (select("离站:取消") == 1) {
		mes "[离站手续办理职员]";
		mes "一旦您离站以后,如果还想要登机的话就要再付一次钱了.";
		mes "只有在您要去朱诺的情况下才好在这里离站.";
		mes "那么,请问您坚持要在朱诺这里离站吗？";
		next;
		if (select("是的:不要了") == 1) {
			warp "y_airport",142,40;
			end;
		}
	}
	mes "[离站手续办理职员]";
	mes "好的,谢谢您的光临.";
	mes "祝您旅途愉快~";
	close;
}

y_airport,126,51,3	duplicate(y_airport2)	离站办理职员#y_air2b	90
y_airport,158,50,3	duplicate(y_airport2)	离站办理职员#y_air2c	90

y_airport,145,63,5	script	国内登机	91,{
	mes "[登机服务员]";
	mes "请问您是否要搭乘飞向";
	mes "艾音布罗克,里希塔乐和毁葛的飞空艇呢?,";
	mes "如果您想去的话,就让我来带领您去相关站台吧.,";

	next;
	if (select("是的:不要了") == 1) {
		warp "yuno",59,244;
		end;
	}
	mes "[登机服务员]";
	mes "多谢光临了.";
	mes "祝您旅途愉快~";
	close;
}

y_airport,140,63,5	script	国际登机	91,{
	mes "[登机服务员]";
	mes "请问您是否要搭乘飞向";
	mes "依斯鲁得岛和拉赫的飞空艇呢?";
	mes "如果您想去的话,就让我来带领您去相关站台吧.";
	next;
	if (select("好的:不要了") == 1) {
		warp "yuno",47,244;
		end;
	}
	mes "[登机服务员]";
	mes "多谢您使用我们的服务.";
	mes "希望您在我们的机场过的愉快.";
	close;
}

yuno,14,262,5	script	飞空艇职员#yuno01	91,{
	mes "[飞空艇职员]";
	mes "欢迎来到朱诺机场.";
	mes "推入旁边的门就可以直线抵达";
	mes "卢恩-米德加兹王国境内的依斯鲁得岛,";
	mes "以及阿鲁娜配滋教国境内的拉赫.";
	next;
	mes "[飞空艇职员]";
	mes "如果朱诺是您的航班终点的话,";
	mes "楼梯下相关的职员会帮助您办理离站手续.";
	mes "谢谢您选择了我们的服务,祝您旅途愉快.";
	close;
}

yuno,88,263,3	script	飞空艇职员#yuno02	91,{
	mes "[飞空艇职员]";
	mes "欢迎来到朱诺机场.";
	mes "推入旁边的门就可以搭乘飞空艇.";
	mes "本飞空艇将在";
	mes "修发兹共和国境内的艾音布罗克,里希塔乐";
	mes "和毁葛做停靠.";
	next;
	mes "[飞空艇职员]";
	mes "如果朱诺是您的航班终点的话,";
	mes "楼梯下相关的职员会帮助您办理离站手续,";
	mes "谢谢您选择了我们的服务,祝您旅途愉快.";
	close;
}
