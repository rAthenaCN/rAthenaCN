//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================  
//= Magic Book Seller
//===== By: ================================================== 
//= Masao
//= Credits to Muad_Dib for the translation.
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena
//===== Description: ========================================= 
//= [Official Conversion]
//= An NPC which sells you different kinds of Magic Books.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Optimized. [Euphy]
//============================================================ 

geffen_in,176,105,4	script	魔法书管理者维洛夫	64,{

	if (checkweight(1201,1) == 0) {
		mes "你身上带了太多东东了，去整理一下再来吧。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "你超重了哦，清理一下减轻重量再来吧。";
		close;
	}
	if ((Upper == 2) && (Class != Job_Baby_Warlock)) {
		mes "[维洛夫]";
		mes "小盆友你是谁呀？";
		mes "你是不是迷路了啊？我可不是照顾小盆友的保姆。";
		close;
	}
	if ((Class != Job_Warlock) && (Class != Job_Warlock_T) && (Class != Job_Baby_Warlock)) {
		mes "[维洛夫]";
		mes "哈哈，你也在寻求新的知识么？";
		next;
		mes "[维洛夫]";
		mes "我很抱歉，我的知识恐怕不太不适合你呀。";
		close;
	}
	if (getskilllv(2230) < 1) {
		mes "[维洛夫]";
		mes "好吧，你满足了所有条件除了...你还需要学习“释放”咒语~";
		next;
		mes "[维洛夫]";
		mes "先学释放咒语吧，我有很多可以教你。";
		close;
	}
	if ((getskilllv(2230) > 0) && (Class == Job_Warlock) || (Class == Job_Warlock_T) || (Class == Job_Baby_Warlock)) {
		mes "[维洛夫]";
		mes "祝贺你，我的朋友！";
		mes "欢迎来到全新的魔法世界！";
		next;
		mes "[维洛夫]";
		mes "我们魔法师经常渴求新的知识和能量。";
		next;
		mes "[维洛夫]";
		mes "我们愿意历经千辛万险去实现理想";
		mes "当我们达成所愿之时，那种成就感一定妙不可言";
		mes "这也是你和我都选择魔法之路的原因。";
		next;
		mes "[维洛夫]";
		mes "对啦，今天来找我有什么事情啊？";
		next;
		switch (select("没什么事情...:你知道阅读魔法书么？:你知道魔法保存么？:我可以看一下魔法书么？:我不用魔法保存，还可以使用魔法书么？:有关终极魔法书的事情")) {
		case 1:
			mes "[维洛夫]";
			mes "哈！真是个傻孩子。";
			close;
		case 2:
			mes "[维洛夫]";
			mes "阅读魔法书顾名思义就是“读一本有魔法咒语的书”啊，你知道怎么来使用魔法书么？";
			next;
			switch (select("我知道啊:我完全不知道")) {
			case 1:
				mes "[维洛夫]";
				mes "好吧，看来不需要我再费唇舌介绍啦";
				close;
			case 2:
				mes "[维洛夫]";
				mes "魔法书是魔法卷轴的高端形式，魔法卷轴上的咒语用过一次就会失效，但魔法书可以反覆使用多次。";
				next;
				mes "[维洛夫]";
				mes "让我们把魔法卷轴比喻成一根普通导线，当你用这根导线去开门或是做其他什么事情的时候，导线便会弯曲或是毁坏，失去了原有的形状。而魔法书就好比有形状记忆功能的合金。";
				next;
				mes "[维洛夫]";
				mes "哈，看来你不知道什么是拥有记忆功能的合金呀~合金线可以反覆改变形状并多次使用，你记住这个就可以啦~";
				next;
				mes "[维洛夫]";
				mes "就像合金一样，魔法书上的咒语使用过一次之后仍然还会存在，尽管再次使用也是需要一些时间。";
				next;
				mes "[维洛夫]";
				mes "...噢,我不想耽误太多时间解释这些。不管怎样，你可以使用魔法书去念咒，这个概念和普通念咒是不一样的。";
				next;
				select("有什么不同呢？");
				mes "[维洛夫]";
				mes "为什么你要把已经有的咒语写到魔法书中呢？";
				next;
				mes "[维洛夫]";
				mes "这个问题很好，这个涉及到魔法保存。";
				next;
				mes "[维洛夫]";
				mes "你已经学会了“释放”魔法，那么你想想看，在魔法释放之前，是什么承载了四种不同自然元素的能量？";
				next;
				mes "[维洛夫]";
				mes "这就是魔法保存和释放过程的链接，接下来我给你详细说说。";
				next;
				mes "[维洛夫]";
				mes "正如你所知晓的，如何在合适的时机念魔法咒语，";
				mes "将四大自然元素的能量释放出来，这些都是基于魔法保存的基础。";
				next;
				mes "[维洛夫]";
				mes "四大元素是指火，水，风和地。魔法的精髓就在于自由支配这些自然元素的能量。";
				next;
				mes "[维洛夫]";
				mes "我们人类利用火炬来作为火的载体，利用扇子来产生风，利用碗来盛水，利用粘土来制作陶器。";
				next;
				mes "[维洛夫]";
				mes "这些行为虽然跟魔法不同，但同样都是运用自然元素的方法。";
				next;
				mes "[维洛夫]";
				mes "当你运用更加复杂的魔法去呼风唤雨，电闪雷鸣，甚至焚烧四野，这都需要消耗很多精神层面和物质层面的能量。";
				next;
				mes "[维洛夫]";
				mes "如果你的念咒不成功导致你没法控制这些能量，那么你的身心都将受到重创。";
				next;
				mes "[维洛夫]";
				mes "这就是为什么我们要借魔法书的保存功能来减轻这样的伤害，魔法书可以帮助你预念咒语，使你更加专注于魔法释放的过程。";
				next;
				mes "[维洛夫]";
				mes "当然，魔法书也有一个缺点，就是无法帮你预念没有学过的咒语。";
				next;
				mes "[维洛夫]";
				mes "正如我之前所说，魔法书里面的咒语，仍然是咒语本身。";
				next;
				mes "[维洛夫]";
				mes "作为一个魔法师，你没法改变魔法书中的法术，或是在魔法书中创造新的法术。";
				next;
				mes "[维洛夫]";
				mes "就算你勉强创造了一个，强大的魔力也会让你觉得很难控制。";
				next;
				mes "[维洛夫]";
				mes "记住啊，为了学习阅读魔法书，你必须先学会魔法保存。";
				close;
			}
		case 3:
			mes "[维洛夫]";
			mes "魔法保存是为了是魔法拥有载体可以释放，帮助在魔法组合中更顺畅的念咒。";
			next;
			mes "[维洛夫]";
			mes "当你将魔法保存在魔法书中，如果你不选择释放，魔法是不会自动释放出来的。";
			next;
			mes "[维洛夫]";
			mes "当然，魔法保存也会消耗你的魔力，但是比起传统念咒要的消耗少得多。";
			next;
			mes "[维洛夫]";
			mes "当你使用更加高级强大的魔法咒语时，魔法保存的难度会提升。";
			next;
			mes "[维洛夫]";
			mes "对于那些不太厉害的咒语来说，在一次魔法组合中，你可以保存两到三个。但是对于比较强大的咒语来说，一两个已经是保存的极限了。";
			next;
			mes "[维洛夫]";
			mes "这都完全取决于你控制咒语的能力，在你学会之后，你必须经过多次的练习才能将魔法保存的功效发挥出来。";
			close;
		case 4:
			mes "[维洛夫]";
			mes "我很抱歉，我这里只有一些给初学者看的魔法书，显然这些不太适合你，你还是想要看么？";
			next;
			switch (select("当然，我还是想看:哪里我可以看到更加高阶的魔法书？")) {
			case 1:
				mes "[维洛夫]";
				mes "你可以去找旁边的 [莉亚]，告诉她你想阅读我存在那里的魔法书。";
				next;
				mes "[维洛夫]";
				mes "特别提醒你一下，她不会平白无故的就给你看的，除非你能给她点什么！";
				if (wm_book < 1) set wm_book, 1;
				close;
			case 2:
				mes "[维洛夫]";
				mes "我很抱歉，魔法协会的管理者最近颁布了一个限制令。由于一些不法之徒会利用魔法去做些坏事情，所以协会决定限制魔法书借阅的权限。";
				next;
				mes "[维洛夫]";
				mes "如果你努力去找肯定是能找到一本半本的，但是我这里真没有。";
				next;
				mes "[维洛夫]";
				mes "如果魔法协会的管理者取消了这个限制，我会第一个告诉你的，放心吧";
				close;
			}
		case 5:
			mes "[维洛夫]";
			mes "当你想保存魔法但却不使用魔法书的话，什么也不会发生，因为没有载体去承载魔法。";
			next;
			mes "[维洛夫]";
			mes "就像你需要碗去盛水一样。";
			next;
			mes "[维洛夫]";
			mes "问题在于，当你一意孤行非要运用一个你没有学过的咒语时，你会走火入魔的。";
			next;
			mes "[维洛夫]";
			mes "当然，所谓的走火入魔也并不严重，不过是睡着了或是陷入晕眩状态罢了。产生这种情况的原因是由于你在使用一个没有学过的咒语时，你的魔力和魔法书之间的传导出现了问题。";
			next;
			mes "[维洛夫]";
			mes "千万别觉得好玩去尝试！ 念咒语的时候睡着了？ 这对于我们魔法师来说是多么大的讽刺和耻辱啊！";
			close;
		case 6:
			if ((BaseLevel > 139) && (getskilllv(2217) > 0) || (getskilllv(2213) > 0)) {
				if(mac_book < 1){
					mes "[维洛夫]";
					mes "抱歉，你可以问其他人么？我现在有点忙。";
					close;
				}
				if (mac_book == 1) {
					mes "[维洛夫]";
					mes "哈？你在找终极魔法书啊？谁让你来找我的？";
					next;
					select("莉亚女士");
					mes "[维洛夫]";
					mes "抱歉啊，关于终极魔法书，我这里也没有太多的信息";
					next;
					mes "[维洛夫]";
					mes "这个王国的一些学者试图去整理并复制远古魔法书，但是...";
					next;
					select("但是什么？");
					mes "[维洛夫]";
					mes "对了，我哥也许知道点什么";
					next;
					select("我就知道问你准没错！");
					mes "[维洛夫]";
					mes "我哥跟你很像，他对终极魔法书也有极大的兴趣。";
					next;
					mes "[维洛夫]";
					mes "他的名字是加尔夫斯";
					mes "他正在饶有兴致的研究着古代魔法书呢";
					next;
					mes "[维洛夫]";
					mes "原本我想着他会很快有成果的，所以我没有给他任何帮助。";
					next;
					mes "[维洛夫]";
					mes "如果他能发现有趣的东西，也许会对你有帮助。";
					next;
					select("那现在加尔夫斯在哪里？");
					mes "[维洛夫]";
					mes "我听说他在远征军营，也就是时空缝隙附近，寻找远古魔法书的踪迹。";
					next;
					mes "[维洛夫]";
					mes "如果你想去找他，你可要做好长期旅行的准备啊";
					set mac_book,2;
					close;
				}
				if (mac_book > 1) {
					mes "[维洛夫]";
					mes "加尔夫斯在远征军营那边，也就是时空缝隙附近。";
					next;
					mes "[维洛夫]";
					mes "如果你想去找他，你可要做好长期旅行的准备啊";
					close;
				}
			}
			mes "[维洛夫]";
			mes "哈哈，也许你该专注于强身健体，而不是天天想着创造魔法书。";
			close;
		}
	}
	mes "[维洛夫]";
	mes "哈哈，你也在寻找新知识么?";
	next;
	mes "[维洛夫]";
	mes "我很抱歉，我的知识不适合你。";
	close;
}

geffen_in,175,112,4	script	莉亚	123,{

	if (checkweight(1201,1) == 0) {
		mes "你身上带了太多东东了，";
		mes "去整理一下再来吧。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "你的负重量已经超标了哦，";
		mes "清理一下减轻重量再来吧。";
		close;
	}
	if ((wm_book > 0) && (Class == Job_Warlock) || (Class == Job_Warlock_T) || (Class == Job_Baby_Warlock)) {
		mes "[莉亚]";
		mes "你只能在这里阅读魔法书，如果你想借阅的话，必须有魔法协会管理者和高级魔法师的许可。";
		next;
		mes "[莉亚]";
		mes "我有什么可以帮你？";
		next;
		select("维洛夫让我来阅读他存放在这里的书");
		mes "[莉亚]";
		mes "这么说你是来借书的啦？";
		next;
		switch (select("让我考虑一下...:我想要初级魔法书:我想要中级魔法书:我想要高级魔法书:我想要终极魔法书")) {
		case 1:
			mes "[莉亚]";
			mes "好的，你慢慢考虑。";
			close;
		case 2:
			mes "[莉亚]";
			mes "我们现在遇到了点麻烦，很多人都来借阅魔法书。";
			next;
			mes "[莉亚]";
			mes "糟糕的是，好多外借的魔法书都没有按时归还，所以我们决定收取一定的抵押金。";
			next;
			select("收取抵押金？");
			mes "[莉亚]";
			mes "是的，最少10000金币，或者你也可以用2本^0000FF古魔法书^000000或是50张^0000FF破旧书页^000000来换。";
			mes "你支付过押金后，我才会让你借书。";
			next;
			set .@Payment, select("如何找到古魔法书和破旧书页？:我还是付现金好了:我用古魔法书来换:我用破旧书页来换")-1;
			mes "[莉亚]";
			if (!.@Payment) {
				mes "我听说飞行魔书、死亡魔书、巫婆等怪物会掉落古魔法书和破旧书页。不过你千万别为了找这些东西去拚命。";
				next;
				mes "[莉亚]";
				mes "生命永远都是最重要的，明白么？";
				close;
			}
			if ((.@Payment == 1 && Zeny > 10000) || (.@Payment == 2 && countitem(1006) > 1) || (.@Payment == 3 && countitem(1097) > 49)) {
				mes "你想要借阅哪种书？";
				next;
				set .@i, select("让我考虑一下...:魔法书(火箭术):魔法书(冰箭术):魔法书(雷击术)")-1;
				mes "[莉亚]";
				if (!.@i) {
					mes "没问题，你慢慢考虑。";
					close;
				}
				if (countitem(6188+.@i)) {
					mes "你已经有了那本魔法书了，你不能重复借同一本。";
					next;
					mes "[莉亚]";
					mes "你知道多少人想看你现在借去的这本书么？如果你不归还反而要再借一本，那就太自私了。";
					close;
				}
				if ((.@Payment == 1 && Zeny > 10000) || (.@Payment == 2 && countitem(1006) > 1) || (.@Payment == 3 && countitem(1097) > 49)) {
					mes "魔法书背面封皮的借书卡上，写明了归还日期。";
					next;
					mes "[莉亚]";
					mes "请不要弄丢或是损坏，否则会罚款的。";
					if (.@Payment == 1) set Zeny, Zeny - 10000;
					else if (.@Payment == 2) delitem 1006,2; //Old_Magic_Book
					else delitem 1097,50; //Worn_Out_Page
					getitem 6188+.@i,1; //Magic_Book_FB, Magic_Book_CB, Magic_Book_LB
					close;
				}
			}
			mes "抱歉，你所准备的抵押金或者抵押物品貌似数量不够啊。";
			close;
		case 3:
			mes "[莉亚]";
			mes "你要借中级魔法书啊，请你支付5万金币，或是2个神秘箱子，或是9个铝。";
			next;
			set .@Payment, select("如何可以找到神秘箱子和铝？:我付5万金币好了:我用神秘箱子来交换:我用铝来交换")-1;
			mes "[莉亚]";
			if (!.@Payment) {
				mes "神秘箱子不太常见,但是世界上很多怪物都会掉落比如邪恶礼盒、邪骸食人鱼、邪恶箱、梦魇、克莱本、诺可伊、九尾狐、废气魔和漂流浪人。";
				mes "如果你资金充足的话，也可以在路上买到神秘箱子。";
				next;
				mes "[莉亚]";
				mes "掉落铝的魔物包括妖君、腐尸战俘、玩偶熊、黑耀石和银月魔女。或者你可以考虑找城里的铁匠，把铝原石提炼成铝，这样会比直接收集铝稍微容易些。";
				close;
			}
			if ((.@Payment == 1 && Zeny > 50000) || (.@Payment == 2 && countitem(603) > 1) || (.@Payment == 3 && countitem(985) > 8)) {
				mes "你想要借阅什么样的魔法书？";
				next;
				set .@i, select("让我考虑一下...:魔法书(暴风雪):魔法书(怒雷强击):魔法书(陨石术):魔法书(雷爆术):魔法书(雷鸣术):魔法书(水球术):魔法书(崩裂术):魔法书(地震术)")-1;
				setarray .@Books[0],6192,6193,6194,6197,6198,6199,6200,6201;
				mes "[莉亚]";
				if (!.@i) {
					mes "没问题，你慢慢考虑。";
					close;
				}
				if (countitem(.@Books[.@i-1])) {
					mes "你已经有了那本魔法书了，你不能重复借同一本。";
					next;
					mes "[莉亚]";
					mes "你知道多少人想看你现在借去的这本书么？如果你不归还反而要再借一本，那就太自私了。";
					close;
				}
				if ((.@Payment == 1 && Zeny > 50000) || (.@Payment == 2 && countitem(603) > 1) || (.@Payment == 3 && countitem(985) > 8)) {
					mes "谢谢你支付抵押金。魔法书背面封皮的借书卡上，写明了归还日期。请不要弄丢或是损坏，否则会罚款的。";
					next;
					mes "[莉亚]";
					mes "此外提醒一下，必须归还了中级和高级魔法书之后才能借阅终极魔法书。你要记住，这些书只是借给你的，你要好好爱惜。";
					if (.@Payment == 1) set Zeny, Zeny - 50000;
					else if (.@Payment == 2) delitem 603,2; //Old_Blue_Box
					else delitem 985,9; //Elunium
					getitem .@Books[.@i-1],1; //Magic_Book_SG, Magic_Book_LOV, Magic_Book_MS, Magic_Book_TS, Magic_Book_JT, Magic_Book_WB, Magic_Book_HD, Magic_Book_ES
					close;
				}
			}
			mes "抱歉，你所准备的抵押金或者抵押物品貌似数量不够啊。";
			close;
		case 4:
			mes "[莉亚]";
			mes "你想借高级魔法书啊？提醒你一下，借阅高级魔法书有很多条件和限制，你确定你要借么？";
			next;
			select("是的，我坚决要借");
			mes "[莉亚]";
			mes "好吧，既然你这么坚定的要借阅，那你选一种支付押金的方式吧。";
			next;
			mes "[莉亚]";
			mes "[12个未知的碎片另加100,000金币]，或者";
			mes "[7个神之金属另加100,000金币]，或者";
			mes "[1个神秘紫箱另加100,000金币]。";
			mes "你想怎么支付呢？";
			next;
			set .@Payment, select("我在哪里可以找到那些东西？:让我考虑一下...:我选择12个未知的碎片另加100,000金币:我选择7个神之金属另加100,000金币:我选择1个神秘紫箱另加100,000金币")-1;
			mes "[莉亚]";
			if (!.@Payment) {
				mes "未知的碎片听说可以从优配擂斯谜一样的奇怪机械怪物身上获得，那种碎片是古代文明的源泉.....啊.....光是想想就让人激动。";
				next;
				mes "[莉亚]";
				mes "神之金属可以从行刑者、狮鹫兽、裘卡、深渊骑士等身上获得，但是同样也是收集神之金属原石会更快一些。";
				next;
				mes "[莉亚]";
				mes "收集到神之金属原石后，你可以在城里的精炼房进行提炼。";
				next;
				mes "[莉亚]";
				mes "神秘紫箱听说可以从邪恶箱、邪恶摩艾、兽人酋长、冰雹骑士、俄塞里斯身上获得。";
				next;
				mes "[莉亚]";
				mes "自从这些神秘箱子用来测试薛定谔的猫后，它们就成了值得研究的对象。";
				close;
			}
			if (.@Payment == 1) {
				mes "如果你无法搜集到这些物品来支付押金，那么我给你另一条路走。";
				next;
				mes "[莉亚]";
				mes "支付200,000金币怎么样？";
				next;
				if(select("抱歉，我没那么多钱:听起来不错") == 1) {
					mes "[莉亚]";
					mes "好的，那么请下次再来吧";
					close;
				}
				mes "[莉亚]";
			}
			if ((.@Payment == 1 && Zeny > 199999) || (Zeny > 99999 && ((.@Payment == 2 && countitem(7094) > 11) || (.@Payment == 3 && countitem(984) > 6) || (.@Payment == 4 && countitem(617))))) {
				mes "你想要借阅什么样的魔法书？";
				next;
				set .@i, select("让我考虑一下.:魔法书(地牛翻身):魔法书(连锁电击):魔法书(碧血陨石):魔法书(吸星大法)")-1;
				mes "[莉亚]";
				if (!.@i) {
					mes "好的，你慢慢考虑。";
					close;
				}
				if (countitem(6201+.@i)) {
					mes "你已经有了那本魔法书了，你不能重复借同一本。";
					next;
					mes "[莉亚]";
					mes "你知道多少人想看你现在借去的这本书么？如果你不归还反而要再借一本，那就太自私了。";
					close;
				}
				if ((.@Payment == 1 && Zeny > 199999) || (Zeny > 99999 && ((.@Payment == 2 && countitem(7094) > 11) || (.@Payment == 3 && countitem(984) > 6) || (.@Payment == 4 && countitem(617))))) {
					mes "谢谢你支付押金。魔法书背面封皮的借书卡上，写明了归还日期。请不要弄丢或是损坏，否则会罚款的。";
					next;
					mes "[莉亚]";
					mes "此外提醒一下，必须归还了中级和高级魔法书之后才能借阅终极魔法书。你要记住，这些书只是借给你的，你要好好爱惜。";
					if (.@Payment == 1) set Zeny, Zeny - 200000;
					else if (.@Payment == 2) delitem 7094,12; //Mystery_Piece
					else if (.@Payment == 3) delitem 984,7; //Oridecon
					else delitem 617,1; //Old_Violet_Box
					if (.@Payment > 1) set Zeny, Zeny - 100000;
					getitem 6201+.@i,1; //Magic_Book_ES_, Magic_Book_CL, Magic_Book_CR, Magic_Book_DL
					close;
				}
			}
			mes "抱歉，你所准备的抵押金或者抵押物品貌似数量不够啊。";
			close;
		case 5:
			if ((BaseLevel > 139) && (mac_book < 1) && ((getskilllv(2217) > 0) || (getskilllv(2213) > 0))) {
				mes "[莉亚]";
				mes "你...你是在说终极魔法书么？";
				next;
				mes "[莉亚]";
				mes "噢，我的天啊...";
				mes "我简直不敢相信有人要借那种书...";
				next;
				select("请别说你这里不能借阅");
				mes "[莉亚]";
				mes "说实话，我从没见过终极魔法书。";
				next;
				mes "[莉亚]";
				mes "如果你想知道更多信息，你应该去问维洛夫。";
				next;
				mes "[莉亚]";
				mes "抱歉，我真的无能为力。";
				set mac_book,1;
				close;
			}
			if ((BaseLevel > 139) && (mac_book > 0) && ((getskilllv(2217) > 0) || (getskilllv(2213) > 0))) {
				mes "[莉亚]";
				mes "如果你想知道更多信息，你应该去问维洛夫。";
				close;
			}
			mes "[莉亚]";
			mes "要不要现在就练习咒语呢？";
			next;
			mes "[莉亚]";
			mes "如果你想借阅终极魔法书，你必须达到140级，并且学会^0000FF元素漩涡^000000和^0000FF毁灭慧星^000000这两个技能。";
			next;
			mes "[莉亚]";
			mes "那种书是不会借给泛泛之辈的，你明白么？";
			close;
		}
	}
	mes "[莉亚]";
	mes "欢迎来到魔法档案室，这里提供有条件借阅魔法书的服务。";
	close;
}

mid_camp,255,244,4	script	加尔夫斯	735,{

	if (checkweight(1201,1) == 0) {
		mes "你身上带了太多东东了，";
		mes "去整理一下再来吧。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "你的负重量已经超标了哦，";
		mes "清理一下减轻重量再来吧。";
		close;
	}
	if (Upper == 2) {
		mes "[加尔夫斯]";
		mes "小盆友你是谁呀？";
		mes "你是不是迷路了啊？我可不是照顾小盆友的保姆。";
		close;
	}
	if ((Class != Job_Warlock) && (Class != Job_Warlock_T) && (Class != Job_Baby_Warlock)) {
		mes "[加尔夫斯]";
		mes "我左臂觉得热得不行右臂又觉得冷得不行，我真是受不了这诡异的天气啊";
		close;
	}
	if (getskilllv(2230) < 1) {
		mes "[加尔夫斯]";
		mes "如果我继续呆在这的话简直是自找麻烦，唉，我已经很麻烦了...";
		close;
	}
	if ((BaseLevel < 140) || (getskilllv(2217) < 1) && (getskilllv(2213) < 1)) {
		mes "[加尔夫斯]";
		mes "不好意思，我不会跟没准备好的人说这些";
		close;
	}
	if (mac_book < 2) {
		mes "[加尔夫斯]";
		mes "你看起来很强，但是似乎跟我没啥关系嘛";
		close;
	}
	if (mac_book == 2) {
		mes "[加尔夫斯]";
		mes "你来找我干啥？";
		next;
		select("我听说你在研究终极魔法书");
		mes "[加尔夫斯]";
		mes "你也感兴趣么？";
		mes "我不觉得真的会有人对这种书感兴趣唉~";
		mes "实话告诉你吧，我确实是在研究，不过你怎么知道的啊？";
		next;
		select("你是维洛夫的哥哥吧？");
		mes "[加尔夫斯]";
		mes "是的，我叫加尔夫斯，维洛夫是我弟弟。";
		next;
		mes "[加尔夫斯]";
		mes "等等，难道是维洛夫";
		mes "那个老家伙推荐你来找我的？";
		next;
		select("就是这样！");
		mes "[加尔夫斯]";
		mes "哇哦!";
		mes "真是意外啊，";
		mes "他从来不觉得我在正儿八经的做研究，";
		mes "我真高兴这个老顽固也有改变的一天啊。";
		next;
		mes "[加尔夫斯]";
		mes "哈哈，你来的时机真准，我刚好在找人做个测试！";
		next;
		select("找人做测试？");
		mes "[加尔夫斯]";
		mes "老实说，我只是对创造终极魔法书有兴趣。";
		next;
		mes "[加尔夫斯]";
		mes "其实我的魔法水平不高，更别提跟人战斗了。不过无所谓，我从魔法书中找到了绝妙有趣的事情。";
		next;
		mes "[加尔夫斯]";
		mes "但问题在于，即使我做出了一本魔法书，但我没法用啊~所以我需要有人来帮我测试魔法书。";
		next;
		mes "[加尔夫斯]";
		mes "这也是我一直以来等待像你这样法力高强的人来做测试的原因。";
		next;
		select("(这丫说啥呢？)");
		mes "[加尔夫斯]";
		mes "我的研究差不多有成果了，";
		mes "但我还差一个问题没解决。";
		next;
		mes "[加尔夫斯]";
		mes "要做成远古终极魔法书，我需要有个触媒来诱发这些咒语。";
		next;
		mes "[加尔夫斯]";
		mes "根据远古的资料，我已经成功的把咒语写进魔法书了，但是我还需要知道必要的魔法触媒信息。";
		next;
		mes "[加尔夫斯]";
		mes "啊！但那有一根银线!";
		next;
		select("老兄，扯远了吧...！");
		mes "[加尔夫斯]";
		mes "哈哈...";
		mes "在邪恶巨人的首都埃迪斯特斯，有很多档案，记载着世界上全部的魔法知识。";
		next;
		mes "[加尔夫斯]";
		mes "也许那里能找到有关终极魔法书触媒的信息。";
		next;
		mes "[加尔夫斯]";
		mes "你知道该做啥了吧?去埃迪斯特斯找有关于魔法书触媒的档案，尽全力去找！";
		next;
		mes "[加尔夫斯]";
		mes "把那个触媒带回来，之后我才能最终完成终极魔法书的制作。";
		set mac_book,3;
		close;
	}
	if (mac_book == 3) {
		mes "[加尔夫斯]";
		mes "你知道该做啥了吧？去埃迪斯特斯找有关于魔法书触媒的档案，尽全力去找！";
		next;
		mes "[加尔夫斯]";
		mes "把那个触媒带回来，之后我才能最终完成终极魔法书的制作。";
		close;
	}
	if (mac_book == 4) {
		mes "[加尔夫斯]";
		mes "噢~有什么发现么？";
		next;
		set .@i, select("我发现了关于毁灭彗星的一切:我完全掌握了元素漩涡的极理要义");
		mes "[加尔夫斯]";
		if ((.@i == 1 && countitem(6195)) || (.@i == 2 && countitem(6196))) {
			mes "你在耍我吧，你已经有那本书了呀！";
			close;
		}
		if ((.@i == 1 && checkquest(12218) == 2) || (.@i == 2 && checkquest(12219) == 2 && checkquest(12220) == 2 && checkquest(12221) == 2 && checkquest(12222) == 2)) {
			mes "哦~我从你身上感觉到了不可思议的魔力！太好啦，我们开始制作终极魔法书吧！";
			next;
			mes "[加尔夫斯]";
			mes "没问题，但是...我太穷了，买不起制作魔法书所需要的材料T。T";
			next;
			mes "[加尔夫斯]";
			mes "我需要至少^0000aa1,000,000金币^000000，这样才够买齐所有制作魔法书所需的材料。你有那么多钱么？";
			next;
			if(select("澹我没有那么多钱:当然，我别的没有，钱多得是") == 1) {
				mes "[加尔夫斯]";
				mes "啊，你没有啊，太失望了，那我就无能为力了...";
				mes "等你有钱再来找我吧。";
				close;
			}
			mes "[加尔夫斯]";
			if (Zeny >= 1000000) {
				mes "太好啦，我们开始吧！";
				next;
				specialeffect2 EF_DISPELL;
				progressbar "ffff00",4;
				specialeffect2 EF_LORD;
				set Zeny, Zeny - 1000000;
				getitem 6194+.@i,1; //Magic_Book_CM, Magic_Book_TV
				mes "[加尔夫斯]";
				mes "这是我倾尽全力制造出来的魔法书！给你！";
				next;
				mes "[加尔夫斯]";
				mes "谢谢你帮我实现梦想，如果你还想要这类的书，请随时回来找我。";
				close;
			}
			mes "我不是告诉过你了么，没钱我就没法准备制作魔法书的材料。";
			close;
		}
		mes "你确定你发现什么了嘛？似乎你没找到我要的信息呀。";
		close;
	}
	mes "[加尔夫斯]";
	mes "你还需要什么别的吗？";
	close;
}

dic_in01,25,190,0	script	神秘的文件资料	844,{

	if (isequipped(2782) < 1) {
		mes "我最好别忘了装备一下我的智慧王之戒。";
		close;
	}
	if (ep13_3_invite < 5) {
		mes "当你要碰这些文件资料的时候，有个貌似是文档管理员的沙帕就要去找守卫加尔顿过来，最好还是尽快离开这里。";
		close;
	}
	if (checkquest(12165,PLAYTIME) == 1) {
		mes "这些文件资料让你觉得恶心，还是等五脏庙平复了再回来吧。";
		close;
	}
	if (checkquest(12165,PLAYTIME) == 2) {
		mes "你不再觉得恶心了，看来可以继续了。";
		erasequest 12165;
		close;
	}
	if ((BaseLevel < 140) || (getskilllv(2217) < 1) && (getskilllv(2213) < 1)) {
		mes "这些文件似乎很难懂，也没啥必要去读懂。";
		close;
	}
	specialeffect2 EF_DISPELL;
	progressbar "ffff00",4;
	if (mac_book < 3) {
		mes "你发现了关于魔法，加速，粒子波的记录。";
		close;
	}
	if (mac_book == 3) {
		mes "你发现了关于魔法，加速，粒子波的记录。也许这些正是加尔夫斯要找的。";
		set mac_book, 4;
		close;
	}
	if (mac_book == 4) {
		if (rand(1,10) == 1) {
			if (getskilllv(2213) > 0) {
				if (checkquest(12218) == 1) {
					mes "你已经有拥有这些资料了。";
					close;
				}
				mes "^660066为了提炼熔岩精华，你必须在图尔火山二层最热的地方去提炼40个燃烧的心脏。^000000";
				mes "^660066在那里你必须倾尽全力才能提炼成功。自古以来，人们用这种方法来研究魔法粒子的加速现象。^000000";
				next;
				mes "你发现了^990099熔岩精华提炼方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12218;
				end;
			}
			mes "这些资料没啥有用的内容。";
			close;
		}
		if (rand(1,10) == 2) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12219) == 1) {
					mes "你已经有拥有这些资料了。";
					close;
				}
				mes "^660066火焰精华的提取，必须在图尔火山一层的平稳的面上进行。10个火种是提炼火焰精华必须的原料。^000000";
				mes "^660066根据人类的记载，火焰是四大自然元素之一。^000000";
				next;
				mes "你发现了^990099火焰精华提炼方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12219;
				end;
			}
			mes "这些资料没啥有用的内容。";
			close;
		}
		if (rand(1,10) == 3) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12220) == 1) {
					mes "你已经有拥有这些资料了。";
					close;
				}
				mes "^660066我在冰洞三层成功提炼过冰河精华，如果你想尝试，就带着10个冰块心脏去那个地方。^000000";
				next;
				mes "你发现了^990099冰川精华提炼方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12220;
				end;
			}
			mes "这些资料没啥有用的。";
			close;
		}
		if (rand(1,10) == 4) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12221) == 1) {
					mes "你已经有拥有这些资料了。";
					close;
				}
				mes "^660066在艾音贝赫矿山二层我已经从古老的岩石提炼出化石精华。目前为止，那个地方是所有测试过的地方中最稳定也是最适合提炼的。";
				mes "如果你想试试，准备10个朱毕雷去那边吧，祝你好运！^000000";
				next;
				mes "你发现了^990099化石精华提炼方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12221;
				end;
			}
			mes "这些资料没啥有用的。";
			close;
		}
		if (rand(1,10) == 5) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12222) == 1) {
					mes "你已经有拥有这些资料了。";
					close;
				}
				mes "^660066为了提炼暴风精华，我去了昆仑的棋盘考察，那里是风精灵的栖息地。";
				mes "提炼的过程很容易，但是我费了好大劲才找到了催化剂-舞狮的假牙。^000000";
				next;
				mes "你发现了^990099暴风精华提炼方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12222;
				end;
			}
			mes "这些资料没啥有用的。";
			close;
		}
		if (rand(1,10) == 10) {
			mes "突然你觉得很恶心，这种感觉似乎没那么容易消退，你需要晚点再来找有用的资料。";
			setquest 12165;
			close;
		}
		mes "这里没什么特别的。";
		close;
	}
	mes "你不再需要继续搜查这个地方了。";
	close;
}

function	script	Magic_Book_Essence	{
	if (MaxWeight - Weight < 1000) {
		mes "请去收拾你的行李，减轻些负重。";
		close;
	}
	if (checkquest(getarg(0)) == 1 && countitem(getarg(1)) >= getarg(2)) {
		specialeffect2 EF_BLIND;
		specialeffect2 EF_BEGINSPELL;
		progressbar "ffff00",4;
		delitem getarg(1),getarg(2);
		if (rand(2)) {
			mes "你使用了"+getarg(2)+"个"+getitemname(getarg(1))+"， 提炼出了^999900"+getarg(3)+"精华^000000。";
			next;
			mes getarg(3)+"精华是永久性的， 除非有啥意外发生。^000000";
			completequest getarg(0);
			specialeffect2 EF_LORD;
			close;
		}
		mes "^008888你使用了"+getarg(2)+"个"+getitemname(getarg(1))+"进行提炼， 可惜提炼失败了。^000000";
		close;
	}
	if (checkquest(getarg(0)) == 2) {
		mes "你已经提炼了 "+getarg(3)+" 精华。";
		close;
	}
	mes "有人已经使用过这里了。";
	close;
}

thor_v02,163,104,0	script	充满熔岩能量的地方#33	844,{ callfunc "Magic_Book_Essence",12218,7097,40,"熔岩"; end; }
thor_v01,185,91,0	script	充满火焰能量的地方#33	844,{ callfunc "Magic_Book_Essence",12219,7098,10,"火焰"; end; }
ice_dun03,137,148,0	script	充满冰川能量的地方	844,{ callfunc "Magic_Book_Essence",12220,7561,10,"冰川"; end; }
ein_dun02,181,124,0	script	充满化石能量的地方	844,{ callfunc "Magic_Book_Essence",12221,7312,10,"化石"; end; }
gon_dun02,252,198,0	script	充满暴风能量的地方#33	844,{ callfunc "Magic_Book_Essence",12222,7266,10,"暴风"; end; }
