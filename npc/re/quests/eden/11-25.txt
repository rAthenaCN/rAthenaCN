//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//============================================================

moc_para01,36,38,3	script	任务 [11 - 25]#Tuto	857,{
	if (countitem(6219) < 1) {
		mes "- 你必须持有 -";
		mes "- ^4d4dff'乐园团的证物'^000000 -";
		mes "- 才能接这些任务 -";
		close;
	}
	for(set .@i,11124; .@i<11134; set .@i,.@i+1)
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for(set .@i,11114; .@i<11123; set .@i,.@i+1) {
		set .@j, checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes " - 在开始前你必须先选择一个任务- ";
			mes " ";
			mes " - 管理员: 斯派克 - ";
			close;
		}
		else if (.@j >= 0) {
			mes " - 你一次只能接受一个任务 - ";
			mes " ";
			mes " - 管理员: 斯派克 - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - 这儿没有你适合做的任务! - ";
		close;
	}
	if (BaseLevel > 25) {
		mes " - 这些任务对你来说太简单了 - ";
		mes " - 你应该去挑战难度更高的任务 - ";
		close;
	}
	mes " - 任务布告栏 -";
	mes " - 11-25级 -";
	mes " ";
	mes " - 管理员: 斯派克 -";
	next;
	switch(select("狩猎蜂兵:狩猎秃鹰:收集蝗虫后腿:狩猎森灵:狩猎魔菇:消灭害虫:狩猎摩卡:狩猎吸血蝙蝠:收集羽毛:收集毒魔菇芽孢")) {
		case 1: callsub L_Quest,11114,"狩猎蜂兵"," - 狩猎10只 ^4d4dff'蜂兵'^000000 - "," - 请往普隆德拉的西南方向寻找. - ";
		case 2: callsub L_Quest,11115,"狩猎秃鹰"," - 狩猎20只 ^4d4dff'秃鹰'^000000 - "," - 它们出没于梦罗克的南面. - ";
		case 3: callsub L_Quest,11116,"收集蝗虫后腿"," - 狩猎10只 ^4d4dff'蝗虫'^000000 - "," - 请往普隆德拉的西南方向寻找 - "," - 并且带回10个^4d4dff蝗虫后腿^000000.- ";
		case 4: callsub L_Quest,11117,"狩猎森灵"," - 狩猎20只 ^4d4dff'森灵'^000000 - "," - 它们出没于斐扬东南方向.- ";
		case 5: callsub L_Quest,11118,"狩猎魔菇"," - 狩猎30只 ^4d4dff'魔菇'^000000 - "," - 你能在斐扬周边找到它们.- ";
		case 6: callsub L_Quest,11119,"消灭害虫"," - 消灭20枚 ^4d4dff盗虫卵^000000 - "," - 它们生存在普隆德拉的下水道中. - "," - 并且带回10个^4d4dff蛹壳^000000. - ";
		case 7: callsub L_Quest,11120,"狩猎摩卡"," - 狩猎20只 ^4d4dff'摩卡'^000000 - "," - 请往普隆德拉南面寻找它们的踪??- ";
		case 8: callsub L_Quest,11121,"狩猎吸血蝙蝠"," - 狩猎20只 ^4d4dff吸血蝙蝠^000000 - "," - 它们出没于普隆德拉的西边. - "," - 在地下水道里能找到哦. - ";
		case 9: callsub L_Quest,11122,"收集羽毛"," - 狩猎30只 ^4d4dff'大嘴鸟'^000000 - "," - 去普隆德拉的南边寻找它们吧. - ";
		case 10: callsub L_Quest,11123,"收集毒魔菇芽孢"," - 狩猎30只 ^4d4dff毒魔菇^000000 - "," - 去吉芬东北吧,那里能找到你想要的. - "," - 记得带回5个^4d4dff毒魔菇芽孢^000000哦. - ";
		case 11: mes " - 取消 - "; close;
	}
	end;
L_Quest:
	if (checkquest(getarg(0),PLAYTIME) == 1) {
		mes " - 你今天已经完成过^4d4dff"+getarg(1)+"^000000任务了! - ";
		mes " ";
		mes " - 管理员: 斯派克 - ";
		close;
	}
	mes " - 任务 '"+getarg(1)+"' - ";
	for(set .@i,2; .@i<getargcount(); set .@i,.@i+1)
		mes getarg(.@i);
	mes " ";
	mes " - 管理员: 斯派克 - ";
	next;
	if(select("接受任务.:查看其他任务.") == 2) {
		mes " - 让我们来看看其他任务吧 -";
		close;
	}
	for(set .@i,2; .@i<getargcount(); set .@i,.@i+1)
		mes getarg(.@i);
	if (getarg(0) == 11123 && checkquest(11123,PLAYTIME) == 2)
		erasequest 11133;	// Special case for "Collect Poison Spores"
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	斯派克	914,{
	for(set .@i,11114; .@i<11124; set .@i,.@i+1) {
		set .@j, checkquest(.@i,HUNTING);
		if (.@j == 2) switch(.@i) {
			case 11114: callsub L_Quest,.@i,"狩猎蜂兵",900;
			case 11115: callsub L_Quest,.@i,"狩猎秃鹰",1800;
			case 11116: callsub L_Quest,.@i,"收集蝗虫后腿",1400,940,10;
			case 11117: callsub L_Quest,.@i,"狩猎森灵",2600;
			case 11118: callsub L_Quest,.@i,"狩猎魔菇",3900;
			case 11119: callsub L_Quest,.@i,"消灭害虫",2900,915,10;
			case 11120: callsub L_Quest,.@i,"狩猎摩卡",3200;
			case 11121: callsub L_Quest,.@i,"狩猎吸血蝙蝠",3200;
			case 11122: callsub L_Quest,.@i,"收集羽毛",5400;
			case 11123: callsub L_Quest,.@i,"收集毒魔菇芽孢",6000,7033,5;
		}
		else if (.@j >= 0) {
			mes "[斯派克]";
			mes "你还没有完成你的任务呢~";
			mes "加油吧~就快完成了~你可以的!~";
			close;
		}
	}
	mes "[斯派克]";
	mes "想找个工作么?";
	mes "我们这儿总是能提供各种机会的.";
	next;
	mes "[斯派克]";
	mes "你怎么不去周围看看有没有你感兴趣的任务呢?";
	close;
L_Quest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		if (getarg(3,0))
			if (countitem(getarg(3)) < getarg(4)) {
				mes "[斯派克]";
				mes "你还没有足够的 "+getitemname(getarg(3))+".";
				mes "你还必须带上 "+getarg(4)+" "+getitemname(getarg(3))+".";
				close;
			}
		mes "[斯派克]";
		mes "嗯?!Ｈ梦依纯纯~~";
		mes "啊哈!你已经完成了 '"+getarg(1)+"' 任务了呀!~";
		next;
		mes "[斯派克]";
		mes "干得好!!";
		mes "我们的委托人对你的工作会满意的,他会给你应得的奖励的.";
		mes "希望下次能继续合作,谢谢!";
		if (getarg(3,0)) delitem getarg(3),getarg(4);
		erasequest getarg(0);
		setquest getarg(0)+10;
		getexp getarg(2),0;
		close;
	}
}
