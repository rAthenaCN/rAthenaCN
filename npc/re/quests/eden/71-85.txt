//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Eden Group Quests - Mission [71 - 85]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 71 - 85.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official scripts. [Euphy]
//============================================================

moc_para01,44,38,3	script	任务 [71 - 85]	857,{
	if (countitem(6219) < 1) {
		mes "- 只有成为伊甸园的 -";
		mes "- 成员后才能接受 -";
		mes "- 布告栏中的任务. -";
		close;
	}

	// Clear the original quest data (no longer used).
	if (slv_quest) set slv_quest,0;
	if (checkquest(10102) > -1 || checkquest(10103) > -1 || checkquest(10104) > -1 || checkquest(10105) > -1 || checkquest(10106) > -1) {
		for(set .@quest,10102; .@quest<=10106; set .@quest,.@quest+1) {
			if (checkquest(.@quest) > -1)
				erasequest .@quest;
		}
		mes "- 由于客户端移动到 -";
		mes "- 另一个地方, -";
		mes "- 申请表格已被删除. -";
		next;
	}	

	if (BaseLevel < 71) {
		mes "- 只有在会员达到 -";
		mes "- 需求的级别时, 才可以 -";
		mes "- 浏览这块告示牌. -";
		mes "- 去找合适自己等级 -";
		mes "- 的任务告示牌吧. -";
		close;
	} else if (BaseLevel > 85) {
		// Quest IDs: 10107-10123, 5055-5057
		setarray .@quests[0],
			10107,10108,10109,
			10110,10111,10112,10113,
			10114,10115,10116,10117,
			10118,10119,10120,
			10121,10122,10123,5055,5056;
		for(set .@i,0; .@i<getarraysize(.@quests); set .@i,.@i+1) {
			if (checkquest(.@quests[.@i],HUNTING) == 2)
				set .@complete[getarraysize(.@complete)], .@quests[.@i];
		}
		if (checkquest(5057) > -1 && countitem(7187) >= 30)
			set .@complete[getarraysize(.@complete)],5057;
		if (getarraysize(.@complete)) {
			mes "您还有进行中的任务.";
			mes "您想把它们转入进来吗?";
			next;
			if(select("- 当然.:- 不.") == 2)
				close;
			for(set .@i,0; .@i<getarraysize(.@complete); set .@i,.@i+1)
				callsub L_Quest,.@complete[.@i];
			close;
		}
		mes "- 只有在会员达到 -";
		mes "- 需求的级别时, 才可以 -";
		mes "- 浏览这块告示牌. -";
		mes "- 去找合适自己等级 -";
		mes "- 的任务告示牌吧. -";
		close;
	}
	mes "- 选择您想去狩猎 -";
	mes "- 的地点. -";
	next;
	switch (select("斯芬克斯密穴:克雷斯特汉姆古城:朱诺地区:艾尔帕兰钟塔:本地群岛")) {
	case 1:
		mes "- 告示牌上写着 -";
		mes "- 猎杀一些 -";
		mes "- 斯芬克斯密穴中的魔物。 -";
		next;
		set .@str1$,"梦罗克的村民担心在斯芬克斯密穴出现越来越多的怪物. ";
		set .@str2$,"- 梅蒂来自梦罗克 -";
		switch(select("猎杀诺可伊:猎杀马尔杜克:猎杀帕莎纳")) {
			case 1: callsub L_Quest,10107,.@str1$+"猎杀10个诺可伊.",.@str2$;
			case 2: callsub L_Quest,10108,.@str1$+"猎杀10个马尔杜克.",.@str2$;
			case 3: callsub L_Quest,10109,.@str1$+"猎杀10个帕莎纳.",.@str2$;
		}
		case 2:
		mes "- 告示牌上写着 -";
		mes "- 猎杀一些 -";
		mes "- 克雷斯特汉姆古城中的魔物。 -";
		next;
		set .@str1$,"我很害怕克雷斯特汉姆古城的怪物会攻击我们. ";
		set .@str2$,"- 安塞莉亚来自吉芬 -";
		switch(select("猎杀镜妖:猎杀神官:猎杀恶灵:猎杀幽灵弓箭手")) {
			case 1: callsub L_Quest,10110,.@str1$+"猎杀10个镜妖.",.@str2$;
			case 2: callsub L_Quest,10111,.@str1$+"猎杀10个神官.",.@str2$;
			case 3: callsub L_Quest,10112,.@str1$+"猎杀10个恶灵.",.@str2$;
			case 4: callsub L_Quest,10113,.@str1$+"猎杀10个幽灵弓箭手.",.@str2$;
		}
		case 3:
		mes "- 告示牌上写着 -";
		mes "- 猎杀一些 -";
		mes "- 朱诺附近的魔物。 -";
		next;
		set .@str1$,"我们这里的人都不擅于战斗, 大量怪物的存在使得我们都不敢出行了. ";
		set .@str2$,"- 金拜, 旅行者 -";
		switch(select("猎杀七彩大嘴鸟:猎杀沙妖:猎杀魔羌:猎杀鸟人哈比")) {
			case 1: callsub L_Quest,10114,.@str1$+"猎杀20个七彩大嘴鸟.",.@str2$;
			case 2: callsub L_Quest,10115,.@str1$+"猎杀20个沙妖.",.@str2$;
			case 3: callsub L_Quest,10116,.@str1$+"猎杀20个魔羌.",.@str2$;
			case 4: callsub L_Quest,10117,.@str1$+"猎杀20个鸟人哈比.",.@str2$;
		}
		case 4:
		mes "- 告示牌上写着 -";
		mes "- 猎杀一些 -";
		mes "- 艾尔帕兰钟塔里的魔物 -";
		next;
		set .@str1$,"我常去爱艾尔帕兰钟塔听甜美的旋律, 但现在再也听不到了, 因为怪物已经完全占据了那里. ";
		set .@str2$,"- 蓝赛特, 喜爱钟塔协会主席 -";
		switch(select("猎杀古钟魔:猎杀朽魔:猎杀飞行魔书")) {
			case 1: callsub L_Quest,10118,.@str1$+"猎杀15个古钟魔.",.@str2$;
			case 2: callsub L_Quest,10119,.@str1$+"猎杀15个朽魔.",.@str2$;
			case 3: callsub L_Quest,10120,.@str1$+"猎杀15个飞行魔书.",.@str2$;
		}
		case 5:
		mes "- 告示牌上写着 -";
		mes "- 猎杀一些 -";
		mes "- 本地群岛周围的魔物 -";
		next;
		set .@str1$,"我想要去冒险, 但有这么多的事情. 我必须处理, 如果我不能, 别人做不到的! ";
		set .@str2$,"- 蓬皮, 谁想要去冒险 -";
		switch(select("猎杀树林女巫:猎杀米雅毕:猎杀米糕:消灭无头骡:消灭塔噜安:收集到庆典面具")) {
			case 1: callsub L_Quest,10121,.@str1$+"猎杀15个树林女巫!",.@str2$;
			case 2: callsub L_Quest,10122,.@str1$+"猎杀15个米雅毕!",.@str2$;
			case 3: callsub L_Quest,10123,.@str1$+"猎杀15个米糕!",.@str2$;
			case 4: callsub L_Quest,5055,"消灭无头骡15只后, 到^0000FF乐园团71-85布告栏^000000报告即可!","- 马里亚纳来自Brasilis -";
			case 5: callsub L_Quest,5056,"消灭塔噜安15只后, 到^0000FF乐园团71-85布告栏^000000报告即可!","- 萨满皮蓬来自从大城 -";
			case 6: callsub L_Quest,5057,"收集到庆典面具30个后, 到^0000FF乐园团71-85布告栏^000000报告即可~","- SaYumMoon来自昆仑 -";
		}
	}
	end;
	
//callsub L_Quest,<quest ID>{,"<description1>","<description2>"};
L_Quest:
	set .@quest, getarg(0);
	switch(.@quest) {
		case 10107: set .@name$,"猎杀诺可伊"; set .@exp,15000; break;
		case 10108: set .@name$,"猎杀马尔杜克"; set .@exp,15000; break;
		case 10109: set .@name$,"猎杀帕莎纳"; set .@exp,15000; break;
		case 10110: set .@name$,"猎杀镜妖"; set .@exp,20000; break;
		case 10111: set .@name$,"猎杀神官"; set .@exp,20000; break;
		case 10112: set .@name$,"猎杀恶灵"; set .@exp,20000; break;
		case 10113: set .@name$,"猎杀幽灵弓箭手"; set .@exp,20000; break;
		case 10114: set .@name$,"猎杀七彩大嘴鸟"; set .@exp,30000; break;
		case 10115: set .@name$,"猎杀沙妖"; set .@exp,30000; break;
		case 10116: set .@name$,"猎杀魔羌"; set .@exp,30000; break;
		case 10117: set .@name$,"猎杀鸟人哈比"; set .@exp,30000; break;
		case 10118: set .@name$,"猎杀古钟魔"; set .@exp,25000; break;
		case 10119: set .@name$,"猎杀朽魔"; set .@exp,25000; break;
		case 10120: set .@name$,"猎杀飞行魔书"; set .@exp,25000; break;
		case 10121: set .@name$,"猎杀树林女巫"; set .@exp,30000; break;
		case 10122: set .@name$,"猎杀米雅毕"; set .@exp,30000; break;
		case 10123: set .@name$,"猎杀米糕"; set .@exp,30000; break;
		case 5055: set .@name$,"消灭无头骡"; set .@exp,30000; break;
		case 5056: set .@name$,"消灭塔噜安"; set .@exp,30000; break;
		case 5057: set .@name$,"收集到庆典面具"; set .@exp,30000; break;
	}
	if (getargcount() == 1) {
		mes "'"+.@name$+"' 完成. 你想提交任务并领取您的奖励?";
		next;
		if(select("提交任务.:取消.") == 1) {
			if (.@quest == 5057) delitem 7187,30; //Festival_Mask
			erasequest .@quest;
			getexp .@exp,0;
			mes "您收到任务完成EXP奖励.";
			next;
			return;
		}
		close;
	} else {
		if (.@quest == 5057) {
			// Quest 5057 is the only collection quest, so separate conditions are unnecessary.
			if (checkquest(5057) == -1)
				set .@hunting,-1;
			else if (countitem(7187) >= 30)
				set .@hunting,2;
		} else
			set .@hunting, checkquest(.@quest,HUNTING);
		if (.@hunting == 0 || .@hunting == 1) {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "等您完成任务后再来这里吧.";
		} else if (.@hunting == 2) {
			mes "您完成了任务.";
			next;
			if(select("提交任务.:取消.") == 1) {
				if (.@quest == 5057) delitem 7187,30; //Festival_Mask
				erasequest .@quest;
				getexp .@exp,0;
				mes "您收到任务完成EXP奖励.";
			}
		}/* else if (.@hunting == 3) {
//			recall_completequest .@quest
			erasequest .@quest;
			mes "您重新领取了任务'"+.@name$+"'.";
		}*/ else {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "您愿意接受这个任务吗?";
			next;
			if(select("是.:否.") == 1) {
				mes "您收到了任务请求 '"+.@name$+"'. 查看任务信息获得更多资料.";
				setquest .@quest;
			}
		}
		close;
	}
}
