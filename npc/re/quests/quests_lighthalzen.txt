//===== Hercules Script =======================================
//= Quest NPCs located in Lighthalzen
//===== By: ==================================================
//= Ziu, Heris (translation)
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= Quests related to Lighthalzen City.
//===== Additional Comments: =================================
//= 1.0 Updated to match the new Izlude Map. [Masao]
//= 1.1 Added Somatology Laboratory F4 quest. [Euphy]
//= 1.2 Added Izlude duplicates. [Euphy]
//============================================================

// Renewal split [Daegaladh]
//============================================================
//izlude,172,73,6	duplicate(Scamp)	恶棍#iz	4_M_YURI
//izlude_a,172,73,6	duplicate(Scamp)	恶棍#iz_a	4_M_YURI
//izlude_b,172,73,6	duplicate(Scamp)	恶棍#iz_b	4_M_YURI
//izlude_c,172,73,6	duplicate(Scamp)	恶棍#iz_c	4_M_YURI
//izlude_d,172,73,6	duplicate(Scamp)	恶棍#iz_d	4_M_YURI

// Somatology Laboratory F4
//============================================================
lighthalzen,303,304,5	script	贾科莫・吉罗拉莫#start	4_M_KHMAN,{
	if (lhz_dun04 == 0) {
		mes "[贾科莫・吉罗拉莫]";
		mes "噢, 多么"+((Sex)?"强壮的男人啊!":"美丽的女人!");
		next;
		mes "[贾科莫・吉罗拉莫]";
		mes ((Sex)?"先生":"女士")+", 请留意 --";
		mes "最近有一个可怕谣言.";
		mes "镇里的所有人都非常害怕.";
		mes "现在最好的方法就是待在家里,";
		mes "把门紧锁.";
		
		// To do: check BaseLevel requirement.
		if (BaseLevel < 90 || hg_tre < 54) close;

		next;
		select("可怕的谣言?");
		mes "[贾科莫・吉罗拉莫]";
		mes "谣言说在雷根贝勒研究所";
		mes "正在进行可怕的秘密实验";
		mes "试验的内容可能和生物";
		mes "相关!";
		next;
		select("什么? 实验?");
		mes "[贾科莫・吉罗拉莫]";
		mes "没错, 生物实验!!";
		mes "在贫民窟的地下实验室, ";
		mes "谣言说就在那里.";
		mes "同时那片区域的居民非常贫穷";
		mes "他们可能收到不公正的待遇";
		mes "被剥夺了很多社会福利.";
		next;
		mes "[贾科莫・吉罗拉莫]";
		mes "谣传不应该被忽视.";
		mes "应该调查这里的生物学实验室.";
		mes "进行的实验";
		mes "是否与外界传闻的一样.";
		mes "这件事真的需要立即";
		mes "开始调查!";
		setquest 5109;
		set lhz_dun04,1;
		close;
	}
	mes "[贾科莫・吉罗拉莫]";
	mes "请调查实验室的最深处";
	mes "我希望能尽快听到你的调查";
	mes "结果.";
	close;
}

// Is this the correct behavior? (limited and conflicting sources)
// - NPC in lhz_dun04 warps into que_lhz. (?)
// - lhz_dun03 warps directly to que_lhz. (?)
function	script	lhz4enter	{
	if (getmapusers("que_lhz") == 0 && mobcount("que_lhz","#controllhz4::OnMyMobDead") < 1) {
		if (getarg(0) == 0) {
			mes "- 有了贾科莫・吉罗拉莫的提醒";
			mes "你忍住呼吸";
			mes "开始穿过被水淹没的";
			mes "电梯. -";
		} else {
			mes "[科研人员]";
			mes "你是谁?";
			mes "................";
			emotion ET_HUK;
			next;
			mes "男子逃跑, 你需要追上他.";
		}
		if (lhz_dun04 > 1) {
			changequest 5110,5109;
			set lhz_dun04,1;
		}
		close2;
		// Check twice because of 'next'.
		if (getmapusers("que_lhz") == 0 && mobcount("que_lhz","#controllhz4::OnMyMobDead") < 1) {
			donpcevent "#lhz4mes1::OnEnable";
			warp "que_lhz",244,52;
		}
		end;
	}
	mes "现在有其他人在里面, 轮到你等着了.";
	close;
}

lhz_dun03,239,77,0	script	#warplhz3lhz4	WARPNPC,1,1,{
OnTouch:
	if (lhz_dun04 == 0) {
		mes "游过这些被淹没的电梯没有任何意义.";
		close;
	} else if (lhz_dun04 < 6) {
		callfunc "lhz4enter",0;
		end;
	}
	warp "lhz_dun04",244,52;
	end;
}
lhz_dun04,245,61,0	warp	#warplhz4lhz3	1,1,lhz_dun03,240,74

// Does this portal exist?
que_lhz,245,61,0	warp	#warplhzqlhz3	1,1,lhz_dun03,240,74

que_lhz,244,52,0	script	#lhz4mes1	-1,2,2,{
OnTouch:
	mes "- 你找到了另外一部电梯";
	mes "已经被拉拽至水面上方. -";
	mes "";
	// Missing dialogue.
	close2;
	donpcevent "#lhz4mes1::OnDisable";
	end;
OnInit:
OnDisable:
	disablenpc "#lhz4mes1";
	end;
OnEnable:
	enablenpc "#lhz4mes1";
	end;
}

lhz_dun04,151,276,3	script	科研人员#lhz4	4_LGTSCIENCE,{
	if (lhz_dun04 == 0) {
		mes "[科研人员]";
		mes "................";
		close;
	} else if (lhz_dun04 < 6) {
		callfunc "lhz4enter",1;
		end;
	} else if (lhz_dun04 == 6) {
		mes "因为这些试验品的关系, 经过再三考虑之后, 必须关闭这个地方.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你已经阻止不了我了, 最后的四个生命体我已经创建出来了!";
		next;
		mes "[沃尔岑夫]";
		mes "嗯, 这个地方将被完全淹没...";
		next;
		mes "[沃尔岑夫]";
		mes "什么情况?";
		next;
		mes "[沃尔岑夫]";
		mes "唔... 该死的高层官员毁了我的实验室, 该死!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿! 沃尔岑夫!!";
		next;
		mes "[沃尔岑夫]";
		mes "恩是? 为了取悦他们! ^FF0080使用不同的标本^000000 进行生化组合试验.";
		next;
		mes "[沃尔岑夫]";
		mes "可能这些 ^00FF00东西^000000 的首领...";
		mes "我无法控制我自己的实验... 这里, 全部都是 ^808000作为诱饵的奴隶们^000000 实验室将被关闭.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿, 沃尔岑夫!! 你口中所说的 ^00FF00首领^000000 ,它是个什么东西?";
		next;
		mes "[沃尔岑夫]";
		mes "...在我的实验室. 我唤醒了它. 我试图把它们都摧毁, 但是这没生效... 你必须出马了...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我怎样才能进入你的实验室?";
		next;
		mes "[沃尔岑夫]";
		mes "现在... 此时!";
		mes "我会协助你.";
		next;
		mes "[沃尔岑夫]";
		mes "真走运, 首领还在沉睡中. 它的实力非常强大并极具破坏性.";
		next;
		mes "[沃尔岑夫]";
		mes "不过还是非常危险. 它和它的同类现在都能自由活动了, 这意味着我们不能再浪费任何一点时间了.";
		set lhz_dun04,7;
		completequest 5111;
		setquest 5113;
		setquest 5114;
		setquest 5115;
		setquest 5116;
		setquest 5117;
		setquest 5118;
		setquest 5119;
		setquest 5120;
		setquest 5121;
		setquest 5122;
		setquest 5123;
		setquest 5124;
		setquest 5125;
		close;
	} else if (lhz_dun04 == 7) {
		mes "[沃尔岑夫]";
		mes "你准备一个人去?";
		mes "你需要一个配置非常优秀的小队才能够从Boss手下活着回来.";
		close;
	}
	end;
}

que_lhz,207,75,3	script	科研人员#ins	4_LGTSCIENCE,3,3,{
OnTouch:
	if (lhz_dun04 == 1) {
		mes "[科研人员]";
		mes "..........";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你是沃尔岑夫教授?";
		emotion ET_HUK,1;
		next;
		mes "[沃尔岑夫]";
		mes "很多人都来这里找我. 不过绝大部分都不是自愿的.";
		mes "你在这里想干嘛?";
		emotion ET_KIK;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我听说这里正在进行生物实验.";
		mes "沃尔岑夫!!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "这些实验...";
		next;
		mes "[沃尔岑夫]";
		mes "我想我知道你想说什么, 你是不是在指责我?";
		mes "我可不想听你唠叨.";
		mes "你最好快点离开这里.";
		emotion ET_KIK;
		next;
		mes "[沃尔岑夫]";
		mes "他似乎已经哭了很长一段时间... 我很忙, 失陪!";
		next;
		mes "沃尔岑夫的双眼充满的泪水, 之后他狂笑起来, 并且跑了.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿! 沃尔岑夫 这...";
		emotion ET_HUK,1;
		changequest 5109,5110;
		set lhz_dun04,2;
		close2;
		warp "que_lhz",96,136;
		end;
	}
	mes "[科研人员]";
	mes "..........";
	close;
}

que_lhz,94,119,3	script	科研人员#ins2	4_LGTSCIENCE,{
	if (lhz_dun04 == 2) {
		mes "[沃尔岑夫]";
		mes "嘿嘿嘿. 你真的执着! 你想从我这里得到什么?";
		emotion ET_PROFUSELY_SWEAT;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "沃尔岑夫... 你自己都坦言它是危险的. 所有这些调查...";
		next;
		mes "[沃尔岑夫]";
		mes "你说的对...呵呵.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你的调查太危险了. 你认为每个人都希望你继续查下去?";
		next;
		mes "[沃尔岑夫]";
		mes "每个人都想成为强大的可以主宰别人的人. 让所有人服从, 得到一切尊重和敬仰. 难道你不想要? 嘿嘿.";
		emotion ET_KIK;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "... ... ...";
		next;
		mes "[沃尔岑夫]";
		mes "如此危险的生命体, 都难免被毁灭... 你看到了吗?. 嘿嘿.";
		next;
		mes "又一次, 沃尔岑夫匆匆逃离.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "噢! 他... 和...";
		set lhz_dun04,3;
		close2;
		warp "que_lhz",148,215;
		end;
	}
	mes "[科研人员]";
	mes "..........";
	close;
}
que_lhz,147,224,3	script	科研人员#ins3	4_LGTSCIENCE,{
	if (lhz_dun04 == 4 || mobcount("que_lhz","#controllhz4::OnMyMobDead") > 0) {
		mes "[沃尔岑夫]";
		mes "沃尔岑夫惊呆了. 整个人失魂落魄.";
		emotion e_turn;
		close;
	} else if (lhz_dun04 == 3) {
		mes "[沃尔岑夫]";
		mes "你真的像一只苍蝇一样烦";
		mes "是不是, 这些是不是就";
		mes "是你想问的一切?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "... ... ...";
		emotion ET_THINK,1;
		next;
		mes "沃尔岑夫再也不想和你说话了, 他变笑边按下了手中的一个控制器. 然后他疯狂了.";
		specialeffect EF_SIGHTRASHER;
		next;
		mes "[沃尔岑夫]";
		mes "你太不走运了, 这里会是你的坟墓.";
		donpcevent "#controllhz4::OnEnable";
		next;
		mes "[沃尔岑夫]";
		mes "你真是个傻瓜.";
		emotion ET_SURPRISE;
		next;
		mes "[沃尔岑夫]";
		mes "看到这些生命体么, 你会被他们包围, 让我们来看看好戏吧.";
		next;
		donpcevent "#controllhz4::OnEnable2";
		progressbar "ffff00",6;
		set lhz_dun04,4;
		next;
		mes "[沃尔岑夫]";
		mes "不!! ...你干了什么!! 你";
		mes "是个疯子!!!";
		emotion ET_HUK;
		next;
		mes "[沃尔岑夫]";
		mes "回去!";
		next;
		mes "首先, 让我们摧毁所有这些实验体!";
		close;
	} else if (lhz_dun04 == 5) {
		mes "沃尔岑夫惊呆了. 整个人失魂落魄.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "*扇他几下耳光* 嘿, 快醒醒!";
		mes "我们搞定它们了!";
		specialeffect EF_STUNATTACK;
		sleep2 1000;
		specialeffect EF_STUNATTACK;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "让我们先离开这里! 有什么地方我们能去的?";
		specialeffect EF_DARKBREATH;
		emotion ET_STARE_ABOUT,1;
		next;
		mes "[沃尔岑夫]";
		mes "这里. 我的实验室... 我的实验室...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "实验室? 我到底在哪?";
		next;
		mes "[沃尔岑夫]";
		mes "回到开始的地方... 这里...";
		next;
		mes "- 沃尔岑夫有气无力的指向12点钟方向. -";
		next;
		mes "我必须去 ^0000FF 沃尔岑夫的实验室^000000...";
		changequest 5110,5111;
		set lhz_dun04,6;
		donpcevent "#controllhz4d::OnEnable";
		close;
	} else if (lhz_dun04 == 6) {
		mes "我必须去 ^0000FF 沃尔岑夫的实验室^000000...";
		close;
	}
	end;
}

-	script	苏醒的生命体#lhz	-1,{
	end;
OnInit:
OnDisable:
	hideonnpc strnpcinfo(0);
	end;
OnEnable:
	hideoffnpc strnpcinfo(0);
	end;
OnEffect1:
	specialeffect EF_SUI_EXPLOSION;
	end;
OnEffect2:
	specialeffect EF_BEGINASURA;
	end;
OnEffect3:
	specialeffect EF_FLASHER;
	end;
}
que_lhz,148,231,0	duplicate(苏醒的生命体#lhz)	苏醒的生命体#0	853
que_lhz,140,231,1	duplicate(苏醒的生命体#lhz)	苏醒的生命体#1	853
que_lhz,140,224,2	duplicate(苏醒的生命体#lhz)	苏醒的生命体#2	853
que_lhz,140,216,3	duplicate(苏醒的生命体#lhz)	苏醒的生命体#3	853
que_lhz,147,216,4	duplicate(苏醒的生命体#lhz)	苏醒的生命体#4	853
que_lhz,155,216,5	duplicate(苏醒的生命体#lhz)	苏醒的生命体#5	853
que_lhz,155,224,6	duplicate(苏醒的生命体#lhz)	苏醒的生命体#6	853
que_lhz,155,231,7	duplicate(苏醒的生命体#lhz)	苏醒的生命体#7	853

-	script	#controllhz4	-1,{
OnInit:
	disablenpc "#controllhz4";
	end;
OnEnable:
	enablenpc "#controllhz4";
	donpcevent "苏醒的生命体#0::OnEnable";
	donpcevent "苏醒的生命体#1::OnEnable";
	donpcevent "苏醒的生命体#2::OnEnable";
	donpcevent "苏醒的生命体#3::OnEnable";
	donpcevent "苏醒的生命体#4::OnEnable";
	donpcevent "苏醒的生命体#5::OnEnable";
	donpcevent "苏醒的生命体#6::OnEnable";
	donpcevent "苏醒的生命体#7::OnEnable";
	end;
OnEnable2:
	initnpctimer;
	end;
OnTimer1000:
	donpcevent "苏醒的生命体#0::OnEffect1";
	end;
OnTimer2000:
	donpcevent "苏醒的生命体#0::OnEffect2";
	donpcevent "苏醒的生命体#7::OnEffect1";
	end;
OnTimer3000:
	donpcevent "苏醒的生命体#7::OnEffect2";
	donpcevent "苏醒的生命体#6::OnEffect1";
	end;
OnTimer4000:
	donpcevent "苏醒的生命体#6::OnEffect2";
	donpcevent "苏醒的生命体#5::OnEffect1";
	end;
OnTimer5000:
	donpcevent "苏醒的生命体#5::OnEffect2";
	donpcevent "苏醒的生命体#4::OnEffect1";
	end;
OnTimer6000:
	donpcevent "苏醒的生命体#4::OnEffect2";
	donpcevent "苏醒的生命体#3::OnEffect1";
	end;
OnTimer7000:
	donpcevent "苏醒的生命体#3::OnEffect2";
	donpcevent "苏醒的生命体#2::OnEffect1";
	end;
OnTimer8000:
	donpcevent "苏醒的生命体#2::OnEffect2";
	donpcevent "苏醒的生命体#1::OnEffect1";
	end;
OnTimer9000:
	donpcevent "苏醒的生命体#1::OnEffect2";
	end;
OnTimer11000:
	donpcevent "苏醒的生命体#0::OnEffect3";
	donpcevent "苏醒的生命体#1::OnEffect3";
	donpcevent "苏醒的生命体#2::OnEffect3";
	donpcevent "苏醒的生命体#3::OnEffect3";
	donpcevent "苏醒的生命体#4::OnEffect3";
	donpcevent "苏醒的生命体#5::OnEffect3";
	donpcevent "苏醒的生命体#6::OnEffect3";
	donpcevent "苏醒的生命体#7::OnEffect3";
	end;
OnTimer14000:
	donpcevent "苏醒的生命体#0::OnDisable";
	donpcevent "苏醒的生命体#1::OnDisable";
	donpcevent "苏醒的生命体#2::OnDisable";
	donpcevent "苏醒的生命体#3::OnDisable";
	donpcevent "苏醒的生命体#4::OnDisable";
	donpcevent "苏醒的生命体#5::OnDisable";
	donpcevent "苏醒的生命体#6::OnDisable";
	donpcevent "苏醒的生命体#7::OnDisable";
	end;
OnTimer16000:
	monster "que_lhz",148,231,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",140,231,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",140,224,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",140,216,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",147,216,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",155,216,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",155,224,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",155,231,"苏醒的生命体",2244,1,"#controllhz4::OnMyMobDead";
	donpcevent "#controllhz4b::OnEnable";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("que_lhz","#controllhz4::OnMyMobDead") < 1) {
		donpcevent "#controllhz4b::OnStop";
		donpcevent "#controllhz4c::OnEnable";
		disablenpc "#controllhz4";
		end;
	}
	end;
}

-	script	#controllhz4b	-1,{
OnInit:
OnDisable:
	disablenpc "#controllhz4b";
	end;
OnEnable:
	enablenpc "#controllhz4b";
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	donpcevent "#controllhz4b::OnDisable";
	end;
OnTimer400000:
	killmonster "que_lhz","#controllhz4::OnMyMobDead";
	mapwarp "que_lhz","lighthalzen",263,200;
	disablenpc "#controllhz4";
	disablenpc "#controllhz4b";
	stopnpctimer;
	end;
}

-	script	#controllhz4c	-1,{
OnInit:
	disablenpc "#controllhz4c";
	end;
OnEnable:
	enablenpc "#controllhz4c";
	initnpctimer;
	end;
OnTimer10000:
	mapannounce "que_lhz","一个微弱的声音: 救命... 求求你... 啊啊... 我不想... 死....",bc_map,0x0080C0;
	donpcevent "#controllhz4d::OnEnable";
	stopnpctimer;
	disablenpc "#controllhz4c";
	end;
}

que_lhz,147,248,0	script	#controllhz4d	-1,6,4,{
OnTouch:
	if (lhz_dun04 == 4) {
		mes "看上去全搞定了.";
		set lhz_dun04,5;
		donpcevent "#controllhz4d::OnDisable";
		close;
	} else if (lhz_dun04 == 6) {
		mes "- 房屋开始倒塌";
		mes "你侧身闪避, 让开从";
		mes "屋顶落下的混凝土. -";
		mes "";
		close2;
		warp "lhz_dun04",148,269;
		donpcevent "#lhz4mes2::OnEnable";
		donpcevent "#controllhz4d::OnDisable";
		end;
	}
	end;
OnInit:
OnDisable:
	disablenpc "#controllhz4d";
	end;
OnEnable:
	enablenpc "#controllhz4d";
	end;
}

lhz_dun04,148,269,0	script	#lhz4mes2	-1,2,2,{
OnTouch:
	if (lhz_dun04 == 6) {
		heal -Hp/2,0; // To do: check HP value.
		mes "- 你被救了, 模糊的听到";
		mes "四周都是忙碌的脚步声";
		mes "你... -";
		close2;
		donpcevent "#lhz4mes2::OnDisable";
		end;
	}
	end;
OnInit:
OnDisable:
	disablenpc "#lhz4mes2";
	end;
OnEnable:
	enablenpc "#lhz4mes2";
	end;
}
