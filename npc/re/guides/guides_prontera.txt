//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Prontera Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Prontera.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guides.
//= 1.1 Navigation system update. [Euphy]
//=     Optimized.
//============================================================ 

prontera,154,187,4	script	向导#01prontera::GuideProntera	8W_SOLDIER,{
	cutin "prt_soldier",2;
	mes "[普隆德拉向导]";
	mes "欢迎来到 ^8B4513普隆德拉^000000,";
	mes "卢恩・米德加兹王国美丽的首都";
	mes "需要向导吗?";
	mes "是否需要为您开启全城的导航服务呢?";
	F_Navi("[普隆德拉向导]");
	next;
	set .@str$,"您还想查询什么地方呢?";
	while (1) {
		switch(select("[ 主要设施 ]:[ 商店 & 小帮手 ]:- 删除小地图的标记:- 结束对话")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 道具店 ]:[ 武器与防具店 ]:[ 铁匠铺 ]:[ 教堂 ]:[ 骑士团 ]:[ 旅馆 ]:[ 酒馆 ]:[ 图书馆 ]:[ 普隆德拉城堡 ]:[ 职业介绍所 ]:[ 市政厅 ]:- 返回上级菜单")) {
				case 1:
					callsub L_Mark, F_Navi("道具店","prontera,134,221","^B9062F");
					mes .@str$;
					viewpoint 1,134,221,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("武器与防具店","prontera,175,220","^0000FF");
					mes .@str$;
					viewpoint 1,175,220,1,0x0A82FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("铁匠铺","prontera,179,184","^FF5400");
					mes .@str$;
					viewpoint 1,179,184,2,0xFFB400;
					next;
					break;
				case 4:
					mes "[普隆德拉向导]";
					mes ""+F_Navi("普隆德拉大教堂","prontera,236,316","^006400")+"是";
					mes "负责服侍转职业务的^006400教堂^000000";
					mes "座落与普隆德拉东北底端的近郊";
					mes "让我为您标记在小地图上吧?";
					mes .@str$;
					viewpoint 1,236,316,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[普隆德拉向导]";
					mes "米德加兹王国首都普隆德拉的"+F_Navi("骑士团","prontera,46,345")+"是";
					mes "负责骑士转职的场所.";
					mes "办事处座落与普隆德拉西北底端的近郊";
					mes .@str$;
					viewpoint 1,46,345,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("旅馆","prontera,203,191","^800080");
					mes .@str$;
					viewpoint 1,204,191,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, "^B9062F酒馆^000000";
					mes "只有"+F_Navi("北方","prontera,208,154","^B9062F")+"酒馆";
					mes "目前在正常营业.";
					mes .@str$;
					viewpoint 1,208,154,6,0xFF0000;
					viewpoint 1,46,67,7,0xFF0000;
					next;
					break;
				case 8:
					mes "[普隆德拉向导]";
					mes "皇家^0000FF图书馆^000000里";
					mes "有着大量有用的资料, 例如怪物百科全书.";
					mes "目前有两个图书馆可以使用,";
					mes "一个坐落于"+F_Navi("东北面","prontera,120,264")+"另一个坐落于 "+F_Navi("西北面","prontera,192,264")+".";
					mes .@str$;
					viewpoint 1,120,264,8,0x0A82FF;
					viewpoint 1,192,264,9,0x0A82FF;
					next;
					break;
				case 9:
					mes "[普隆德拉向导]";
					mes ""+F_Navi("普隆德拉城堡","prontera,156,360","^FF5400")+"在这里的北面,";
					mes "也就是12点钟方向.";
					mes "其后方的连接北部的普隆德拉区域.";
					mes "位置已经标记在小地图上了.";
					mes .@str$;
					viewpoint 1,156,360,10,0xFFB400;
					next;
					break;
				case 10:
					callsub L_Mark, F_Navi("职业介绍所","prontera,133,183","^006400");
					mes .@str$;
					viewpoint 1,133,183,11,0xAAFF00;
					next;
					break;
				case 11:
					mes "[普隆德拉向导]";
					mes "让我标记本地的"+F_Navi("市政厅","prontera,75,91","^7F3300");
					mes "在您的小地图上吧.";
					mes "有一位发型师在那里";
					mes "可以改变你的头发颜色.";
					mes .@str$;
					viewpoint 1,75,91,12,0xD2691E;
					next;
					break;
				case 12:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 伊甸园传送专员 ]:[ 大嘴鸟管理员 ]:[ 巨兽管理员 ]:[ 魔导机甲管理员 ]:[ 大嘴鸟回收人员 ]:[ 符文销售人员 ]:[ 佣兵工会管理员 ]:[ 催眠术师 ]:[ 卡普拉职员 ]:[ 雷亚布连 ]:[ 实习工匠 ]:[ 宠物美容师 ]:- 返回上级菜单")) {
				case 1:
					callsub L_Mark, F_Navi("伊甸园传送专员","prontera,124,76","^800080"),1;
					mes .@str$;
					viewpoint 1,124,76,13,0xDA70D6;
					next;
					break;
				case 2:
					mes "[普隆德拉向导]";
					mes "共有2名 ^0000FF大嘴鸟管理员^000000,";
					mes "1名是服务于^0000FF骑士^000000的,在"+F_Navi("西北面","prontera,55,350","^0000FF");
					mes "另1名是服务于^0000FF十字军^000000的,在"+F_Navi("东北面","prontera,232,310","^0000FF")+".";
					viewpoint 1,55,350,14,0x0A82FF;
					viewpoint 1,232,310,15,0x0A82FF;
					next;
					break;
				case 3:
					mes "[普隆德拉向导]";
					mes ""+F_Navi("巨兽管理员","prontera,131,213","^FF5400")+"是服务于";
					mes "^FF5400符文骑士^000000 和 ^FF5400皇家卫士^000000 的.";
					mes "位置已经标记在";
					mes "小地图上了.";
					mes .@str$;
					viewpoint 1,131,213,16,0xFFB400;
					next;
					break;
				case 4:
					mes "[普隆德拉向导]";
					mes ""+F_Navi("魔导机甲管理员","prontera,163,178","^006400");
					mes "是为";
					mes "^006400机匠^000000提供服务的.";
					mes "位置已经标记在";
					mes "小地图上了.";
					mes .@str$;
					viewpoint 1,163,178,17,0xAAFF00;
					next;
					break;
				case 5:
					mes "[普隆德拉向导]";
					mes "如果您的大嘴鸟";
					mes "不愿离开您的话,";
					mes "您可以找 "+F_Navi("大嘴鸟回收人员","prontera,125,208","^7F3300");
					mes "他就在中央喷泉附近.";
					mes .@str$;
					viewpoint 1,125,208,18,0xD2691E;
					next;
					break;
				case 6:
					mes "[普隆德拉向导]";
					mes "符文商人, 忍者商店, 和其它";
					mes F_Navi("贸易商人","prontera,178,244","^800080")+" 已经";
					mes "标记在您的小地图上了.";
					mes .@str$;
					viewpoint 1,178,244,19,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("佣兵工会管理员","prontera,35,337","^0000FF"),1;
					mes .@str$;
					viewpoint 1,35,337,20,0x0A82FF;
					next;
					break;
				case 8:
					mes "[普隆德拉向导]";
					mes "普隆德拉的 ^FF5400催眠术师^000000";
					mes "帮助初出茅庐的冒险家们";
					mes "重置他们的技能.";
					mes "他已经搬到依斯鲁得岛, 普隆德拉东南面的卫星城市.";
					mes "不过首都的"+F_Navi("孙燕姿","prontera,147,91","^0000FF")+"也同样具备催眠技能.";
					mes .@str$;
					viewpoint 1,147,91,21,0xFFB400;
					next;
					break;
				case 9:
					callsub L_Mark, "^006400卡普拉职员^000000",1;
					mes .@str$;
					viewpoint 1,152,326,22,0xAAFF00;
					viewpoint 1,30,207,23,0xAAFF00;
					viewpoint 1,282,200,24,0xAAFF00;
					viewpoint 1,151,29,25,0xAAFF00;
					viewpoint 1,146,89,26,0xAAFF00;
					next;
					break;
				case 10:
					mes "[普隆德拉向导]";
					mes F_Navi("雷亚布连","prontera,244,169","^0000FF");
					mes "是一名工匠";
					mes "他能够在武器和防具上 ^0000FF打洞^000000,";
					mes "听上去很惊人吧,";
					mes "不过不是所有的装备都可以打洞.";
					mes .@str$;
					viewpoint 1,244,169,27,0x0A82FF;
					next;
					break;
				case 11:
					callsub L_Mark, F_Navi("见习工匠","prontera,165,60","^800080"),1;
					mes .@str$;
					viewpoint 1,165,60,28,0xDA70D6;
					next;
					break;
				case 12:
					callsub L_Mark, F_Navi("宠物美容师","prontera,218,211"),1;
					mes .@str$;
					viewpoint 1,218,211,29,0xFF0000;
					next;
					break;
				case 13:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[普隆德拉向导]";
			mes "好的, 我会抹掉所有在小地图上的标记.";
			mes "还有什么能为您服务的吗?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			viewpoint 2,1,1,14,0xFFFF00;
			viewpoint 2,1,1,15,0xFFFF00;
			viewpoint 2,1,1,16,0xFFFF00;
			viewpoint 2,1,1,17,0xFFFF00;
			viewpoint 2,1,1,18,0xFFFF00;
			viewpoint 2,1,1,19,0xFFFF00;
			viewpoint 2,1,1,20,0xFFFF00;
			//viewpoint 2,1,1,21,0xFFFF00;
			viewpoint 2,1,1,22,0xFFFF00;
			viewpoint 2,1,1,23,0xFFFF00;
			viewpoint 2,1,1,24,0xFFFF00;
			viewpoint 2,1,1,25,0xFFFF00;
			viewpoint 2,1,1,26,0xFFFF00;
			viewpoint 2,1,1,27,0xFFFF00;
			viewpoint 2,1,1,28,0xFFFF00;
			viewpoint 2,1,1,29,0xFFFF00;
			next;
			break;
		case 4:
			mes "[普隆德拉向导]";
			mes "祝您在普隆德拉生活愉快.";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[普隆德拉向导]";
	if (getarg(1,0)) {
		mes "让我为您提供到"+getarg(0);
		mes "的导航吧.";
	} else
		mes "我已经将"+getarg(0);
	mes "标记在您的小地图上了.";
	return;
}
prontera,282,208,2	duplicate(GuideProntera)	向导#02prontera	8W_SOLDIER
prontera,29,200,6	duplicate(GuideProntera)	向导#03prontera	8W_SOLDIER
prontera,160,29,0	duplicate(GuideProntera)	向导#04prontera	8W_SOLDIER
prontera,151,330,4	duplicate(GuideProntera)	向导#05prontera	8W_SOLDIER
