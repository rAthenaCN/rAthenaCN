//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Geffen Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Geffen.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Fixed and optimized. [Euphy]
//============================================================ 

geffen,203,116,4	script	导游#01geffen::GuideGeffen	705,{
	cutin "gef_soldier",2;
	mes "[吉芬导游]";
	mes "欢迎光临 魔法之都^8B4513吉芬^000000,";
	mes "需要为您介绍吗?";
	next;
	while (1) {
		switch(select("[ 主要设施 ]:[ 商店 & 小帮手 ]:删除小地图的标记:结束对话")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 魔法学校 ]:[ 吉芬塔 ]:[ 喷水池 ]:[ 道具店 ]:[ 装备维修及购买 ]:[ 旅馆 ]:[ 酒店 ]:[ 克里斯多夫铁工厂 ]:返回上一选单")) {
				case 1:
					mes "[吉芬导游]";
					mes "^B9062F魔法学校^000000是";
					mes "负责魔法师";
					mes "转职等的工作,";
					mes "位置已经标记在";
					mes "小地图上了,	";
					mes "还需要其它服务吗?";
					viewpoint 1,61,180,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark,"^0000FF吉芬塔";
					viewpoint 1,120,114,1,0x0000FF;
					next;
					break;
				case 3:
					callsub L_Mark,"^FF5400喷水池";
					viewpoint 1,126,48,2,0xFFB400;
					next;
					break;
				case 4:
					callsub L_Mark,"^006400道具店";
					viewpoint 1,44,86,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[吉芬导游]";
					mes "^7F3300装备维修及购买^000000";
					mes "位置已经标记在";
					mes "小地图上了,";
					mes "还需要其它服务吗?";
					viewpoint 1,99,140,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark,"^800080旅馆";
					viewpoint 1,172,174,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark,"^B9062F酒店";
					viewpoint 1,138,138,6,0xFF0000;
					next;
					break;
				case 8:
					mes "[吉芬导游]";
					mes "^0000FF克里斯多夫铁工厂^000000是";
					mes "出售各种精炼";
					mes "道具的地方,";
					mes "据说以前那里曾是铁匠工会.";
					viewpoint 1,182,59,6,0x0000FF;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 伊甸园传送师 ]:[ 卡普拉职员 ]:[ 认养宠物商人 ]:[ 料理材料商 ]:[ 异次元术士解说员 ]:返回上一选单")) {
				case 1:
					callsub L_Mark,"^B9062F伊甸园传送师";
					viewpoint 1,61,180,7,0xFF0000;
					viewpoint 1,132,66,8,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark,"^FF5400卡普拉职员";
					viewpoint 1,203,123,10,0xFFB400;
					viewpoint 1,120,62,11,0xFFB400;
					next;
					break;
				case 3:
					callsub L_Mark,"^006400认养宠物商人";
					viewpoint 1,193,152,12,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark,"^7F3300料理材料商";
					viewpoint 1,196,111,13,0xD2691E;
					next;
					break;
				case 5:
					callsub L_Mark,"^800080异次元术士解说员";
					viewpoint 1,140,196,14,0xDA70D6;
					next;
					break;
				case 6:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[吉芬导游]";
			mes "标记已删除,";
			mes "还需要其它服务吗?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			viewpoint 2,1,1,14,0xFFFF00;
			next;
			break;
		case 4:
			mes "[吉芬导游]";
			mes "祝你旅途愉快!";
			close2;
			cutin "gef_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[吉芬导游]";
	mes "让我在你的小地图上标记一下";
	mes getarg(0)+"^000000";
	mes "的位置.";
	mes "你还需要确认其他建筑物的位置吗?";
	return;
}

geffen,118,62,0	duplicate(GuideGeffen)	导游#02geffen	705
geffen,36,123,6	duplicate(GuideGeffen)	导游#03geffen	705
geffen,123,203,4	duplicate(GuideGeffen)	导游#04geffen	705
