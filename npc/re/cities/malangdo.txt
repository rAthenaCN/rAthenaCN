//============================================================
//= rAthenaCN [城镇人员]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== = ======================================
//= Malangdo Town
//===== By: ==================================================
//= Muad_Dib
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Malangdo Town Script
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Added entrance scripts.
//= 1.2 Moved "Odgnalam" coordinates.
//= 1.3 Fixed dialogues.
//= 1.4 Dialogue edits, source iRO.
//= 1.5 Added Izlude duplicates.
//============================================================

// Malangdo Transportation :: malangdo_go
//============================================================
-	script	Dolangmal	-1,{
	if (checkquest(5091) == -1) {
		mes "[喵绵绵]";
		mes "嘿~! 人类你好呀~ 我是喵绵绵. 哇卡卡~";
		next;
		mes "[喵绵绵]";
		mes "我被派在这里是为了带你们去我们猫的天堂, ^A2314B绵绵岛^000000.";
		next;
		mes "[喵绵绵]";
		set .@s$,"你为什么不去 ^A2314B绵绵岛^000000?";
		switch(atoi(strnpcinfo(2))) {
			case 1: mes "为什么呢? 普隆德拉虽说不错, 但是太拥挤了. 你为什么不去我们的猫的天堂, ^A2314B绵绵岛^000000玩玩呢?"; break;
			case 2: mes "你为什么不去 ^A2314B绵绵岛^000000? 那里可比吉芬棒多了, 随处可见诚实的向导们."; break;
			case 3: mes "斐扬的空气是不错, 但你为什么不去我们猫的天堂, ^A2314B绵绵岛^000000 看看呢?"; break;
			case 4: mes .@s$+"那里可比阿尔帕兰美丽清净多了, 这里如此嘈杂, 还有时钟滴答滴答的声音."; break;
			case 5: mes .@s$+"那里可比梦罗克美丽清净多了, 这里到处都是沙尘暴, 而且热的要命."; break;
			case 6: mes .@s$+"那里可比朱诺美丽清净多了, 这里到处都是让人看了头疼的书."; break;
			case 7: mes .@s$+"那里的地面可比拉赫平整多了, 这里也太冷清了."; break;
			case 8: mes .@s$+"那里可比这安全多了, 这里到处充满危险."; break;
			case 9: mes .@s$+"那里有着海滩和阳光, 莫拉怎么能和那里比, 到处充满着青草的香味."; break;
		}
		next;
		if(select("- 不去:- ^A2314B绵绵岛^000000~ Go~ Go~") == 1) {
			mes "[喵绵绵]";
			mes "什么!? 你不想去 ^A2314B绵绵岛^000000? Chet!";
			next;
			mes "[喵绵绵]";
			mes "你是这样一个讨厌的家伙. 如果你不去, 那你为什么要对我说话...但...";
			next;
			mes "[喵绵绵]";
			mes "^9E1837海军上将托马斯^000000 说绑架人类...不..把他们有礼貌地邀请?";
			next;
			mes "[喵绵绵]";
			mes "无论如何, 我已经通知你了. 你什么时候想去就来找我吧.";
			close;
		}
		mes "[喵绵绵]";
		mes "好的! 我喜欢你的态度, 充满激情.";
		next;
		mes "[喵绵绵]";
		mes "现在, 在去 ^A2314B绵绵岛^000000 之前, 让我先给你一些建议.";
		next;
		mes "[喵绵绵]";
		mes "第一!! 这是很自然的, 当你到达 ^A2314B绵绵岛^000000, 你应该通知 ^9E1837海军上将托马斯^000000.";
		next;
		mes "[喵绵绵]";
		mes "如果你不这样做的话, 当你走过黑暗的街道时你可能会被可怕野生猫科动物袭击.";
		next;
		mes "[喵绵绵]";
		mes "第二!! ^A2314B绵绵岛^000000 的主人永远是我们猫, 即使我们开放给人类.";
		next;
		mes "[喵绵绵]";
		mes "我们很有个性... 不... 不... 尊重我们猫的信仰.";
		next;
		mes "[喵绵绵]";
		mes "还有下一条是...";
		next;
		mes "喵绵绵开始滔滔不绝, 并给了数百条警告.";
		next;
		mes "[喵绵绵]";
		mes "好了! 这是所有的注意事项!! 很简单吧... 是伐? 你能记住吗?";
		next;
		switch(select("- 记得他的忠告:- 完全记不住")) {
		case 1:
			mes "[喵绵绵]";
			mes "好了! 别想一口吃掉两只老鼠. ^9E1837海军上将托马斯^000000 位于 ^A2314B绵绵岛中心的船上^000000.";
			next;
			mes "[喵绵绵]";
			mes "你到了不要忘了通知他.";
			next;
			mes "[喵绵绵]";
			mes "好了. 让我们出发去猫的天堂, ^A2314B绵绵岛^000000~";
			setquest 5091;
			completequest 5091;
			warp "malangdo",217,85;
			close;
		case 2:
			mes "[喵绵绵]";
			mes "神马!? 呢么简单的规定你都记不住? Chet!";
			next;
			mes "[喵绵绵]";
			mes "你是这样一个讨厌的家伙. 如果你不能记住这些, 在我们的谈话的时候为什么你不告诉我? 我什么都教你了......但..";
			next;
			mes "[喵绵绵]";
			mes "^9E1837海军上将托马斯^000000 说绑架人类...不..要对他们有礼貌?";
			next;
			mes "[喵绵绵]";
			mes "无论如何, 我已经通知你了. 你什么时候想去就来找我吧.";
			close;
		}
	}
	mes "[喵绵绵]";
	mes "唔? 你就是上次那个家伙...? 好吧. 你去参观了 ^A2314B绵绵岛^000000?";
	next;
	switch(select("- 再把我送去 ^A2314B绵绵岛^000000:- 我去过了")) {
	case 1:
		mes "[喵绵绵]";
		mes "我真的很想送你去, 但是我不能违反规定. 如果已经去过 ^A2314B绵绵岛^000000 的人, 再次送去是很难的.";
		next;
		mes "[喵绵绵]";
		mes "我说的是大概情况...那是规则第53条...";
		next;
		mes "[喵绵绵]";
		mes "其实, 让我告诉你第53条的内容. 我是一个随意的猫.";
		next;
		mes "[喵绵绵]";
		mes "如果你想回到 ^A2314B绵绵岛^000000, 可以从伊斯鲁德或者阿尔贝塔坐船.";
		next;
		mes "[喵绵绵]";
		mes "在你享受一段舒适的海上旅行后, 你就能到达猫的天堂, ^A2314B绵绵岛^000000.";
		next;
		mes "[喵绵绵]";
		mes "所以很容易! 难道不是吗? 好吧, 再见~";
		close;
	case 2:
		mes "[喵绵绵]";
		mes "怎么样? 你喜欢吗? 我就知道. 嘎嘎嘎~";
		next;
		mes "[喵绵绵]";
		mes "啊啊! 我只是想确认下...";
		next;
		mes "[喵绵绵]";
		mes "再次提醒, 如果你想回 ^A2314B绵绵岛^000000, 你知道在伊斯鲁德和阿尔贝塔哪里可以坐到船吗?";
		next;
		mes "[喵绵绵]";
		mes "唔~ 看上去你是个聪明人, 应该知道. 好吧, 再见~";
		close;
	}
}
prontera,114,75,6	duplicate(Dolangmal)	喵绵绵#1	4_CAT_DOWN
geffen,109,61,6	duplicate(Dolangmal)	喵绵绵#2	4_CAT_DOWN
payon,200,106,4	duplicate(Dolangmal)	喵绵绵#3	4_CAT_DOWN
aldebaran,170,104,4	duplicate(Dolangmal)	喵绵绵#4	4_CAT_DOWN
morocc,168,275,4	duplicate(Dolangmal)	喵绵绵#5	4_CAT_DOWN
yuno,165,122,4	duplicate(Dolangmal)	喵绵绵#6	4_CAT_DOWN
rachel,121,126,4	duplicate(Dolangmal)	喵绵绵#7	4_CAT_DOWN
lighthalzen,254,83,5	duplicate(Dolangmal)	喵绵绵#8	4_CAT_DOWN
mora,57,150,4	duplicate(Dolangmal)	喵绵绵#9	4_CAT_DOWN

-	script	::Odgnalam	-1,{
	mes "这只猫在暖和的阳光下睡着了 "+((strnpcinfo(2) == "albe")?"阿尔贝塔":"伊斯鲁德")+" .";
	next;
	if(select("- 让他睡吧.:- 和他说话.") == 1) close;
	mes "[猫奥克]";
	mes "喵? 嗳?";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$,"俄..你真烦人!快给我 ^0000FF1000 Zeny^000000,我马上送你去^A2314B绵绵岛^000000!";
		set .@s2$,"钱呢?没有钱你还想旅行?真为你感到羞愧!";
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"原来你和我们猫猫的关系不错嘛~我会给你点小优惠的,只要 ^0000FF999 Zeny^000000 就好.";
		set .@s2$,"钱不够啊!你一点诚意都没有.";
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"你给过我们猫族不少的帮助,所以让我来给你打个5折吧,给我 ^0000FF500 Zeny^000000 就好.";
		set .@s2$,"你居然连5折的价格都给不起?真不敢相信!";
	} else {
		mes "[猫奥克]";
		mes "您是我们猫岛的英雄!您的丰功伟绩已经传遍了整个猫界~";
		next;
		mes "[猫奥克]";
		mes "我会免费带你去^A2314B绵绵岛^000000,这是给您的特殊待遇!";
		next;
		if(select("带我去绵绵岛.:不想去了.") == 1) {
			mes "[猫奥克]";
			mes "祝你旅途愉快~ 好了,我们出发了!";
			warp "malangdo",217,85;
		}
		close;
	}
	mes "[猫奥克]";
	mes .@s1$;
	next;
	if(select("带我去绵绵岛.:不想去了.") == 2) close;
	if (Zeny < .@price) {
		mes "[猫奥克]";
		mes "钱呢?没有钱你还想旅行?真为你感到羞愧!";
		close;
	}
	set Zeny, Zeny - .@price;
	mes "[猫奥克]";
	mes "现在就出发~~ 走咯!";
	warp "malangdo",217,85;
	close;
}
alberta,200,151,4	duplicate(Odgnalam)	猫奥克#albe	4_CAT_REST
izlude,182,218,4	duplicate(Odgnalam)	猫奥克#iz	4_CAT_REST	// Old coordinates: (182,192)
izlude_a,182,218,4	duplicate(Odgnalam)	猫奥克#iz_a	4_CAT_REST
izlude_b,182,218,4	duplicate(Odgnalam)	猫奥克#iz_b	4_CAT_REST
izlude_c,182,218,4	duplicate(Odgnalam)	猫奥克#iz_c	4_CAT_REST
izlude_d,182,218,4	duplicate(Odgnalam)	猫奥克#iz_d	4_CAT_REST

malangdo,219,86,4	script	猫豆豆#malang	4_CAT_SAILOR2,{
	mes "[猫豆豆]";
	mes "在绵绵岛还快乐吧?";
	mes "我们可以助你返回伊斯鲁德和艾尔贝塔.";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$, "1000 zeny";
		set .@s2$, "祝你在绵绵岛过的愉快~";
		set .@no_money$, "想要免费的话可不行..";
		set .@yes_money$, "旅途愉快!";
		mes "[猫豆豆]";
		mes "搭乘船的费用只要 1000 Zeny,你想去哪儿呢?";
		next;
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"999 zeny";
		set .@s2$, "祝你在绵绵岛好运~";
		set .@no_money$, "尽管你和我们很熟啦,不过还是不能免费的哦.";
		set .@yes_money$, "谢谢啊~祝你旅途愉快!~";
		mes "[猫豆豆]";
		mes "你可以享受我们的特价 999 Zeny,你想去哪里呢?";
		next;
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"500 zeny";
		set .@s2$, "绵绵岛上有好多机会在等着你~!";
		set .@no_money$, "不是吧?!你连 500 Zeny 都没有?我们不能给你免费啦..";
		set .@yes_money$, "旅途愉快~期待您的在此到来~";
		mes "[猫豆豆]";
		mes "Wow~ 你对我们猫族贡献很大~好吧!给你特殊的优惠哦,只要 500 Zeny,你想去哪儿呢?";
		next;
	} else {
		set .@price,0;
		set .@s1$,"给您--我们的大英雄--免费的服务!那是必须的!!";
		set .@s2$, "没有人会拒绝我们的大英雄继续留在绵绵岛..";
		mes "[猫豆豆]";
		mes "啊~~!您是我们猫族的传奇英雄~";
		next;
		mes "[猫豆豆]";
		mes "我们不能向大英雄收钱呐!您的目的地是哪儿呢?";
		next;
	}
	set .@i, select("- 目的地：伊斯鲁德 --- "+.@s1$+":- 目的地：艾尔贝塔 --- "+.@s1$+":- 继续留在绵绵岛.");
	mes "[猫豆豆]";
	if (.@i == 3) {
		mes .@s2$;
		close;
	}
	if (ep13_yong1 > 80) {
		mes "祝您旅途愉快~";
		if (.@i == 1)
			warp "izlude",195,212; // Old coordinates: (194,180)
		else
			warp "alberta",192,150;
		close;
	}
	if (Zeny < .@price) {
		mes .@no_money$;
		close;
	}
	Zeny -= .@price;
	mes .@yes_money$;
	if (.@i == 1)
		warp "izlude",195,212;
	else
		warp "alberta",192,150;
	close;
}

// Generic Malangdo NPCs :: malang_tre
//============================================================
malangdo,147,117,3	script	旅店老板#malang	4_CAT_REST,{
	mes "你看见了一只懒洋洋的猫站在椅子上.";
	mes "你猜那是这里的旅店老板.";
	next;
	switch(select("- 唔..我需要休息一下.:- 呃..我需要在这里储存.:- 离开.")) {
	case 1:
		mes "[旅店老板]";
		mes "嗳?";
		mes "俄..如果你能给我一个^FF00005绵绵岛风味罐头^000000,就能在这个吊床上休息啦.";
		next;
		switch(select("- 给你吧.:- 算了.")) {
		case 1:
			if (countitem(12636) > 4) {
				delitem 12636,5; //Malang_Sp_Can
				percentheal 100,100;
				specialeffect2 EF_HEALSP;
				mes "[旅店老板]";
				mes "现在休息去吧!";
				emotion e_kis;
				next;
				warp "malangdo",140,121;
				close;
			}
			mes "[旅店老板]";
			mes "这儿不是没有慈善..";
			close;
		case 2:
			mes "[旅店老板]";
			mes "等你想休息的时候告诉我.";
			close;
		}
	case 2:
		mes "[旅店老板]";
		mes "已经储存好了.我们应该要保持联系~哈哈~";
		savepoint "malangdo",142,118;
		close;
	case 3:
		mes "[旅店老板]";
		mes "有时候你必须得休息下.";
		next;
		mes "- Wow, 这真是只大懒猫! -";
		close;
	}
	close;
}

malangdo,151,120,3	script	宫廷乐师#mal	4_M_MINSTREL,{
	mes "[宫廷乐师]";
	mes "少年,哪只懒猫弄得我每天都昏昏欲睡的.";
	next;
	mes "[宫廷乐师]";
	mes "他让生活看起来如此简单.";
	close;
}

malangdo,149,120,5	script	漫游舞者#mal	4_F_WANDERER,5,1,{
	mes "[漫游舞者]";
	mes "居住在这里的猫太有活力了!!";
	mes "弄得我整天都在跳舞,这是不是很奇怪?";
	mes "啦哒嘀哒,嘀,嘀哒嘀~!";
	close;
OnTouch:
	switch(rand(5)) {
	case 0:
		soundeffect "ring_of_nibelungen.wav",0;
		break;
	case 1:
		soundeffect "dont_forget_me_not.wav",0;
		break;
	case 2:
		soundeffect "humming.wav",0;
		break;
	case 3:
		soundeffect "assassin_of_sunset.wav",0;
		break;
	case 4: 
		emotion e_ho;
		emotion e_ho,0,"Wandering Minstrel#mal";
		break;
	}
	end;
}

malangdo,216,168,3	script	猫训练者#mal1	4_F_ALCHE_A,0,8,{
	emotion e_awsm;
	mes "[猫训练者]";
	mes "Ooo~ 他们毛茸茸的尾巴和软乎乎的毛~~";
	mes "我受不了了啦~~这里是天堂啦!!!";
	next;
	emotion e_gg;
	mes "[猫训练者]";
	mes "有一种物品,^FF0000猕猴桃果实^000000,可以让绵绵岛的猫咪都变成我滴小奴隶~~~瓦咔咔咔~!";
	next;
	select("^FF0000猕猴桃果实^000000?");
	mes "[猫训练者]";
	mes "这真是稀有的物品!";
	mes "虽然很困难,不过我还是能搞定它的!";
	mes "不过其它可爱的猫咪对它可就无法抗拒啦!";
	next;
	select("我怎么才能获得^FF0000猕猴桃果实^000000?");
	mes "[猫训练者]";
	mes "既然我可以自己勉强弄到它,我为什么要告诉你方法?";
	mes "就算你给我全世界所有的Zeny我都不会告诉你!";
	next;
	emotion e_gg;
	mes "[猫训练者]";
	mes "所有绵绵岛的猫猫都是我的~~!!";
	mes "木哈哈哈哈哈~~!";
	mes "有了足够的^FF0000猕猴桃果实^000000我就是绵绵岛的女王啦!~噢呵呵呵呵!";
	close;
OnTouch:
	emotion e_flash;
	end;
}

malangdo,164,203,3	script	猫训练者#mal2	4_M_YURI,{
	emotion e_awsm;
	mes "[猫训练者]";
	mes "Ooo~ 他们毛茸茸的尾巴和软乎乎的毛~";
	mes "我受不了了啦~~这里是天堂啦!!!";
	next;
	emotion e_gg;
	mes "[猫训练者]";
	mes "这是我的小秘密~";
	mes "知道^FF0000猕猴桃果实^000000不?";
	mes "有了它,我就能让所有绵绵岛的猫咪变成我的小奴隶~~!!";
	mes "木哈哈哈哈!~~~";
	next;
	select("^FF0000猕猴桃果实^000000?");
	mes "[猫训练者]";
	mes "唔..我打赌你不知道这个~";
	mes "这些绵绵岛的猫咪对它可无法抗拒啦!~";
	mes "但是,这东西太稀有了,你别想着能搞到它哦~!";
	next;
	select("我怎么才能获得^FF0000猕猴桃果实^000000?");
	mes "[猫训练者]";
	mes "其实,很简单的,你只要...";
	next;
	mes "[猫训练者]";
	mes "嘿!你差点让我说漏了嘴!";
	next;
	emotion e_gg;
	mes "[猫训练者]";
	mes "总有一天,这些猫咪都会是我的~";
	mes "我将是绵绵岛的国王~~!!噢呵呵呵呵!~~~";
	close;
}
