//============================================================
//= rAthenaCN [城镇人员]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= Port Malaya
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= [Official Conversion]
//= Port Malaya Town Script
//===== Additional Comments: =================================
//= 0.1 Alberta sailors only. [Euphy]
//= 0.2 Adapted from Masao's conversion.
//= 1.0 Added remaining NPCs.
//= 1.1 Added Jeepney script. [DeadlySilence]
//============================================================

// Port Malaya Transportation
//============================================================
// Old coordinates: alberta (237,71)
alberta,196,202,3	script	船员 奥塔玛拉#alberta	4W_SAILOR,{
	mes "[船员 奥塔玛拉]";
	mes "嘿~这儿~";
	mes "10,000 Zeny就能去马来西亚港口,有兴趣么?";
	next;
	switch(select("有关马来西亚港口.:去往马来西亚港口.:取消.")) {
	case 1:
		mes "[船员 奥塔玛拉]";
		mes "马来西亚是一个小村庄,从艾尔贝塔坐船向西南方向航行便能到达.";
		next;
		mes "[船员 奥塔玛拉]";
		mes "之前一阵我都不去马来西亚,因为那里有些问题,不过似乎现在都已经解决了.";
		next;
		mes "[船员 奥塔玛拉]";
		mes "我不是很确定啦,不过那里就像是一个舒适又安逸的公园~";
		next;
		mes "[船员 奥塔玛拉]";
		mes "我差点儿就待在那里再也不回艾尔贝塔了!";
		next;
		mes "[船员 奥塔玛拉]";
		mes "我相信,等你到了那里也会有同样感受的.";
		close;
	case 2:
		if (Zeny < 10000) {
			mes "[船员 奥塔玛拉]";
			mes "噢!天哪!";
			mes "这不能免费啦,你不知道去马来西亚有多远!等你有了10,000 Zeny的时候再来找我吧!";
			close;
		}
		mes "[船员 奥塔玛拉]";
		mes "太好了!";
		mes "方向马来西亚港口~走起!~";
		set Zeny, Zeny - 10000;
		close2;
		warp "malaya",271,55;
		end;
	case 3:
		mes "[船员 奥塔玛拉]";
		mes "冒险家们最近似乎都很忙的样子.好吧,等你有时间的时候再回来找我一起去马来西亚港口吧.";
		close;
	}
}

malaya,276,55,4	script	船员 奥塔玛拉#malaya	4W_SAILOR,{
	if (malaya_hi < 10) {
		mes "[船员 奥塔玛拉]";
		mes "这个小村庄现在依旧繁忙.你已经参观完毕了吗?这儿一直有返回艾尔贝塔的船只.";
		next;
	} else if (malaya_hi < 20) {
		mes "[船员 奥塔玛拉]";
		mes "我记得一位无名的冒险家帮助了村民,使他们放下心来.好了~你想回艾尔贝塔吗?";
		next;
	} else {
		mes "[船员 奥塔玛拉]";
		mes "你想和我一起返回艾尔贝塔吗?";
		next;
	}
	switch(select("回去.:不回.")) {
	case 1:
		mes "[船员 奥塔玛拉]";
		mes "好了!~我们起航返回艾尔贝塔!";
		close2;
		warp "alberta",239,68;
		end;
	case 2:
		mes "[船员 奥塔玛拉]";
		mes "没关系,等你想回去的时候再来找我吧.";
		close;
	}
}

// Generic Port Malaya NPCs
//============================================================
ma_in01,30,94,4	script	旅店老板#ma	4_F_MALAYA,{
	if (malaya_hi < 10) {
		mes "[旅店老板]";
		mes "俄..你是来旅行的?";
		mes "你来得真不是时候..目前这个村庄还有些混乱,我的旅馆一直没有营业.";
		close;
	} else if (malaya_hi < 20) {
		mes "[旅店老板]";
		mes "你就是那个村民们正在谈论的冒险家?!为了表示感谢,我的旅馆为您马上开业!";
		next;
	} else {
		mes "[旅店老板]";
		mes "欢迎光临.";
		mes "这儿是马来西亚最棒的旅店---城中小屋~";
		next;
	}
	mes "[旅店老板]";
	mes "需要在这里休息吗?住宿费用为 5,000 Zeny.";
	next;
	switch(select("储存服务.:住宿--5,000 Zeny.:取消.")) {
	case 1:
		mes "[旅店老板]";
		mes "已经成功储存.下次再见~";
		savepoint "ma_in01",43,98;
		close;
	case 2:
		if (Zeny < 5000) {
			mes "[旅店老板]";
			mes "不好意思,您的钱不够.";
			close;
		}
		mes "[旅店老板]";
		mes "住宿愉快~!";
		close2;
		set Zeny, Zeny - 5000;
		percentheal 100,100;
		warp "ma_in01",43,98;
		end;
	case 3:
		close;
	}
}

-	script	不明生物#i	-1,{
	end;
OnInit:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"01","02","03","04"; break;
		case 5: setarray .@npc$,"05","06","07","08"; break;
		case 9: setarray .@npc$,"09","10","11"; break;
		default: end;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "不明生物#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"02","03","04"; break;
		case 5: setarray .@npc$,"06","07","08"; break;
		case 9: setarray .@npc$,"10","11"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "不明生物#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch:
	if (getcharid(1)) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "不明生物瞥了你一眼,脸红了下后消失了.";
			close;
		}
		if (rand(1,5)%2) {
			switch(atoi(strnpcinfo(2))) {
				case 1: set $ma_name04$,strcharinfo(0); break;
				case 5: set $ma_name05$,strcharinfo(0); break;
				case 9: set $ma_name06$,strcharinfo(0); break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "不明生物瞥了你一眼后消失了.";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "不明生物朝你看看,然后跑开了.";
	close;
}
ma_fild01,74,367,6	duplicate(不明生物#i)	不明生物#01	4_MAL_BUDIDAI,2,2
ma_fild02,282,41,4	duplicate(不明生物#i)	不明生物#05	4_MAL_BUDIDAI,2,2
ma_scene01,195,92,4	duplicate(不明生物#i)	不明生物#09	4_MAL_BUDIDAI,2,2

-	script	不明生物#j	-1,{
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 2: setarray .@npc$,"01","03","04"; break;
		case 3: setarray .@npc$,"01","02","04"; break;
		case 4: setarray .@npc$,"01","02","03"; break;
		case 6: setarray .@npc$,"05","07","08"; break;
		case 7: setarray .@npc$,"05","06","08"; break;
		case 8: setarray .@npc$,"05","06","07"; break;
		case 10: setarray .@npc$,"09","11"; break;
		case 11: setarray .@npc$,"09","10"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "不明生物#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch:
	set .@i, atoi(strnpcinfo(2));
	if (.@i%4 == 2) {
		if (getcharid(0) == getpartyleader(getcharid(1),2))
			set .@pass,1;
	} else if (.@i%4 == 3) {
		if (checkquest(4229,PLAYTIME) > -1 && checkquest(9223,PLAYTIME) > -1 && checkquest(12278,PLAYTIME) > -1)
			set .@pass,1;
	} else if (.@i%4 == 0) {
		if (rand(1,100)%10 == 7)
			set .@pass,1;
	}
	if (.@pass) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "不明生物瞥了你一眼,脸红了下后消失了.";
			close;
		}
		if (rand(1,5)%2) {
			switch(.@i) {
			case 2:
			case 3:
			case 4:
				set $ma_name04$,strcharinfo(0);
				break;
			case 6:
			case 7:
			case 8:
				set $ma_name05$,strcharinfo(0);
				break;
			case 10:
			case 11:
				set $ma_name06$,strcharinfo(0);
				break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "不明生物瞥了你一眼后消失了.";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "不明生物朝你看看,然后跑开了.";
	close;
}
ma_fild01,109,116,4	duplicate(不明生物#j)	不明生物#02	4_MAL_BUDIDAI,2,2
ma_fild01,280,150,6	duplicate(不明生物#j)	不明生物#03	4_MAL_BUDIDAI,2,2
ma_fild01,309,221,6	duplicate(不明生物#j)	不明生物#04	4_MAL_BUDIDAI,2,2
ma_fild02,246,324,4	duplicate(不明生物#j)	不明生物#06	4_MAL_BUDIDAI,2,2
ma_fild02,71,296,6	duplicate(不明生物#j)	不明生物#07	4_MAL_BUDIDAI,2,2
ma_fild02,32,263,4	duplicate(不明生物#j)	不明生物#08	4_MAL_BUDIDAI,2,2
ma_scene01,158,139,4	duplicate(不明生物#j)	不明生物#10	4_MAL_BUDIDAI,2,2
ma_scene01,167,112,6	duplicate(不明生物#j)	不明生物#11	4_MAL_BUDIDAI,2,2

malaya,227,311,4	script	婆婆#ma01	4_F_BARYO_OLD,{
	if (malaya_hi < 10) {
		mes "[婆婆]";
		mes "你不是这儿附近的人吧?自己小心点~!";
		next;
		mes "[婆婆]";
		mes "我的媳妇儿怀孕了,我有点担心,希望别出什么岔子.";
		close;
	} else if (malaya_hi < 20) {
		mes "[婆婆]";
		mes "我听说一个外族人正在帮助我们的村庄.";
		next;
		mes "[婆婆]";
		mes "我想我的媳妇儿会平安的!";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[婆婆]";
		mes "听说"+getd("$ma_name0"+rand(1,6)+"$")+"一直跟随在噩梦护士左右.";
		next;
		mes "[婆婆]";
		mes "我会祝福他们这种禁忌爱恋的.";
		close;
	}
	mes "[婆婆]";
	mes "我很担心我怀孕的媳妇儿.希望噩梦护士不会来伤害她.";
	next;
	select("噩梦护士?");
	mes "[婆婆]";
	mes "你不是附近的村民?即使你是外乡人,在这里也得小心.";
	next;
	mes "[婆婆]";
	mes "噩梦护士在很久以前是一个居住在马来西亚港口的女人,她嫁给了一个体面的男人后不久便怀孕了.";
	next;
	mes "[婆婆]";
	mes "可是,快乐总是短暂的.在一次意外中,她流产了..";
	next;
	mes "[婆婆]";
	mes "她失去孩子后无比悲伤,没过几天便离开了人世.";
	next;
	select("这真是个悲伤的故事.");
	mes "[婆婆]";
	mes "之后,噩梦护士的灵魂一直在村子里徘徊,带走村庄里所有怀孕女人腹中的孩子.";
	next;
	mes "[婆婆]";
	mes "如果夜晚你还在马来西亚港口行走..";
	next;
	mes "[婆婆]";
	mes "听见tik-tik或者wak-wak的声音,那你得小心了,那是噩梦护士在煽动翅膀.";
	close;
}

malaya,189,263,4	script	击鼓的 年轻人#ma02	4_M_BARYO_MAN,{
	if (malaya_hi < 10) {
		emotion ET_HUK;
		mes "[击鼓的 年轻人]";
		mes "呃..这就和以前那时候一样...";
		next;
		emotion ET_HUK;
		mes "[击鼓的 年轻人]";
		mes "当月亮被吞掉的时候..呃!!";
		close;
	} else if (malaya_hi < 20) {
		mes "[击鼓的 年轻人]";
		mes "我记得当时村庄被一位无名的英雄所拯救.";
		next;
		emotion ET_OHNO;
		mes "[击鼓的 年轻人]";
		mes "唔..我觉得食月暴龙又出现了!!";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[击鼓的 年轻人]";
		mes getd("$ma_name0"+rand(1,6)+"$")+"据说曾经阻止了食月暴龙吞下月亮.是不是很可怕?不过我不担心~ ";
		close;
	}
	mes "[击鼓的 年轻人]";
	mes "在马来西亚港口,传说食月暴龙是一只能吞下月亮的怪物!";
	next;
	mes "[击鼓的 年轻人]";
	mes "不过我还知道,食月暴龙还会吞食人类!!";
	next;
	select("靠!那不是很危险?!");
	emotion ET_KIK;
	mes "[击鼓的 年轻人]";
	mes "哈哈哈,不过还是有办法阻止食月暴龙吞食人类的~";
	next;
	mes "[击鼓的 年轻人]";
	mes "即使是已经吞掉了6个月亮的怪物也是有弱点的嘛!~";
	next;
	select("哦?那弱点是什么?");
	mes "[击鼓的 年轻人]";
	mes "当它要吞食月亮的时候,对外界的噪音相当敏感.";
	next;
	mes "[击鼓的 年轻人]";
	mes "这时候你的机会就来了!!不管是锅碗瓢盆还是鼓~无论哪种噪音都可以!!";
	next;
	mes "[击鼓的 年轻人]";
	mes "食月暴龙受到惊吓后便会吐出月亮随即逃走!~";
	next;
	mes "[击鼓的 年轻人]";
	mes "随身带一个鼓吧,很方便的~!";
	close;
}

malaya,270,59,4	script	港口 守卫#ma03	4_MAL_SOLDIER,{
	if (malaya_hi < 10) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[港口 守卫]";
		mes "村子里最近不怎么太平.我就这么离开不好吧?";
		close;
	} else if (malaya_hi < 20) {
		emotion ET_STARE_ABOUT;
		mes "[港口 守卫]";
		mes "现在,村庄里已经安定多了,不过即使这样,夜晚出门的时候也请小心.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[港口 守卫]";
		mes getd("$ma_name0"+rand(1,6)+"$")+"据说曾经被宝箱巨鳄俘获过,不过他逃脱了.他肯定是个厉害角色!";
		close;
	}
	emotion ET_HUK;
	mes "[港口 守卫]";
	mes "什么?!港口解除警报!!";
	next;
	mes "[港口 守卫]";
	mes "咦?你不是队长?靠!吓死我了..好了,我接到了一份有关游客的官方文件.";
	next;
	select("什么文件?");
	mes "[港口 守卫]";
	mes "文件上说,游客夜晚在村庄里活动的时候一定要小心这里的怪物出没.";
	next;
	mes "[港口 守卫]";
	mes "宝箱巨鳄并不会经常在村子里出没,不过它们会将人诱拐进箱子并在夜晚带走他们.";
	next;
	mes "[港口 守卫]";
	mes "俄..还有一种叫食尸鬼的怪物经常会在晚上出现,一定要小心别碰见.";
	next;
	switch(select("我知道了.:有什么办法阻止它们进村么?")) {
	case 1:
		mes "[港口 守卫]";
		mes "晚上的时候要小心!";
		close;
	case 2:
		mes "[港口 守卫]";
		mes "听说,如果你被关在宝箱巨鳄背着的箱子里时,可以趁机在箱子里攻击它的弱点!";
		next;
		mes "[港口 守卫]";
		mes "如果你听见食尸鬼从远处飞翔而来的声音,一定要立刻躲进最近的建筑里去.";
		next;
		emotion ET_SCRATCH;
		mes "[港口 守卫]";
		mes "不过要是听不见声音或者煽动翅膀的声音并不是很大,那你就自求多福,随时保持警惕吧!";
		close;
	}
}

malaya,88,252,4	script	小女孩#ma04	4_F_BARYO_GIRL,{
	if (malaya_hi < 10) {
		mes "[小女孩]";
		mes "我很害怕,不过我还是要去拜访森林里的精灵.";
		close;
	} else if (malaya_hi < 20) {
		mes "[小女孩]";
		mes "呵呵.爸爸说只要村子恢复平静了我就能去森林拜访精灵.";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		mes "[小女孩]";
		mes "不久以前,"+.@name$+"来到这里说了一句有趣的话~";
		next;
		mes "[小女孩]";
		mes "'我在院子里种了一对梨树~'";
		mes " 呵呵,这真有趣~不是么?"+.@name$+"看起来是个很有趣的人~!";
		close;
	}
	mes "[小女孩]";
	mes "爸爸说,只要和森林里的精灵成为朋友就不会得皮肤病了!";
	next;
	select("这是什么精灵?");
	mes "[小女孩]";
	mes "他们是仙女!他们住在森林里的大树或岩石里.仙女有女的,也有男的...";
	next;
	mes "[小女孩]";
	mes "不过,男仙女比较好看哦...为什么呢...";
	close;
}

malaya,219,92,6	script	小孩#ma05	4_M_BARYO_BOY,{
	if (malaya_hi < 10) {
		mes "[小孩]";
		mes "妈妈说外面太危险了,不让我出去玩.为什么呢?";
		close;
	} else if (malaya_hi < 20) {
		mes "[小孩]";
		mes "妈妈只让我在马来西亚港口玩耍.";
		next;
		mes "[小孩]";
		mes "呵呵,不过我从来没想过去马来西亚港口以外的世界玩呢.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[小孩]";
		mes "我听说哦,有人在神秘的村庄里面拿走了食人波利的帽子..";
		next;
		mes "[小孩]";
		mes "为什么会有人要偷怪物的帽子呢?"+getd("$ma_name0"+rand(1,6)+"$")+"肯定要绝望了.";
		next;
		mes "[小孩]";
		mes "哦!!等等.. 这应该是个秘密的.嘘~~~~~你什么都没有听见哦!";
		close;
	}
	mes "[小孩]";
	mes "我想有人偷走了村子里所有的帽子!";
	next;
	mes "[小孩]";
	mes "邻居们的帽子在几天前就已经消失了,我妈昨天刚洗好的帽子也不见了.";
	next;
	mes "[小孩]";
	mes "唔...你觉得会不会是被食人波利拿走了?";
	next;
	mes "[小孩]";
	mes "我的朋友说,他曾经看见食人波利戴着这些消失的帽子...";
	close;
}

malaya,363,283,4	script	当地居民#ma06	4_M_MALAYA,{
	if (malaya_hi < 10) {
		mes "[当地居民]";
		mes "唔...女巫的诅咒又来了!";
		next;
		mes "[当地居民]";
		mes "外乡人,一定要小心噩梦巫师手里的针!";
		close;
	} else if (malaya_hi < 20) {
		mes "[当地居民]";
		mes "欢迎来到马来西亚!";
		next;
		mes "[当地居民]";
		mes "村子里最近有些不安全,如果你见到有谁的眼睛是红色的,切忌不要盯着他们的眼睛看!!";
		close;
	}
	set .@name$, getd("$ma_name0"+rand(1,6)+"$");
	switch(rand(1,6)) {
	case 1:
		mes "[当地居民]";
		mes "夜晚出门的时候一定要小心,听说"+.@name$+"被食尸鬼带走了.";
		close;
	case 2:
		mes "[当地居民]";
		mes .@name$+"据说成功地驯服了一只邪魔马.这家伙一定很勇敢!!真希望我能亲眼见到这只邪魔马呢!";
		close;
	case 3:
		mes "[当地居民]";
		mes "你有见过独眼怪么?听说"+.@name$+"抓捕到了一只.";
		close;
	case 4:
		mes "[当地居民]";
		mes "如果你想去村庄的外面走走,一定要小心恶魔婴儿."+.@name$+"住院了,听说他被恶魔婴儿骗了.";
		close;
	case 5:
	case 6:
		break;
	}
	mes "[当地居民]";
	mes "最近村子里很混乱,看起来噩梦巫师又回来了..";
	next;
	select("噩梦巫师?");
	mes "[当地居民]";
	mes "那怪物是巫师,它们也穿着衣服,看起来就和人一样.";
	next;
	mes "[当地居民]";
	mes "噩梦巫师会把它看见的人制做成娃娃并用针来诅咒.";
	next;
	select("有没有办法可以辨认出这种怪物?");
	mes "[当地居民]";
	mes "有一种办法,所有的噩梦巫师都是红色的眼睛.";
	next;
	mes "[当地居民]";
	mes "你可以通过它们的眼睛颜色辨别出,不过一定要切记一点!";
	next;
	mes "[当地居民]";
	mes "别盯着它们的眼睛看太久!不然你会被噩梦巫师俘获的.";
	next;
	mes "[当地居民]";
	mes "记着!别盯着它们的眼睛看!!";
	close;
}

malaya,41,127,6	script	老人 #ma07	4_M_BARYO_OLD,{
	if (malaya_hi < 10) {
		mes "[老人]";
		mes "我们并不希望有外乡人来,因为这里最近不安全.";
		close;
	} else if (malaya_hi < 20) {
		mes "[老人]";
		mes "现在真不是来我们村庄旅游的时候,我们不希望您前来.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[老人]";
		mes getd("$ma_name0"+rand(1,6)+"$")+"听说在村子里丢掉了杰勒比结晶. ";
		next;
		mes "[老人]";
		mes "啧,啧..肯定是个不值得拥有杰勒比结晶的人!";
		close;
	}
	mes "[老人]";
	mes "如果你要砍树的话还必须注意是不是有噩梦死神住在那棵树上.";
	next;
	mes "[老人]";
	mes "要是使用了噩梦死神居住过的树木上的木头建造房子,会发生奇怪的事情!";
	next;
	select("会发生什么?");
	mes "[老人]";
	mes "对噩梦死神来说,这意味着它们失去了自己的家,你就等着被报复吧!";
	next;
	mes "[老人]";
	mes "当你在屋子里想要入睡的时候,它们会对你恶作剧,让你无法睡着.";
	next;
	mes "[老人]";
	mes "随着时间的推移,复仇升级,他们会坐在你身上直到你没气了.";
	next;
	select("俄..天哪..");
	mes "[老人]";
	mes "哈哈哈!不过通常房梁里面没有噩梦死神的,别担心会失眠.";
	close;
}

malaya,63,185,4	script	女人#ma08	4_F_MALAYA,{
	if (malaya_hi < 10) {
		mes "[女人]";
		mes "我没什么可以和你说的.";
		close;
	} else if (malaya_hi < 20) {
		mes "[女人]";
		mes "我很担心那些孩子们.希望村子里发生的可怕事件别给他们留下阴影.";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		set .@rand, rand(1,10);
		if (.@rand < 5) {
			mes "[女人]";
			mes "我喜欢有趣的语句.不久之前,"+.@name$+"来到这里告诉了我一个有趣的段子.";
			next;
		}
		switch(.@rand) {
		case 1:
			mes "[女人]";
			mes "'我的阿姨踩到了一只蚂蚁.'";
			next;
			break;
		case 2:
			mes "[女人]";
			mes "'你在一个寒冷的日子吃辣椒.'";
			next;
			break;
		case 3:
			mes "[女人]";
			mes "'你为什么往花上撒面粉?'";
			next;
			break;
		case 4:
			mes "[女人]";
			mes "在工具店里, '我想去买针线,你却买了干草把针都藏了进去.'";
			next;
			mes "[女人]";
			mes .@name$+"说完后被扔出了工具店.";
			next;
			break;
		case 5:
			mes "[女人]";
			mes "我的一个卡普拉服务员朋友告诉了我有关一位客人的故事.";
			next;
			mes "[女人]";
			mes "'聪明如卡普拉,她们从不结婚,一旦他们结婚了,就再也不是卡普拉了.'";
			next;
			mes "[女人]";
			mes "呵呵~~我喜欢有趣的故事~不过要合我的口味有点困难~";
			close;
		default:
			mes "[女人]";
			mes "我喜欢有趣的语句,等一下~还有一个~ "+.@name$+", 爱情就像照片,你同意么?";
			next;
			mes "[女人]";
			mes "爱情就像照片,需要黑暗来显影感光.";
			next;
		}
		mes "[女人]";
		mes "呵呵~这不是很有趣嘛~?";
		close;
	}
	mes "[女人]";
	mes "如果你听见从村外的树林传来婴儿的哭声,千万不要靠近那里.";
	next;
	select("为什么?");
	mes "[女人]";
	mes "有一种叫恶魔婴儿的怪物会在村外游荡,它们将自己变身成为啼哭的婴儿吸引行人的注意.";
	next;
	mes "[女人]";
	mes "一旦你抱起了婴儿,它就会立刻现出原形攻击你.";
	next;
	select("听起来真是个卑鄙的怪物.");
	mes "[女人]";
	mes "是的,这种怪物利用旅行者的好心真是太无耻了..但是..";
	next;
	mes "[女人]";
	mes "但是..传闻这种怪物是一个未出生孩子的灵魂转变的..哎..真是太悲伤了..";
	close;
}

// Jeepneys
//============================================================
function	script	F_Malaya_Jeepney	{
	.@mapname$        = getarg(0);
	.@passengers      = getarg(1);

	// set the other messages of varying amount)
	for (.@i = 5; .@i < getargcount(); .@i++) {
		setd(".@msgJeepneyInfo$[" + (.@i - 5) + "]", getarg(.@i));
	}

	if (malaya_hi < 10) {
		mes "[Jeepney Driver]";
		mes getarg(2);
		close;
	} else if ((malaya_hi >= 10) && (malaya_hi < 20)) {
		mes "[Jeepney Driver]";
		mes getarg(3);
		close;
	} else {
		mes "[Jeepney Driver]";
		mes getarg(4);
		next;
	}
	switch(select("Board [Passenger " + getmapusers(.@mapname$) + "/" + .@passengers +"]:Jeepney?:Are there any other Jeepneys?:Ah... Yes...")) {
	case 1:
		if (getmapusers(.@mapname$) >= .@passengers) {
			mes "[Jeepney Driver]";
			mes "I'm afraid the Jeepney is full.";
			mes "I'm sorry but how about some other Jeepney?";
			close;
		} else {
			mes "[Jeepney Driver]";
			mes "Have a nice day.";
			close2;
			warp .@mapname$,29,24;
			end;
		}
	case 2:
		// iterate through all the jeepney information for this specific NPC
		for (.@i = 0; .@i < getarraysize(.@msgJeepneyInfo$); .@i++) {
			mes "[Jeepney Driver]";
			mes getd(".@msgJeepneyInfo$[" + .@i + "]");

			if (.@i < (getarraysize(.@msgJeepneyInfo$) - 1)) {
				next;
			}
		}
		close;
	case 3:
		mes "[Jeepney Driver]";
		mes "Oh! Other Jeepneys are in operation, of course.";
		next;
		mes "[Jeepney Driver]";
		mes "In Port Malaya there are 12, 30 and 60 passenger Jeepneys with 3 each operating.";
		next;
		mes "[Jeepney Driver]";
		mes "Would you like to know the location of other cars?";
		next;
		switch (select("I'd like to know where the 12 passenger car is.:I'd like to know where the 30 passenger car is.:I'd like to know where the 60 passenger car is.")) {
		case 1:
			viewpoint 1, 237, 240, 1, 0xF7E009;
			viewpoint 1, 67 , 44 , 2, 0xF7E009;
			viewpoint 1, 282, 129, 3, 0xF7E009;
			set .@zif_in, 12;
			break;
		case 2:
			viewpoint 1, 134, 250, 4, 0xF7E009;
			viewpoint 1, 341, 153, 5, 0xF7E009;
			viewpoint 1, 293, 290, 6, 0xF7E009;
			set .@zif_in, 30;
			break;
		case 3:
			viewpoint 1, 242, 221, 7, 0xF7E009;
			viewpoint 1, 62 , 245, 8, 0xF7E009;
			viewpoint 1, 257, 58 , 9, 0xF7E009;
			set .@zif_in, 60;
			break;
		}
		mes "[Jeepney Driver]";
		mes .@zif_in+" passenger Jeepney's location has been marked on your map.";
		next;
		break;
	}
	mes "[Jeepney Driver]";
	mes "Have a nice trip.";
	close;
}

malaya,237,240,4	script	Jeepney Driver#01	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif01",
		12,

		// Messages for malaya_hi checks.
		"UUrgghhhh.... I'm scared... so scared... what happened to this place?",
		"The place is still a mess... I wonder if I can operate a jeepney here...",
		"Ha ha ha Welcome. I am ^1561EAVol^000000, operating the 12 man Jeepney here.",

		// Monologues of variable lengths.
		"Is this your first time in Port Malaya?",
		"Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"Since everybody has different tastes, you can see those with cool and elaborate decorations.",
		"Of course you don't need to drive them yourselves, but you can ride them so please feel free to do so."
	);
}

malaya,67,44,4	script	Jeepney Driver#02	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif02",
		12,

		"Wha... What's... Going on in that hospital...",
		"I think the moaning coming from the hospital has lessened...",
		"I'm ^1561EAChui^000000, operating a 12 man Jeepney. Hi there~",

		"Is this your first time in Port Malaya?",
		"Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"I modified my Jeepney myself...",
		"For safety reasons there's a limit to the number of passengers, and it's safe so no worries..."
	);
}

malaya,282,129,4	script	Jeepney Driver#03	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif03",
		12,

		"It's not good for outsiders to have a chat... Perhaps the Mumbaki Leader may have the solution for this.",
		"Are you the adventurer who recently met Mumbaki? No wait... Then you shouldn't be here... Yes it's a ghost... Arghhhh...",
		"The name's ^1561EATop^000000, operating one of the 3 12 man Jeepneys in town.",

		"First time in town?",
		"Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"They all look different depending on their drivers...",
		"Now it is the fame of Port Malaya."
	);
}

malaya,134,250,6	script	Jeepney Driver#04	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif04",
		30,

		"G...Go... Go away...",
		"Arghh... Scared the devil out of me... Go away... Shoo...",
		"^1561EABrav^000000, 30 man Jeepney driver at your service, driving as safely as I can since I get scared easily.",

		"Is this your first time in Port Malaya?",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"The modified cars are decorated by their operators.",
		"You come to Port Malaya, you must ride a Jeepney.",
		"Ha ha ha, of course you can't drive it yourself."
	);
}

malaya,341,153,4	script	Jeepney Driver#05	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif05",
		30,

		"G... Ghosts in town... Hey... Do you have one on you?",
		"I think there are fewer ghosts now... The town is slightly quieter...",
		"Hi I'm ^1561EALivil^000000 operator for the 30 man Jeepney. Safety is my priority.",

		"First time in town?",
		"I'll give you a quick guide as a service.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"For safety reasons, Jeepneys have a limit on the number of passengers.",
		"Some carried 180 people, but there aren't any of those in Port Malaya today."
	);
}

malaya,293,290,6	script	Jeepney Driver#06	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif06",
		30,

		"Gu... Guards... What are the guards doing...",
		"Did the guards finally do something? Phew... What a rush...",
		"I'm ^1561EAGad^000000, the trustworthy 30 man Jeepney driver.",

		"I see you're new to this town.",
		"To guide travelers is also my task!! Let me explain.",
		"Jeepneys were goods transport vehicles, now decorated and modified with Port Malaya's new technology!!",
		"This Jeepney is my very own handiwork. Ha ha ha... It's cool right? Ha ha ha",
		"Even though the customer is king, I can't let you drive it... Ha ha ha"
	);
}

malaya,242,221,4	script	Jeepney Driver#07	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif07",
		60,

		"Oh no... I think the whole village is bewitched by ghosts... Maybe... I should give the Mumbaki Leader a visit...",
		"Ghost... I'm sure the Mumbaki Leader knows how to get rid of these ghosts...",
		"^1561EAHott^000000, at your service, the safest 60 man Jeepney Operator.",

		"Jeepney is the public transportation of Port Malaya.",
		"Therefore they are everywhere. And it's free! I know they all have different passenger limits, but that's no problem because they're everywhere."
	);
}

malaya,62,245,6	script	Jeepney Driver#08	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif08",
		60,

		"Shoo~ Be gone if you're a ghost, and go away even if you're human~ Shoo~",
		"Hey... You... Are human right? Right? Huh? Say you are... Please...",
		"Hullo, I'm ^1561EAMuyan^000000, operator of this 60 man Jeepney. Nice to meet you.",

		"Jeepneys are public transport, so safety is our priority.",
		"My Jeepney received the best class of Port Malaya, a '60 man Jeepney' certificate.",
		"Meaning!! That it is perfectly safe!! Don't you worry about the ride."
	);
}

malaya,257,58,6	script	Jeepney Driver#09	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif09",
		60,

		"...You're an outsider... Don't go touching anything and be careful in town...",
		"The town is still quite dangerous. Best not touch anything until it is safe.",
		"Hi there. I'm ^1561EAGramma^000000, Operator of the 60 man Jeepney of Port Malaya.",

		"Is this your first time in Port Malaya?",
		"We Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"Since everybody have different tastes, you can see those with cool and elaborate decorations.",
		"Of course you don't need to drive them yourselves, but you can ride them so please feel free to do so."
	);
}
