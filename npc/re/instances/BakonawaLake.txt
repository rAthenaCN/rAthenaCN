//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Bakonawa Lake
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//============================================================ 

ma_scene01,174,179,4	script	酸奶	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您携带的物品种类太多, 请稍做处理.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您的负重不足, 请稍做处理.";
		close;
	}
	if (BaseLevel < 140) {
		mes "[酸奶]";
		mes "嘿. 这地方对你来说太危险了. 你会丧命的. 立即离开这里回镇上!";
		close;
	}
	if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
		mes "[酸奶]";
		mes "我们都会丧命在食月暴龙手上的. 啊, 我们必须杀了它, 但不幸的是我们都没有做好准备.";
		close;
	}

	set .@party_id,getcharid(1);
	set .@md_name$, "Bakonawa Lake";

	set .@baku_time, checkquest(12278,PLAYTIME);
	if (.@baku_time == -1) {
		if (checkquest(12279) == -1) setquest 12279;
	}
	if (!.@party_id) {
		mes "[酸奶]";
		mes "怎么你没有其他伙伴, 难道你想一个人去和食月暴龙干架? 组织点人手再来吧.";
		close;
	}
	if (.@baku_time == -1) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[酸奶]";
			mes "你是猎杀暴龙队伍的队长吗? 这里几乎没有任何正常的路可以到那里, 我们会用绳索把你们吊下去, 没问题吧?";
			next;
			set .@i, select("- 请制作吊绳.:- 我现在要下去了.:- 取消.");
		} else {
			mes "[酸奶]";
			mes "你们是来猎杀暴龙的. 队长请出来和我一起编织下吊绳吧.";
			next;
			set .@i, select(":- 我现在要下去了.:- 取消.");
		}
		switch(.@i) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[酸奶]";
				mes "噢, 吊绳断了. 我必须弄条新的.";
				close;
			}
			mes "[酸奶]";
			mes "我现在在整吊绳, 等我完工后你们就可以下去了.";
			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case 3:
				mes "[酸奶]";
				mes "发生未知错误.";
				close;
			case 2:
				mes "[酸奶]";
				mes "吊绳还没做完. 请稍等.";
				close;
			case 1:
				mes "[酸奶]";
				mes "平民不能进入.";
				close;
			case 0:
				mapannounce "ma_scene01","队伍成员, "+strcharinfo(0)+" 属于队伍 "+getpartyname(.@party_id)+" 已经进入副本, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
				setquest 12278;
				//warp "1@ma_b",64,51;
				close;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 0 || .@baku_time == 1) {
		mes "[酸奶]";
		mes "这里唯一能用的交通工具吊绳, 已经断掉了. 我们正在重做并清理附近的战场, 你们请稍候再来.";
		close;
	} else if (.@baku_time == 2) {
		mes "[酸奶]";
		mes "附近全部清扫干净了. 等吊绳做好你们就能够再次进入.";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	Bakonawa's Rage	541,{
	callfunc "F_GM_NPC";
	mes "[Taho]";
	mes "My name is Minky.";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		set malaya_bakona1,7;
		set malaya_bakona2,15;
	}
	close;
}

1@ma_b,62,52,4	script	Taho#nf	541,{
	mes "[Taho]";
	mes "Heehee, I could join the Bakonawa hunt, but I'd rather hide behind that rock and cheer you guys on.";
	next;
	mes "[酸奶]";
	mes "暴龙有时会变的很无敌. 当碰到这种情况时, 仔细听我的指导. 只要相信我, 一切都会好.";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[酸奶]";
		mes "现在我会把暴龙给弄出湖水.";
		next;
		switch(select("- 等等! 我还没准备好!:- 上吧!")) {
		case 1:
			mes "[酸奶]";
			mes "噢... 好吧... 准备好就通知我.";
			close;
		case 2:
			mes "[酸奶]";
			mes "一会我会洒一些 Albopal 到湖水里.";
			next;
			mes "[酸奶]";
			mes "一旦暴龙闻到这个味道, 它就会从湖里出来...";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("酸奶#nf");
			close;
		}
	} else {
		mes "[酸奶]";
		mes "我洒的 Alpopal 会让暴龙变的很疯狂. 暴龙的肉可是蕴含大量蛋白质的好东西.";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"酸奶: 我看到它浮出水面了! 攻击!!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	monster .@map$,78,81,"食月暴龙",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead"; //original: (78,83)
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"10 minutes";
OnTimer60000:	callsub OnAnnounce,"9 minutes";
OnTimer120000:	callsub OnAnnounce,"8 minutes";
OnTimer180000:	callsub OnAnnounce,"7 minutes";
OnTimer240000:	callsub OnAnnounce,"6 minutes";
OnTimer300000:	callsub OnAnnounce,"5 minutes";
OnTimer360000:	callsub OnAnnounce,"4 minutes";
OnTimer420000:	callsub OnAnnounce,"3 minutes";
OnTimer480000:	callsub OnAnnounce,"2 minutes";
OnTimer540000:	callsub OnAnnounce,"1 minute";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"时间限制 : 30 秒",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"暴龙逃脱进入湖底了.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("酸奶#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"时间限制 : "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	set .@map$, instance_mapname("1@ma_b");
	for(set .@i,0; .@i<9; set .@i,.@i+1) {
		set .@rand, rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"食月暴龙的意志",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"食月暴龙的意志",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 很好! 暴龙躲在湖底. 它在尝试吞掉月亮.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 我们必须制造噪音, 用上锅子和铜锣. 不能让它专注在食月上面!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 不用去打它, 就算它浮在湖面上也不用管. 专心制造噪音!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"任务 - 在湖的左右两侧, 分别敲坏2个锅子和2个铜锣.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	monster .@map$,95,98,"锅子",2328,1,.@label$;
	monster .@map$,60,98,"锅子",2328,1,.@label$;
	monster .@map$,97,104,"铜锣",2328,1,.@label$;
	monster .@map$,58,104,"铜锣",2328,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"酸奶: Good! "+.@mob_dead_num+" left to go!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 end;
OnTimer1000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer60000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer120000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer180000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer240000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer270000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer280000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer290000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer295000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer296000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer297000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer298000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer299000:	callsub OnAnnounce,"1 second",20,0;
OnTimer300000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"食月暴龙逃进了湖底.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("酸奶#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,81,"愤怒的食月暴龙",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead"; //original: (78,93)
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 它又逃进湖里了. 结束了吗?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 嗯! 有什么东西正在水下迅速上浮! 暴... 暴龙来了!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"任务 - 消灭变得狂暴的食月暴龙.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,81,"愤怒的食月暴龙",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead"; //original: (78,83)
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"酸奶: 我们逮到它了! 我看见它身上掉了一个盒子一样的东西.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("酸奶#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10 minutes",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer360000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer420000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer480000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer540000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer570000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer580000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer590000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer595000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer596000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer597000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer598000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer599000:	callsub OnAnnounce,"1 second",20,0;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"暴龙逃进湖底了.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("酸奶#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	set .@mob_dead_num, mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		set .@mob_rg, 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			set .@mob_rg, getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"食月暴龙的人偶",2334,1,.@label$;
			set .@mob_dead_num, mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	酸奶#Completed	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您携带的物品种类太多, 请稍做处理.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您的负重不足, 请稍做处理.";
		close;
              }
	if (checkquest(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[酸奶]";
		mes "这是从暴龙身上掉落的. 我想应该对你有用.";
//		if (IsPremiumPcCafe == 10)
//			getitem 6499,7; //Ancient_Grudge
//		else
			getitem 6499,5; //古代符文
			getitem 10701,1;	//任务奖励P点袋
			getexp 0,100000;
		next;
	}
	mes "[酸奶]";
	mes "我们最好快点离开这里, 就算暴龙不在这里还是让人不舒服...";
	next;
	mes "[酸奶]";
	mes "让我们用绳子上去!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("酸奶#Completed");
	end;
OnStart:
	enablenpc instance_npcname("酸奶#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"食月暴龙掉落的宝盒",2335,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 打开宝盒后, 请到山顶来找我. 我有些东西要给你.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	酸奶#Fail	541,{
	mes "[酸奶]";
	mes "嗯. 我们差点就抓住它了, 它动作非常敏捷...";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[酸奶]";
		mes "想不想再来次?";
		next;
		switch(select("- 打住!:- 当然! 我们不能袖手旁观!")) {
		case 1:
			mes "[酸奶]";
			mes "啊哈哈... 差点就逮住它了.";
			close;
		case 2:
			mes "[酸奶]";
			mes "我会在湖里再用次 Albopal.";
			next;
			mes "[酸奶]";
			mes "我想我们爱上暴龙狂怒时的那张脸了...";
			disablenpc instance_npcname("酸奶#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[酸奶]";
		mes "我会像上次一样向湖里洒 Albopal. 我期待看到它拉屎的样子...";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("酸奶#Fail");
	end;
OnStart:
	enablenpc instance_npcname("酸奶#Fail");
	end;
}
