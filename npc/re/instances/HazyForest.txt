//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Hazy Forest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Cross through the Hazy Forest to reach the Bifrost.
//= Contains the "Wandering Guardian" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Instance system rewrite. [Euphy]
//============================================================ 

// Instance Creation :: md_mistwoods_enter
//============================================================
bif_fild01,158,340,5	script	拉菲尼士兵#mist	447,{

	set .@party_id,getcharid(1);
	set .@md_name$,"Mistwood Maze";

	mes "[拉菲尼士兵]";
	mes "你想进入森林?";
	next;
	if (!.@party_id) {
		mes "[拉菲尼士兵]";
		mes "很好, 很好, 很好, 你是不是头脑发热了?";
		mes "你至少也要在一个组队中吧?";
		close;
	}
	set .@playtime, checkquest(7211,PLAYTIME);
	if (getcharid(0) == getpartyleader(.@party_id,2)) {
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[拉菲尼士兵]";
			mes "...你确定你不需要再多准备一下?";
			mes "我想你需要多休息一下.";
			mes "只有你一个人的话, 什么都做不了.";
			next;
			mes "[拉菲尼士兵]";
			mes "我可不认为当局会让你进入这里, 而且.";
			mes "...我认为他们会发出禁止通行的命令.";
			next;
			select("你能明白我说的吧, 是不是?");
			mes "[拉菲尼士兵]";
			mes "...难道我?";
			mes "其实, 我说的不是你们的语言.";
			mes "我说的是拉菲尼语.";
			next;
			mes "[拉菲尼士兵]";
			mes "不过这对迷雾森林来说是个秘密.";
			mes "就是我们用不同的语言对话, 但是却能够听懂对方说什么.";
			next;
			mes "[拉菲尼士兵]";
			mes "能够和所有的种族对话...";
			mes "可能这是彩虹岛的神奇力量.";
			close;
		} else if (.@playtime == 2) {
			mes "[拉菲尼士兵]";
			mes "很好, 很好.";
			mes "这片森林看样子没有驱逐你.";
			mes "接下去你想怎么做?";
			erasequest 7211;
		} else {
			mes "[拉菲尼士兵]";
			mes "你是个令人难以置信的人类冒险家.";
			mes "你竟然敢于进入大家都惧怕的森林深处.";
			next;
			mes "[拉菲尼士兵]";
			mes "唷. 但是, 我必须向有关部门上报,";
			mes "以防万一, 乱七八糟的人都进入森林冒险.";
			next;
			mes "[拉菲尼士兵]";
			mes "你会得到许可";
			mes "在你进行注册之后.";
			mes "你确定要进入迷雾森林吗?";
		}
		next;
		switch(select("- 进入迷雾森林探险.:- 放弃.")) {
		case 1:
			mes "[拉菲尼士兵]";
			mes "队伍名称"+getpartyname(.@party_id)+"...";
			mes "队长 "+strcharinfo(0)+"...";
			if (instance_create(.@md_name$) < 0) {
				mes "嗯...";
				next;
				mes "[拉菲尼士兵]";
				if (.@playtime == -1)
					mes "森林中充满危险.";
				else
					mes "现在森林里的气氛有些紧张.";
				mes "...今天你为什么不回去呢?";
				close;
			}
			mes "我知道了. 我记录好了会上报这些信息.";
			next;
			mes "[拉菲尼士兵]";
			mes "你会很快得到许可.";
			mes "现在可以去传送门了, 那是迷雾森林的唯一入口.";
			mes "明白了吗?";
			next;
			mes "[拉菲尼士兵]";
			mes "我们都不能违背森林的规则,";
			mes "不过可能你们人类会有机会.";
			mes "祝你们好运!";
			close;
		case 2:
			mes "[拉菲尼士兵]";
			mes "很好, 你已经做出了正确的决定.";
			if (.@playtime == -1)
				mes "这对你有好处.";
			close;
		}
	} else {
		mes "[拉菲尼士兵]";
		mes "你们的队长呢?";
		mes "那里非常危险.";
		next;
		if (.@playtime == 2) {
			mes "[拉菲尼士兵]";
			mes "我认为在你们已经进入森林前.";
			mes "好吧, 至少这片森林还没有把你们驱逐出去.";
			mes "然而...";
			erasequest 7211;
			next;
		}
		mes "[拉菲尼士兵]";
		mes "跟着我的脚步, 入口的大门, 还有让我见一下你们队伍的代表.";
		mes "我要与他/她讨论一些事情.";
		close;
	}
}

bif_fild01,161,355,0	script	彩虹桥	844,{
	mes "透过彩虹桥我看到了整片森林.";
	mes "看上去我能穿过彩虹桥进入森林.";
	next;
	if(select("- 进入彩虹桥.:- 放弃.") == 2)
		close;
	switch(instance_enter("Mistwood Maze")) {
	case 3:
		mes "[拉菲尼士兵]";
		mes "感觉有点不对劲. 看样子有什么危险的事情要发生, 你最好今天回去.";
		close;
	case 2:
		mes "你试图抓住彩虹, 但是有股神秘的力量, 将你吹了回来.";
		mes "看上去你无法进入森林了.";
		close;
	case 1:
		mes "[拉菲尼士兵]";
		mes "嘿, 快看!";
		mes "你打算单独一人?";
		mes "这不可能. 太冲动了.";
		mes "组队和一些朋友一起去!";
		close;
	case 0:
		if (checkquest(7211,PLAYTIME) == 2) erasequest 7211;
		if (checkquest(7211,PLAYTIME) == -1) setquest 7211;
		mapannounce "bif_fild01",getpartyname(getcharid(1))+" party's "+strcharinfo(0)+" member is entering the Mistwood Maze.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		//warp "1@mist",89,29;
		close;
	default:
		mes "彩虹桥以外的世界看起来黑暗和令人窒息.";
		mes "你的直觉告诉你这片森林是危险的.";
		close;
	}
}

// Instance Scripts :: md_mistwoods_in
//============================================================
1@mist,89,29,0	script	#Whisper_mist	844,1,1,{
	end;
OnInstanceInit:
	enablenpc instance_npcname("#Whisper_mist");
	end;
OnTouch:
	disablenpc instance_npcname("#Whisper_mist");
	initnpctimer;
	end;
OnTimer10000:
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"呢喃声: 这是谁? 这些是谁!? 森林开启了! 汤姆, 汤姆巴, 疲倦的雷姆, 雷米! 快起来!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer60000:
	mapannounce instance_mapname("1@mist"),"大声的呢喃: 森林的守护者想做什么? 那些园丁在干什么??",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer120000:
	mapannounce instance_mapname("1@mist"),"大声的呢喃: 别出去, 永远四处流浪, 和我们一起玩!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer180000:
	mapannounce instance_mapname("1@mist"),"大声的呢喃: 没用, 没用. 四处晃悠, 我们最终会碰面的!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
}

// callfunc "F_Mora_Mist",<warp to enable>,<success message>,<fail message>{,<end flag>}
function	script	F_Mora_Mist	{
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMyMobDead") == 0) {
		mes "显而易见, 这里无人照料.";
		mes "看上去你可以砍掉这些花园里的树木.";
		next;
		if(select("- 砍掉这些树木.:- 放弃.") == 2)
			close;
		mes "你砍掉了树, 那些阻挡这迷宫路线的, "+((getarg(3,0))?"扫清走出森林的障碍.":"你现在可以继续了.");
		mapannounce .@map$,getarg(1),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
		enablenpc instance_npcname(getarg(0));
		disablenpc instance_npcname(strnpcinfo(0));
		close;
	} else
		mapannounce .@map$,((getarg(3,0))?getarg(2):getarg(2)+" 的哭声: 嗯? 是谁对我的树木做了这些?!"),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
}

1@mist,251,121,3	script	汤姆的花园	858,{ end; }
1@mist,247,123,0	script	汤姆花园的树	527,{
	callfunc "F_Mora_Mist",
		"a25_a18-1",
		"咆哮呐喊: 汤姆的螺栓被打破了! 当心, 汤姆巴! 当心, 汤姆巴!",
		"汤姆";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),249,120,"汤姆",2136,1,instance_npcname("汤姆花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,226,95,3	script	汤姆巴的花园	858,{ end; }
1@mist,225,98,0	script	汤姆巴花园的树	527,{
	callfunc "F_Mora_Mist",
		"a27-2_af1-2",
		"咆哮哭泣: 汤姆巴花园的树跟着汤姆巴走了. 雷米, 你现在准备怎么做?",
		"汤姆巴";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),200,64,"汤姆巴",2136,1,instance_npcname("汤姆巴花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,157,183,3	script	雷米的花园	858,{ end; }
1@mist,159,184,0	script	雷米花园的树	527,{
	callfunc "F_Mora_Mist",
		"a15-2_a4-1",
		"咆哮哭泣: 雷米, 雷米花园的树 被砍掉了 - 雷姆, 下一个会是你了. 愚蠢的雷姆!",
		"雷米";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),154,184,"疲倦的雷米",2137,1,instance_npcname("雷米花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,58,39,3	script	疲倦雷姆的花园	858,{ end; }
1@mist,61,39,0	script	疲倦雷姆花园的树	527,{
	callfunc "F_Mora_Mist",
		"a4-2_a11",
		"雷姆绝望的哭泣: 啊... 雷姆要睡觉. 雷姆要睡觉了, 永远不会醒来!",
		"雷姆";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),101,107,"雷姆的园丁",2136,1,instance_npcname("疲倦雷姆花园的树")+"::OnMyMobDead";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@mist"),"森林的呢喃声: 汤姆的, 汤姆巴的, 雷米的, 和雷姆的花园里的树都被砍掉了?",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@mist"),"森林的呢喃声: 麻烦, 麻烦, 我们遇到大麻烦了.",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"巨大的呢喃声: 他们已经来第二片森林. 森林深处的园丁, 警报!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,227,179,3	script	罗恩的花园	858,{ end; }
1@mist,230,179,0	script	罗恩花园的树	527,{
	callfunc "F_Mora_Mist",
		"b4-2_b2",
		"罗恩园丁的哭泣: 我的工作完了! 别找罗恩了, 第二个园丁, 更多!!",
		"罗恩";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),227,178,"罗恩的园丁",2134,1,instance_npcname("罗恩花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,288,226,3	script	罗孚的花园	858,{ end; }
1@mist,285,225,0	script	罗孚花园的树	527,{
	callfunc "F_Mora_Mist",
		"b7_bf1-2",
		"咆哮的声音: 罗恩, 罗孚的树木被砍下. 是谁逼着他们走进森林? 是谁?",
		"罗孚";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),304,237,"罗孚的Strutter",2134,1,instance_npcname("罗孚花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,159,318,3	script	莫纳的花园	858,{ end; }
1@mist,161,316,0	script	莫纳花园的树	527,{
	callfunc "F_Mora_Mist",
		"b31_b16",
		"吵闹的呢喃声: 蒙娜丽莎的种子探索者花园的树已经没了! 蒙娜丽莎也死了! 纳蒙会没事的, 他很勇敢, 对吗? 纳蒙, 你还好吗?",
		"莫纳";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),239,253,"莫纳的种子探索者",2133,1,instance_npcname("莫纳花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,205,300,3	script	纳蒙的花园	858,{ end; }
1@mist,204,299,0	script	纳蒙花园的树	527,{
	callfunc "F_Mora_Mist",
		"b27-3_bN-1",
		"吵杂的呢喃声: 勇敢的纳蒙在遭遇宿命之时却没有那么勇敢... *傻笑*",
		"纳蒙";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),89,173,"勇敢的纳蒙",2134,1,instance_npcname("纳蒙花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,219,232,3	script	悲伤尼奥隆的花园	858,{ end; }
1@mist,221,236,0	script	悲伤尼奥隆花园的树	527,{
	callfunc "F_Mora_Mist",
		"b6-1_b33",
		"惊叹的呢喃声: 悲伤的尼奥隆变快乐了! 斯帕德的高级蜘蛛, 你准备怎么做? 你现在准备怎么办?",
		"尼奥隆";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),143,265,"悲伤的尼奥隆",2137,1,instance_npcname("悲伤尼奥隆花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,206,202,3	script	斯帕德的花园	858,{ end; }
1@mist,206,200,0	script	斯帕德花园的树	527,{
	callfunc "F_Mora_Mist",
		"b5_b14",
		"郁闷的呢喃声: 现在第二片森林一切都结束了. 园丁正在消失-",
		"斯帕德";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),209,200,"斯帕德的八条腿",2132,1,instance_npcname("斯帕德花园的树")+"::OnMyMobDead";
	end;
OnTimer5000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃声: 本通路即将开启. 紧绷的螺栓已经被移除!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer9000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃声: 一片森林打开了, 另一片也是, 还有一片也快了. 蒂托和普姆巴, 他们正朝着你而来!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer13000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃声: 四处游荡了一圈又一圈, 在那里你你所遇见的的每个人都转悠了一圈又一圈,",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer17000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃声: 所有的园丁, 都手持武器等待着你-!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,96,288,3	script	蒂托的花园	858,{ end; }
1@mist,95,287,0	script	蒂托花园的树	527,{
	callfunc "F_Mora_Mist",
		"c1_c13-1",
		"蒂托的哭声: *呜咽* *呜咽* 蒂托就要死了! 蒂托做了! Boo hoo!!",
		"蒂托";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),264,291,"蒂托的脚蹼",2133,1,instance_npcname("蒂托花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,326,325,3	script	普姆巴的花园	858,{ end; }
1@mist,324,325,0	script	普姆巴花园的树	527,{
	callfunc "F_Mora_Mist",
		"c13-3_c4",
		"咆哮的呢喃声: 蒂托的努力没能避免失败的命运! 停止嘻嘻快去工作, 特塔! 开始做事了!",
		"普姆巴";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),309,165,"勤奋的普姆巴",2134,1,instance_npcname("普姆巴花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,278,345,3	script	特塔的花园	858,{ end; }
1@mist,280,344,0	script	特塔花园的树	527,{
	callfunc "F_Mora_Mist",
		"c19_c4",
		"呢喃声: 他们很强壮, 确实强大. 我们现在已经怎么办? 是否有任何离开的园丁? 是否有任何?",
		"特塔";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),277,343,"安闲的特塔",2136,1,instance_npcname("特塔花园的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,345,186,0	script	园丁的树	527,{
	callfunc "F_Mora_Mist",
		"#to_bif02",
		"咆哮的呢喃声: 森林会记得你! 它会记住你, 那个将所有园丁的电锯全部击碎的人!",
		"园丁: 我们还没死, 还没!!!!",1;
	end;
OnInstanceInit:
	set .@map$, instance_mapname("1@mist");
	monster .@map$,318,137,"Baby Tom",2136,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,321,137,"Tomba the Baby",2136,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,324,137,"Exhausted Remi",2137,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,319,135,"Rem the Exhausted",2136,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,323,135,"Ron the Ex-Gardener",2134,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,320,133,"Rover the Strutter",2134,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,323,133,"Mona the Seedpicker",2133,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,317,132,"Timid Namon",2134,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,326,132,"Indifferent Neoron",2137,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,317,129,"Spyder the Seven-Legged",2132,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,320,129,"Tito the Flapper",2133,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,324,129,"Lazy Pumba",2134,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	monster .@map$,327,129,"Careless Tete",2136,1,instance_npcname("园丁的树")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,73,290,0	script	神秘的花#1	844,{
	specialeffect EF_LEVEL99_4;
	disablenpc instance_npcname(strnpcinfo(0));
	getitem 12561,1; //Mysterious_Seed
	end;
}
1@mist,69,295,0	duplicate(神秘的花#1)	神秘的花#2	844
1@mist,62,291,0	duplicate(神秘的花#1)	神秘的花#3	844
1@mist,54,293,0	duplicate(神秘的花#1)	神秘的花#4	844
1@mist,48,286,0	duplicate(神秘的花#1)	神秘的花#5	844
1@mist,54,277,0	duplicate(神秘的花#1)	神秘的花#6	844
1@mist,52,270,0	duplicate(神秘的花#1)	神秘的花#7	844
1@mist,62,268,0	duplicate(神秘的花#1)	神秘的花#8	844
1@mist,72,271,0	duplicate(神秘的花#1)	神秘的花#9	844
1@mist,72,276,0	duplicate(神秘的花#1)	神秘的花#10	844
1@mist,118,194,0	duplicate(神秘的花#1)	神秘的花#11	844
1@mist,122,204,0	duplicate(神秘的花#1)	神秘的花#12	844
1@mist,137,209,0	duplicate(神秘的花#1)	神秘的花#13	844
1@mist,144,203,0	duplicate(神秘的花#1)	神秘的花#14	844
1@mist,146,186,0	duplicate(神秘的花#1)	神秘的花#15	844
1@mist,142,182,0	duplicate(神秘的花#1)	神秘的花#16	844
1@mist,132,179,0	duplicate(神秘的花#1)	神秘的花#17	844
1@mist,123,184,0	duplicate(神秘的花#1)	神秘的花#18	844
1@mist,194,118,0	duplicate(神秘的花#1)	神秘的花#19	844
1@mist,188,109,0	duplicate(神秘的花#1)	神秘的花#20	844
1@mist,193,94,0	duplicate(神秘的花#1)	神秘的花#21	844
1@mist,205,92,0	duplicate(神秘的花#1)	神秘的花#22	844
1@mist,213,96,0	duplicate(神秘的花#1)	神秘的花#23	844
1@mist,216,103,0	duplicate(神秘的花#1)	神秘的花#24	844
1@mist,212,116,0	duplicate(神秘的花#1)	神秘的花#25	844
1@mist,207,120,0	duplicate(神秘的花#1)	神秘的花#26	844
1@mist,169,310,0	duplicate(神秘的花#1)	神秘的花#27	844
1@mist,172,296,0	duplicate(神秘的花#1)	神秘的花#28	844
1@mist,191,301,0	duplicate(神秘的花#1)	神秘的花#30	844
1@mist,195,301,0	duplicate(神秘的花#1)	神秘的花#31	844
1@mist,192,316,0	duplicate(神秘的花#1)	神秘的花#32	844
1@mist,187,320,0	duplicate(神秘的花#1)	神秘的花#33	844
1@mist,174,318,0	duplicate(神秘的花#1)	神秘的花#34	844
1@mist,308,136,0	duplicate(神秘的花#1)	神秘的花#35	844
1@mist,314,122,0	duplicate(神秘的花#1)	神秘的花#36	844
1@mist,321,118,0	duplicate(神秘的花#1)	神秘的花#37	844
1@mist,331,123,0	duplicate(神秘的花#1)	神秘的花#38	844
1@mist,335,126,0	duplicate(神秘的花#1)	神秘的花#39	844
1@mist,332,141,0	duplicate(神秘的花#1)	神秘的花#40	844
1@mist,328,144,0	duplicate(神秘的花#1)	神秘的花#41	844
1@mist,314,144,0	duplicate(神秘的花#1)	神秘的花#42	844
1@mist,238,224,0	duplicate(神秘的花#1)	神秘的花#43	844
1@mist,244,231,0	duplicate(神秘的花#1)	神秘的花#44	844
1@mist,257,232,0	duplicate(神秘的花#1)	神秘的花#45	844
1@mist,262,229,0	duplicate(神秘的花#1)	神秘的花#46	844
1@mist,265,216,0	duplicate(神秘的花#1)	神秘的花#47	844
1@mist,260,214,0	duplicate(神秘的花#1)	神秘的花#48	844
1@mist,252,206,0	duplicate(神秘的花#1)	神秘的花#49	844
1@mist,244,211,0	duplicate(神秘的花#1)	神秘的花#50	844

// Wandering Guardian Quest
//============================================================

// ----------------- Part of md_mistwoods_in -----------------
1@mist,2,2,0	script	#Dragon Herder	844,{
	end;
OnInstanceInit:
	switch(rand(1,6)) {
		case 1: setarray .@c[0],77,240; break;
		case 2: setarray .@c[0],147,261; break;
		case 3: setarray .@c[0],182,263; break;
		case 4: setarray .@c[0],194,350; break;
		case 5: setarray .@c[0],268,183; break;
		case 6: setarray .@c[0],65,278; break;
	}
	monster instance_mapname("1@mist"),.@c[0],.@c[1],"流浪紫金神龙",2131,1,instance_npcname("#Dragon Herder")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname("#Dragon Herder")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("倒在地上的女子#mist")+"::OnEnable";
		donpcevent instance_npcname("神秘的年轻男子#mis")+"::OnEnable";
		donpcevent instance_npcname("洛奇#mist")+"::OnEnable";
		mapannounce .@map$,"龙的叫声: 我被惩罚了... 放弃这方式... 守护者...?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		disablenpc instance_npcname("#Dragon Herder");
		initnpctimer;
	}
	end;
OnTimer3000:
	stopnpctimer;
	mapannounce instance_mapname("1@mist"),"在你的脑袋里的声音: ...你会... 救... 我... 请求你......?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	end;
}

1@mist,97,30,3	script	神秘的年轻男子#mis	512,{
	if (BaseLevel < 98) {
		mes "[神秘的年轻男子]";
		mes "嗯? 你打算从这里经过?";
		mes ".....恐怕它超出了你的能力, 但我不会干涉你的事.";
		mes "我们会再见面的, 如果你活着.";
		close;
	}
	if (ep14_1_mistwoods == 0) {
		if (ep13_3_secret > 23) {
			mes "[神秘的年轻男子]";
			mes "你... 我记得我们在营地见过.";
			mes "什么是有用的信息.";
			mes "谢谢!";
			next;
			mes "[神秘的年轻男子]";
			mes "但是你打算通过这里?";
			mes "穿越彩虹桥对你有什么好处呢?";
			next;
		} else {
			mes "[神秘的年轻男子]";
			mes "你打算从这里经过?";
			mes "如果你只是好奇, 我会反对这样做.";
			mes "这里只会带给你死亡...";
			next;
		}
		mes "[神秘的年轻男子]";
		mes "如果你有决心通过这里,";
		mes "我会请求你们的帮我一些忙.";
		next;
		select("你是谁?");
		mes "[洛奇]";
		mes "洛奇... 来自刺客公会.";
		mes "这是我的名字.";
		mes "你的呢?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "....";
		mes "...."+strcharinfo(0)+" 先生.";
		next;
		mes "[洛奇]";
		mes "我明白. "+strcharinfo(0)+".";
		mes "我再问你一次.";
		mes "你确定打算从穿越这片森林?";
		next;
		switch(select("- 是, 我确定.:- 不, 我没有.")) {
		case 1:
			mes "[洛奇]";
			mes "然后我问你一个问题.";
			mes "你是否见过一名警卫... 一个紫色头发的女孩?";
			next;
			switch(select("- 没, 没见过.:- 有, 我可能有...")) {
			case 1:
				mes "[洛奇]";
				mes "嗯, 你没见过.";
				mes "你说的是真话吗.";
				next;
				break;
			case 2:
				mes "[洛奇]";
				mes "等等.";
				mes "(洛奇靠近了.)";
				mes "... 不.";
				mes "你说的是真的.";
				next;
				break;
			}
			mes "[洛奇]";
			mes "我们现在要分别了.";
			mes "当然, 希望你能够活着走出这片森林.";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		case 2:
			mes "[洛奇]";
			mes "我明白了.";
			mes "如果你还没准备好, 记得退回来.";
			mes "这片森林不喜欢陌生人.";
			set ep14_1_mistwoods,1;
			close;
		}
	} else if (ep14_1_mistwoods == 1) {
		mes "[洛奇]";
		mes "我告诉过你 - 如果不想死就退回来.";
		mes "穿越森林不是一件简单的事情.";
		mes "这可能要你的命.";
		next;
		switch(select("- 好的.:- 你想要什么?")) {
		case 1:
			mes "- 当你退一步,";
			mes "洛奇轻轻地点点头, 然后看着远处. -";
			close;
		case 2:
			mes "[洛奇]";
			mes "我在找人.";
			mes "你有没有见过一名警卫... 一个紫色头发的女孩?";
			next;
			switch(select("- 没, 没见过.:- 有, 我可能有...")) {
			case 1:
				mes "[洛奇]";
				mes "你没见过.";
				mes "你说的是事实.";
				next;
				break;
			case 2:
				mes "[洛奇]";
				mes "等等.";
				mes "(洛奇靠近了你.)";
				mes "... 不.";
				mes "你说的是真话.";
				next;
				break;
			}
			mes "[洛奇]";
			mes "现在我们应该分别了.";
			mes "希望, 我们会在莫拉村再见.";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		}
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛奇]";
		mes "我不会和你同行.";
		mes "希望, 我们会在莫拉村再见.";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛奇]";
		mes "我们又见面了.";
		mes "你经常来这地方?";
		mes "来莫拉的旅馆.";
		mes "我有些东西要给你.";
		close;
	} else {
		mes "[洛奇]";
		mes "这些天以来, 我们对对方都了解了很多.";
		mes "看上去你对这里的环境非常的适应.";
		mes "不过你可别太骄傲.";
		close;
	}
	end;
OnEnable:
	enablenpc instance_npcname("神秘的年轻男子#mis");
	end;
OnDisable:
	disablenpc instance_npcname("神秘的年轻男子#mis");
	end;
}

1@mist,183,304,3	script	洛奇#mist	512,{
	if (ep14_1_mistwoods < 2) {
		mes "[洛奇]";
		mes "在森林中的冒险家.";
		mes ".....";
		mes "通过这片森林, 你可以到达莫拉村.";
		close;
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛奇]";
		mes "你能不能照顾好这个刁蛮的丫头?";
		mes "我想我要感谢你.";
		mes "你能让她温顺下来.";
		next;
		select("- 发生什么了?:- 这女孩...?");
		mes "[洛奇]";
		mes "你也许能够改变你的外观, 但你不能改变你的特点, 惧怕光线.";
		mes "强大到足以毁掉你的鼻子的气味.";
		next;
		mes "[洛奇]";
		mes "这是尼德霍格， 世界之树的守护者,";
		mes "我要找的人.";
		mes "现在她不会到处闲逛和发疯了.";
		next;
		select("你现在想怎么做?");
		mes "[洛奇]";
		mes "我等她醒来, 然后走出这片森林.";
		mes "我可以自己来管束她.";
		mes "希望在莫拉村再见到你.";
		set ep14_1_mistwoods,3;
		erasequest 7212;
		setquest 7213;
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛奇]";
		mes "我不会和你同行.";
		mes "我可以自己管束住她.";
		mes "如果你还有什么要说的, 就在我们离开森林之前都说完把.";
		close;
	} else {
		mes "[洛奇]";
		mes "我不会和你同行.";
		mes "如果你还有什么要说的, 就在我们离开森林之前都说完把.";
		close;
	}
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("洛奇#mist");
	end;
OnEnable:
	enablenpc instance_npcname("洛奇#mist");
	end;
}

1@mist,181,303,3	script	倒在地上的女子#mist	511,{
	mes "你看到一个长着神秘头发的女子瘫倒在地上.";
	mes "当你接近她, 你闻到一股花的香味, 这浓郁的香味使你头晕.";
	close;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("倒在地上的女子#mist");
	end;
OnEnable:
	enablenpc instance_npcname("倒在地上的女子#mist");
	end;
}

// --------------- Part of md_mistwoods_enter ----------------
mora,46,152,5	script	满身花香的女子	510,{
	if (ep14_1_mistwoods < 3) {
		mes "[满身花香的女子]";
		mes "我需要休息.";
		mes "和你说话让我非常疲惫.";
		mes "我和抱歉.";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[守护者尼德霍格]";
		mes "我在这里等你.";
		mes "我感到十分的困惑, 但是我知道我做错了什么.";
		next;
		mes "[守护者尼德霍格]";
		mes "如果不是你, 我会仍然在黑暗的深渊中徘徊.";
		mes "感谢你, 发自我的肺腑.";
		next;
		mes "[守护者尼德霍格]";
		mes "我为自己感到羞愧, 在孤独的时候陶醉在甜蜜却空洞的话语中.";
		mes "....";
		next;
		mes "[守护者尼德霍格]";
		mes "我已经不配再成为守护者了.";
		mes "一条疯了的龙不再是守护者了.";
		next;
		mes "[洛奇]";
		mes "世界之树并不这样认为.";
		mes "尼蒂, 不要忘记你的使命.";
		next;
		mes "[守护者尼德霍格]";
		mes "你是...?";
		mes "我明白...";
		mes "我想我被抛弃了...";
		next;
		mes "守护者慢慢移开目光仿佛陷入沉思.";
		set ep14_1_mistwoods,4;
		close2;
	} else if (ep14_1_mistwoods == 4) {
		mes "- 他似乎是在思想深处. -";
		close;
	} else {
		mes "[守护者尼德霍格]";
		mes "我还没有恢复我所有力量.";
		mes "对不起, 我保护不了你.";
		next;
		mes "[守护者尼德霍格]";
		mes "洛奇说...";
		mes "我不会因为孤独而再度哭泣,";
		mes "我将会一直有你这样的新朋友...";
		next;
		mes "[守护者尼德霍格]";
		mes "...我失去了一切, 但是现在我很快乐.";
		close;
	}
}

mora,48,152,3	script	眼神如炬的男人	512,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 你的物品种类太多了. -";
		close;
	}
	if (ep14_1_mistwoods < 3) {
		mes "他没有看你一眼. 感觉他对任何事物都不感兴趣.";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛奇]";
		mes "你已经穿越了森林.";
		mes "看上去尼蒂好像有话要对你说.";
		close;
	} else if (ep14_1_mistwoods == 4) {
		mes "[洛奇]";
		mes "尼德霍格是世界之树的守护者之一.";
		mes "但是在梦罗克诱惑他的时候, 他放弃了作为守护者的职责.";
		next;
		mes "[洛奇]";
		mes "有群傻瓜在梦罗克举行了一次破除封印的仪式.";
		mes "不过仪式没有任何效果. 梦罗克的随从们未能打破封印.";
		next;
		mes "[洛奇]";
		mes "但封印已经非常脆弱, 它有可能随时都会被破开.";
		mes "世界之树用它的意志支撑着封印, 避免封印被毁.";
		next;
		mes "[守护者尼德霍格]";
		mes "这是正确的. 世界之树贯穿着整个世界的力量.";
		mes "当守护者和世界之树直接发生问题时, 世界之树会变得不稳定.";
		next;
		mes "[守护者尼德霍格]";
		mes "这就是为什么所有的灾难都针对世界之树而发生.";
		mes "当世界之树不稳定时, 它对封印的控制也会随之减弱.";
		next;
		mes "[洛奇]";
		mes "然后梦罗克自己突破了封印.";
		mes "虽然看上去好像他只是用了自己的力量打破封印...";
		next;
		mes "[守护者尼德霍格]";
		mes "这都是我的错.";
		mes "我无法挣脱来自梦罗克的诱惑.";
		mes "太... 太久...";
		next;
		mes "[守护者尼德霍格]";
		mes "...我想缓解我成为数千守护者的孤独感.";
		mes "并没有在正确的在我心灵，然后.";
		mes "我想没有任何一件事能够比我对世界之树的伤害更大.";
		next;
		mes "[守护者尼德霍格]";
		mes "我失去了意识, 并做了我不应该做的事情.";
		mes "我的另一面, 成为一个影子, 一个鲁莽的暴君.";
		next;
		mes "[守护者尼德霍格]";
		mes "面对世界, 我意识到...";
		mes "我感到太大的失落感... 它比我忍受了几千年的寂寞痛苦得多.";
		next;
		mes "[守护者尼德霍格]";
		mes "因此我变成了罪人, 被世界所遗忘.";
		next;
		mes "[洛奇]";
		mes "还为时不晚.";
		mes "你始终还是守护者.";
		mes "你始终还有事情需要去面对.";
		next;
		mes "[守护者尼德霍格]";
		mes "... 呃, 是的. 现在已经晚了, 但是我必须履行我作为守护者的职责.";
		mes "现在我知道. 我将要用自己的双手来摧毁梦罗克.";
		mes "那是我的使命, 守护者的使命.";
		next;
		switch(select("- 你并不孤单.:- 这些都是非常重要的职责.")) {
		case 1:
			mes "[守护者尼德霍格]";
			mes "你说得对.";
			mes "这可能就是为什么我没有被抛弃 - 要认识到我并不孤单.";
			mes "你就是一种心灵寄托.";
			next;
			break;
		case 2:
			mes "[守护者尼德霍格]";
			mes "不过这是我的赎罪.";
			mes "我很高兴, 虽然.";
			mes "我不用再去对抗无尽的寂寞了.";
			next;
			break;
		}
		mes "[守护者尼德霍格]";
		mes strcharinfo(0)+"...";
		mes "谢谢你听我说完呢么长的故事.";
		next;
		mes "[守护者尼德霍格]";
		mes "这就是我永远有我.";
		mes "我敢肯定, 总有一天它会保护你的.";
		next;
		mes "[洛奇]";
		mes "(从他的脖子上摘下他的披肩, 并把它交给你.)";
		next;
		mes "[守护者尼德霍格]";
		mes "愿大地母亲的祝福与你同在...";
		set ep14_1_mistwoods,10;
		completequest 7213;
		getitem 2568,1; //Muffler_Of_Roki
		getitem 2858,1; //Pendant_Of_Guardian
		getexp 400000,400000;
		close;
	} else {
		mes "[洛奇]";
		mes "我会在这里住一段时间.";
		mes "我们会再见的.";
		next;
		mes "[洛奇]";
		mes "当梦罗克揭示了他的卑劣本质的那天...";
		mes "我会等着他.";
		close;
	}
}

// Monster Spawns :: md_mist_mob
//============================================================
1@mist,1,1,0	script	#Mist Mobs Respawn1	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"米米怪",2137,15,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"米米怪",2137,.@num_mobs,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
}

1@mist,1,2,0	script	#Mist Mobs Respawn2	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"胖姆蜘蛛",2132,35,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"胖姆蜘蛛",2132,.@num_mobs,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
}

1@mist,1,3,0	script	#Mist Mobs Respawn3	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"安格拉螳螂",2133,35,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"安格拉螳螂",2133,.@num_mobs,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
}

1@mist,1,4,0	script	#Mist Mobs Respawn4	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"煤山雀",2134,35,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"煤山雀",2134,.@num_mobs,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
}

1@mist,1,5,0	script	#Mist Mobs Respawn5	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"小幻兽",2136,15,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"小幻兽",2136,.@num_mobs,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
}

1@mist,1,6,0	script	#记忆的碎片	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#记忆的碎片");
	monster instance_mapname("1@mist"),0,0,"守护者的记忆的碎片",2138,15;
	end;
}

// Warp Portals :: md_mist_in_link
//============================================================
bif_fild01,38,374,0	script	神秘的花#ep14_1	844,{
	mes "这是一朵充满神秘力量的巨大花朵.";
	if (countitem(12561)) {
		mes "它对你手中的神奇种子产生了巨大的反应.";
		next;
		switch(select("- 观察反应.:- 终止反应.")) {
		case 1:
			mes "花朵开始剧烈摇晃";
			mes "并向前摆动, 并试图尽可能的将种子送到远处!!!";
			close2;
			delitem 12561,1; //Mysterious_Seed
			warp "bif_fild02",160,230;
			end;
		case 2:
			mes "正如你紧紧地抓住了神秘的花卉种子,";
			mes "花剧烈地颤抖起来, 而后又恢复了平静.";
			close;
		}
	}
	mes "它看起来应该有一些特殊的用途, 但你还不清楚是什么.";
	close;
}

1@mist,104,23,0	warp	#to_bif01	1,1,bif_fild01,160,352

1@mist,109,70,0	script	a1_a2	45,1,1,{
	end;
OnTouch:
	if (checkquest(7211,PLAYTIME) == -1)
		setquest 7211;
	warp instance_mapname("1@mist"),116,40;
	end;
}

1@mist,113,40,0	warp	a2_a7	1,1,1@mist,90,81
1@mist,93,81,0	warp	a7_a14	1,1,1@mist,108,152
1@mist,111,154,0	warp	a14_a24	1,1,1@mist,236,108
1@mist,236,110,0	warp	a24_a1	1,1,1@mist,106,69
1@mist,161,67,0	warp	a18-1_a22	1,1,1@mist,159,85
1@mist,180,116,0	warp	a23_a18-1	1,1,1@mist,164,66
1@mist,180,63,0	warp	a18-2_a9	1,1,1@mist,141,90
1@mist,116,84,0	warp	a8_a26	1,1,1@mist,222,133
1@mist,220,133,0	warp	a26_a27-1	1,1,1@mist,200,64
1@mist,197,64,0	warp	a27-1_a23	1,1,1@mist,177,116
1@mist,146,194,0	warp	af1-2_a27-2	1,1,1@mist,225,96
1@mist,133,195,0	warp	af1-1_af2-1	1,1,1@mist,202,110
1@mist,202,107,0	warp	af2-1_af1-1	1,1,1@mist,134,189
1@mist,217,110,0	warp	af2-2_a20-1	1,1,1@mist,240,56
1@mist,240,54,0	warp	a20-1_af2-2	1,1,1@mist,214,110
1@mist,234,67,0	warp	a20-2_a21	1,1,1@mist,239,80
1@mist,242,77,0	warp	a21_a15-1	1,1,1@mist,153,137
1@mist,156,139,0	warp	a15-1_a3	1,1,1@mist,123,55
1@mist,124,51,0	warp	a3_a20-1	1,1,1@mist,240,56
1@mist,48,41,0	warp	a4-1_a17	1,1,1@mist,174,165
1@mist,171,165,0	warp	a17_a16	1,1,1@mist,168,143
1@mist,169,141,0	warp	a16_a10	1,1,1@mist,101,107
1@mist,99,107,0	warp	a10_a4-2	1,1,1@mist,57,38
1@mist,65,113,0	warp	a11_a28	1,1,1@mist,218,151
1@mist,221,151,0	warp	a28_a13	1,1,1@mist,117,126
1@mist,114,126,0	warp	a13_a5	1,1,1@mist,36,62
1@mist,61,88,0	warp	a6_a19	1,1,1@mist,189,139
1@mist,186,140,0	warp	a19_a12	1,1,1@mist,98,126
1@mist,101,126,0	warp	a12_a29-1	1,1,1@mist,238,151
1@mist,235,152,0	warp	a29-1_af1-2	1,1,1@mist,144,194
1@mist,254,151,0	warp	a29-2E_b1	1,1,1@mist,271,164
1@mist,275,211,0	warp	b1_b13	1,1,1@mist,277,112
1@mist,301,141,0	warp	b14_b24	1,1,1@mist,99,232
1@mist,99,230,0	warp	b24_b25	1,1,1@mist,103,255
1@mist,102,253,0	warp	b25_b4-1	1,1,1@mist,215,177
1@mist,212,177,0	warp	b4-1_b1	1,1,1@mist,276,208
1@mist,289,167,0	warp	b2_b19	1,1,1@mist,152,219
1@mist,149,219,0	warp	b19_b20	1,1,1@mist,119,227
1@mist,115,227,0	warp	b20_b23	1,1,1@mist,230,353
1@mist,233,353,0	warp	b23_b7	1,1,1@mist,289,225
1@mist,76,273,0	warp	bf1-2_b7	1,1,1@mist,289,225
1@mist,61,282,0	warp	bf1-1_bf2-1	1,1,1@mist,256,220
1@mist,252,220,0	warp	bf2-1_bf1-1	1,1,1@mist,65,282
1@mist,267,213,0	warp	bf2-2_b28	1,1,1@mist,214,268
1@mist,212,265,0	warp	b28_b12	1,1,1@mist,288,266
1@mist,285,266,0	warp	b12_b21	1,1,1@mist,143,234
1@mist,143,231,0	warp	b21_b29	1,1,1@mist,222,281
1@mist,223,279,0	warp	b29_b31	1,1,1@mist,158,317
1@mist,134,287,0	warp	b30_b3	1,1,1@mist,293,195
1@mist,295,197,0	warp	b3_b10	1,1,1@mist,239,253
1@mist,236,253,0	warp	b10_b12	1,1,1@mist,288,266
1@mist,111,203,0	warp	b16_b34	1,1,1@mist,202,333
1@mist,199,333,0	warp	b34_b27-1	1,1,1@mist,203,251
1@mist,206,249,0	warp	b27-1_b22	1,1,1@mist,168,239
1@mist,165,239,0	warp	b22_b15	1,1,1@mist,89,173
1@mist,177,265,0	warp	b27-2_a14	1,1,1@mist,108,152
1@mist,166,314,0	warp	bN-1_b27-3	1,1,1@mist,206,297
1@mist,197,299,0	warp	bN-2_b17	1,1,1@mist,167,200
1@mist,165,200,0	warp	b17_bN-2	1,1,1@mist,193,299
1@mist,86,173,0	warp	b15_b32	1,1,1@mist,218,313
1@mist,215,313,0	warp	b32_b28	1,1,1@mist,214,268
1@mist,230,324,0	warp	b33_b6-1	1,1,1@mist,221,233
1@mist,230,230,0	warp	b6-2_b11	1,1,1@mist,263,261
1@mist,263,258,0	warp	b11_b9	1,1,1@mist,272,246
1@mist,269,246,0	warp	b9_b26	1,1,1@mist,143,265
1@mist,140,265,0	warp	b26_b5	1,1,1@mist,209,200
1@mist,195,206,0	warp	b18_b35	1,1,1@mist,169,341
1@mist,166,341,0	warp	b35_b36	1,1,1@mist,198,354
1@mist,201,354,0	warp	b36_b37-1	1,1,1@mist,83,224
1@mist,87,224,0	warp	b37-1_b35	1,1,1@mist,169,341
1@mist,84,273,0	warp	b37-2_c1	1,1,1@mist,98,287
1@mist,111,298,0	warp	c2_c3	1,1,1@mist,82,307
1@mist,79,307,0	warp	c3_c7	1,1,1@mist,284,89
1@mist,281,89,0	warp	c7_c12	1,1,1@mist,264,291
1@mist,261,291,0	warp	c12_c2	1,1,1@mist,115,298
1@mist,326,275,0	warp	c13-1_b13	1,1,1@mist,277,112
1@mist,294,293,0	warp	c13-2_c5	1,1,1@mist,78,330
1@mist,81,330,0	warp	c5_c17	1,1,1@mist,308,330
1@mist,308,327,0	warp	c17_c10	1,1,1@mist,309,165
1@mist,306,165,0	warp	c10_c13-2	1,1,1@mist,298,293
1@mist,46,316,0	warp	c4_c6	1,1,1@mist,113,328
1@mist,95,328,0	warp	c6_c9	1,1,1@mist,342,157
1@mist,339,157,0	warp	c9_c16	1,1,1@mist,343,328
1@mist,343,325,0	warp	c16_c8-2	1,1,1@mist,346,120
1@mist,344,123,0	warp	c8-2_c19	1,1,1@mist,277,343
1@mist,256,313,0	warp	c18_c15	1,1,1@mist,343,307
1@mist,342,305,0	warp	c15_cKEY	1,1,1@mist,332,121
1@mist,335,120,0	warp	cKEY_c18	1,1,1@mist,259,313
1@mist,317,89,0	warp	c8-1_c14	1,1,1@mist,339,284
1@mist,339,281,0	warp	c14_c11-1	1,1,1@mist,332,172
1@mist,329,171,0	warp	c11-1_c8-2	1,1,1@mist,346,120
1@mist,301,237,0	warp	b8_b2	1,1,1@mist,289,170

// Disable Garden Tree warps.
// Custom, but saves processing and lines.
1@mist,1,1,0	script	#mist_warp_init	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("a25_a18-1");
	disablenpc instance_npcname("a27-2_af1-2");
	disablenpc instance_npcname("a15-2_a4-1");
	disablenpc instance_npcname("a4-2_a11");
	disablenpc instance_npcname("b4-2_b2");
	disablenpc instance_npcname("b7_bf1-2");
	disablenpc instance_npcname("b31_b16");
	disablenpc instance_npcname("b27-3_bN-1");
	disablenpc instance_npcname("b6-1_b33");
	disablenpc instance_npcname("b5_b14");
	disablenpc instance_npcname("c1_c13-1");
	disablenpc instance_npcname("c13-3_c4");
	disablenpc instance_npcname("c19_c4");
	disablenpc instance_npcname("#to_bif02");
	disablenpc instance_npcname("#mist_warp_init");
	end;
}
1@mist,247,123,0	warp	a25_a18-1	1,1,1@mist,164,66	//Tom
1@mist,225,98,0	warp	a27-2_af1-2	1,1,1@mist,144,194	//Tomba
1@mist,159,184,0	warp	a15-2_a4-1	1,1,1@mist,49,37	//Remi
1@mist,61,39,0	warp	a4-2_a11	1,1,1@mist,68,113	//Rem
1@mist,230,179,0	warp	b4-2_b2	1,1,1@mist,289,170	//Ron
1@mist,285,225,0	warp	b7_bf1-2	1,1,1@mist,72,274	//Rover
1@mist,161,316,0	warp	b31_b16	1,1,1@mist,108,203	//Mona
1@mist,204,299,0	warp	b27-3_bN-1	1,1,1@mist,169,314	//Namon
1@mist,221,236,0	warp	b6-1_b33	1,1,1@mist,233,324	//Neoron
1@mist,206,200,0	warp	b5_b14	1,1,1@mist,298,142	//Spyder
1@mist,95,287,0	warp	c1_c13-1	1,1,1@mist,323,276	//Tito
1@mist,324,325,0	warp	c13-3_c4	1,1,1@mist,49,316	//Pumba
1@mist,280,344,0	warp	c19_c4	1,1,1@mist,49,316	//Tete
1@mist,345,186,0	warp	#to_bif02	1,1,bif_fild02,151,121	//The Gardeners' Tree
