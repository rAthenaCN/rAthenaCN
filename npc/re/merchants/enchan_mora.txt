//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Mora Enchants
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds enchantments to various items in exchange for
//= Mora coins.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//= 1.1 Optimized. [Euphy]
//= 1.1a Bug fixes. [Lemongrass]
//= 1.2 Bug fixes (bugreport:7930) [DeadlySilence]
//============================================================ 

// Arch Bishop Enchants :: coin_arc
//============================================================
mora,88,89,5	script	秘密的守护者#pa082	495,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	disable_items;
	if (countitem(6380) < 10) {
		mes "[技巧]";
		mes "大主教的遗迹装备功能主要都是为了防御和支援!";
		next;
		mes "[技巧]";
		mes "就像其他监护人, 他们可能会以莫拉硬币为货币出售他们的文物, 所以收集莫拉硬币!";
		next;
		mes "[技巧]";
		mes "如果你收集超过10个莫拉硬币, 可以到我这里来交易!";
		close;
	}
	mes "[技巧]";
	mes "光明! 你看起来好像已经准备好, 来获取我们的遗物了.";
	next;
	mes "[技巧]";
	mes "戒指, 鞋, 披肩, 长袍, 盾, 武器.";
	mes "选择上述6样装备之一.";
	next;
	mes "[技巧]";
	mes "依据天气情况和我的心情, 我会随机给你的装备2个类型.";
	next;
	mes "[技巧]";
	mes "做为交换, 我需要10个莫拉硬币!";
	next;
	switch(select("- 我认为我还没准备好.:- 我已经准备好了用莫拉银币来交换了.:- 交换我们的戒指.")) {
	case 1:
		mes "[技巧]";
		mes "再见!";
		close;
	case 2:
		mes "[技巧]";
		// What does this even mean? (custom dialogue below)
		//mes "You influence what part are you?";
		mes "你想要什么样的遗物?";
		next;
		switch(select("- 我还没准备好.:- 指环:- 鞋子:- 披肩:- 长袍:- 盾:- 武器")) {
		case 1:
			mes "[技巧]";
			mes "带着光明去吧.";
			close;
		case 2:
			if (countitem(2864) || countitem(2865) || countitem(2866)) {
				mes "[技巧]";
				mes "您所拥有的指环, 足以以让你访问我们的遗迹.!";
				close;
			}
			if (Class == Job_Arch_Bishop || Class == Job_Arch_Bishop_T || Class == Job_Baby_Bishop) {
				setarray .@items[0],2864,2865,2866; //Light_Of_Cure,Seal_Of_Cathedral,Ring_Of_Archbishop
				break;
			} else {
				mes "[技巧]";
				mes "我们宁愿是只与大主教打交道.";
				close;
			}
		case 3:
			setarray .@items[0],2471,2472; //Shoes_Of_Affection,Shoes_Of_Judgement
			break;
		case 4:
			setarray .@items[0],2569,2570; //Shawl_Of_Affection,Shawl_Of_Judgement
			break;
		case 5:
			setarray .@items[0],15029,15030; //Robe_Of_Affection,Robe_Of_Judgement
			break;
		case 6:
			setarray .@items[0],2156; //Bible_Of_Promise1
			break;
		case 7:
			setarray .@items[0],1657,16013; //Wand_Of_Affection,Mace_Of_Judgement
			break;
		}
		set .@item, .@items[rand(getarraysize(.@items))];
		delitem 6380,10; //Mora_Coin
		getitem .@item,1;
		mes "[技巧]";
		mes "您已经获得 ^aa00aa"+getitemname(.@item)+"^000000. 愿圣光的祝福与你同在.";
		close;
	case 3:
		if (Class == Job_Arch_Bishop || Class == Job_Arch_Bishop_T || Class == Job_Baby_Bishop) {
			mes "[技巧]";
			mes "我们可以用硬币交换那些失控的戒指.";
			next;
			mes "[技巧]";
			mes "你看上去依然对指环有控制力, 所以我们无法进行交换.";
			close;
		}
		     if (countitem(2864)) set .@item,2864; //Light_Of_Cure
		else if (countitem(2865)) set .@item,2865; //Seal_Of_Cathedral
		else if (countitem(2866)) set .@item,2866; //Ring_Of_Archbishop
		else {
			mes "[技巧]";
			mes "你想改变什么?";
			close;
		}
		mes "[技巧]";
		mes "你有1个 "+getitemname(.@item)+". 你确定想把它换成10个莫拉币吗?";
		next;
		switch(select("- 我没准备好:- 交换我的 "+getitemname(.@item))) {
		case 1:
			mes "[技巧]";
			mes "如果你改变了主意, 请选择重复来停止.";
			close;
		case 2:
			delitem .@item,1;
			getitem 6380,10; //Mora_Coin
			mes "[技巧]";
			mes "我认为这是交易.";
			close;
		}
	}
}

mora,96,74,5	script	遗迹大师#pa0829	495,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	disable_items;
	mes "[遗迹大师]";
	mes "我为大主教附魔遗迹装备, 这将花费2个莫拉币, 同时需要遗迹装备允许我去附魔它.";
	next;
	switch(select("- 了解更多详情请:- 请附魔我的遗物装备..")) {
	case 1:
		mes "[遗迹大师]";
		mes "让我简单介绍一下, 用2个莫拉币和遗迹装备, 我可以在遗迹装备上为你附魔.";
		next;
		mes "[遗迹大师]";
		mes "即使这个遗迹装备已经附魔, 我们将重新进行附魔, 不核对他的状态..";
		next;
		mes "[遗迹大师]";
		mes "你有一个已经附魔过的遗迹装备, 请将它安全的保存好后再来..";
		next;
		mes "[遗迹大师]";
		mes "啊, 还有一件事! 我有可能会失败, 如果这样的话你将永远失去这个遗迹装备..";
		close;
	case 2:
		mes "[遗迹大师]";
		mes "好吧, 如果技能运用合理的话, 即使失败你将会得到一个新的遗迹装备.";
		next;
		mes "[遗迹大师]";
		mes "如果你有1个 幸运的仪式, 你现在就可以召唤了!";
		next;
		set .@select, select("- 请稍微等等!:- 治愈之光:- 大教堂圣印:- 大主教戒指:- 慈爱之靴:- 审判之靴:- 慈爱披肩:- 审判披肩:- 慈爱法袍:- 审判长袍:- 誓约之书第1卷:- 慈爱之锤:- 审判之锤");
		switch(.@select) {
		case 1:
			mes "[遗迹大师]";
			mes "当然. 带着光明去吧.";
			close;
		case 2:
		case 3:
		case 4:
			setarray .@items[0],2864,2865,2866; //Light_Of_Cure,Seal_Of_Cathedral,Ring_Of_Archbishop
			setarray .@special[0],4803,4804,4805; //Highness_Heal_3sec,Coluceo_Heal30,Heal_Amount2
			set .@item, .@items[.@select-2];
			set .@sp, .@special[.@select-2];
			set .@enchant_type,1;
			break;
		case 5:
		case 6:
		case 7:
		case 8:
		case 9:
		case 10:
		case 11:
			setarray .@items[0],2471,2472,2569,2570,15029,15030,2156; //Shoes_Of_Affection,Shoes_Of_Judgement,Shawl_Of_Affection,Shawl_Of_Judgement,Robe_Of_Affection,Robe_Of_Judgement,Bible_Of_Promise1
			set .@item, .@items[.@select-5];
			set .@enchant_type,2;
			break;
		case 12:
			set .@item,1657; //Wand_Of_Affection
			set .@enchant_type,3;
			break;
		case 13:
			set .@item,16013; //Mace_Of_Judgement
			set .@enchant_type,4;
			break;
		}
		if (countitem(6380) < 2 || countitem(.@item) == 0) {
			mes "[遗迹大师]";
			mes "需要2个莫拉硬币, 我可以在各方面强化你的遗迹套装!";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		delitem 6380,2; //Mora_Coin
		delitem .@item,1;
		switch(.@enchant_type) {
		case 1:
			set .@i, rand(1,1487);
			if (.@i <= 1024) {
				// Basic combinations
				// (raw: .@i increments by 16, 4 repeats)
				setarray .@enchant_slot2[0],4711,4720,4721,4740; //Inteligence2,Dexterity1,Dexterity2,Vitality1
				setarray .@enchant_slot3[0],.@sp,4799,4766,4788; //[sp],HP500,Atk2,Mdef6
				setarray .@enchants[2],
					.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
					.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
			}
			else if (.@i <= 1040) setarray .@enchants[2],.@sp,.@sp; //[sp] [sp]
			else if (.@i <= 1487) {
				mes "[遗迹大师]";
				mes "有时候事情的发展可能不能如你所愿. 希望下次会变的更好. 再见.";
				close;
			}
			break;
		case 2:
			set .@i, rand(1,1487);
			if (.@i <= 1024) {
				// Basic combinations
				// (raw: .@i increments by 16)
				setarray .@enchant_slot1[0],4710,4711,4720,4721; //Inteligence1,Inteligence2,Dexterity1,Dexterity2
				setarray .@enchant_slot2[0],4711,4720,4721,4740; //Inteligence2,Dexterity1,Dexterity2,Vitality1
				setarray .@enchant_slot3[0],4764,4799,4766,4788; //Critical5,HP500,Atk2,Mdef6
				setarray .@enchants[1],
					.@enchant_slot1[rand(getarraysize(.@enchant_slot1))],
					.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
					.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
			}
			else if (.@i <= 1026) setarray .@enchants[1],4761,4761,4761; //Matk2 Matk2 Matk2
			else if (.@i <= 1028) setarray .@enchants[1],4712,4713,4713; //Inteligence3 Inteligence4 Inteligence4
			else if (.@i <= 1030) setarray .@enchants[1],4712,4761,4761; //Inteligence3 Matk2 Matk2
			else if (.@i <= 1032) setarray .@enchants[1],4712,4713,4761; //Inteligence3 Inteligence4 Matk2
			else if (.@i <= 1034) setarray .@enchants[1],4722,4723,4723; //Dexterity3 Dexterity4 Dexterity4
			else if (.@i <= 1036) setarray .@enchants[1],4722,4703,4703; //Dexterity3 Strength4 Strength4
			else if (.@i <= 1038) setarray .@enchants[1],4722,4767,4767; //Dexterity3 Atk3 Atk3
			else if (.@i <= 1040) setarray .@enchants[1],4767,4767,4767; //Atk3 Atk3 Atk3
			else if (.@i <= 1487) {
				mes "[遗迹大师]";
				mes "有时候事情的发展可能不能如你所愿. 希望下次会变的更好. 再见.";
				close;
			}
			break;
		case 3:
			set .@i, rand(1,2852);
			if (.@i <= 1984) {
				// Basic combinations
				// (raw: .@i increments by 31, 4 repeats)
				setarray .@enchant_slot2[0],4720,4740,4741,4801; //Dexterity1,Vitality1,Vitality2,SP100
				setarray .@enchant_slot3[0],4710,4711,4721,4760; //Inteligence1,Inteligence2,Dexterity2,Matk1
				setarray .@enchants[2],
					.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
					.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
			}
			else if (.@i <= 1986) setarray .@enchants[2],4761,4761; //Matk2 Matk2
			else if (.@i <= 1988) setarray .@enchants[2],4761,4723; //Matk2 Dexterity4
			else if (.@i <= 1990) setarray .@enchants[2],4761,4714; //Matk2 Inteligence5
			else if (.@i <= 1992) setarray .@enchants[2],4714,4714; //Inteligence5 Inteligence5
			else if (.@i <= 1994) setarray .@enchants[2],4714,4723; //Inteligence5 Dexterity4
			else if (.@i <= 1996) setarray .@enchants[2],4723,4723; //Dexterity4 Dexterity4
			else if (.@i <= 2852) {
				mes "[遗迹大师]";
				mes "有时候事情的发展可能不能如你所愿. 希望下次会变的更好. 再见.";
				close;
			}
			break;
		case 4:
			set .@i, rand(1,2852);
			if (.@i <= 1984) {
				// Basic combinations
				// (raw: .@i increments by 31, 4 repeats)
				setarray .@enchant_slot2[0],4720,4740,4741,4701; //Dexterity1,Vitality1,Vitality2,Strength2
				setarray .@enchant_slot3[0],4700,4701,4721,4767; //Strength1,Strength2,Dexterity2,Atk3
				setarray .@enchants[2],
					.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
					.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
			}
			else if (.@i <= 1986) setarray .@enchants[2],4767,4767; //Atk3 Atk3
			else if (.@i <= 1988) setarray .@enchants[2],4767,4723; //Atk3 Dexterity4
			else if (.@i <= 1990) setarray .@enchants[2],4767,4704; //Atk3 Strength5
			else if (.@i <= 1992) setarray .@enchants[2],4704,4704; //Strength5 Strength5
			else if (.@i <= 1994) setarray .@enchants[2],4704,4723; //Strength5 Dexterity4
			else if (.@i <= 1996) setarray .@enchants[2],4723,4723; //Dexterity4 Dexterity4
			else if (.@i <= 2852) {
				mes "[遗迹大师]";
				mes "有时候事情的发展可能不能如你所愿. 希望下次会变的更好. 再见.";
				close;
			}
			break;
		}
		getitem2 .@item,1,1,0,0,0,.@enchants[1],.@enchants[2],.@enchants[3];
		mes "[遗迹大师]";
		mes "附魔过程很顺利, 你非常幸运!";
		close;
	}
}

// Warlock Enchants :: coin_warrock
//============================================================
mora,104,76,4	script	遗迹守护#p	495,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	if (countitem(6380) < 10) {
		mes "[遗迹守护]";
		mes "我的职责是守护大法师们的遗迹装备.";
		next;
		mes "[遗迹守护]";
		mes "好吧, 这其实不是什么大不了的事情. 我所给的遗迹装备肯定值这些莫拉硬币.";
		next;
		mes "[遗迹守护]";
		mes "等你什么时候有10个莫拉币的时候, 再来和我交易吧.";
		close;
	}
	mes "[遗迹守护]";
	mes "噢~ 看上去你已经装备好了大法师的遗迹装备了.";
	next;
	mes "[遗迹守护]";
	mes "你可以选择下述4种装备类别:";
	mes "法杖, 装饰品, 鞋子, 法袍.";
	next;
	mes "[遗迹守护]";
	mes "对于每种类型有四种可供选择, 但是每天可选择的类型不同, 取决于我的心情.";
	next;
	mes "[遗迹守护]";
	mes "你想用莫拉币购买遗迹装备吗?";
	next;
	if(select("- 可能要晚点... 我还有事情要做.:- 是的, 我要.") == 1) {
		mes "[遗迹守护]";
		mes "好的, 一会见.";
		close;
	}
	mes "[遗迹守护]";
	mes "你需要购买什么类型的装备?";
	next;
	switch(select("- 离开:- 鞋子:- 装饰品:- 衣服:- 法杖")) {
	case 1:
		mes "[遗迹守护]";
		mes "好的, 再见.";
		close;
 	case 2:
		setarray .@items[0],2467,2468,2469,2470; //Golden_Rod_Shoes,Aqua_Shoes,Crimson_Shoes,Forest_Shoes
		set .@plural,1;
		break;
	case 3:
		setarray .@items[0],2859,2860,2861,2862; //Golden_Rod_Orb,Aqua_Orb,Crimson_Orb,Forest_Orb
		break;
	case 4:
		setarray .@items[0],15025,15026,15027,15028; //Golden_Rod_Robe,Aqua_Robe,Crimson_Robe,Forest_Robe
		break;
	case 5:
		setarray .@items[0],2007,2008,2009,2010; //Golden_Rod_Staff,Aqua_Staff,Crimson_Staff,Forest_Staff
		break;
	}
	if (countitem(6380) < 10) {
		mes "[遗迹守护]";
		mes "等你筹集足够的莫拉币再来找我吧?";
		close;
	}
	set .@item, .@items[rand(getarraysize(.@items))];
	delitem 6380,10; //Mora_Coin
	getitem .@item,1;
	mes "[遗迹守护]";
	mes "我有 ^aa00aa"+getitemname(.@item)+"^000000 今天可出售. 我希望 "+((.@plural)?"它们":"它")+" 会派上用场...";
	close;
}

mora,99,93,5	script	遗迹工匠#pa0829	495,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	disable_items;
	mes "[遗迹工匠]";
	mes "我的工作就是给遗迹装备注入新的力量. 如果你有足够的硬币, 你就可以将一些物品增强.";
	next;
	if(select("- 告诉我更多的信息.:- 我想强化我的物品.") == 1) {
		mes "[遗迹工匠]";
		mes "让我解释一下. 一次附魔需要花费2个莫拉硬币.";
		next;
		set .@info_only,1;
	}
	mes "[遗迹工匠]";
	mes "无论现有的装备是否附魔我都会进行加工.";
	next;
	mes "[遗迹工匠]";
	mes "所有如果你有1个自己非常中意的遗迹装备, 就先把它存放好再来找我.";
	next;
	mes "[遗迹工匠]";
	mes "还有一件事, 如果附魔失败遗迹装备可能损毁. 请牢记这一点.";
	if (.@info_only)
		close;
	next;
	set .@select, select("- 下一次吧.:- 黄金领主之杖:- 流水之杖:- 赤红之杖:- 森林之杖:- 黄金领主之靴:- 流水之靴:- 赤红之靴:- 森林之靴:- 黄金领主之珠:- 流水之珠:- 赤红之珠:- 森林之珠:- 黄金领主之袍:- 流水之袍:- 赤红之袍:- 森林之袍");
	switch(.@select) {
	case 1:
		mes "[遗迹工匠]";
		mes "如您所愿.";
		close;
	case 2:
	case 3:
	case 4:
	case 5:
		setarray .@items[0],2007,2008,2009,2010; //Golden_Rod_Staff,Aqua_Staff,Crimson_Staff,Forest_Staff
		set .@enchant_type,1;
		break;
	case 6:
	case 7:
	case 8:
	case 9:
		setarray .@items[0],2467,2468,2469,2470; //Golden_Rod_Shoes,Aqua_Shoes,Crimson_Shoes,Forest_Shoes
		set .@enchant_type,2;
		break;
	case 10:
	case 11:
	case 12:
	case 13:
		setarray .@items[0],2859,2860,2861,2862; //Golden_Rod_Orb,Aqua_Orb,Crimson_Orb,Forest_Orb
		set .@enchant_type,2;
		break;
	case 14:
	case 15:
	case 16:
	case 17:
		setarray .@items[0],15025,15026,15027,15028; //Golden_Rod_Robe,Aqua_Robe,Crimson_Robe,Forest_Robe
		set .@enchant_type,2;
		break;
	}
	set .@item, .@items[(.@select-2)%4];
	if (countitem(6380) < 2 || countitem(.@item) == 0) {
		mes "[遗迹工匠]";
		mes "你必须为了对一件遗迹装备附魔而支付2个莫拉硬币.";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	delitem 6380,2; //Mora_Coin
	delitem .@item,1;
	switch(.@enchant_type) {
	case 1:
		set .@i, rand(1,2847);
		if (.@i <= 1984) {
			// Basic combinations
			// (raw: .@i increments by 31, 4 repeats)
			setarray .@enchant_slot2[0],4720,4796,4710,4801; //Dexterity1,HP200,Inteligence1,SP100
			setarray .@enchant_slot3[0],4786,4760,4711,4721; //Mdef2,Matk1,Inteligence2,Dexterity2
			setarray .@enchants[2],
				.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
				.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
		}
		else if (.@i <= 1986) setarray .@enchants[2],4713,4761; //Inteligence4 Matk2
		else if (.@i <= 1988) setarray .@enchants[2],4713,4713; //Inteligence4 Inteligence4
		else if (.@i <= 1990) setarray .@enchants[2],4761,4761; //Matk2 Matk2
		else if (.@i <= 1992) setarray .@enchants[2],4761,4713; //Matk2 Inteligence4
		else if (.@i <= 2847) {
			mes "[遗迹工匠]";
			mes "噢不...他损坏了...好吧, 悲剧发生了. 希望下次能有好运.";
			close;
		}
		break;
	case 2:
		set .@i, rand(1,2858);
		if (.@i <= 1984) {
			// Basic combinations
			// (raw: .@i increments by 31)
			setarray .@enchant_slot1[0],4710,4711,4720,4721; //Inteligence1,Inteligence2,Dexterity1,Dexterity2
			setarray .@enchant_slot2[0],4720,4796,4710,4801; //Dexterity1,HP200,Inteligence1,SP100
			setarray .@enchant_slot3[0],4786,4760,4711,4721; //Mdef2,Matk1,Inteligence2,Dexterity2
			setarray .@enchants[1],
				.@enchant_slot1[rand(getarraysize(.@enchant_slot1))],
				.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
				.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
		}
		else if (.@i <= 1986) setarray .@enchants[1],4712,4713,4761; //Inteligence3 Inteligence4 Matk2
		else if (.@i <= 1988) setarray .@enchants[1],4712,4713,4713; //Inteligence3 Inteligence4 Inteligence4
		else if (.@i <= 1990) setarray .@enchants[1],4712,4761,4761; //Inteligence3 Matk2 Matk2
		else if (.@i <= 1992) setarray .@enchants[1],4712,4761,4713; //Inteligence3 Matk2 Inteligence4
		else if (.@i <= 1994) setarray .@enchants[1],4722,4713,4761; //Dexterity3 Inteligence4 Matk2
		else if (.@i <= 1996) setarray .@enchants[1],4722,4713,4713; //Dexterity3 Inteligence4 Inteligence4
		else if (.@i <= 1998) setarray .@enchants[1],4722,4761,4761; //Dexterity3 Matk2 Matk2
		else if (.@i <= 2000) setarray .@enchants[1],4722,4761,4713; //Dexterity3 Matk2 Inteligence4
		else if (.@i <= 2858) {
			mes "[遗迹工匠]";
			mes "噢不...他损坏了...好吧, 悲剧发生了. 希望下次能有好运.";
			close;
		}
		break;
	}
	getitem2 .@item,1,1,0,0,0,.@enchants[1],.@enchants[2],.@enchants[3];
	mes "[遗迹工匠]";
	mes "看上去我的努力成功了.";
	close;
}

// Rune Knight, Guillotine Cross, & Ranger Enchants :: new_artifact
//============================================================
mora,152,97,5	script	权利监护者#pa082	515,{
	if (checkweight(1201,1) == 0) {
		mes "你必须减少你所携带的物品种类, 然后再继续.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你的负重不够了, 请清理.";
		close;
	}
	if (countitem(6380) < 10) {
		mes "[权利监护者]";
		mes "我给符文骑士, 十字切割者, 和游侠 来完成他们的遗迹装备附魔任务.";
		next;
		mes "[权利监护者]";
		mes "你可以通过使用莫拉硬币在我这里换取遗迹装备.";
		next;
		mes "[权利监护者]";
		mes "如果你有10个以上的莫拉硬币, 你就可以和我兑换遗迹装备.";
		close;
	}
	mes "[权利监护者]";
	mes "我想你已经准备好兑换遗迹装备了.";
	next;
	mes "[权利监护者]";
	mes "根据你的职业分类, 这里有不同的遗迹装备.";
	next;
	mes "[权利监护者]";
	mes "^ff0000仔细选择, 这些遗迹装备来之不易^000000. 你想兑换哪个职业的装备?";
	next;
	setarray .@jobs$[0],"符文骑士","十字切割着","游侠";
	set .@job, select("- 取消:- 符文骑士:- 十字切割着:- 游侠")-2;
	if (.@job == -1) {
		mes "[权利监护者]";
		mes "稍后再联系我.";
		close;
	}
	mes "[权利监护者]";
	mes .@jobs$[.@job]+"?";
	mes "你想要附魔哪个职业的装备?"; 
	next;
	switch(.@job) {
	case 0: // Rune Knight
		setarray .@items[0],2475,2476,2574,2575,15036,15037,2883,2884;
		set .@i, select("- 取消:- 乌鲁兹符文护胫:- 珀斯符文护胫:- 乌鲁兹符文斗篷:- 珀斯符文斗篷:- 乌鲁兹符文铠甲:- 珀斯符文铠甲:- 乌鲁兹符文印记:- 珀斯符文印记")-2;
		break;
	case 1: // Guillotine Cross
		setarray .@items[0],2477,2478,2577,2578,15038,15039,2886,2887;
		set .@i, select("- 取消:- 萨巴斯的靴子:- 奈博的靴子:- 萨巴斯的披风:- 奈博的披肩:- 萨巴斯的外袍:- 奈博的外袍:- 萨巴斯的指环:- 奈博的指环")-2;
		break;
	case 2: // Ranger
		setarray .@items[0],2479,2480,2580,2581,15042,15043,2890,2891;
		set .@i, select("- 取消:- 白翼之靴:- 黑翼之靴:- 白翼斗篷:- 黑翼斗篷:- 白翼之衣:- 黑翼之衣:- 白翼胸针:- 黑翼胸针")-2;
		break;
	}
	if (.@i == -1) {
		mes "[权利监护者]";
		mes "稍后再联系我.";
		close;
	}
	if (countitem(6380) < 10) {
		mes "[权利监护者]";
		mes "难道我没有告诉过你交换遗迹装备的莫拉币数量?";
		close;
	}
	delitem 6380,10; //Mora_Coin
	getitem .@items[.@i],1;
	mes "[权利监护者]";
	mes "感谢你支付10个莫拉硬币. 这是你的遗迹装备 "+.@jobs$[.@job]+" 职业. 等你有更多的莫拉硬币时记得再来找我.";
	close;
}

mora,148,98,3	script	遗迹研究员#new	521,{
	if (checkweight(1201,1) == 0) {
		mes "你必须减少你所携带的物品种类, 然后再继续.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你的负重不够了, 请清理.";
		close;
	}
	disable_items;
	mes "[遗迹研究员]";
	mes "见到你真高兴.";
	mes "我在莫拉村研究古代遗迹和文物. 冒险家们对这村子的很多新发现, 让我非常兴奋..";
	next;
	switch(select("- 告诉我更多的信息...:- 给遗迹装备新的能力.:- 重置遗迹装备的附魔.")) {
	case 1:
		mes "[遗迹研究员]";
		mes "在莫拉村发现了新的古代文物, 你难道没听说吗?";
		next;
		mes "[遗迹研究员]";
		mes "如果你将这些远古的文物应用到装备上, 将激发现有装备的 隐藏能力.";
		next;
		mes "[遗迹研究员]";
		mes "为了能够进行持续的研究并提供开发装备能力的附魔服务, 我们开始接受以莫拉硬币为货币单位的有偿服务服务...";
		next;
		mes "[遗迹研究员]";
		mes "对于遗迹装备的改造和附魔, 我们每次收取的费用是 100,000 Zeny 和1个莫拉币..";
		next;
		mes "[遗迹研究员]";
		mes "我会尽可能的将遗迹装备保护好, 并提升它们的能力.";
		close;
	case 2:
		set @mora_enchant_select,1;
		break;
	case 3:
		set @mora_enchant_select,2;
		break;
	}
	if (Zeny < 100000 || countitem(6380) == 0) {
		mes "[遗迹研究员]";
		mes "我不会免费提供这些服务的. 100,000 Zeny 和1个莫拉币. 这个要求非常高?";
		close;
	}
	mes "[遗迹研究员]";
	mes "好的, 你准备好了是吧? 在我开始之前, 请你先选择一下你需要附魔的装备类型?";
	next;
	set .@i, select("- 我不需要附魔装备:- 1. 武器:- 2. 鞋子:- 3. 披肩:- 4. 铠甲:- 5. 装饰品")-2;
	if (.@i == -1) {
		mes "[遗迹研究员]";
		mes "请先穿上需要附魔的装备.";
		close;
	}
	setarray .@parts[0], EQI_HAND_R, EQI_SHOES, EQI_GARMENT, EQI_ARMOR, EQI_ACC_L;
	set .@part, .@parts[.@i];
	if (!getequipisequiped(.@part)) {
		mes "[遗迹研究员]";
		mes "请先穿上需要附魔的装备.";
		close;
	}
	set .@equip_id, getequipid(.@part);
	set @mora_equip_part, .@part;

	//callsub L_Socket,<enchant type>,<bonus enchant type>,<4-x enchants possible>;
	// Enchant types:
	//   [1]ATK [2]CRIT [3]EVA [4]Healer [5]Spell 1 [6]Assist 1 [7]Assist 2 [8]STR [9]Range [10]Physical [11]Spell 2
	switch(.@part) {
	case EQI_HAND_R:
		switch(.@equip_id) {
			case 1660: callsub L_Socket,4,5,1;  //Wand_Of_Affection2
			case 2011: callsub L_Socket,5,11,1; //Golden_Rod_Staff2
			case 2012: callsub L_Socket,5,11,1; //Aqua_Staff2
			case 2013: callsub L_Socket,5,11,1; //Crimson_Staff2
			case 2014: callsub L_Socket,5,11,1; //Forest_Staff2
			case 16018: callsub L_Socket,1,5,1; //Mace_Of_Judgement2
		}
		break;
	case EQI_SHOES:
		switch(.@equip_id) {
			case 2475: callsub L_Socket,6,8,1;  //Ur_Greave
			case 2476: callsub L_Socket,6,10,1; //Peuz_Greave
			case 2477: callsub L_Socket,6,2,1;  //Sabah_Shoes
			case 2478: callsub L_Socket,6,1,1;  //Nab_Shoes
			case 2479: callsub L_Socket,6,2,1;  //White_Wing_Boots
			case 2480: callsub L_Socket,6,1,1;  //Black_Wing_Boots
		}
		break;
	case EQI_GARMENT:
		switch(.@equip_id) {
			case 2574: callsub L_Socket,3,8,1; //Ur_Manteau
			case 2575: callsub L_Socket,3,2,1; //Peuz_Manteau
			case 2577: callsub L_Socket,3,2,1; //Sabah_Hood
			case 2578: callsub L_Socket,3,2,1; //Nab_Hood
			case 2580: callsub L_Socket,3,1,1; //White_Wing_Manteau
			case 2581: callsub L_Socket,3,1,1; //Black_Wing_Manteau
		}
		break;
	case EQI_ARMOR:
		switch(.@equip_id) {
			case 15036: callsub L_Socket,8,1,1;  //Ur_Plate
			case 15037: callsub L_Socket,10,2,1; //Peuz_Plate
			case 15038: callsub L_Socket,2,10,1; //Sabah_Cloth
			case 15039: callsub L_Socket,1,2,1;  //Nab_Cloth
			case 15042: callsub L_Socket,2,9,1;  //White_Wing_Suits
			case 15043: callsub L_Socket,1,9,1;  //Black_Wing_Suits
		}
		break;
	case EQI_ACC_L:
		switch(.@equip_id) {
			case 2883: callsub L_Socket,6,0,2; //Ur_Seal
			case 2884: callsub L_Socket,6,0,2; //Peuz_Seal
			case 2886: callsub L_Socket,6,0,2; //Sabah_Ring
			case 2887: callsub L_Socket,6,0,2; //Nab_Ring
			case 2890: callsub L_Socket,6,0,2; //White_Wing_Brooch
			case 2891: callsub L_Socket,6,0,2; //Black_wing_Brooch
		}
		break;
	}
	mes "[遗迹研究员]";
	mes "^990099"+getequipname(.@part)+"^000000??";
	mes "我真想好好研究下这个装备...";
	set @mora_equip_part,0;
	close;

L_Socket:
	set .@select, @mora_enchant_select;
	set .@enchant_type, getarg(0);
	.@bonus_enchant = getarg(1);
	.@allowed_slot = getarg(2);
	set .@part, @mora_equip_part;
	set .@equip_id, getequipid(.@part);
	set .@equip_name$, getitemname(.@equip_id);
	set .@equip_refine, getequiprefinerycnt(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	set @mora_equip_part,0;
	set @mora_enchant_select,0;

	if (.@select == 1) {
		// If refine is +9 or higher, unlock bonus enchant type.
		if (.@equip_refine >= 9 && .@bonus_enchant > 0) {
			if (.@enchant_type < 1 || .@enchant_type > 11 || .@bonus_enchant > 11) {
				mes "[遗迹研究员]";
				mes "嗯, 看来有地方出错了.";
				close;
			}
			setarray .@enchant_type$[1],"攻击类型","暴击类型","闪避类型","治疗类型","法术能力1","辅助能力1","辅助能力2","力量","范围类型","物理类型","法术能力2";
			mes "[遗迹研究员]";
			mes "这是对装备一次巨大的提升. 在这个情况下可以进行多次附魔. 你想要附魔哪部分?";
			next;
			switch(select(.@enchant_type$[.@enchant_type],.@enchant_type$[.@bonus_enchant])) {
			case 1:
				break;
			case 2:
				set .@enchant_type, .@bonus_enchant;
				break;
			}
		}
		if (.@equip_card[3] == 0 && .@allowed_slot < 4) {
			set .@slot,4;
			mes "[遗迹研究员]";
			mes "尝试进一步加强装备的能力. 之前的附魔不会受到任何影响. 你是否想继续?";
		} else if (.@equip_card[2] == 0 && .@allowed_slot < 3) {
			set .@slot,3;
			mes "[遗迹研究员]";
			mes "尝试第2次附魔. 之前的附魔不会受到任何影响.";
			next;
			mes "[遗迹研究员]";
			mes "有一定概率会失败. 是否继续?";
		} else if (.@equip_card[1] == 0 && .@allowed_slot < 2) {
			set .@slot,2;
			mes "[遗迹研究员]";
			mes "让我们开始第三次附魔. 这是我能够给的最大的能力了, 但请牢记, ^990000如果这次失败, 之前的附魔会被摧毁^000000. 是否继续?";
		} else {
			mes "[遗迹研究员]";
			mes "这个遗迹装备已经附魔到了极限. 在现有情况下不可能再被强化了.";
			close;
		}
		next;
		if(select("- 我会稍后再来.:- 请继续!") == 1) {
			mes "[遗迹研究员]";
			mes "如果你改变主意, 请再来找我.";
			close;
		}
		switch(.@enchant_type) {
		case 1: //Attack
			     if (.@slot == 4) set .@i, rand(1,525);
			else if (.@slot == 3) set .@i, rand(301,655);
			else if (.@slot == 2) set .@i, rand(451,750);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4700; //Strength1
			else if (.@i < 201) set .@enchant,4811; //Fighting_Spirit1
			else if (.@i < 301) set .@enchant,4819; //Atk1
			else if (.@i < 351) set .@enchant,4701; //Strength2
			else if (.@i < 401) set .@enchant,4810; //Fighting_Spirit2
			else if (.@i < 451) set .@enchant,4766; //Atk2
			else if (.@i < 476) set .@enchant,4702; //Strength3
			else if (.@i < 501) set .@enchant,4809; //Fighting_Spirit3
			else if (.@i < 526) set .@enchant,4767; //Atk3
			else if (.@i < 626) set .@enchant,0;
			else if (.@i < 641) set .@enchant,4703; //Strength4
			else if (.@i < 656) set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 661) set .@enchant,4704; //Strength5
			else if (.@i < 666) set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 668) set .@enchant,4705; //Strength6
			else if (.@i < 670) set .@enchant,4821; //Fighting_Spirit6
			else set .@enchant,9;
			break;
		case 2: //Critical
			     if (.@slot == 4) set .@i, rand(1,470);
			else if (.@slot == 3) set .@i, rand(201,610);
			else if (.@slot == 2) set .@i, rand(381,750);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4750; //Luck1
			else if (.@i < 201) set .@enchant,4700; //Strength1
			else if (.@i < 261) set .@enchant,4751; //Luck2
			else if (.@i < 321) set .@enchant,4701; //Strength2
			else if (.@i < 351) set .@enchant,4752; //Luck3
			else if (.@i < 381) set .@enchant,4702; //Strength3
			else if (.@i < 411) set .@enchant,4764; //Critical5
			else if (.@i < 441) set .@enchant,4818; //Sharp1
			else if (.@i < 471) set .@enchant,4752; //Luck3
			else if (.@i < 571) set .@enchant,0;
			else if (.@i < 586) set .@enchant,4753; //Luck4
			else if (.@i < 601) set .@enchant,4754; //Luck5
			else if (.@i < 606) set .@enchant,4765; //Critical7
			else if (.@i < 611) set .@enchant,4817; //Sharp2
			else if (.@i < 616) set .@enchant,4703; //Strength4
			else if (.@i < 618) set .@enchant,4816; //Sharp3
			else set .@enchant,9;
			break;
		case 3: //Evasion
			     if (.@slot == 4) set .@i, rand(1,525);
			else if (.@slot == 3) set .@i, rand(301,670);
			else if (.@slot == 2) set .@i, rand(451,800);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4859; //Evasion1
			else if (.@i < 201) set .@enchant,4750; //Luck1
			else if (.@i < 301) set .@enchant,4730; //Agility1
			else if (.@i < 351) set .@enchant,4860; //Evasion3
			else if (.@i < 401) set .@enchant,4751; //Luck2
			else if (.@i < 451) set .@enchant,4731; //Agility2
			else if (.@i < 476) set .@enchant,4731; //Agility2
			else if (.@i < 501) set .@enchant,4752; //Luck3
			else if (.@i < 526) set .@enchant,4732; //Agility3
			else if (.@i < 626) set .@enchant,0;
			else if (.@i < 641) set .@enchant,4762; //Evasion6
			else if (.@i < 656) set .@enchant,4753; //Luck4
			else if (.@i < 671) set .@enchant,4733; //Agility4
			else if (.@i < 676) set .@enchant,4763; //Evasion12
			else if (.@i < 681) set .@enchant,4754; //Luck5
			else if (.@i < 683) set .@enchant,4734; //Agility5
			else set .@enchant,9;
			break;
		case 4: //Healer
			     if (.@slot == 4) set .@i, rand(1,375);
			else if (.@slot == 3) set .@i, rand(201,535);
			else if (.@slot == 2) set .@i, rand(301,650);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4710; //Inteligence1
			else if (.@i < 201) set .@enchant,4720; //Dexterity1
			else if (.@i < 251) set .@enchant,4711; //Inteligence2
			else if (.@i < 301) set .@enchant,4721; //Dexterity2
			else if (.@i < 326) set .@enchant,4805; //Heal_Amount2
			else if (.@i < 351) set .@enchant,4712; //Inteligence3
			else if (.@i < 376) set .@enchant,4722; //Dexterity3
			else if (.@i < 476) set .@enchant,0;
			else if (.@i < 491) set .@enchant,4760; //Matk1
			else if (.@i < 506) set .@enchant,4850; //Heal_Amount3
			else if (.@i < 521) set .@enchant,4713; //Inteligence4
			else if (.@i < 536) set .@enchant,4723; //Dexterity4
			else if (.@i < 541) set .@enchant,4761; //Matk2
			else if (.@i < 546) set .@enchant,4851; //Heal_Amount4
			else if (.@i < 548) set .@enchant,4806; //Matk3
			else if (.@i < 550) set .@enchant,4852; //Heal_Amount5
			else set .@enchant,9;
			break;
		case 5: //Spell 1
			     if (.@slot == 4) set .@i, rand(1,600);
			else if (.@slot == 3) set .@i, rand(401,760);
			else if (.@slot == 2) set .@i, rand(401,766);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4710; //Inteligence1
			else if (.@i < 201) set .@enchant,4720; //Dexterity1
			else if (.@i < 301) set .@enchant,4795; //HP100
			else if (.@i < 401) set .@enchant,4815; //Spell1
			else if (.@i < 451) set .@enchant,4711; //Inteligence2
			else if (.@i < 501) set .@enchant,4721; //Dexterity2
			else if (.@i < 551) set .@enchant,4796; //HP200
			else if (.@i < 601) set .@enchant,4814; //Spell2
			else if (.@i < 701) set .@enchant,0;
			else if (.@i < 716) set .@enchant,4712; //Inteligence3
			else if (.@i < 731) set .@enchant,4722; //Dexterity3
			else if (.@i < 746) set .@enchant,4797; //HP300
			else if (.@i < 761) set .@enchant,4813; //Spell3
			else if (.@i < 763) set .@enchant,4713; //Inteligence4
			else if (.@i < 765) set .@enchant,4723; //Dexterity4
			else if (.@i < 767) set .@enchant,4812; //Spell4
			else set .@enchant,9;
			break;
		case 6: //Assist 1
			     if (.@slot == 4) set .@i, rand(1,520);
			else if (.@slot == 3) set .@i, rand(321,720);
			else if (.@slot == 2) set .@i, rand(521,850);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 81)  set .@enchant,4792; //Def6
			else if (.@i < 161) set .@enchant,4787; //Mdef4
			else if (.@i < 241) set .@enchant,4801; //SP100
			else if (.@i < 321) set .@enchant,4796; //HP200
			else if (.@i < 371) set .@enchant,4700; //Strength1
			else if (.@i < 421) set .@enchant,4720; //Dexterity1
			else if (.@i < 471) set .@enchant,4730; //Agility1
			else if (.@i < 521) set .@enchant,4740; //Vitality1
			else if (.@i < 621) set .@enchant,0;
			else if (.@i < 646) set .@enchant,4793; //Def9
			else if (.@i < 671) set .@enchant,4788; //Mdef6
			else if (.@i < 696) set .@enchant,4802; //SP150
			else if (.@i < 721) set .@enchant,4797; //HP300
			else if (.@i < 731) set .@enchant,4701; //Strength2
			else if (.@i < 741) set .@enchant,4721; //Dexterity2
			else if (.@i < 751) set .@enchant,4731; //Agility2
			else set .@enchant,9;
			break;
		case 7: //Assist 2
			     if (.@slot == 4) set .@i, rand(1,520);
			else if (.@slot == 3) set .@i, rand(321,720);
			else if (.@slot == 2) set .@i, rand(521,850);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 81)  set .@enchant,4792; //Def6
			else if (.@i < 161) set .@enchant,4787; //Mdef4
			else if (.@i < 241) set .@enchant,4801; //SP100
			else if (.@i < 321) set .@enchant,4796; //HP200
			else if (.@i < 371) set .@enchant,4710; //Inteligence1
			else if (.@i < 421) set .@enchant,4720; //Dexterity1
			else if (.@i < 471) set .@enchant,4730; //Agility1
			else if (.@i < 521) set .@enchant,4740; //Vitality1
			else if (.@i < 621) set .@enchant,0;
			else if (.@i < 646) set .@enchant,4793; //Def9
			else if (.@i < 671) set .@enchant,4788; //Mdef6
			else if (.@i < 696) set .@enchant,4802; //SP150
			else if (.@i < 721) set .@enchant,4797; //HP300
			else if (.@i < 731) set .@enchant,4711; //Inteligence2
			else if (.@i < 741) set .@enchant,4721; //Dexterity2
			else if (.@i < 751) set .@enchant,4741; //Vitality2
			else set .@enchant,9;
			break;
		case 8: //Strength
			     if (.@slot == 4) set .@i, rand(1,525);
			else if (.@slot == 3) set .@i, rand(301,685);
			else if (.@slot == 2) set .@i, rand(451,800);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4740; //Vitality1
			else if (.@i < 201) set .@enchant,4797; //HP300
			else if (.@i < 301) set .@enchant,4791; //Def3
			else if (.@i < 351) set .@enchant,4741; //Vitality2
			else if (.@i < 401) set .@enchant,4798; //HP400
			else if (.@i < 451) set .@enchant,4792; //Def6
			else if (.@i < 476) set .@enchant,4742; //Vitality3
			else if (.@i < 501) set .@enchant,4793; //Def9
			else if (.@i < 526) set .@enchant,4799; //HP500
			else if (.@i < 626) set .@enchant,0;
			else if (.@i < 641) set .@enchant,4742; //Vitality3
			else if (.@i < 656) set .@enchant,4743; //Vitality4
			else if (.@i < 671) set .@enchant,4794; //Def12
			//else if (.@i < 686) set .@enchant,01; // ??
			else if (.@i < 688) set .@enchant,4744; //Vitality5
			//else if (.@i < 690) set .@enchant,02; // ??
			else set .@enchant,9;
			break;
		case 9: //Range
			     if (.@slot == 4) set .@i, rand(1,470);
			else if (.@slot == 3) set .@i, rand(201,610);
			else if (.@slot == 2) set .@i, rand(321,750);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4750; //Luck1
			else if (.@i < 201) set .@enchant,4720; //Dexterity1
			else if (.@i < 261) set .@enchant,4751; //Luck2
			else if (.@i < 321) set .@enchant,4721; //Dexterity2
			else if (.@i < 351) set .@enchant,4752; //Luck3
			else if (.@i < 381) set .@enchant,4722; //Dexterity3
			else if (.@i < 411) set .@enchant,4764; //Critical5
			else if (.@i < 441) set .@enchant,4832; //Expert_Archer1
			else if (.@i < 471) set .@enchant,4753; //Luck4
			else if (.@i < 571) set .@enchant,0;
			else if (.@i < 586) set .@enchant,4723; //Dexterity4
			else if (.@i < 601) set .@enchant,4833; //Expert_Archer2
			else if (.@i < 606) set .@enchant,4765; //Critical7
			else if (.@i < 611) set .@enchant,4834; //Expert_Archer3
			else if (.@i < 616) set .@enchant,4724; //Dexterity5
			else if (.@i < 618) set .@enchant,4835; //Expert_Archer4
			else set .@enchant,9;
			break;
		case 10: //Physical
			     if (.@slot == 4) set .@i, rand(1,600);
			else if (.@slot == 3) set .@i, rand(401,800);
			else if (.@slot == 2) set .@i, rand(601,930);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4791; //Def3
			else if (.@i < 201) set .@enchant,4730; //Agility1
			else if (.@i < 301) set .@enchant,4750; //Luck1
			else if (.@i < 401) set .@enchant,4795; //HP100
			else if (.@i < 451) set .@enchant,4792; //Def6
			else if (.@i < 501) set .@enchant,4731; //Agility2
			else if (.@i < 551) set .@enchant,4751; //Luck2
			else if (.@i < 601) set .@enchant,4796; //HP200
			else if (.@i < 701) set .@enchant,0;
			else if (.@i < 726) set .@enchant,4793; //Def9
			else if (.@i < 751) set .@enchant,4732; //Agility3
			else if (.@i < 776) set .@enchant,4752; //Luck3
			else if (.@i < 801) set .@enchant,4797; //HP300
			else if (.@i < 816) set .@enchant,4733; //Agility4
			else if (.@i < 831) set .@enchant,4753; //Luck4
			else if (.@i < 833) set .@enchant,4807; //Atk_Speed1
			else set .@enchant,9;
			break;
		case 11: //Spell 2
			     if (.@slot == 4) set .@i, rand(1,600);
			else if (.@slot == 3) set .@i, rand(401,760);
			else if (.@slot == 2) set .@i, rand(401,766);
			else {
				mes "[遗迹研究员]";
				mes "发生一个未知错误.";
				close;
			}
			     if (.@i < 101) set .@enchant,4711; //Inteligence2
			else if (.@i < 201) set .@enchant,4721; //Dexterity2
			else if (.@i < 301) set .@enchant,4796; //HP200
			else if (.@i < 401) set .@enchant,4814; //Spell2
			else if (.@i < 451) set .@enchant,4712; //Inteligence3
			else if (.@i < 501) set .@enchant,4722; //Dexterity3
			else if (.@i < 551) set .@enchant,4760; //Matk1
			else if (.@i < 601) set .@enchant,4813; //Spell3
			else if (.@i < 701) set .@enchant,0;
			else if (.@i < 716) set .@enchant,4713; //Inteligence4
			else if (.@i < 731) set .@enchant,4723; //Dexterity4
			else if (.@i < 746) set .@enchant,4761; //Matk2
			else if (.@i < 761) set .@enchant,4812; //Spell4
			else if (.@i < 763) set .@enchant,4714; //Inteligence5
			else if (.@i < 765) set .@enchant,4724; //Dexterity5
			else if (.@i < 767) set .@enchant,4806; //Matk3
			else set .@enchant,9;
			break;
		default:
			mes "[遗迹研究员]";
			mes "好吧, 我想我弄错了...";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) ||
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		if (.@equip_card[3] == 0 && .@allowed_slot < 4) {
			set .@equip_card[3],.@enchant;
		} else if (.@equip_card[2] == 0 && .@allowed_slot < 3) {
			set .@equip_card[2],.@enchant;
			if (.@enchant == 0) {
				set .@equip_card[3],0;
			}
		} else if (.@equip_card[1] == 0 && .@allowed_slot < 2) {
			set .@equip_card[1],.@enchant;
			if (.@enchant == 0) {
				set .@equip_card[2],0;
				set .@equip_card[3],0;
			}
		} else {
			mes "[遗迹研究员]";
			mes "我想可能这个遗迹装备有限制.";
			close;
		}
		if (Zeny < 100000 || countitem(6380) == 0) {
			mes "[遗迹研究员]";
			mes "如果你不能付费用的话, 我可没办法继续工作.";
			close;
		}
		delitem 6380,1; //Mora_Coin
		set Zeny, Zeny-100000;
		delequip .@part;
		if (.@enchant == 9) {
			specialeffect2 EF_SUI_EXPLOSION;
			mes "[遗迹研究员]";
			mes "这!";
 			mes "我... 我很抱歉, 这个装备损坏了.";
			close;
		}
		if (.@enchant == 0) {
			specialeffect2 EF_SHIELDCHARGE;
			mes "[遗迹研究员]";
			mes "由于不符合力量的平衡原理, 现有的附魔被毁坏了. 这非常不幸, 希望下次能够走运.";
		} else {
			specialeffect2 EF_REPAIRWEAPON;
			mes "[遗迹研究员]";
			// Replacing original dialogue since it doesn't make sense.
			//mes "The artifact ^990000"+.@slot+" will have the first enhancement retained.";
			mes "我已经完成了遗迹装备的附魔 ^990000"+.@equip_name$+"^000000 在插槽 "+.@slot+".";
		}

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		if (Zeny < 100000 || countitem(6380) == 0) {
			mes "[遗迹研究员]";
			mes "你必须为了这个服务而付费.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[遗迹研究员]";
		mes "提升的能力依然存在.";
		delitem 6380,1; //Mora_Coin
		set Zeny, Zeny-100000;
		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	}
}

// Arch Bishop & Warlock Empowering :: relic_change
//============================================================
mora,124,82,4	script	遗迹收藏家#blan	522,{
	if (checkweight(1201,1) == 0) {
		mes "你必须减少你所携带的物品种类, 然后再继续.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你的负重不够了, 请清理.";
		close;
	}
	disable_items;
	emotion ET_DELIGHT;
	mes "这些莫拉的文物看上去是如此的完美!";
	mes "但我知道你希望他们能够变的更强!";
	next;
	mes "[遗迹收藏家]";
	mes "向冒险家致敬. 我们会成为很好的朋友, 我会指导你如何加强你的遗迹装备!";
	next;
	switch(select("- 让我们干起来吧!:- 强化遗迹套装?:- 不, 这不适合我!")) {
	case 1:
		break;
	case 2:
		emotion ET_OK;
		mes "[遗迹收藏家]";
		mes "你看上去像个不错的大主教.";
		next;
		mes "[遗迹收藏家]";
		mes "我们在这里研究莫拉的古代遗物已经超过200年了. 为了得到完美的 +9 遗物!";
		next;
		select("咽口水... +9?!");
		emotion ET_HUK,1;
		mes "[遗迹收藏家]";
		mes "我的生意是收集并提取这些文物的精华.";
		next;
		emotion ET_CRY;
		mes "[遗迹收藏家]";
		mes "通过试验来获得完美遗迹装备是我毕生的心愿.";
		mes "能够看到祖先的遗物获得附加的能力, 这是多让人高兴的事情啊.";
		next;
		select("我能想象那有多高兴.");
		emotion ET_GOODBOY;
		mes "[遗迹收藏家]";
		mes "谢谢你人类.";
		next;
		mes "[遗迹收藏家]";
		mes "我已经在这里工作了200年了, 垂手可得的神器的时代已经一去不复返了, 遗迹装备和遗迹工匠已经无法卖给我更多有用的东西了...";
		next;
		emotion ET_THINK;
		mes "[遗迹收藏家]";
		mes "不过也许人类的来访能够带来一些新的契机!";
		next;
		select("人类... Stock?");
		emotion ET_QUESTION,1;
		mes "[遗迹收藏家]";
		mes "是的! 如果人类能带给我新的文物, 我想我有可能能够完成我的终极成就!";
		next;
		select("终极成就?");
		emotion ET_QUESTION,1;
		emotion ET_OK;
		mes "[遗迹收藏家]";
		mes "耐心等待, 我们就会得到...";
		next;
		mes "[遗迹收藏家]";
		mes "我是一个收藏家, 通过收购来获得各类文物. 但我最大的成就是能够让这些遗迹装备获得更强的能力!";
		mes "到目前为止我只完善了几个遗迹装备的完美能力.";
		next;
		mes "[遗迹收藏家]";
		mes "你很可能就是那个能够帮我完成完美计划的人类.";
		next;
		emotion ET_OTL;
		mes "[遗迹收藏家]";
		mes "我太热爱这份工作了...";
		next;
		mes "[遗迹收藏家]";
		mes "+1, +2, +3, +4, +5, +6 这些级别的精炼还无法承载我的技艺.";
		next;
		emotion ET_OTL,0;
		mes "[遗迹收藏家]";
		mes "但是 +7! +7 是最低的一个精炼级别, 可以勉强的承载我的技艺.";
		next;
		select("我会再度被滥用...");
		emotion ET_SCRATCH,1;
		mes "[遗迹收藏家]";
		mes "你是对的! 我知道你们人类很乐意帮助自己的朋友, 即使你的朋友没有提出帮忙的要求...";
		next;
		select("我不确定如果...");
		emotion ET_QUESTION,1;
		mes "[遗迹收藏家]";
		mes "当然, 我接受你的帮助!  找到...";
		next;
		mes "- 黄金权杖 -";
		mes "- 流水之杖 -";
		mes "- 赤红之杖 -";
		mes "- 森林之杖 -";
		mes "- 慈爱之杖 -";
		mes "- 审判之锤 -";
		next;
		select("还有其他事情吗?");
		emotion ET_QUESTION,1;
		mes "[遗迹收藏家]";
		mes "除了 +7, 有助于推动这一进程的还有一些非常特殊的遗迹装备附魔.";
		next;
		mes "[遗迹收藏家]";
		mes "除非 +7 或者特定附魔, 否则我是无法做到这点的.";
		next;
		select("那精炼值会被保留?");
		emotion ET_HUK,1;
		emotion ET_FRET;
		mes "[遗迹收藏家]";
		mes "不, 但你将得到的会是更好的!";
		next;
		mes "[遗迹收藏家]";
		mes "你知道我们现在正在寻找更多的文物遗迹?";
		next;
		emotion ET_QUESTION;
		mes "[遗迹收藏家]";
		mes "来吧继续!";
		next;
		setarray .@artifacts[0],1657,16013,2007,2008,2009,2010;
		setarray .@empowered[0],1660,16018,2011,2012,2013,2014;
		while(1) {
			set .@i, select("- 我不感兴趣:- 慈爱权杖:- 审判之锤:- 黄金权杖:- 流水之杖:- 赤红之杖:- 森林之杖")-2;
			if (.@i == -1) {
				emotion ET_QUESTION;
				mes "[遗迹收藏家]";
				mes "好吧, 也许有一天你会.";
				close;
			}
			mes "[遗迹收藏家]";
			mes getitemname(.@artifacts[.@i])+" 我的附魔必须在精炼值+7或者更高的遗迹装备上.";
			next;
			switch(.@i) {
			case 0:
				setarray .@enchants[0],4761,4723,4714;
				set .@str$, "MATK+2%, DEX+4, INT+5";
				break;
			case 1:
				setarray .@enchants[0],4767,4723,4704;
				set .@str$, "ATK+3%, DEX+4, STR+5";
				break;
			default:
				setarray .@enchants[0],4761,4713;
				set .@str$, "MATK+2%, INT+4";
				break;
			}
			mes "[遗迹收藏家]";
			mes "还要能承载这些附魔: "+.@str$+" 由我这里.";
			next;
			select(getitemname(.@empowered[.@i])+" 属性?");
			callsub L_ShowInfo, .@empowered[.@i];
			mes "[遗迹收藏家]";
			mes "如果你有的话, 请让我来附魔它, 谢谢你了.";
			next;
		}
	case 3:
		mes "[遗迹收藏家]";
		mes "祝你游戏快乐. 再见~";
		close;
	}
	emotion ET_SPARK;
	mes "[遗迹收藏家]";
	mes "让我看看你带来了什么.";
	next;
	if (!getequipisequiped(EQI_HAND_R)) {
		emotion ET_FRET;
		mes "[遗迹收藏家]";
		mes "我不清楚你究竟想干什么... 你没有任何东西可以让我帮你附魔的.";
		close;
	}
	set .@equip_id, getequipid(EQI_HAND_R);
	setarray .@equip_card[2], getequipcardid(EQI_HAND_R,2),getequipcardid(EQI_HAND_R,3);
	switch(.@equip_id) {
	case 2007: //Golden_Rod_Staff
	case 2008: //Aqua_Staff
	case 2009: //Crimson_Staff
	case 2010: //Forest_Staff
		if (.@equip_card[2] == 4761 || .@equip_card[3] == 4761 || //Matk2
		    .@equip_card[2] == 4713 || .@equip_card[3] == 4713)   //Inteligence4
			set .@empowered,.@equip_id+4; //Golden_Rod_Staff2,Aqua_Staff2,Crimson_Staff2,Forest_Staff2
		break;
	case 1657: //Wand_Of_Affection
		if (.@equip_card[2] == 4761 || .@equip_card[3] == 4761 || //Matk2
		    .@equip_card[2] == 4714 || .@equip_card[3] == 4714 || //Inteligence5
		    .@equip_card[2] == 4723 || .@equip_card[3] == 4723)   //Dexterity4
			set .@empowered,1660; //Wand_Of_Affection2
		break;
	case 16013: //Mace_Of_Judgement
		if (.@equip_card[2] == 4723 || .@equip_card[3] == 4723 || //Dexterity4
		    .@equip_card[2] == 4704 || .@equip_card[3] == 4704 || //Strength5
		    .@equip_card[2] == 4767 || .@equip_card[3] == 4767)   //Atk3
			set .@empowered,16018; //Mace_Of_Judgement2
		break;
	default:
		emotion ET_FRET;
		mes "[遗迹收藏家]";
		mes "我无法为一件不属于遗迹装备的武器进行附魔.";
		close;
	}
	if (getequiprefinerycnt(EQI_HAND_R) < 7) {
		emotion ET_FRET;
		mes "[遗迹收藏家]";
		mes "你的遗迹装备精炼值至少需要达到 +7!";
		close;
	}
	if (!.@empowered) {
		emotion ET_FRET;
		mes "[遗迹收藏家]";
		mes "这些属性不在我的研究范围内, 尝试重新附魔.";
		close;
	}
	emotion ET_SEXY;
	mes "[遗迹收藏家]";
	mes "我太兴奋了!";
	mes "你带来了我想要的一切!!";
	next;
	emotion ET_OK;
	mes "[遗迹收藏家]";
	mes "如果你准备好了, 我将告诉你我可以如何改造你的 +"+getequiprefinerycnt(EQI_HAND_R)+" 遗迹装备!";
	next;
	mes "[遗迹收藏家]";
	mes "我将拿走你的 ^FF0000"+getequipname(EQI_HAND_R)+"^000000 作为交换, 你准备好了吗?";
	next;
	switch(select("- 不!:- 是的, 准备好了!:- 之后会发生什么?")) {
	case 1:
		emotion ET_FRET;
		mes "[遗迹收藏家]";
		mes "你一定是在开玩笑... 你无视我的理想...";
		close;
	case 2:
		break;
	case 3:
		callsub L_ShowInfo, .@empowered;
		mes "[遗迹收藏家]";
		mes "你确定要继续?";
		next;
		if(select("- 不, 我不要.:- 是的, 干吧!") == 1) {
			emotion ET_FRET;
			mes "[遗迹收藏家]";
			mes "你一定是在开玩笑... 你无视我的理想...";
			close;
		}
		break;
	}
	emotion ET_SMILE;
	delequip EQI_HAND_R;
	getitem .@empowered,1;
	mes "[遗迹收藏家]";
	mes "这是完全可以做到, 你肯定会很高兴! 再见朋友~";
	close;

L_ShowInfo:
	mes "[遗迹收藏家]";
	mes getitemname(getarg(0));
	switch(getarg(0)) {
	case 1660: //Wand_Of_Affection2
		mes "单手法杖 / 攻击 : 30";
		mes "重量 : 50 / 无性别限制";
		mes "仅大主教可用";
		mes "武器等级 : 4 / ^ff0000装备等级: 130^000000";
		mes "MATK + 180, INT + 4.";
		mes "增加 20% 治疗奖励.";
		mes "和慈爱之袍、慈爱之鞋、慈爱披肩一起装备时, 恢复效果+45%, 治愈术的SP消耗增加50.";
		mes "受到物理和魔法攻击一定机率发动净化.";
		mes "圣礼、羔羊赞歌和大树赞歌的独立冷却时间减少3秒.";
		break;
	case 16018: //Mace_Of_Judgement2
		mes "锤子 / 攻击 : 170";
		mes "重量 : 120 / 无性别限制";
		mes "仅大主教可用";
		mes "武器等级 : 4 / ^ff0000装备等级: 130^000000";
		mes "MATK + 180, STR + 2, INT + 2.";
		mes "物理攻击和魔法攻击时, 一定机率在7秒对恶魔种族的伤害+40%.";
		mes "和审判之袍、审判之靴、审判披肩一起装备时, 对不死种族的物理和魔法伤害+30%.";
		mes "赞歌的伤害提高200%, 赞歌的SP消耗提高30.";
		break;
	case 2011: //Golden_Rod_Staff2
		mes "法杖 / 攻击 : 30";
		mes "重量 : 90 / 无性别限制";
		mes "大法师专用";
		mes "武器等级 : 4 / ^ff0000装备等级 : 130^000000";
		mes "INT + 5, MATK + 270.";
		mes "雷鸣术的伤害提高30%.";
		mes "和黄金领主外袍、黄金领主之靴、黄金领主法球一起装备时, 使用所有风属性技能的伤害提高60%.";
		mes "使用所有地属性技能的伤害降低60%, 对地属性攻击的抵抗力-50%.";
		break;
	case 2012: //Aqua_Staff2
		mes "法杖 / 攻击 : 30";
		mes "重量 : 90 / 无性别限制";
		mes "大法师专用";
		mes "武器等级 : 4 / ^ff0000装备等级: 130^000000";
		mes "INT + 5, MATK + 270.";
		mes "冰箭术和冰冻术的伤害提高30%.";
		mes "和水之外袍、水之靴、水之法球一起装备时, 使用所有水属性技能的伤害提高60%.";
		mes "使用所有风属性技能的伤害降低60%, 对风属性攻击的抵抗力-50%.";
		break;
	case 2013: //Crimson_Staff2
		mes "法杖 / 攻击 : 30";
		mes "重量 : 90 / 无性别限制";
		mes "大法师专用";
		mes "武器等级 : 4 / ^ff0000装备等级: 130^000000";
		mes "INT + 5, MATK + 270.";
		mes "火箭术和火球术的伤害提高30%.";
		mes "和赤红外袍、赤红之靴、赤红法球一起装备时, 使用所有火属性技能的伤害提高60%.";
		mes "使用所有火属性技能的伤害降低60%, 对火属性攻击的抵抗力-50%.";
		break;
	case 2014: //Forest_Staff2
		mes "法杖 / 攻击 : 30";
		mes "重量 : 90 / 无性别限制";
		mes "大法师专用";
		mes "武器等级 : 4 / ^ff0000装备等级: 130^000000";
		mes "INT + 5, MATK + 270.";
		mes "地震术和崩裂术的伤害提高30%.";
		mes "和森林外袍、森林之靴、森林法球一起装备时, 使用所有地属性技能的伤害提高60%.";
		mes "使用所有火属性技能的伤害降低60%, 对火属性攻击的抵抗力-50%.";
		break;
	}
	next;
	return;
}

// Other Enchants :: sangjo
//============================================================
function	script	F_Mora_Enchant	{
	specialeffect2 EF_REPAIRWEAPON;
	progressbar "ffff00",3;
	delitem 6380,5; //Mora_Coin
	delitem getarg(0),1;
	set .@i, rand(1,4568);
	if (.@i <= 3168) {
		// Basic combinations
		// (raw: .@i increments by 11)
		setarray .@enchant_slot1[0],4766,4767,4764,4765,4762,4763,4760,4761; //Atk2,Atk3,Critical5,Critical7,Evasion6,Evasion12,Matk1,Matk2
		setarray .@enchant_slot2[0],4720,4721,4710,4711,4750,4751; //Dexterity1,Dexterity2,Inteligence1,Inteligence2,Luck1,Luck2
		setarray .@enchant_slot3[0],4700,4701,4730,4731,4740,4741; //Strength1,Strength2,Agility1,Agility2,Vitality1,Vitality2
		setarray .@enchants[1],
			.@enchant_slot1[rand(getarraysize(.@enchant_slot1))],
			.@enchant_slot2[rand(getarraysize(.@enchant_slot2))],
			.@enchant_slot3[rand(getarraysize(.@enchant_slot3))];
	}
	else if (.@i <= 3179) setarray .@enchants[1],4761,4720,4700; //Matk2 Dexterity1 Strength1
	else if (.@i <= 3181) setarray .@enchants[1],4761,4712,4712; //Matk2 Inteligence3 Inteligence3
	else if (.@i <= 3183) setarray .@enchants[1],4765,4732,4732; //Critical7 Agility3 Agility3
	else if (.@i <= 3185) setarray .@enchants[1],4763,4752,4753; //Evasion12 Luck3 Luck4
	else if (.@i <= 3187) setarray .@enchants[1],4763,4742,4742; //Evasion12 Vitality3 Vitality3
	else if (.@i <= 3189) setarray .@enchants[1],4763,4722,4722; //Evasion12 Dexterity3 Dexterity3
	else if (.@i <= 3191) setarray .@enchants[1],4742,4742,4742; //Vitality3 Vitality3 Vitality3
	else if (.@i <= 3193) setarray .@enchants[1],4761,4722,4722; //Matk2 Dexterity3 Dexterity3
	else if (.@i <= 3195) setarray .@enchants[1],4767,4702,4702; //Atk3 Strength3 Strength3
	else if (.@i <= 3197) setarray .@enchants[1],4763,4732,4732; //Evasion12 Agility3 Agility3
	else if (.@i <= 4568) return 0;

	getitem2 getarg(0),1,1,0,0,0,.@enchants[1],.@enchants[2],.@enchants[3];
	return 1;
}
mora,105,176,4	script	裁缝大师#pa0829	509,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	if (countitem(6380) < 5 || countitem(15024) < 1) {
		mes "[裁缝大师]";
		mes "经过我手, 我能够将军装改造成艺术品. 我是一名裁缝大师, 为军装提供款式制作.";
		next;
		mes "[裁缝大师]";
		mes "名字里有什么? 我可以把你的军装变成一件艺术品 ^ff0000一套军装和5个莫拉硬币^000000.";
		if (countitem(6380)) {
			next;
			mes "[裁缝大师]";
			mes "我这里正好有一些军装的装饰品存货. 你有没有兴趣买一些? 只需要1个莫拉币. 非常便宜.";
			next;
			switch(select("- 下一次吧.:- 我想买一个.")) {
	 		case 1:
				mes "[裁缝大师]";
				mes "好的, 我明白.";
				mes "如果你改变主意了, 就来找我吧.";
				close;
			case 2:
				delitem 6380,1; //Mora_Coin
				getitem 15024,1; //Army_Padding
				mes "[裁缝大师]";
				mes "拿着吧.";
				mes "这非常漂亮和舒适. 请下次再来.";
				close;
			}
		}
		close;
	}
	mes "[裁缝大师]";
	mes "你是来这里将军装改造成艺术品的吗?";
	next;
	switch(select("- 请, 告诉我更多的信息.:- 是的, 我想.")) {
	case 1:
		mes "[裁缝大师]";
		mes "一套标准的军装不够暖和. 也不漂亮, 并且会损伤你的皮肤. 艺术价值? 这可粘不上边.";
		next;
		mes "[裁缝大师]";
		mes "因此, 我会重新缝制它们, 并且增加三个特色.";
		next;
		mes "[裁缝大师]";
		mes "给我 ^8888005个莫拉币^000000 和一件军装, 让我们一起给它们加点艺术色彩!";
		close;
	case 2:
		mes "[裁缝大师]";
		mes "我告诉你, 以防万一 - 我会做什么, 我需要做的是将你的军装修饰, 无论它原来是新的或者是旧的.";
		next;
		mes "[裁缝大师]";
		mes "如果你有很中意的装备不想改制的话, 就先把它存放好再来找我.";
		next;
		mes "[裁缝大师]";
		mes "还有一件事 - 如果我改造失败, 它会变成废品... 你就需要再买一件新的了.";
		next;
		switch(select("- 什么, 呢么匆忙?:- 好的! 让我们操作吧!!")) {
		case 1:
			mes "[裁缝大师]";
			mes "你怎么了?";
			mes "这里的村民已经让我够忙乎了.";
			close;
		case 2:
			if (countitem(6380) < 5 || countitem(15024) == 0) {
				mes "[裁缝大师]";
				mes "看上去你准备的东西不够啊.";
				next;
				mes "[裁缝大师]";
				mes "你至少需要有5个莫拉币和一件军用内衣, 这样我才可以帮你操作.";
				close;
			}
			if (callfunc("F_Mora_Enchant",15024) == 0) {
				mes "[裁缝大师]";
				mes "呃......";
				mes "不敢相信, 我失手了.";
				next;
				mes "[裁缝大师]";
				mes "我想一个人静静. 请自便!";
				close;
			}
			mes "[裁缝大师]";
			mes "我的作品太优秀了.";
			close;
		}
	}
}

mora,123,177,6	script	坠饰工匠#pa0829	509,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	if (countitem(6380) < 5 || countitem(2858) == 0) {
		mes "[坠饰工匠]";
		mes "我的工作是把这些石头给镶嵌到坠饰上去, 然后将它进行篆刻, 使其拥有特别的力量.";
		next;
		mes "[坠饰工匠]";
		mes "这个过程将会让你的装饰品获得比一般的装饰品更强大的威力.";
		next;
		mes "[坠饰工匠]";
		mes "如果你有兴趣, 我需要 ^ff00005个莫拉币^000000作为酬劳. 当然你还必须拥有一件守护者坠饰.";
		close;
	}
	mes "[坠饰工匠]";
	mes "你准备好让你的守护者坠饰升级了吗?";
	next;
	switch(select("- 请告诉我更多的信息.:- 是的, 我装备好了.")) {
	case 1:
		mes "[坠饰工匠]";
		mes "我的工作是把这些石头给镶嵌到坠饰上去,  然后将它进行篆刻, 使其拥有特别的力量.";
		next;
		mes "[坠饰工匠]";
		mes "这个过程将会让你的装饰品获得比一般的装饰品更强大的威力.";
		next;
		mes "[坠饰工匠]";
		mes "如果你有兴趣, 我需要 ^ff00005个莫拉币^000000作为酬劳. 当然你还必须拥有一件守护者坠饰.";
		close;
	case 2:
		mes "[坠饰工匠]";
		mes "我告诉你, 以防万一 - ";
		mes "如果你给我的守护者坠饰之前有强化过, 那它将失去之前的强化效果.";
		next;
		mes "[坠饰工匠]";
		mes "更糟糕的是, 它可能会失去其魔力, 成为一个普通的挂件. 当然, 石头可不像布料呢么容易损坏, 所以你不会失去挂件本身.";
		next;
		switch(select("- 什么, 呢么匆忙?:- 好的! 让我们操作吧!!")) {
		case 1:
			mes "[坠饰工匠]";
			mes "等你什么时候有空再来吧. 我总是能腾出一些时间的.";
			close;
		case 2:
			if (countitem(6380) < 5 || countitem(2858) == 0) {
				mes "[坠饰工匠]";
				mes "看上去你的准备不够充分啊.";
				next;
				mes "[坠饰工匠]";
				mes "你至少需要有5个莫拉币, 同时有一件守护者坠饰才能让我开工.";
				close;
			}
			if (callfunc("F_Mora_Enchant",2858) == 0) {
				getitem 2858,1; //Pendant_Of_Guardian
				mes "[坠饰工匠]";
				mes "什么~!!";
				mes "地属性出错了!!";
				close;
			}
			mes "[坠饰工匠]";
			mes "看看我的杰作!";
			close;
		}
	}
}

mora,134,166,4	script	巴宝莉 维斯特伍德#pa0829	509,{
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类过多, 请清理自己的背包后再来.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "你携带的物品过重, 你现在的负重不够, 请清理自己的背包后再来.";
		close;
	}
	if (countitem(6380) < 5 || countitem(2568) == 0) {
		mes "[巴宝莉 维斯特伍德]";
		mes "我是巴宝莉 维斯特伍德, 保留传统的制作工艺.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "我只处理高品质物品 - 不是从市场里面随便买来的那些.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "如果你有1个遗迹装备想获得更强的属性, 就带 ^ff00005个莫拉币^000000来找我吧.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "我可以用自己的双手改变它品质.";
		close;
	}
	mes "[巴宝莉 维斯特伍德]";
	mes "你有1个 洛奇的披肩 1042 特别版本. 你真的有一双敏锐的眼睛. 我可以马上开始工作?";
	next;
	switch(select("- 我不明白.:- 是的, 请.")) {
	case 1:
		mes "[巴宝莉 维斯特伍德]";
		mes "如我所述, 我只处理高品质物品 - 不是从市场里面随便买来的那些.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "这件洛奇的披肩非常棒. 这在洛奇的装饰品中属于限量版!";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "我的专长是花费小成本, 引导出装备隐藏的力量... ";
		mes "5个莫拉币.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "如果你改变主意了请来找我.";
		close;
	case 2:
		mes "[巴宝莉 维斯特伍德]";
		mes "我告诉你, 以防万一 - ";
		mes "如果你的洛奇的披肩曾经附魔过, 他原来的附魔会消失.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "更糟糕的是, 它可能会失去其所有的力量, 成为一条普通的披肩.";
		next;
		mes "[巴宝莉 维斯特伍德]";
		mes "当然, 洛奇的披肩品质非常好, 所以你不会在这个过程中失去披肩本身.";
		next;
		switch(select("- 也许以后, 先生...:- 我想让它更优秀!")) {
		case 1:
			mes "[巴宝莉 维斯特伍德]";
			mes "哈...!";
			mes "你又想获得最好的东西, 但又举棋不定? 好吧, 你有自己的想法.";
			close;
		case 2:
			if (countitem(6380) < 5 || countitem(2568) == 0) {
				mes "[巴宝莉 维斯特伍德]";
				mes "看上去你的准备不够充分.";
				next;
				mes "[巴宝莉 维斯特伍德]";
				mes "想获得最好的回报, 你至少需要准备1条洛奇的披肩和5个莫拉硬币.";
				close;
			}
			if (callfunc("F_Mora_Enchant",2568) == 0) {
				getitem 2568,1; //Muffler_Of_Roki
				mes "[巴宝莉 维斯特伍德]";
				mes "啊啊, 这不可能!";
				mes "我的! 我的披肩!....";
				next;
				mes "[巴宝莉 维斯特伍德]";
				mes "噢, 我对我的失误进行道歉. 我非常难过.";
				next;
				mes "[巴宝莉 维斯特伍德]";
				mes "幸好, 它还是我操作前的样子. 没有任何变形或损坏. 它毕竟是件好装备. 噢呵呵~";
				close;
			}
			mes "[巴宝莉 维斯特伍德]";
			mes "哇噢~ 任务完成!";
			close;
		}
	}
}
