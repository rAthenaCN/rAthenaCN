//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================
//= Upgrade Weapon Enchants
//===== By: ==================================================
//= Skorm
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds enchantments to Upgrade weapons.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Standardizing, grammar and bug fixes. [Euphy]
//============================================================

// Main NPC :: 201105_luk_enc
//============================================================
prt_in,26,73,3	script	恶魔附魔大师#prq	1_M_SMITH,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "你所带的物品太多, 请稍候再来尝试!";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "你的负重不够, 请稍候再来尝试!";
		close;
	}
	mes "[恶魔附魔大师]";
	mes "唔? 你在找我?";
	next;
	switch(select("- 初次见面请多多关照!:- 我听说你是最棒的!:- 请初始化附魔.")) {
	case 1:
		mes "[恶魔附魔大师]";
		mes "哈哈哈~ 当然, 我可不是一般的附魔大师.";
		next;
		mes "[恶魔附魔大师]";
		mes "连同你在内! 仅仅付钱给我, 我是不会帮你附魔的!!";
		next;
		mes "[恶魔附魔大师]";
		mes "你有没注意我的称号? 很少有人有这个称号是有原因的!";
		next;
		mes "[恶魔附魔大师]";
		mes "想我我出手, 你只能带着 ^0000ff附魔卷轴^000000 来才行!";
		next;
		mes "[恶魔附魔大师]";
		mes "不然, 我是不会出手的....";
		close;
	case 2:
		if (!countitem(6484)) {
			mes "[恶魔附魔大师]";
			mes "你难道不明白我的话? 只有附魔卷轴才能让我出手!";
			close;
		}
		set .@select,1;
		break;
	case 3:
		mes "[恶魔附魔大师]";
		if (Zeny < 100000) {
			mes "重置附魔需要 100,000 Zeny. 看上去你没带够钱...";
			close;
		}
		mes "重置附魔需要 100,000 Zeny, 并需要先确认该武器有无附魔!";
		next;
		if (select("- 让我想想.:- 重置!") == 1) {
			mes "[恶魔附魔大师]";
			mes "考虑清楚再来吧!";
			close;
		}
		set .@select,2;
		break;
	}
	set .@part, EQI_HAND_R;

	mes "[恶魔附魔大师]";
	if (!getequipisequiped(.@part)) {
		mes "你确定要附魔的武器已经装备上了吗?";
		close;
	}
	setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	if (!getequipisequiped(.@part)) {
		mes "清除装备附魔是一件非常危险的事情!";
		close;
	}
	set .@equip_id, getequipid(.@part);
	set .@item$, "|1292|1293|1294|1295|1296|1298|1394|1397|1398|1491|1493|1494|1585|1588|1590|1596|1599|2015|2017|2018|13071|13073|13074|13075|13076|13077|13078|13079|13081|13088|13089|13090|13092|13094|13115|13116|13118|13119|13120|13122|13124|16019|16020|16023|16024|16026|16030|16031|16032|16033|18112|18115|18116|18117|18120|18122|18123|21000|21003|21004|21005|21010|21011|";
	if (!compare(.@item$,"|"+.@equip_id+"|")) {
		mes "你的武器我不想碰!";
		close;
	}

	if (.@select == 1) {
		if (!countitem(6484)) {
			mes "你难道不明白我的话? 只有附魔卷轴才能让我出手!";
			close;
		}
		mes "选择你想附魔的类型?";
		next;
		switch(select("- 我再想想.:- 物理系:- 魔法系")) {
		case 1:
			mes "[恶魔附魔大师]";
			mes "等你改主意了再来找我吧!";
			close;
		case 2:
			set .@enc_type,1;
			break;
		case 3:
			set .@enc_type,2;
			break;
		}
		mes "[恶魔附魔大师]";
		if (.@equip_card[3]) {
			mes "这件武器已经附魔过了! 如果你想再次附魔, 需要先重置当前的附魔.";
			close;
		}
		mes "附魔过程可能失败, 并且 ^ff0000可能会降低精炼等级^000000, 不过武器和卡片不会受到任何损伤! 你确定好了要继续吗?";
		next;
		if (select("- 还是下次再说吧!:- 现在开始吧!") == 1) {
			mes "[恶魔附魔大师]";
			mes "等你下定决心后再来吧!";
			close;
		}
		if (.@equip_card[3]) {
			mes "[恶魔附魔大师]";
			mes "好像有点意外情况, 让我先看看.";
			close;
		}

		if (.@enc_type == 1) { // Physical Series
			set .@i, rand(1,1300);
			     if (.@i < 51)    set .@enchant,4734; //Agility5
			else if (.@i < 76)    set .@enchant,4735; //Agility6
			else if (.@i < 88)    set .@enchant,4736; //Agility7
			else if (.@i < 93)    set .@enchant,4737; //Agility8
			else if (.@i < 95)    set .@enchant,4738; //Agility9
			else if (.@i < 96)    set .@enchant,4739; //Agility10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4704; //Strength5
			else if (.@i < 341)   set .@enchant,4705; //Strength6
			else if (.@i < 366)   set .@enchant,4706; //Strength7
			else if (.@i < 378)   set .@enchant,4707; //Strength8
			else if (.@i < 383)   set .@enchant,4708; //Strength9
			else if (.@i < 384)   set .@enchant,4709; //Strength10
			else if (.@i < 434)   set .@enchant,4754; //Luck5
			else if (.@i < 459)   set .@enchant,4755; //Luck6
			else if (.@i < 471)   set .@enchant,4756; //Luck7
			else if (.@i < 476)   set .@enchant,4757; //Luck8
			else if (.@i < 478)   set .@enchant,4758; //Luck9
			else if (.@i < 479)   set .@enchant,4759; //Luck10
			else if (.@i < 679)   set .@enchant,4744; //Vitality5
			else if (.@i < 779)   set .@enchant,4745; //Vitality6
			else if (.@i < 829)   set .@enchant,4746; //Vitality7
			else if (.@i < 854)   set .@enchant,4747; //Vitality8
			else if (.@i < 866)   set .@enchant,4748; //Vitality9
			else if (.@i < 867)   set .@enchant,4749; //Vitality10
			else if (.@i < 967)   set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 992)   set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 1092)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1117)  set .@enchant,4836; //Expert_Archer5
			else if (.@i < 1217)  set .@enchant,4835; //Expert_Archer4
			else if (.@i < 1242)  set .@enchant,4836; //Expert_Archer5
			else set .@enchant,0;
		} else if (.@enc_type == 2) { // Magical Series
			set .@i, rand(1,1200);
			     if (.@i < 51)    set .@enchant,4714; //Inteligence5
			else if (.@i < 76)    set .@enchant,4715; //Inteligence6
			else if (.@i < 88)    set .@enchant,4716; //Inteligence7
			else if (.@i < 93)    set .@enchant,4717; //Inteligence8
			else if (.@i < 95)    set .@enchant,4718; //Inteligence9
			else if (.@i < 96)    set .@enchant,4719; //Inteligence10
			else if (.@i < 146)   set .@enchant,4724; //Dexterity5
			else if (.@i < 171)   set .@enchant,4725; //Dexterity6
			else if (.@i < 183)   set .@enchant,4726; //Dexterity7
			else if (.@i < 188)   set .@enchant,4727; //Dexterity8
			else if (.@i < 190)   set .@enchant,4728; //Dexterity9
			else if (.@i < 191)   set .@enchant,4729; //Dexterity10
			else if (.@i < 291)   set .@enchant,4734; //Agility5
			else if (.@i < 341)   set .@enchant,4735; //Agility6
			else if (.@i < 366)   set .@enchant,4736; //Agility7
			else if (.@i < 378)   set .@enchant,4737; //Agility8
			else if (.@i < 383)   set .@enchant,4738; //Agility9
			else if (.@i < 384)   set .@enchant,4739; //Agility10
			else if (.@i < 484)   set .@enchant,4754; //Luck5
			else if (.@i < 534)   set .@enchant,4755; //Luck6
			else if (.@i < 559)   set .@enchant,4756; //Luck7
			else if (.@i < 571)   set .@enchant,4757; //Luck8
			else if (.@i < 576)   set .@enchant,4758; //Luck9
			else if (.@i < 577)   set .@enchant,4759; //Luck10
			else if (.@i < 777)   set .@enchant,4744; //Vitality5
			else if (.@i < 877)   set .@enchant,4745; //Vitality6
			else if (.@i < 927)   set .@enchant,4746; //Vitality7
			else if (.@i < 952)   set .@enchant,4747; //Vitality8
			else if (.@i < 964)   set .@enchant,4748; //Vitality9
			else if (.@i < 969)   set .@enchant,4749; //Vitality10
			else if (.@i < 1069)  set .@enchant,4812; //Spell4
			else if (.@i < 1094)  set .@enchant,4826; //Spell5
			else if (.@i < 1119)  set .@enchant,4761; //Matk2
			else if (.@i < 1124)  set .@enchant,4806; //Matk3
			else set .@enchant,0;
		} else {
			mes "[恶魔附魔大师]";
			mes "唔! 这件物品有些问题, 请你自己先检查一下!";
			close;
		}
		mes "[恶魔附魔大师]";
		if (.@equip_card[3]) {
			mes "附魔成功了!";
			close;
		}
		if (!countitem(6484)) {
			mes "你难道不明白我的话? 只有附魔卷轴才能让我出手!!";
			close;
		}
		set .@equip_refine, getequiprefinerycnt(.@part);
		if (.@enchant == 0) {
			specialeffect EF_SHIELDCHARGE;
			mes "呃! 不可思议!! 失败了!! 请再试一次!";
			set .@lost_refine, rand(0,.@equip_refine);
			set .@equip_refine, .@equip_refine - .@lost_refine;
		} else {
			specialeffect EF_REPAIRWEAPON;
			mes "第^9900004^000000个插槽已经附魔好了!";
		}
		delitem 6484,1; //Enchant_Book

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) ||
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@enchant
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;

		if (.@lost_refine) {
			next;
			mes "[恶魔附魔大师]";
			mes "附魔失败降低了精炼等级 "+.@lost_refine+" 级"+((.@lost_refine == 1)?"":"s")+"! 不要沮丧!";
		}
		close;
	} else if (.@select == 2) {
		if (Zeny < 100000) {
			mes "你的钱不够重置附魔!!";
			close;
		}
		if (.@equip_card[3] < 4700) { // Armor Enchant System
			mes "这把武器没有附魔过!";
			close;
		}
		if (!getequipisequiped(.@part)) {
			mes "你现在就装备脱掉?";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		mes "附魔已经重置了.";
		set Zeny, Zeny - 100000;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) ||
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		set .@equip_refine, getequiprefinerycnt(.@part);
		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] 0
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],0;

		close;
	} else {
		mes "看上去, 你好像选错了职业??";
		close;
	}
}
