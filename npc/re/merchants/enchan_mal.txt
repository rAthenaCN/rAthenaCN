//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Malangdo Enchants
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.0.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds enchantments to many high level weapons in exchange
//= for Malangdo coins.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//= 1.0.1 Added 'delequip' command. [Euphy]
//= 1.0.2 Remove redundant usage of 'getarg' [Cydh]
//= 1.0.3 Fixed typo in 1.0.2 [Dastgir]
//============================================================ 

// Enchanter NPC :: mal_yong
//============================================================
malangdo,213,167,4	script	喵呦喵呦#mal	555,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "你携带的物品种类太多了. 继续之前你需要先整理下背包.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "满负重的情况下是无法继续的. 你解决负重问题后我们继续.";
		close;
	}
	mes "[喵呦喵呦]";
	mes "噢欢迎. 这里是喵呦喵呦新的附魔商店~ 我可以附魔武器包含4级的武器.";
	next;
	switch(select("- 商店的经营政策有什么变化?:- 附魔高级武器:- 重置附魔[需要新鲜水果]")) {
	case 1:
		mes "[喵呦喵呦]";
		mes "在以往的附魔过程中, 卡片和精炼等级经常会被毁掉. 这让我十分恼火. 然而, 今天我已经找到了更安全的方法.";
		next;
		mes "[喵呦喵呦]";
		mes "你曾经是否见到过有个人在附近的屋子里为人提供附魔的?";
		next;
		if(select("- 我曾经见过.:- 我从没见过.") == 2) {
			mes "[喵呦喵呦]";
			mes "那个屋子就在这里的东南面. 你为什么不去那里看看呢?";
			close;
		}
		mes "[喵呦喵呦]";
		mes "如果你到那里的话, 那里有一个专门为永恒之翼附魔的人. 当时那是举世无双的, 当时我认为这对我来说是天方夜谭.";
		next;
		mes "[喵呦喵呦]";
		mes "我做了他的佣人. 不分昼夜的学习如何附魔.";
		next;
		mes "[喵呦喵呦]";
		mes "经过不断的改进, 无数的附魔规则被改变.";
		close;
	case 2:
		set @mal_enchant_select,1;
		break;
	case 3:
		set @mal_enchant_select,2;
		break;
	}

	if (!getequipisequiped(EQI_HAND_R)) {
		mes "[喵呦喵呦]";
		mes "你是不是脱掉了你的装备?";
		set @mal_enchant_select,0;
		close;
	}
	set @mal_equip_id, getequipid(EQI_HAND_R);
	set .@equip_type, getiteminfo(@mal_equip_id,11);

	//callsub L_Socket,<cost multiplier>,<4-x enchants possible>;
	switch(.@equip_type) { // Check weapon type first to speed up the checks.
	case 1: // Daggers
		switch(@mal_equip_id) {
			case 1224: callsub L_Socket,1,2; //Sword_Breaker
			case 1225: callsub L_Socket,1,2; //Mail_Breaker
			case 1227: callsub L_Socket,1,2; //Weeder_Knife
			case 1229: callsub L_Socket,1,2; //Mama's_Knife
			case 1232: callsub L_Socket,1,2; //Assasin_Dagger
			case 1233: callsub L_Socket,1,2; //Exercise
			case 1235: callsub L_Socket,1,2; //Azoth
			case 1236: callsub L_Socket,1,2; //Sucsamad
			case 1237: callsub L_Socket,1,2; //Grimtooth_
			case 1240: callsub L_Socket,1,2; //Princess_Knife
			case 1241: callsub L_Socket,1,2; //Cursed_Dagger
			case 1242: callsub L_Socket,1,2; //Counter_Dagger
			case 1244: callsub L_Socket,1,2; //Holy_Dagger
			case 13046: callsub L_Socket,1,3; //Krieg
			case 13047: callsub L_Socket,1,2; //Weihna
			case 13061: callsub L_Socket,1,2; //Black_Wing
			case 1223: callsub L_Socket,2,2; //Forturn_Sword
			case 1228: callsub L_Socket,2,2; //Combat_Knife
			case 1234: callsub L_Socket,2,2; //Moonlight_Sword
			case 1230: callsub L_Socket,4,2; //House_Auger
			case 1231: callsub L_Socket,4,2; //Bazerald
			case 13017: callsub L_Socket,1,2; //破甲短锥
			case 13062: callsub L_Socket,4,2; //Ancient_Dagger
			case 13073: callsub L_Socket,2,2; //反击短剑

		}
		break;
	case 16: // Katars
		switch(@mal_equip_id) {
			case 1271: callsub L_Socket,1,2; //Blood_Tears
			case 1263: callsub L_Socket,1,2; //Unholy_Touch
			case 1270: callsub L_Socket,1,2; //Drill_Katar
			case 1284: callsub L_Socket,1,2; //Krishna
			case 1285: callsub L_Socket,1,2; //Cakram
			case 1268: callsub L_Socket,2,2; //Wild_Beast_Claw
			case 1269: callsub L_Socket,2,2; //Inverse_Scale
			case 1265: callsub L_Socket,2,2; //Bloody_Roar
			case 1261: callsub L_Socket,4,2; //Infiltrator
			case 1266: callsub L_Socket,4,2; //Infiltrator_
			case 1295: callsub L_Socket,3,2; //血之泪
		}
		break;
	case 6: // 1-H Axes
		switch(@mal_equip_id) {
			case 1305: callsub L_Socket,1,2; //Cleaver
			case 1311: callsub L_Socket,1,2; //Vecer_Axe
		}
		break;
	case 7: // 2-H Axes
		switch(@mal_equip_id) {
			case 1364: callsub L_Socket,1,2; //Great_Axe
			case 1365: callsub L_Socket,1,2; //Sabbath
			case 1385: callsub L_Socket,1,2; //Bradium_Stonehammer
			case 1367: callsub L_Socket,1,2; //Slaughter
			case 1368: callsub L_Socket,1,2; //Tomahawk
			case 1387: callsub L_Socket,2,2; //Giant_Axe
			case 1369: callsub L_Socket,2,2; //Guillotine
			case 1363: callsub L_Socket,2,2; //Brood_Axe
			case 1376: callsub L_Socket,4,2; //Heart_Breaker
			case 1377: callsub L_Socket,4,2; //Hurricane_Fury
			case 1366: callsub L_Socket,4,2; //Right_Epsilon
			case 1370: callsub L_Socket,4,2; //Doom_Slayer
			case 1371: callsub L_Socket,4,2; //Doom_Slayer_
			case 1397: callsub L_Socket,2,2; //魔力之石石锤
		}
		break;
	case 2: // 1-H Swords
		switch(@mal_equip_id) {
			case 1131: callsub L_Socket,1,2; //Ice_Falchon
			case 1133: callsub L_Socket,1,2; //Fire_Brand
			case 1134: callsub L_Socket,1,2; //Scissores_Sword
			case 1135: callsub L_Socket,1,2; //Cutlas
			case 1136: callsub L_Socket,1,2; //Solar_Sword
			case 1138: callsub L_Socket,1,2; //Mysteltainn_
			case 1139: callsub L_Socket,1,2; //Tale_Fing_
			case 1140: callsub L_Socket,1,2; //Byeorrun_Gum
			case 1141: callsub L_Socket,1,2; //Immaterial_Sword
			case 1148: callsub L_Socket,1,2; //Star_Dust_Blade
			case 13421: callsub L_Socket,1,2; //Ruber
			case 13431: callsub L_Socket,2,2; //Chrome_Sword
			case 1137: callsub L_Socket,2,2; //Excalibur
			case 1130: callsub L_Socket,2,2; //Nagan
			case 1132: callsub L_Socket,4,2; //Edge
			case 13435: callsub L_Socket,1,2; //火灵之剑
			case 13436: callsub L_Socket,3,2; //水灵之刀
		}
		break;
	case 3: // 2-H Swords
		switch(@mal_equip_id) {
			case 1164: callsub L_Socket,1,2; //Muramasa
			case 1166: callsub L_Socket,1,2; //Dragon_Slayer
			case 1167: callsub L_Socket,1,2; //Schweizersabel
			case 1168: callsub L_Socket,1,2; //Zweihander
			case 1170: callsub L_Socket,1,2; //Katzbalger
			case 1171: callsub L_Socket,1,2; //Zweihander_
			case 1176: callsub L_Socket,1,2; //Muscle_Cutter
			case 1178: callsub L_Socket,1,2; //Schweizersabel_
			case 1180: callsub L_Socket,1,2; //Dragon_Slayer_
			case 1181: callsub L_Socket,1,2; //TaET_GOo_Lyeon
			case 1182: callsub L_Socket,1,2; //Bloody_Eater
			case 1188: callsub L_Socket,1,2; //Veteran_Sword
			case 1189: callsub L_Socket,1,3; //Krasnaya
			case 1196: callsub L_Socket,2,2; //Chrome_Twohand_Sword
			case 1165: callsub L_Socket,2,2; //Masamune
			case 1169: callsub L_Socket,2,2; //Executioner_
			case 1179: callsub L_Socket,2,2; //Executioner__
			case 1175: callsub L_Socket,2,2; //Altas_Weapon
			case 1185: callsub L_Socket,4,2; //Violet_Fear
			case 1186: callsub L_Socket,4,2; //Death_Guidance
			case 21003: callsub L_Socket,2,2; //名刀不知火
			case 21004: callsub L_Socket,2,2; //海鹰召唤者
		}
		break;
	case 4: // 1-H Spears
		switch(@mal_equip_id) {
			case 1420: callsub L_Socket,1,2; //Long_Horn
			case 1413: callsub L_Socket,1,2; //Gungnir
			case 1414: callsub L_Socket,1,2; //Gelerdria
			case 1416: callsub L_Socket,1,2; //Tjungkuletti
			case 1418: callsub L_Socket,1,2; //Gungnir_
			case 1433: callsub L_Socket,2,2; //Imperial_Spear
			case 1415: callsub L_Socket,2,2; //Skewer
			case 1421: callsub L_Socket,2,2; //BattlET_DELIGHTok
			case 1422: callsub L_Socket,4,2; //Hunting_Spear
		}
		break;
	case 5: // 2-H Spears
		switch(@mal_equip_id) {
			case 1466: callsub L_Socket,1,2; //Crescent_Scythe
			case 1467: callsub L_Socket,1,2; //Bill_Guisarme
			case 1468: callsub L_Socket,1,2; //Zephyrus
			case 1469: callsub L_Socket,1,2; //Longinus's_Spear
			case 1470: callsub L_Socket,1,2; //Brionac
			case 1471: callsub L_Socket,1,2; //Hell_Fire
			case 1474: callsub L_Socket,1,2; //Gae_Bolg
			case 1477: callsub L_Socket,1,2; //Spectral_Spear
			case 1478: callsub L_Socket,1,2; //Ahlspiess
			case 1479: callsub L_Socket,1,2; //Spectral_Spear_
			case 1480: callsub L_Socket,1,2; //Gae_Bolg_
			case 1481: callsub L_Socket,1,3; //Zephyrus_
			case 1484: callsub L_Socket,2,2; //Cardo
		}
		break;
	case 10: // Staves
		switch(@mal_equip_id) {
			case 1616: callsub L_Socket,1,2; //Staff_Of_Wing
			case 1629: callsub L_Socket,1,2; //Walking_Stick
			case 1631: callsub L_Socket,1,2; //Holy_Stick
			case 1643: callsub L_Socket,1,2; //Dead_Tree_Cane
			case 1654: callsub L_Socket,1,2; //Mental_Stick
			case 1636: callsub L_Socket,2,2; //Thorn_Staff
			case 1637: callsub L_Socket,2,2; //Eraser
			case 1473: callsub L_Socket,1,2; //Wizardy_Staff
			case 1662: callsub L_Socket,1,2; //诅咒魔杖
			case 1663: callsub L_Socket,2,2; //强化波尔多之杖
			case 1664: callsub L_Socket,1,2; //黑暗荆棘手杖
		}
		break;
	case 23: // New 2-H Staves
		switch(@mal_equip_id) {
			case 2004: callsub L_Socket,1,2; //Kronos
			case 2005: callsub L_Socket,1,2; //Dea_Staff
			case 2001: callsub L_Socket,2,2; //Divine_Cross
			case 2000: callsub L_Socket,4,2; //Destruction_Rod
			case 2017: callsub L_Socket,1,2; //圣十字之杖
		}
		break;
	case 8: // Maces
		switch(@mal_equip_id) {
			case 1524: callsub L_Socket,1,2; //Golden_Mace
			case 1525: callsub L_Socket,1,2; //Long_Mace
			case 1527: callsub L_Socket,1,2; //Quadrille
			case 1539: callsub L_Socket,1,2; //Golden_Mace_
			case 1541: callsub L_Socket,1,2; //Nemesis
			case 16000: callsub L_Socket,1,2; //Erde
			case 16001: callsub L_Socket,1,2; //Red_Square_Bag
			case 16010: callsub L_Socket,1,2; //Red_Ether_Bag
			case 1523: callsub L_Socket,2,2; //Spike
			case 1538: callsub L_Socket,2,2; //Spike_
			case 1526: callsub L_Socket,2,2; //Slash
			case 1528: callsub L_Socket,4,2; //Grand_Cross
			case 1540: callsub L_Socket,4,2; //Grand_Cross_
			case 16022: callsub L_Socket,2,2; //复仇女神
		}
		break;
	case 15: // Books
		switch(@mal_equip_id) {
			case 1557: callsub L_Socket,1,2; //Book_Of_The_Apocalypse
			case 1558: callsub L_Socket,1,2; //Girl's_Diary
			case 1559: callsub L_Socket,1,2; //Legacy_Of_Dragon
			case 1561: callsub L_Socket,1,2; //Hardback
			case 1562: callsub L_Socket,1,2; //Bible_Of_Battlefield
			case 1565: callsub L_Socket,4,2; //Death_Note
		}
		break;
	case 12: // Knuckles
		switch(@mal_equip_id) {
			case 1813: callsub L_Socket,1,2; //Kaiser_Knuckle
			case 1814: callsub L_Socket,1,2; //Berserk
			case 1815: callsub L_Socket,1,2; //Claw_Of_Garm
			case 1816: callsub L_Socket,1,2; //Berserk_
			case 1830: callsub L_Socket,2,2; //Sura_Rampage
		}
		break;
	case 11: // Bows
		switch(@mal_equip_id) {
			case 1719: callsub L_Socket,1,2; //Bow_Of_Roguemaster
			case 1722: callsub L_Socket,1,2; //Balistar
			case 1724: callsub L_Socket,1,2; //Dragon_Wing
			case 1725: callsub L_Socket,1,2; //Bow_Of_Minstrel
			case 1727: callsub L_Socket,1,2; //Balistar_
			case 1737: callsub L_Socket,1,2; //Ixion_Wing
			case 1740: callsub L_Socket,1,2; //Nepenthes_Bow
			case 1741: callsub L_Socket,1,2; //Cursed_Lyre
			case 1745: callsub L_Socket,1,2; //Falken_Blitz
			case 18103: callsub L_Socket,1,2; //Mystic_Bow
			case 1720: callsub L_Socket,2,2; //Bow_Of_Rudra
			case 18117: callsub L_Socket,2,2; //皇家之弓
			case 18122: callsub L_Socket,2,2; //巨弓
			case 18123: callsub L_Socket,2,2; //风暴之弓
		}
		break;
	case 13: // Musical Instruments
		switch(@mal_equip_id) {
			case 1913: callsub L_Socket,1,2; //Electronic_Guitar
			case 1918: callsub L_Socket,1,2; //Oriental_Lute
			case 1920: callsub L_Socket,1,2; //Berserk_Guitar
			case 1922: callsub L_Socket,1,2; //Oriental_Lute_
			case 1926: callsub L_Socket,1,2; //Harp_Of_Nepenthes
			case 1930: callsub L_Socket,2,2; //Green_Whistle
		}
		break;
	case 14: // Whips
		switch(@mal_equip_id) {
			case 1962: callsub L_Socket,1,2; //Lariat
			case 1963: callsub L_Socket,1,2; //Rapture_Rose
			case 1964: callsub L_Socket,1,2; //Chemeti
			case 1969: callsub L_Socket,1,2; //Bladed_Whip
			case 1970: callsub L_Socket,1,2; //Queen's_Whip
			case 1972: callsub L_Socket,1,2; //Electric_Eel
			case 1973: callsub L_Socket,1,2; //Sea_Witch_Foot
			case 1974: callsub L_Socket,1,2; //Carrot_Whip
			case 1976: callsub L_Socket,1,2; //Queen's_Whip_
			case 1979: callsub L_Socket,1,2; //Stem_Of_Nepenthes
			case 1984: callsub L_Socket,2,2; //Stem_Whip
			case 1985: callsub L_Socket,4,2; //Rosebine
		}
		break;
	}
	mes "[喵呦喵呦]";
	if (@mal_enchant_select == 1)
		mes "这件装备不能附魔. 难道你忘记我的原则了么, 我只对高级武器附魔包含4级武器~";
	else
		mes "这件装备不能被重置附魔. 难道你忘记我的原则了么, 我只对高级武器附魔包含4级武器~";
	set @mal_equip_id,0;
	set @mal_enchant_select,0;
	close;

L_Socket:
	set .@select, @mal_enchant_select;
	set .@equip_id, @mal_equip_id;
	set .@equip_name$, getitemname(.@equip_id)+((getitemslots(.@equip_id))?"["+getitemslots(.@equip_id)+"]":"");
	setarray .@equip_card[0], getequipcardid(EQI_HAND_R,0),getequipcardid(EQI_HAND_R,1),getequipcardid(EQI_HAND_R,2),getequipcardid(EQI_HAND_R,3);
	set @mal_equip_id,0;
	set @mal_enchant_select,0;

	if (.@select == 1) {
		mes "[喵呦喵呦]";
		mes "^0000ff"+.@equip_name$+"^000000! 你是否准备附魔这件装备? 你想用什么来支付酬劳?";
		next;

		.@enccost = getarg(0);
		.@enclimit = getarg(1);
		setarray .@coin[0],6422,6421,6420,6419,6418,6423; // Payment ID
		setarray .@cost[0],  15,  10,   6,   3,   2,   1; // Payment multiplier

		set .@menu$, "暂停:";
		for(set .@i,0; .@i<getarraysize(.@coin); set .@i,.@i+1) {
			set .@count[.@i], countitem(.@coin[.@i]);
			set .@total[.@i], .@enccost*.@cost[.@i];
			if (.@count[.@i] < .@total[.@i])
				set .@menu$, .@menu$+"^999999"+getitemname(.@coin[.@i])+" (缺少 "+(.@total[.@i]-.@count[.@i])+")^000000:";
			else
				set .@menu$, .@menu$+getitemname(.@coin[.@i])+" (现有 "+.@count[.@i]+", 需要 "+.@total[.@i]+"):";
		}
		set .@coin_select, select(.@menu$)-2;
		if (.@coin_select == -1) {
			mes "[喵呦喵呦]";
			mes "等你拿定主意的时候再来找我.";
			close;
		} else if (.@count[.@coin_select] < .@total[.@coin_select]) {
			mes "[喵呦喵呦]";
			mes "你无法支付足够的酬劳. 我想你最好再确认一下?";
			close;
		}
		switch(.@coin_select) {
		case 0: //Egrade_Coin
			set .@enchant_type,10;
			break;
		case 1: //Dgrade_Coin
			set .@enchant_type,9;
			break;
		case 2: //Cgrade_Coin
			set .@enchant_type,8;
			break;
		case 3: //Bgrade_Coin
			set .@enchant_type,7;
			break;
		case 4: //Agrade_Coin
		case 5: //Anger_Seagod
			mes "[喵呦喵呦]";
			mes "当你使用 "+getitemname(.@coin[.@coin_select])+", 你可以选择附魔类型.";
			next;
			switch(select("- 暂停:- 近战类型:- 远程类型:- 施法类型")) {
			case 1:
				mes "[喵呦喵呦]";
				mes "好吧. 如果你改变想法了, 就来找我吧.";
				close;
			case 2:
				set .@enchant_type,((.@coin_select == 4)?4:1);
				break;
			case 3:
				set .@enchant_type,((.@coin_select == 4)?5:2);
				break;
			case 4:
				set .@enchant_type,((.@coin_select == 4)?6:3);
				break;
			}
			break;
		}
		if (.@equip_card[3] == 0 && .@enclimit < 4) {
			set .@socket,4;
			set .@str$,"1st";
		} else if (.@equip_card[2] == 0 && .@enclimit < 3) {
			set .@socket,3;
			set .@str$,"2nd";
		} else {
			mes "[喵呦喵呦]";
			mes "这件装备已经附魔到极致了. 只有初始化附魔后你才能够再次对他进行附魔, 或者直接带一件新装备来.";
			close;
		}
		mes "[喵呦喵呦]";
		mes "我正在努力为 "+.@str$+" 附魔. ^ff0000现在附魔不会损害卡片和精炼等级. 同时, 物品也不会损毁.^000000 是否继续?";
		next;
		if(select("- 我稍后再来.:- 请你, 继续.") == 1) {
			mes "[喵呦喵呦]";
			mes "好的. 如果你改变想法了, 就来找我吧.";
			close;
		}
		switch(.@enchant_type) {
		case 10: //Egrade_Coin
			set .@i, rand(1,531);
			     if (.@i < 101) set .@enchant,4787; //Mdef4
			else if (.@i < 201) set .@enchant,4792; //Def6
			else if (.@i < 301) set .@enchant,4801; //SP100
			else if (.@i < 351) set .@enchant,4795; //HP100
			else if (.@i < 401) set .@enchant,4796; //HP200
			else if (.@i < 451) set .@enchant,4819; //Atk1
			else if (.@i < 476) set .@enchant,4720; //Dexterity1
			else if (.@i < 501) set .@enchant,4740; //Vitality1
			else if (.@i < 526) set .@enchant,4750; //Luck1
			else if (.@i < 528) set .@enchant,4700; //Strength1
			else if (.@i < 530) set .@enchant,4730; //Agility1
			else if (.@i < 532) set .@enchant,4710; //Inteligence1
			else set .@enchant,9;
			break;
		case 9: //Dgrade_Coin
			set .@i, rand(1,531);
			     if (.@i < 101) set .@enchant,4795; //HP100
			else if (.@i < 201) set .@enchant,4796; //HP200
			else if (.@i < 301) set .@enchant,4819; //Atk1
			else if (.@i < 351) set .@enchant,4720; //Dexterity1
			else if (.@i < 401) set .@enchant,4740; //Vitality1
			else if (.@i < 451) set .@enchant,4750; //Luck1
			else if (.@i < 476) set .@enchant,4700; //Strength1
			else if (.@i < 501) set .@enchant,4730; //Agility1
			else if (.@i < 526) set .@enchant,4710; //Inteligence1
			else if (.@i < 528) set .@enchant,4701; //Strength2
			else if (.@i < 530) set .@enchant,4731; //Agility2
			else if (.@i < 532) set .@enchant,4711; //Inteligence2
			else set .@enchant,9;
			break;
		case 8: //Cgrade_Coin
			set .@i, rand(1,531);
			     if (.@i < 101) set .@enchant,4720; //Dexterity1
			else if (.@i < 201) set .@enchant,4740; //Vitality1
			else if (.@i < 301) set .@enchant,4750; //Luck1
			else if (.@i < 351) set .@enchant,4700; //Strength1
			else if (.@i < 401) set .@enchant,4730; //Agility1
			else if (.@i < 451) set .@enchant,4710; //Inteligence1
			else if (.@i < 476) set .@enchant,4701; //Strength2
			else if (.@i < 501) set .@enchant,4731; //Agility2
			else if (.@i < 526) set .@enchant,4711; //Inteligence2
			else if (.@i < 528) set .@enchant,4702; //Strength3
			else if (.@i < 530) set .@enchant,4732; //Agility3
			else if (.@i < 532) set .@enchant,4712; //Inteligence3
			else set .@enchant,9;
			break;
		case 7: //Bgrade_Coin
			set .@i, rand(1,531);
			     if (.@i < 101) set .@enchant,4700; //Strength1
			else if (.@i < 201) set .@enchant,4730; //Agility1
			else if (.@i < 301) set .@enchant,4710; //Inteligence1
			else if (.@i < 351) set .@enchant,4701; //Strength2
			else if (.@i < 401) set .@enchant,4731; //Agility2
			else if (.@i < 451) set .@enchant,4711; //Inteligence2
			else if (.@i < 476) set .@enchant,4702; //Strength3
			else if (.@i < 501) set .@enchant,4732; //Agility3
			else if (.@i < 526) set .@enchant,4712; //Inteligence3
			else if (.@i < 528) set .@enchant,4703; //Strength4
			else if (.@i < 530) set .@enchant,4733; //Agility4
			else if (.@i < 532) set .@enchant,4713; //Inteligence4
			else set .@enchant,9;
			break;
		case 6: //Agrade_Coin - Caster
			set .@i, rand(1,555);
			     if (.@i < 81)  set .@enchant,4711; //Inteligence2
			else if (.@i < 161) set .@enchant,4721; //Dexterity2
			else if (.@i < 241) set .@enchant,4814; //Spell2
			else if (.@i < 311) set .@enchant,4712; //Inteligence3
			else if (.@i < 371) set .@enchant,4722; //Dexterity3
			else if (.@i < 431) set .@enchant,4813; //Spell3
			else if (.@i < 476) set .@enchant,4713; //Inteligence4
			else if (.@i < 516) set .@enchant,4812; //Spell4
			else if (.@i < 526) set .@enchant,4760; //Matk1
			else if (.@i < 546) set .@enchant,4714; //Inteligence5
			else if (.@i < 551) set .@enchant,4826; //Spell5
			else if (.@i < 556) set .@enchant,4761; //Matk2
			else set .@enchant,9;
			break;
		case 5: //Agrade_Coin - Long Range
			set .@i, rand(1,555);
			     if (.@i < 81)  set .@enchant,4731; //Agility2
			else if (.@i < 161) set .@enchant,4833; //Expert_Archer2
			else if (.@i < 241) set .@enchant,4817; //Sharp2
			else if (.@i < 311) set .@enchant,4732; //Agility3
			else if (.@i < 371) set .@enchant,4834; //Expert_Archer3
			else if (.@i < 431) set .@enchant,4816; //Sharp3
			else if (.@i < 476) set .@enchant,4733; //Agility4
			else if (.@i < 516) set .@enchant,4835; //Expert_Archer4
			else if (.@i < 526) set .@enchant,4807; //Atk_Speed1
			else if (.@i < 546) set .@enchant,4734; //Agility5
			else if (.@i < 551) set .@enchant,4836; //Expert_Archer5
			else if (.@i < 556) set .@enchant,4807; //Atk_Speed1
			else set .@enchant,9;
			break;
		case 4: //Agrade_Coin - Short Range
			set .@i, rand(1,555);
			     if (.@i < 81)  set .@enchant,4731; //Agility2
			else if (.@i < 161) set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 241) set .@enchant,4817; //Sharp2
			else if (.@i < 311) set .@enchant,4732; //Agility3
			else if (.@i < 371) set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 431) set .@enchant,4816; //Sharp3
			else if (.@i < 476) set .@enchant,4733; //Agility4
			else if (.@i < 516) set .@enchant,4821; //Fighting_Spirit6
			else if (.@i < 526) set .@enchant,4807; //Atk_Speed1
			else if (.@i < 546) set .@enchant,4734; //Agility5
			else if (.@i < 551) set .@enchant,4822; //Fighting_Spirit7
			else if (.@i < 556) set .@enchant,4807; //Atk_Speed1
			else set .@enchant,9;
			break;
		case 3: //Anger_Seagod - Caster
			set .@i, rand(1,555);
			     if (.@i < 81)  set .@enchant,4712; //Inteligence3
			else if (.@i < 161) set .@enchant,4722; //Dexterity3
			else if (.@i < 241) set .@enchant,4813; //Spell3
			else if (.@i < 311) set .@enchant,4713; //Inteligence4
			else if (.@i < 371) set .@enchant,4812; //Spell4
			else if (.@i < 431) set .@enchant,4760; //Matk1
			else if (.@i < 476) set .@enchant,4714; //Inteligence5
			else if (.@i < 516) set .@enchant,4826; //Spell5
			else if (.@i < 526) set .@enchant,4761; //Matk2
			else if (.@i < 546) set .@enchant,4715; //Inteligence6
			else if (.@i < 551) set .@enchant,4827; //Spell6
			else if (.@i < 556) set .@enchant,4761; //Matk2
			else set .@enchant,9;
			break;
		case 2: //Anger_Seagod - Long Range
			set .@i, rand(1,555);
			     if (.@i < 81)  set .@enchant,4732; //Agility3
			else if (.@i < 161) set .@enchant,4834; //Expert_Archer3
			else if (.@i < 241) set .@enchant,4843; //Sharp4
			else if (.@i < 311) set .@enchant,4733; //Agility4
			else if (.@i < 371) set .@enchant,4835; //Expert_Archer4
			else if (.@i < 431) set .@enchant,4844; //Sharp5
			else if (.@i < 476) set .@enchant,4734; //Agility5
			else if (.@i < 516) set .@enchant,4836; //Expert_Archer5
			else if (.@i < 526) set .@enchant,4807; //Atk_Speed1
			else if (.@i < 546) set .@enchant,4735; //Agility6
			else if (.@i < 551) set .@enchant,4837; //Expert_Archer6
			else if (.@i < 556) set .@enchant,4807; //Atk_Speed1
			else set .@enchant,9;
			break;
		case 1: //Anger_Seagod - Short Range
			set .@i, rand(1,555);
			     if (.@i < 81)  set .@enchant,4732; //Agility3
			else if (.@i < 161) set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 241) set .@enchant,4843; //Sharp4
			else if (.@i < 311) set .@enchant,4733; //Agility4
			else if (.@i < 371) set .@enchant,4821; //Fighting_Spirit6
			else if (.@i < 431) set .@enchant,4844; //Sharp5
			else if (.@i < 476) set .@enchant,4734; //Agility5
			else if (.@i < 516) set .@enchant,4822; //Fighting_Spirit7
			else if (.@i < 526) set .@enchant,4807; //Atk_Speed1
			else if (.@i < 546) set .@enchant,4735; //Agility6
			else if (.@i < 551) set .@enchant,4823; //Fighting_Spirit8
			else if (.@i < 556) set .@enchant,4807; //Atk_Speed1
			else set .@enchant,9;
			break;
		default:
			mes "[喵呦喵呦]";
			mes "有点流程不正确. 请重试一下.";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_HAND_R, .@equip_id) ||
		    callfunc("F_IsEquipCardHack", EQI_HAND_R, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		     if (.@equip_card[3] == 0 && .@enclimit < 4) set .@equip_card[3],.@enchant;
		else if (.@equip_card[2] == 0 && .@enclimit < 3) set .@equip_card[2],.@enchant;
		else if (.@equip_card[1] == 0 && .@enclimit < 2) set .@equip_card[1],.@enchant;
		else if (.@equip_card[0] == 0 && .@enclimit < 1) set .@equip_card[0],.@enchant;
		else {
			mes "[喵呦喵呦]";
			mes "这个装备已经达到附魔的极致了. 一件装备我只能附魔2次.";
			next;
			mes "[喵呦喵呦]";
			mes "当然我也不会在卡片洞上附魔, 如果武器有3个洞的话, 那只能附魔一次. 请牢记这点~";
			close;
		}
		if (.@enchant == 9) { // Should never happen.
			specialeffect2 EF_SUI_EXPLOSION;
			mes "[喵呦喵呦]";
			mes "我的天!";
			mes "这件装备被毁了, 它无法承载我的强大的附魔能力. 太抱歉了.";
			delitem .@coin[.@coin_select],.@total[.@coin_select];
			delequip EQI_HAND_R;
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[喵呦喵呦]";
		mes "我已经附魔了 ^990000插槽 "+.@socket+"^000000 在这装备上.";
		delitem .@coin[.@coin_select],.@total[.@coin_select];
		set .@equip_refine, getequiprefinerycnt(EQI_HAND_R);
		delequip EQI_HAND_R;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[喵呦喵呦]";
		mes "我只会初始化附魔效果, 我会影响原来的精炼和卡片.";
		next;
		if(select("- 暂停:- 继续") == 1) {
			mes "[喵呦喵呦]";
			mes "如果你改变主意了, 随时欢迎.";
			close;
		}
		if (countitem(6417) == 0) {
			mes "[喵呦喵呦]";
			mes "抱歉. 你没有A猴桃果实. 请检查你的库存?";
			close;
		}
		if (!getequipisequiped(EQI_HAND_R)) {
			mes "[喵呦喵呦]";
			mes "你怎么把装备脱了?";
			close;
		}
		if (.@equip_card[3] == 0) {
			mes "[喵呦喵呦]";
			mes "这件装备无法重置附魔, 或者本身就没有附魔效果. 请再检查一下.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[喵呦喵呦]";
		mes "重置装备的附魔效果.";
		delitem 6417,1; //A猴桃果实

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_HAND_R, .@equip_id) ||
		    callfunc("F_IsEquipCardHack", EQI_HAND_R, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		set .@equip_refine, getequiprefinerycnt(EQI_HAND_R);
		delequip EQI_HAND_R;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	}
}
