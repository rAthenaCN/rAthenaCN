//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Card Separation System
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Separates cards from equipment.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//= 1.1 Added "Richard" NPC. [Euphy]
//============================================================ 

-	script	::CardSeparation_mal	-1,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您携带的物品种类过多. 等您解决这个问题后我们才能继续.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "无法继续, 因为您的负重不足. 等您解决负重问题后我们再继续.";
		close;
	}
	if (strnpcinfo(1) == "杰瑞米") {
		set .@Jeremy,1;
		set .@n$, "[Jeremy]";
		setarray .@equip_name$[0],  "Armor",  "Shoes",  "Garment", "Upper Hat";
		setarray .@equip_slot[0], EQI_ARMOR,EQI_SHOES,EQI_GARMENT,EQI_HEAD_TOP;

		mes .@n$;
		mes "好久不见~";
		mes "我已经学会了新的技能, 可以从武器. 头饰, 护甲, 鞋子, 披肩和头饰上分离卡片. 想不想试试看?";
	} else {
		set .@Jeremy,0;
		set .@n$, "[Richard]";
		setarray .@equip_name$[0], "Left hand", "Right hand";
		setarray .@equip_slot[0],   EQI_HAND_L,   EQI_HAND_R;

		mes .@n$;
		mes "我不用触摸的武器和盾牌. 因为拆除卡片, 可能会损坏. 这就是为什么我准备一个的卡片技能来操控武器和盾牌...";
	}
	next;
	mes .@n$;
	mes "费用一般是 1,000,000 Zeny. 在操作之前, 你可以使用 ^990000特殊的物品来降低卡片或者装备受损的概率^000000. 我们不收取额外的费用.";
	next;
	mes .@n$;
	mes "使用了特殊物品还是有几率损坏装备和卡片. 同时, ^ff0000装备精炼等级也会消失^000000. 你有需要分离的装备吗?";
	next;

	for(set .@i,0; .@i<getarraysize(.@equip_slot); set .@i,.@i+1) {
		if (getequipisequiped(.@equip_slot[.@i]))
			set .@menu$, .@menu$+getequipname(.@equip_slot[.@i])+":";
		else
			set .@menu$, .@menu$+"^999999"+.@equip_name$[.@i]+" (empty)^000000:";
	}

	set .@i, select("- 停止拆卡:"+((.@Jeremy)?"- 有什么可能性?":"")+":"+.@menu$);
	switch(.@i) {
	case 1:
		mes .@n$;
		mes "如果你想操作, 随时来找我.";
		close;
	case 2:
		mes .@n$;
		mes "你想了解特殊物品. 好吧, 我不喜欢随口说说, 所以让我告诉你整个完整的故事...";
		next;
		mes "^000099杰瑞米舒展了下筋骨. 他可能是在等待某人与他交谈.^000000";
		next;
		mes .@n$;
		mes "你知不知道喵喵岛的特产是罐头食品?";
		next;
		select("- 我知道那很不错. 难道有什么限制?");
		mes .@n$;
		mes "嘿嘿... 人人都喜欢. 不过从前它确实有些问题.";
		next;
		select("- 问题? 是不是用了有污染的鱼?");
		mes .@n$;
		mes "不不, 不是鱼的问题. 问题是, 很多加工后产生的鱼油. 也就是说等同于工业废弃物.";
		next;
		mes .@n$;
		mes "然而, 在一次制作流程革新之后, 把这些鱼油变废为宝并成功用于旧设备保养和用作工业润滑剂.";
		next;
		mes .@n$;
		mes "此外, 我们可以通过金币购买普通或者高级润滑油, 这些润滑油对于我们分离装备上的卡片非常有用.";
		next;
		mes .@n$;
		mes "不用担心钱的问题. 高级润滑油是昂贵的. 如果你支付一些金币, 我可以给你便宜的润滑剂.";
		next;
		mes .@n$;
		mes "我不确定普通润滑剂的效果如何. 不过, 它确实很便宜, 对吧?";
		close;
	default:
		set .@equip_num, .@equip_slot[.@i-3];
		if (!getequipisequiped(.@equip_num)) {
			mes .@n$;
			if (.@Jeremy)
				mes "这部分的说明, 你听明白了吗?";
			else
				mes "关于这部分你有没有问题?";
			close;
		}
		break;
	}

	setarray .@equip_card[0], getequipcardid(.@equip_num,0),getequipcardid(.@equip_num,1),getequipcardid(.@equip_num,2),getequipcardid(.@equip_num,3);
	set .@mvp_list$,
		"|4408|4128|4456|4168|4142"+  //Gloom_Under_Night_Card, Golden_Bug_Card, Nidhogg_Shadow_Card, Dark_Lord_Card, Doppelganger_Card
		"|4134|4137|4386|4407|4357"+  //Dracula_Card, Drake_Card, Detale_Card, Randgris_Card, B_Seyren_Card
		"|4146|4132|4147|4372|4145"+  //Maya_Card, Mistress_Card, Baphomet_Card, Bacsojin_Card, Berzebub_Card
		"|4374|4352|4367|4236|4425"+  //Apocalips_H_Card, B_Ygnizem_Card, B_Shecil_Card, Amon_Ra_Card, Atroce_Card
		"|4359|4123|4144|4135|4143"+  //B_Eremes_Card, Eddga_Card, Osiris_Card, Orc_Load_Card, Orc_Hero_Card
		"|4263|4131|4430|4276|4419"+  //Incant_Samurai_Card, Moonlight_Flower_Card, Ifrit_Card, Lord_Of_Death_Card, Ktullanux_Card
		"|4403|4399|4376|4441|4302"+  //Kiel_Card, Thanatos_Card, Lady_Tanee_Card, Fallen_Bishop_Card, Tao_Gunka_Card
		"|4305|4148|4318|4121|4365"+  //Turtle_General_Card, Pharaoh_Card, Knight_Windstorm_Card, Phreeoni_Card, B_Katrinn_Card
		"|4363|4324|4361|4330|4342|"; //B_Magaleta_Card, Garm_Card, B_Harword_Card, Dark_Snake_Lord_Card, Rsx_0806_Card

	if (.@Jeremy) {
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		if (!getarraysize(.@equip_card)) {
			mes .@n$;
			mes "没有检测到卡片, 请检查下你的装备?";
			close;
		}
		if ((.@equip_card[0] && compare(.@mvp_list$,"|"+.@equip_card[0]+"|")) ||
			(.@equip_card[1] && compare(.@mvp_list$,"|"+.@equip_card[1]+"|")) ||
			(.@equip_card[2] && compare(.@mvp_list$,"|"+.@equip_card[2]+"|")) ||
			(.@equip_card[3] && compare(.@mvp_list$,"|"+.@equip_card[3]+"|")))
			set .@boss_chk,1;
	} else {
		// Official "Richard" script uses a hardcoded list including every possible item.
		//if (!getequipisequiped(.@equip_num)) {
		//	mes "[Richard]";
		//	mes "非常抱歉, 我们还不支持这个装备.";
		//	close;
		//}

		mes "[理查德]";
		mes "你想从那个卡片插槽上拔下卡片? 从左向右看的话, 分别为 1,2,3,4号插槽.";
		next;
		set .@menu$,"";
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] && .@equip_card[.@i] < 4700) // Armor Enchant System
				set .@menu$, .@menu$+"插槽 "+(.@i+1)+" - "+getitemname(.@equip_card[.@i])+":";
			else
				set .@menu$, .@menu$+"^777777Socket "+(.@i+1)+" - 没有卡片^000000:";
		}
		set .@i, select("- 停止拆除:"+.@menu$);
		switch(.@i) {
		case 1:
			mes .@n$;
			mes "等你有需要的时候, 可以再来找我.";
			close;
		default:
			set .@slot, .@i-2;
			if (.@equip_card[.@slot] == 0 || .@equip_card[.@slot] >= 4700) {
				mes .@n$;
				mes "此卡片插槽内没有配备任何卡片, 请再次检查?";
				close;
			}
			break;
		}
		if (compare(.@mvp_list$,"|"+.@equip_card[.@slot]+"|"))
			set .@boss_chk,1;
	}
	if (.@boss_chk == 0) {
		mes .@n$;
		if (.@Jeremy)
			mes "例外的卡片, ^ff0000附魔效果都将消失.^000000 如果你同意, 请选择继续制作选项:";
		else
			mes "请选择润滑剂.";
		next;
		set .@menu$,
			"Next time...:"+
			((Zeny >= 1000000)?"使用 1,000,000z (不使用任何润滑剂):":"^999999使用 1,000,000z (不足)^000000:")+
			((countitem(6441))?"使用 高级润滑剂 :":"^999999高级润滑剂 (不足)^000000:")+
			((countitem(6440))?"使用 普通润滑剂":"^999999普通润滑剂 (不足)^000000");
		switch(select(.@menu$)) {
		case 1:
			mes .@n$;
			mes "你需要时,来这里可以找到我.";
			close;
		case 2:
			if (Zeny < 1000000) {
				mes .@n$;
				mes "你的钱没带够. 等有钱的时候再来吧.";
				close;
			}
			mes .@n$;
			mes "这是一件非常旧的装备. 这会导致在分离过程中被损毁的概率大大提升. 你确定要继续?";
			next;
			if(select("- 下次再说...:- 继续") == 1) {
				mes .@n$;
				mes "你需要时,来这里可以找到我.";
				close;
			}
			set .@sf_c_num,150;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			set Zeny, Zeny - 1000000;
			break;
		case 3:
			if (countitem(6441) == 0) {
				mes .@n$;
				mes "你没有高级润滑剂.";
				close;
			}
			mes .@n$;
			mes "如果使用高级润滑剂, 安全系数会大大提高, 但是我无法给你 100% 保证. 你确定要继续?";
			next;
			if(select("- 下次再说...:- 继续") == 1) {
				mes "[杰瑞米]";
				mes "你需要时,来这里可以找到我.";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,75;
			set .@sf_w_num,75;
			delitem 6441,1; //High_RankLubricant
			break;
		case 4:
			if (countitem(6440) == 0) {
				mes .@n$;
				mes "你没有普通润滑剂.";
				close;
			}
			mes .@n$;
			mes "如果使用普通润滑剂, 安全系数会有所提高, 但是我无法给你 100% 保证. 你确定要继续?";
			next;
			if(select("- 下次再说...:- 继续") == 1) {
				mes "[杰瑞米]";
				mes "你需要时,来这里可以找到我.";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			delitem 6440,1; //General_Lubricant
			break;
		}
	} else if (.@boss_chk == 1) {
		mes .@n$;
		mes "这件装备插了一张 MVP 卡片. 这类卡片无法通过润滑剂来分离. 如果你带来超级活性剂 ^0000ff王水瓶^000000, 我就能够操作了.";
		next;
		switch(select("- 下次再说...:- 继续.")) {
		case 1:
			mes .@n$;
			mes "你需要时,来这里可以找到我.";
			close;
		case 2:
			if (countitem(6443) == 0) {
				mes .@n$;
				mes "你没有王水瓶.";
				close;
			}
			break;
		}
		mes .@n$;
		if (.@Jeremy) {
			mes "例外的卡片, ^ff0000附魔效果都将消失.^000000 如果你同意, 请选择继续制作选项:";
			set .@menu$,"Alright, let's do it!";
		} else {
			mes "May I continue?";
			set .@menu$,"I got it. Just do it quickly!";
		}
		next;
		switch(select("- 下次再说...:- 没问题,继续!")) {
		case 1:
			mes .@n$;
			mes "你需要时,来这里可以找到我.";
			close;
		case 2:
			set .@sf_c_num,60;
			set .@sf_r_num,60;
			set .@sf_w_num,60;
			delitem 6443,1; //Sillit_Pong_Bottle
			break;
		}
	}

	set .@equip_id, getequipid(.@equip_num);
	set .@equip_refine, getequiprefinerycnt(.@equip_num);

	// anti-hack
	if (callfunc("F_IsEquipCardHack", .@equip_num, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
		close;

	delequip .@equip_num;

	// Chance of retaining refine level.
	if (rand(1,.@sf_r_num) >= 61)
		set .@equip_refine,0;

	if (.@Jeremy) {
		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,0,0;
		}

		// Chance of retaining cards.
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i]) {
				if (rand(1,.@sf_c_num) < 61)
					getitem .@equip_card[.@i],1;
				else
					set .@card_break,1;
			}
		}
	} else {
		set .@card, .@equip_card[.@slot];
		set .@equip_card[.@slot],0;

		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		}

		// Chance of retaining card.
		if (rand(1,.@sf_c_num) < 61)
			getitem .@card,1;
		else
			set .@card_break,1;
	}

	// Display corresponding effect.
	if (!.@equip_safe && .@card_break)
		specialeffect2 EF_LORD;
	else if (.@equip_safe && .@card_break)
		specialeffect2 EF_SUI_EXPLOSION;
	else if (!.@equip_safe && !.@card_break)
		specialeffect2 EF_FIREPILLAR;
	else
		specialeffect2 EF_MAXPOWER;

	// Output results.
	mes "-- 拆卡结果 --";
	if (.@equip_safe) {
		mes "卡片在分离过程中未发现裂纹.";
		mes "^0000FF装备检测正常.^000000";
	} else {
		mes "卡片在分离过程中未发现裂纹.";
		mes "装备损毁了. ^ff0000无法还原.^000000";
	}
	mes "-------------------";
	if (!.@card_break) {
		mes "分离过程中, 卡片未被腐蚀.";
		mes "^0000ff拆卡成功.^000000";
	} else {
		mes "发生在分离过程中, 卡片的表面受到侵蚀.";
		mes "卡片损毁了. ^ff0000无法还原.^000000";
	}
	next;
	mes .@n$;
	mes "这是拆卡可能出现的所有结果. 再见.";
	close;
}
malangdo,215,166,4	duplicate(CardSeparation_mal)	杰瑞米#pa0829	4_CAT_DOWN
malangdo,208,166,6	duplicate(CardSeparation_mal)	理查德#pa0829	4_CAT_ADV1
