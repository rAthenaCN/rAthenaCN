//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= Rare Diamond Merchant
//===== By: ==================================================
//= Z3R0
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Exchanges 17 Carat Diamond
//===== Additional Comments: =================================
//= 1.0 First / Optimized Version
//= 1.1 Adjusted for Missing ; on Line #271 (#15425)
//= 1.2 Removed Comments per Request (#15426)
//= 1.3 Changed Emotion # to Const Definition (#15427)
//= 1.4 Removed Double Space on Line #148 (#15428)
//= 1.5 Added This Version History (#15429)
//============================================================

-	script	RareDiamondMerchant	1_M_MERCHANT,{

	set .@npc$, "[稀有钻石商人]";

	mes .@npc$;
	mes "先生们女士们! ! !";
	mes "你们虽然听说过, 但你们从来没有一次用自己的眼睛亲眼见过.";
	mes "世界上最稀有的钻石!";
	mes "^FF82FF'17克拉钻石'^000000 目前在低价出售!!";
	mes "如果对这珍贵的钻石感兴趣, 您可以继续听听!";
	next;
	
	mes .@npc$;
	mes "你可以把它作为礼物送给你的爱人.";
	mes "收到 ^FF82FF'17克拉钻石'^000000 的人一定会爱死它的";
	mes "当然也会爱死你.";
	emotion ET_BLABLA;
	next;
	
	mes .@npc$;
	mes "什么? 你没有心上人?";
	mes "那你可以自己佩戴着它.";
	mes "^FF82FF'17克拉钻石'^000000 上那迷人的光环很可能";
	mes "将爱情带入你的生活.";
	next;
	
	mes .@npc$;
	mes "迷人的菱形被称为钻石...";
	mes "就是 ^FF82FF'17克拉钻石'^000000!";
	next;

	set .@menu$, "^FF82FF'17克拉钻石'^000000?";
	if (countitem(6024))
		set .@menu$, .@menu$ + ":用Zeny交易17克拉钻石...";

	switch(select(.@menu$)) {
	case 1:
		mes .@npc$;
		mes "是的!";
		mes "呵呵, 你现在看到的 ^FF82FF'17克拉钻石'^000000";
		mes "是最高质量的 17克拉钻石.";
		mes "如同豌豆般大小, 比任何一颗你";
		mes "能够从梦罗克挖到的更完美!";
		emotion ET_HUK;
		next;
		
		mes .@npc$;
		mes "有些人造谣说这在克魔岛的";
		mes "Kachua 很常见!";
		next;
		
		if (select("- 结束对话:- 需要多少钱?") == 1) {
			mes .@npc$;
			mes "它是这么价廉物美!";
			mes "如果你什么时候感兴趣了, 请随时和我联系!";
			close;
		}
		
		mes .@npc$;
		mes "当然! 最重要的就是价格因素!";
		mes "正如我之前所说, 这 ^FF82FF'17克拉钻石'^000000";
		mes "连续多年被评为钻石之首.";
		mes "在世界上不会有太多.";
		next;
		
		mes .@npc$;
		mes "它的价格是5,000,000Zeny!";
		mes "还有10,000?服务费.";
		mes "与钻石的品量相比";
		mes "这微不足道.";
		next;
		
		if (select("- ^828282这也太贵了.^000000:- 我要了.") == 1) {
			mes .@npc$;
			mes "你竟然认为这颗";
			mes "^FF82FF'17克拉钻石'^000000 太贵, ";
			mes "我无语, 你走吧.";
			close;
		}
		
		mes .@npc$;
		mes "喔喔! 自我看到你的那一刻起, 我就知道你是会";
		mes "成为这颗钻石的主人.";
		mes "我很高兴我猜对了!";
		mes "我相信这款钻石非你莫属.";
		mes "最后需要你来完成本合同.";
		next;
		
		while(1) {
			if (select("- ^B9062F我会签合同.^000000:- ^828282抱歉, 我改主意了.^000000") == 2) {
				mes .@npc$;
				mes "我明白了, 太糟糕了..";
				close;
			}
			
			mes .@npc$;
			mes "您的名字?";
			next;
			
			mes "[" + strcharinfo(0) + "]";
			mes "我的名字是 " + strcharinfo(0) + ".";
			next;

			mes .@npc$;
			mes "好的, 呢么请在这里签字.";
			next;
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "Okay, 好了.";
				mes "现在所剩下的是付款.";
				next;
				
				if (Zeny >= 5010000) {
					mes .@npc$;
					mes ". . . . . .";
					next;
					
					mes .@npc$;
					mes "合计的钻石价格和服务费..";
					mes "5,010,000 Zeny.";
					mes "已确认金额.";
					emotion ET_BLABLA;
					set Zeny, Zeny - 5010000;
					getitem 6024, 1;
					next;

					mes .@npc$;
					mes "您可以随时将钻石变现.";
					mes "当然, 需要 10,000z 的手续费.";
					mes "感谢您的惠顾.";
					emotion ET_THANKS;
					close;
				}
				else {
					mes .@npc$;
					mes "唔?";
					mes "我很抱歉, 但你没有足够的资金.";
					mes "包括服务费, 需要5,010,000 z.";
					mes "请检查您的金币余额, 然后再试一次.";
					emotion ET_SCRATCH;
					close;
				}
			}
			else {
				mes .@npc$;
				mes "这真的是你的签名?";
				mes "将再次签名确认?";
				next;
			}
		}
	case 2:
		mes .@npc$;
		mes "啊哈. 您想将您的 ^FF82FF'17克拉钻石'^000000 换成 Zeny?";
		emotion ET_SURPRISE;
		next;
		if (select("- 是:- 否") == 2) {
			mes .@npc$;
			mes "那, 您想干嘛?..";
			mes ". . . . . ....";
			emotion ET_THINK;
			close;
		}

		mes .@npc$;
		mes "我明白了. 当我收到您的签名, ";
		mes "你必须提供你的 ^FF82FF'17克拉钻石'^000000.";
		mes "也不要忘记有10,000?服务费!";
		next;
		
		mes .@npc$;
		mes "您的名字是?";
		next;
		
		mes "[" + strcharinfo(0) + "]";
		mes "我的名字是 " + strcharinfo(0) + ".";
		next;
		
		mes .@npc$;
		mes "那么好吧, 请在这里签字.";
		next;
		
		while (1) {
			if (select("- 签字:- 不签") == 2) {
				mes .@npc$;
				mes "交易可不能没有你的签名.";
				close;
			}
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "你的签名已经完成.";
				mes "现在将拿走您的 ^FF82FF'17克拉钻石'^000000..";
				mes "之后您将收到 ^0000FF4,990,000z^000000.";
				next;
				
				mes .@npc$;
				mes "感谢您的惠顾.";
				mes "如果您还需要 ^FF82FF'17克拉钻石'^000000,";
				mes "请随时带着5,000,000z来取.";
				emotion ET_THANKS;
				delitem 6024, 1;
				set Zeny, Zeny + 4990000;
				close;
			}
			else {
				mes .@npc$;
				mes "这真的是你的签名?";
				mes "将再次签名确认?";
				next;
			}
		}
	}
}

prontera,165,91,6	duplicate(RareDiamondMerchant)	稀有钻石商人#1	1_M_MERCHANT
geffen,106,63,6	duplicate(RareDiamondMerchant)	稀有钻石商人#2	1_M_MERCHANT
morocc,146,100,6	duplicate(RareDiamondMerchant)	稀有钻石商人#3	1_M_MERCHANT
alberta,129,60,6	duplicate(RareDiamondMerchant)	稀有钻石商人#4	1_M_MERCHANT
lighthalzen,163,65,6	duplicate(RareDiamondMerchant)	稀有钻石商人#5	1_M_MERCHANT
rachel,106,142,6	duplicate(RareDiamondMerchant)	稀有钻石商人#6	1_M_MERCHANT
payon,180,130,6	duplicate(RareDiamondMerchant)	稀有钻石商人#7	1_M_MERCHANT
