//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= Alchemist Shop
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= [Official Conversion]
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: =================================
//= 1.0 Split Geneticist shop from main file. [Euphy]
//=     Updated dialogue to match the official script.
//============================================================

alde_alche,31,186,3	script	制作书商人#alde	4_M_ALCHE_D,{
	mes "[制作书商人]";
	mes "欢迎.";
	mes "我在这里出售";
	mes "基因学者的^0000FF特殊制作书^000000.";
	next;
	mes "[制作书商人]";
	mes "基因学者有一些技能";
	mes "需要制作书来激活.";
	next;
	mes "[制作书商人]";
	mes "更多的制作书,";
	mes "可以让你使用更多的技能.";
	next;
	mes "[制作书商人]";
	mes "请明白一点";
	mes "我只卖这些特殊的制作书";
	mes "我^FF0000不卖任何其他东西^000000.";
	next;
	mes "[制作书商人]";
	mes "您是不是需要看一下我的制作书?";
	next;
	if(select("- 当然.:- 不用.") == 2) {
		mes "[制作书商人]";
		mes "感谢您光临我的铺子.";
		mes "祝您一路顺风.";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "[制作书商人]";
		mes "抱歉, 您的物品携带种类过多.";
		mes "请先整理下背包.";
		close;
	}
	if (MaxWeight - Weight < 2500) {
		mes "[制作书商人]";
		mes "请先减轻一下负重.";
		close;
	}
	mes "[制作书商人]";
	mes "好的, 这里是制作书的列表.";
	next;
	switch(select("[苹果炸弹制造书] 100,000 Zeny:[菠萝炸弹制造书] 100,000 Zeny:[椰果炸弹制造书] 100,000 Zeny:[甜瓜炸弹制造书] 100,000 Zeny:[香蕉炸弹制造书] 100,000 Zeny:[植物基因栽培法] 100,000 Zeny:[高级药水制造手册] 100,000 Zeny:[组合烹饪料理书] 100,000 Zeny:[体力爆发研究书] 100,000 Zeny:[活力药水制造法] 100,000 Zeny:关闭")){
	case 1: set .@item,6279; break;
	case 2: set .@item,6280; break;
	case 3: set .@item,6281; break;
	case 4: set .@item,6282; break;
	case 5: set .@item,6283; break;
	case 6: set .@item,6284; break;
	case 7: set .@item,6285; break;
	case 8: set .@item,11022; break;
	case 9: set .@item,11023; break;
	case 10: set .@item,11024; break;
	case 11:
		mes "[制作书商人]";
		mes "感谢您的惠顾.";
		mes "请下次再来.";
		close;
	}
	mes "[制作书商人]";
	mes "^0000FF["+getitemname(.@item)+"] 需要";
	mes "100,000 Zeny.";
	mes "您希望购买多少本?";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[制作书商人]";
		mes "您是否对其它的书籍感兴趣呢?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[制作书商人]";
		mes "您每次不能购买超过 100 本.";
		close;
	}
	mes "[制作书商人]";
	mes "您输入了购买数量为 "+.@amount+"本 ^0000FF["+getitemname(.@item)+"]^000000.";
	mes "是否要继续交易?";
	next;
	if (select("- 是:- 否") == 1) {
		set .@total, .@amount * 100000;
		if (Zeny < .@total) {
			mes "[制作书商人]";
			mes "抱歉, 您的Zeny不够.";
			close;
		}
		if (checkweight(.@item,.@amount) == 0){
			mes "[制作书商人]";
			mes "看上去您的背包很满了.";
			mes "请先处理下再来吧.";
			close;
		}
		mes "[制作书商人]";
		mes "感谢您的采购.";
		Zeny -= .@total;
		getitem .@item,.@amount;
		close;
	}
	mes "[制作书商人]";
	mes "决定之前";
	mes "请略花费一些时间.";
	close;
}
