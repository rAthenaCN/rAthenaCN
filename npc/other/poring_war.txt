//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================  
//= Poring War
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Join a team in battle between Angeling and Deviling.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Kisuka][5511]
//= 1.1 Fixed some typos/bugs. [CalciumKid]
//= 1.1a Fixed Waiting rooms witht he default MAX_LEVEL [Slim]
//= 1.1b Fixed incorrect label calls. [Euphy]
//= 1.2 Added missing barrier resets, credits to Zopokx. [Euphy]
//============================================================

// 波利战争传送员
//============================================================
prt_fild08,159,371,3	script	波利战争传送员#wop	4_PORING,{
	mes "[波利]";
	mes "!!!!!";
	mes "是谁-! 人类, 呤呤呤~!!";
	mes "快躲起来~!";
	mes "别被骗了! 他们用垃圾在诱惑我们!";
	next;
	mes "[波利]";
	mes "哇靠!!";
	mes "呤呤呤, 呤呤呤~ 你的小伙伴们怎么啦..?";
	mes "嘿.. 嘿, 那里. 是.. 人类...";
	mes "波利..";
	next;
	mes "[波利]";
	mes "我.. 我有些有意思的事情.. 你感兴趣么?";
	mes "我们.. 我们波利需要一些非常非常勇敢的人类战士, 呤呤呤~.";
	next;
	switch(select("- 好, 我帮你!:- 什么事情:- 忽略")) {
	case 1:
		if (Zeny > 499) {
			mes "[波利]";
			mes "噢, 需要 500 Zeny 的入场费.";
			mes "祝你玩的开心, 呤呤呤.";
			delitem 7773,countitem(7773);
			set Zeny,Zeny-500;
			close2;
			warp "poring_w01",112,138;
			end;
		} else {
			mes "[波利]";
			mes "噢, 需要 500 Zeny 的入场费.";
			mes "...........";
			mes "嘿, 这就是生活, 呤呤呤~ 我们也需要 Zeny 你知道不!";
			close;
		}
	case 2:
		mes "[波利]";
		mes "事情是这样的,在我们波利世界有一些小的冲突.";
		mes "所以嘛.............来一场对决来解决它..";
		mes "到那里你就知道了.";
		close;
	case 3:
		mes "[波利]";
		mes "唔唔唔! 呼.. 人类太冷漠, 太残忍了!!!";
		close;
	}
}

// 波利兑奖机
//============================================================
poring_w01,100,97,3	script	波利兑奖机#w	4_PORING,{
	if (checkweight(714,3) == 0) {
		mes "- 你超负重了,无法使用波利兑奖机. -";
		close;
	}
	mes "这是一台波利兑奖机. 你可以使用波利硬币来兑换.";
	next;
	if(select("- 兑换.:- 阅读有关商品的描述.") == 1) {
		mes "你需要使用波利硬币进行兑换.";
		mes "你无法使用Zeny进行兑换.";
		mes "物品名称 - 波利硬币(P.Co)";
		next;
		switch(select("- 神奇的奖牌[4xP.Co]:- 组织的团结力[20xP.Co]:- 波利箱子[30xP.Co]:- 继续")) {
		case 1:
			callsub S_PoringVending,7515,4;
		case 2:
			callsub S_PoringVending,658,20;
		case 3:
			callsub S_PoringVending,12109,30;
		case 4:
			mes "这是个特殊的物品.";
			mes "物品名称 - 波利硬币(P.Co)";
			next;
			switch(select("- 狂暴野猫召唤书[15xP.Co]:- 死灵召唤书[20xP].Co:- ●剑士召唤书[20xP.Co]:- 收集品")) {
			case 1:
				callsub S_PoringVending,12300,15;
			case 2:
				callsub S_PoringVending,12301,20;
			case 3:
				callsub S_PoringVending,12302,20;
			case 4:
				mes "一次转职职业包含初心者的手办!";
				mes "除了初心者手办之外, 其它的都是^4d4dff帐号绑定物品^000000.";
				mes "请先弄明白后再选择兑换~";
				mes "物品名称 - 波利硬币(P.Co)";
				next;
				switch(select("- 初心者手办[50xP.Co]:- 剑士手办[100xP.Co]:- 盗贼手办[100xP.Co]:- 商人手办[100xP.Co]:- 服侍手办[100xP.Co]:- 魔法师手办[100xP.Co]:- 弓箭手手办[100xP.Co]:- 随机抽奖[50xP.Co]:- 取消")) {
				case 1:
					callsub S_PoringVending,2765,50;
				case 2:
					callsub S_PoringVending,2766,100;
				case 3:
					callsub S_PoringVending,2770,100;
				case 4:
					callsub S_PoringVending,2771,100;
				case 5:
					callsub S_PoringVending,2767,100;
				case 6:
					callsub S_PoringVending,2768,100;
				case 7:
					callsub S_PoringVending,2769,100;
				case 8:
					mes "你选择了随机抽奖.";
					mes "这7种类型都有可能抽中.";
					next;
					if(select("- 抽奖:- 取消") == 1) {
						if (countitem(7539) >= 50) {
							mes "放入波利硬币之后, 拉动摇杆.";
							mes "然后点击~ 在设备摇奖停止后, 奖励会从自动设备里弹出来.";
							mes "它会是什么呢?";
							next;
							switch(rand(1,17)) {
							case 5: set .@Random_Figure,2766; break; // Swordman_Figure
							case 6: set .@Random_Figure,2767; break; // Acolyte_Figure
							case 8: set .@Random_Figure,2770; break; // Thief_Figure
							case 11: set .@Random_Figure,2771; break; // Merchant_Figure
							case 13: set .@Random_Figure,2769; break; // Archer_Figure
							case 14: set .@Random_Figure,2768; break; // Mage_Figure
							default: set .@Random_Figure,2765; break; // Novice_Figure
							}
							mes "一个漂亮的" + getitemname(.@Random_Figure) + ".";
							delitem 7539,50; // Poring_Coin
							getitem .@Random_Figure,1;
							close;
						} else {
							mes "波利硬币数量不够.";
							close;
						}
					}
					mes "["+strcharinfo(0)+"]";
					mes "... 希望下次光临...";
					close;
				case 9:
					mes "["+strcharinfo(0)+"]";
					mes "... 希望下次光临...";
					close;
				}
			}
		}
	}
	mes "出售列表";
	mes "====================";
	mes "[神奇的奖牌]";
	mes " : 只有对毁葛有巨大贡献的才能够获得的特殊金属制成的奖章.";
	mes " ";
	mes "[组织的团结力]";
	mes " : 可让一个团体, 拥有增强团结力的图案. 实际上看到这雕像的人, 让人感觉到有团结力增强的感觉.";
	mes " ";
	mes "[波利箱子]";
	mes " : 包装上印有波利花纹的箱子, 好像听到有某种东西在里面动弹的声音.";
	mes " ";
	mes "[狂暴野猫召唤书]";
	mes " : 可以把狂暴野猫召唤成佣兵的召唤书.";
	mes " ";
	mes "[死灵召唤书]";
	mes " : 可以把死灵召唤成佣兵的召唤书. 不过比实际上的魔物弱地多.";
	mes " ";
	mes "[●剑士召唤书]";
	mes " : 可以把死灵召唤成佣兵的召唤书. 不过比实际上的魔物弱地多.";
	mes " ";
	mes "[初心者手办]";
	mes " : 以初心者的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " HP + 70, 初心者装备时额外增加 HP + 30 .";
	mes " ";
	mes "[剑士手办]";
	mes " : 以剑士的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " VIT + 1, 剑士装备时额外增加 DEF + 2.";
	mes " ";
	mes "[商人手办]";
	mes " : 以商人的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " STR + 1, 商人装备时额外增加 CRI + 5.";
	mes " ";
	mes "[盗贼手办]";
	mes " : 以盗贼的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " AGI + 1, 盗贼装备时额外增加 ASPD + 3%.";
	mes " ";
	mes "[魔法师手办]";
	mes " : 以魔法师的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " INT + 1, 魔法师装备时额外增加 SP恢复速度 5% .";
	mes " ";
	mes "[服侍手办]";
	mes " : 以服侍的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " INT + 1, 服侍装备时额外增加 SP + 50.";
	mes " ";
	mes "[弓箭手手办]";
	mes " : 以弓箭手的形象为原型的手办. 装备位置为 '^4d4dff装饰品^000000'.";
	mes " DEX + 1, 弓箭手装备时额外增加 ATK + 10.";
	close;
	end;

S_PoringVending:
	if (countitem(7539) >= getarg(1)) {
		mes "点击~ 在设备摇奖停止后, 奖励会从自动设备里弹出来.";
		delitem 7539,getarg(1); // Poring_Coin
		getitem getarg(0),1;
	} else {
		mes "波利硬币数量不够.";
	}
	close;
}

// Information
//============================================================
poring_w01,96,97,3	script	小恶魔#wop	4_DEVIRUCHI,{
	if (MaxWeight-Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- 稍等 !! -";
		mes "- 检查你的负重 -";
		mes "- 你带了太多东西了. -";
		mes "- 等你减轻一些负重后 -";
		mes "- 再来试一次吧. -";
		close;
	}
	mes "[小恶魔]";
	mes "噢, 又一个人类战士!";
	mes "今天怎么呢么多人类要参与我们的波利战争?";
	next;
	mes "[小恶魔]";
	mes "好吧,正好让我多赚点钱~";
	mes "好吧, 勇士-! 哈哈哈,我多么想大声喊出来~!!";
	next;
	mes "[小恶魔]";
	mes "你, 勇敢的战士, 已经准备好参与这场天使与恶魔的战争了吗?";
	next;
	mes "[小恶魔]";
	mes "啊哈哈哈, 我有点害羞了.";
	mes "无论如何, 人类. 你准备好参加波利战争了吗?";
	next;
	switch(select("我有资格参加吗?:我如何参加?:取消:快让我离开这里!")) {
	case 1:
		mes "[小恶魔]";
		mes "没有种族歧视! 任何愿意参加战斗的人都可以参与.";
		mes "奇怪的是你们人类是我们最大的敌人之一.";
		next;
		mes "[小恶魔]";
		mes "战争招募者早就已经准备好了, 如果你要参站就找他吧.";
		mes "完全取决于你要加入哪个队伍~";
		close;
	case 2:
		mes "[小恶魔]";
		mes "简单说. 你看到那个团队招聘房间吗?";
		mes "每个队伍5个成员. 当5个成员齐聚时, 比赛就自动开始了.";
		mes "杀掉对方的波利们,你就胜利了.";
		next;
		mes "[小恶魔]";
		mes "这就代表比赛开启需要一共10个人.";
		next;
		mes "[小恶魔]";
		mes "当10个人都选择完毕后, 队伍中的成员都有份参与. 没个队伍中5个成员要看他们是否组队在一起.";
		next;
		mes "[小恶魔]";
		mes "最后一半的人代表天使队";
		mes "另一半则代表恶魔队.";
		next;
		mes "[小恶魔]";
		mes "如果你代表天使队出战, 则要击杀恶魔队成员, 反之亦然.";
		next;
		mes "[小恶魔]";
		mes "每支队伍都将试图杀死对方基地中的波利. 每杀死一只, 你有一段限制击杀时间. 否则, 你杀死的波利会复活.";
		next;
		mes "[小恶魔]";
		mes "他看上去就像一个抓捕游戏, 只不过由波利来替代了. 明白?";
		mes "你必须搞明白谁和你在一个队伍中.";
		close;
	case 3:
		mes "[" + strcharinfo(0) + "]";
		mes "嗯, 我明白.";
		close;
	case 4:
		mes "[小恶魔]";
		mes "好了.";
		mes "完成后我会送到你记录点.";
		close2;
		if(WoP_SaveMap$ != "") {
			savepoint WoP_SaveMap$,WoP_SaveMap_X,WoP_SaveMap_Y;
			set WoP_SaveMap$,"";
			set WoP_SaveMap_X,0;
			set WoP_SaveMap_Y,0;
		}
		warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
		end;
	}

OnPCLogoutEvent:
	getmapxy .@map$,.@x,.@y,0;
	if (.@map$ == "poring_w02") {
		if(WoP_SaveMap$ != "") {
			savepoint WoP_SaveMap$,WoP_SaveMap_X,WoP_SaveMap_Y;
			set WoP_SaveMap$,"";
			set WoP_SaveMap_X,0;
			set WoP_SaveMap_Y,0;
		}
		delitem 7773,countitem(7773);
		set wop_team,0;
	}
	end;

OnPCDieEvent:
	getmapxy .@map$,.@x,.@y,0;
	if (.@map$ == "poring_w02" && wop_team) {
		if(getsavepoint(0) != "poring_w02" && WoP_SaveMap$ == "") {
			set WoP_SaveMap$,getsavepoint(0);
			set WoP_SaveMap_X,getsavepoint(1);
			set WoP_SaveMap_Y,getsavepoint(2);
		}
		if (wop_team == 1) {
			savepoint "poring_w02",rand(44,51),rand(76,87);
		}
		if (wop_team == 2) {
			savepoint "poring_w02",rand(146,153),rand(76,87);
		}
	}
	end;

OnPCKillEvent:
	getmapxy .@map$,.@x,.@y,0;
	if (.@map$ == "poring_w02" && wop_team) {
		getnameditem 7773,rid2name(killedrid);
	}
	end;

OnInit:
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	set $@wop_teamcount,0;
	set $@wop_deadcount_a,0;
	set $@wop_deadcount_d,0;
	set $@wop_team_a,0;
	set $@wop_team_d,0;
	set $@wop_doorcount_a,0;
	set $@wop_doorcount_d,0;
	end;
}

// Poring Registers
//============================================================
poring_w01,91,97,3	script	波利#wop_door_all	1002,{
	end;
	
OnInit:
	waitingroom "[招募10名参赛者]",11,"波利#wop_door_all::OnStartArena",10,500,9;
	enablewaitingroomevent;
	end;

OnStartArena:
	warpwaitingpc "poring_w01",101,70;
	donpcevent "#wop_warp_rtry::OnReady";
	disablewaitingroomevent;
	end;

OnEnable:
	enablewaitingroomevent;
	end;

OnDisable:
	disablewaitingroomevent;
	end;
}

poring_w01,91,68,5	script	波利#wop_door_a	1002,{
	end;

OnInit:
	waitingroom "[天使阵营招募]",6,"波利#wop_door_a::OnStartArena",5,0,9;
	enablewaitingroomevent;
	end;

OnStartArena:
	warpwaitingpc "poring_w02",26,175;
	disablewaitingroomevent;
	if ($@wop_teamcount == 0) {
		set $@wop_teamcount,1;
		donpcevent "波利#wop_door_d::OnDevilingStart";
		donpcevent "#wop_warp_rtry::OnStop";
	}
	else if ($@wop_teamcount == 1) {
		donpcevent "#wop_master::OnStart";
		donpcevent "波利#wop_door_d::OnStop";
		stopnpctimer;
	}
	end;

OnEnable:
	enablewaitingroomevent;
	end;

OnDisable:
	disablewaitingroomevent;
	end;

OnAngelingStart:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "poring_w01","伟大的战斗即将开始, 恶魔战队招募完成, 呤!",0,0xf08080;
	end;

OnTimer4000:
	mapannounce "poring_w01","加入拥有令人骄傲天使之翼的天使阵营!!",0,0xf08080;
	end;

OnTimer8000:
	mapannounce "poring_w01","你只有1分钟的考虑时间, 是否加入天使战队. 超过1分钟战斗将取消!!",0,0xf08080;
	donpcevent "#wop_master::OnAngelingWarn";
	end;

OnTimer13000:
	mapannounce "poring_w01","就是现在, 加入伟大的天使阵营. 呤!",0,0xf08080;
	end;

OnTimer20000:
	mapannounce "poring_w01","为了波利的荣耀而战! 呤! 加入天使阵营!!",0,0xf08080;
	end;

OnTimer30000:
	mapannounce "poring_w01","我们所剩的时间不多了, 呤. 不要让恶魔们污染你!",0,0xf08080;
	end;

OnTimer40000:
	mapannounce "poring_w01","来吧! 加入我们!",0,0xf08080;
	end;

OnTimer50000:
	mapannounce "poring_w01","太遗憾了! 真不可思议, 勇敢的战士们去哪里了!",0,0xf08080;
	end;

OnTimer55000:
	mapannounce "poring_w01","恶魔阵营: 你不能永远不能离开我们!! 我们就快要被取消比赛了, 呤!",0,0x33FF66;
	end;

OnTimer55100:
	donpcevent "波利#wop_door_a::OnDisable";
	donpcevent "波利#wop_door_d::OnDisable";
	donpcevent "波利#wop_door_all::OnDisable";
	set $@wop_teamcount,0;
	donpcevent "#wop_master::OnAngelingEnd";
	donpcevent "#wop_warp_rtry::OnEnable";
	end;

OnTimer58000:
	mapannounce "poring_w01","...我们已经尽力了, 呤... 让我们为下一次摇旗呐喊吧, 呤.",0,0xf08080;
	donpcevent "#wop_master::OnReset";
	stopnpctimer;
	end;
}

poring_w01,112,68,3	script	波利#wop_door_d	1002,{
	end;

OnInit:
	waitingroom "[恶魔阵营招募]",6,"波利#wop_door_d::OnStartArena",5,0,9;
	enablewaitingroomevent;
	end;

OnStartArena:
	warpwaitingpc "poring_w02",170,175;
	disablewaitingroomevent;
	if ($@wop_teamcount == 0) {
		set $@wop_teamcount,1;
		donpcevent "波利#wop_door_a::OnAngelingStart";
		donpcevent "#wop_warp_rtry::OnStop";
	}
	else if ($@wop_teamcount == 1) {
		donpcevent "#wop_master::OnStart";
		donpcevent "波利#wop_door_a::OnStop";
		stopnpctimer;
	}
	end;

OnEnable:
	enablewaitingroomevent;
	end;

OnDisable:
	disablewaitingroomevent;
	end;

OnDevilingStart:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "poring_w01","没有更好的人选了, 天使阵营已经完成招募, 呤!",0,0x33FF66;
	end;

OnTimer4000:
	mapannounce "poring_w01","恶魔阵营的成员们! 一起来加油, 呤!!",0,0x33FF66;
	end;

OnTimer8000:
	mapannounce "poring_w01","这场战斗将会取消,如果不能在1分钟内完成队员招募的话!!",0,0x33FF66;
	donpcevent "#wop_master::OnDevilingWarn";
	end;

OnTimer13000:
	mapannounce "poring_w01","是时候加入勇敢的恶魔阵营了, 呤!",0,0x33FF66;
	end;

OnTimer20000:
	mapannounce "poring_w01","D,E,V,I,L,I,N,G! 恶魔阵营! 快来加入我们!",0,0x33FF66;
	end;

OnTimer30000:
	mapannounce "poring_w01","时间不够了, 呤! 如果你想成为恶魔阵营的一员, 来吧, 加入我们!",0,0x33FF66;
	end;

OnTimer40000:
	mapannounce "poring_w01","D,E,V,I,L,I,N,G! 恶魔阵营! 快来加入我们!!",0,0x33FF66;
	end;

OnTimer50000:
	mapannounce "poring_w01","日啊! 不敢相信, 我们的人竟然不够!",0,0x33FF66;
	end;

OnTimer55000:
	mapannounce "poring_w01","天使阵营: 我们等了很久了, SB恶魔战队! 战斗要取消了, 呤!",0,0xf08080;
	end;

OnTimer55100:
	donpcevent "波利#wop_door_a::OnDisable";
	donpcevent "波利#wop_door_d::OnDisable";
	donpcevent "波利#wop_door_all::OnDisable";

	set $@wop_teamcount,0;
	donpcevent "#wop_master::OnDevilingEnd";
	donpcevent "#wop_warp_rtry::OnEnable";
	end;

OnTimer58000:
	mapannounce "poring_w01","...Ughhhhhh... 告诉我发生什么了, 呤! 好吧, 为了下一次做准备, 呤!!",0,0x33FF66;
	donpcevent "#wop_master::OnReset";
	stopnpctimer;
	end;
}

poring_w01,103,70,0	script	#wop_warp_rtry	HIDDEN_WARP_NPC,13,13,{
	end;

OnInit:
	disablenpc "#wop_warp_rtry";
	end;

OnEnable:
	enablenpc "#wop_warp_rtry";
	end;

OnDisable:
	disablenpc "#wop_warp_rtry";
	end;

OnReady:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTouch:
	warp "poring_w01",112,138;
	end;

OnTimer3000:
	mapannounce "poring_w01","裁判: 我给你1分钟的时间. 选择自己的队伍, 呤!",0,0xf08080;
	end;

OnTimer33000:
	mapannounce "poring_w01","裁判: 30秒过去了! 快啊, 选择队伍, 呤?!!",0,0xf08080;
	end;

OnTimer58000:
	mapannounce "poring_w01","裁判: 你确定你是战士?!!! 我惊呆了, 呤!",0,0xf08080;
	end;

OnTimer60000:
	mapannounce "poring_w01","由于双方队伍无法募集足够的队员, 因此比赛取消了!!",0,0xf08080;
	end;

OnTimer61000:
	enablenpc "#wop_warp_rtry";
	end;

OnTimer65000:
	disablenpc "#wop_warp_rtry";
	donpcevent "#wop_master::OnReset";
	stopnpctimer;
	end;
}

// 多佩尔先生
//============================================================
poring_w02,26,181,3	script	多佩尔先生#wop_team_a	8_DOPPEL,{
	if (checkweight(714,3) == 0) {
		mes "- 稍等!! -";
		mes "- 检查你的负重 -";
		mes "- 你带的东西太多啦. -";
		mes "- 等你清理下负重后 -";
		mes "- 再来找我吧. -";
		close;
	}
	set .@a_tname$,getpartyname($@wop_team_a);
	set .@pname$,getpartyname(getcharid(1));
	if ($@wop_team_a != 0 && $@wop_team_a == getcharid(1)) {
		mes "[多佩尔先生]";
		mes "所有人都组队了吗?";
		mes "队伍名称是... " + .@pname$ + ", 对吗?";
		mes "我将传送你们到比赛场地.";
		next;
		switch(select("- 不不! 等等!:- 去比赛场地.")) {
		case 1:
			mes "[多佩尔先生]";
			mes "怎么回事?";
			mes "有什么其他的意见?";
			mes "没时间了. 现在决定!";
			next;
			mes "[多佩尔先生]";
			mes "如果现在不去比赛场地, 就将失去比赛资格了.";
			close;
		case 2:
			if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
				mes "[多佩尔先生]";
				mes "你是队长吧. 在去比赛场地之前, 你要核查你所有的队员.";
				next;
				switch(select("- 很好.这是最后一次 .:- 我是最后一个, 送我去比赛场地.")) {
				case 1:
					mes "[多佩尔先生]";
					mes "首先, 确定所有队员都在你的组队里.";
					close;
				case 2:
					mes "[多佩尔先生]";
					mes "很好. 你的准备很充分.";
					delitem 7773,countitem(7773);
					close2;
					set wop_team,0;
					warp "poring_w02",44,82;
					end;
				}
			}
			else {
				mes "[多佩尔先生]";
				mes "很好.我要送你们去比赛场地了.";
				mes "等待其他参赛队伍.";
				delitem 7773,countitem(7773);
				close2;
				set wop_team,0;
				warp "poring_w02",44,82;
				end;
			}
		}
	}
	else {
		if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
			if ($@wop_team_a == 0) {
				mes "[多佩尔先生]";
				mes "你是天使阵营的队长吧. ";
				mes "确定所有队员在你的组队里?";
				mes "首先, 让我们注册你们的战队名字, 然后, 我们将检查你的队伍.";
				next;
				mes "[多佩尔先生]";
				mes "让我看看... 队伍名称是...";
				mes " ";
				mes "^4d4dff " + .@pname$ + " ^000000";
				mes " ";
				mes "对吧? 这是你想起的名字?";
				next;
				switch(select("- 不不! 你错了.:- 是的. 我注册了这名字.:- 取消")) {
				case 1:
					mes "[多佩尔先生]";
					mes "嘿, 我可没有一天时间来陪你! 快点想啊.";
					mes "不要忘记告诉所有队员一起组队. 只有注册过的队伍才可以参加战斗.";
					close;
				case 2:
					mes "[多佩尔先生]";
					mes "我把你们的队伍名称注册为 - " +.@pname$ + " -.";
					mes "现在, 告诉你的 ^4d4dff队员们一起组队^000000.";
					mes "我马上送你们去比赛场地.";
					set $@wop_team_a,getcharid(1);
					mapannounce "poring_w02","天使战队的注册已经完成. 队伍成员们请到 多佩尔先生.这里确认.",0,0x33FF66;
					close;
				case 3:
					mes "[多佩尔先生]";
					mes "浪费时间啊. 动作要快啊.";
					close;
				}
			}
			else {
				mes "[多佩尔先生]";
				mes "好的,你是队长吧.";
				mes "队伍注册好了没?";
				mes "你必须在一个已经注册过的队伍里!";
				next;
				mes "[多佩尔先生]";
				mes "队伍名称是 ";
				mes " " + .@a_tname$ + " ";
				mes "请, 确认.";
				close;
			}
		}
		else {
			mes "[多佩尔先生]";
			mes "如果你不是队伍的注册成员, 你是无法参加战斗的.";
			next;
			mes "[多佩尔先生]";
			mes "我在检查一次, 之后我将送你们去比赛场地.";
			mes "我们必须在一起, 这是一个团队游戏. 不然我们会有麻烦的.";
			close;
		}
	}

OnInit:
	disablenpc "多佩尔先生#wop_team_a";
	end;

OnEnable:
	enablenpc "多佩尔先生#wop_team_a";
	end;

OnDisable:
	disablenpc "多佩尔先生#wop_team_a";
	end;
}

poring_w02,170,181,3	script	多佩尔先生#wop_team_d	8_DOPPEL,{
	if (checkweight(714,3) == 0) {
		mes "- 稍等!! -";
		mes "- 检查你的负重 -";
		mes "- 你带的东西太多啦. -";
		mes "- 等你清理下负重后 -";
		mes "- 再来找我吧. -";
		close;
	}
	set .@d_tname$,getpartyname($@wop_team_d);
	set .@pname$,getpartyname(getcharid(1));
	if ($@wop_team_d != 0 && $@wop_team_d == getcharid(1)) {
		mes "[多佩尔先生]";
		mes "所有人都组队了吗?";
		mes "队伍名称是... " + .@pname$ + ", 对吗?";
		mes "我将传送你们到比赛场地.";
		next;
		switch(select("- 不不! 等等!:- 去比赛场地.")) {
		case 1:
			mes "[多佩尔先生]";
			mes "怎么回事?";
			mes "有什么其他的意见?";
			mes "没时间了. 现在决定!";
			next;
			mes "[多佩尔先生]";
			mes "如果现在不去比赛场地, 就将失去比赛资格了.";
			close;
		case 2:
			if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
				mes "[多佩尔先生]";
				mes "你是队长吧. 在去比赛场地之前, 你要核查你所有的队员.";
				next;
				switch(select("- 很好.这是最后一次 .:- 我是最后一个, 送我去比赛场地.")) {
				case 1:
					mes "[多佩尔先生]";
					mes "首先, 确定所有队员都在你的组队里.";
					close;
				case 2:
					mes "[多佩尔先生]";
					mes "很好. 你的准备很充分.";
					delitem 7773,countitem(7773);
					close2;
					set wop_team,0;
					warp "poring_w02",153,82;
					end;
				}
			}else{
				mes "[多佩尔先生]";
				mes "很好.我要送你们去比赛场地了.";
				mes "等待其他参赛队伍.";
				delitem 7773,countitem(7773);
				close2;
				set wop_team,0;
				warp "poring_w02",153,82;
				end;
			}
		}
	} else {
		if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
			if ($@wop_team_d == 0) {
				mes "[多佩尔先生]";
				mes "你是恶魔阵营的队长吧. ";
				mes "确定所有队员在你的组队里?";
				mes "首先, 让我们注册你们的战队名字, 然后, 我们将检查你的队伍.";
				next;
				mes "[多佩尔先生]";
				mes "让我看看... 队伍名称是...";
				mes " ";
				mes "^4d4dff " + .@pname$ + " ^000000";
				mes " ";
				mes "对吧? 这是你想起的名字?";
				next;
				switch(select("- 不不! 你错了.:- 是的. 我注册了这名字.:- 取消")) {
				case 1:
					mes "[多佩尔先生]";
					mes "嘿, 我可没有一天时间来陪你! 快点想啊.";
					mes "不要忘记告诉所有队员一起组队. 只有注册过的队伍才可以参加战斗.";
					close;
				case 2:
					mes "[多佩尔先生]";
					mes "我把你们的队伍名称注册为 - " +.@pname$ + " -.";
					mes "现在, 告诉你的 ^4d4dff队员们一起组队^000000.";
					mes "我马上送你们去比赛场地.";
					set $@wop_team_d,getcharid(1);
					mapannounce "poring_w02","天使战队的注册已经完成. 队伍成员们请到 多佩尔先生.这里确认.",0,0x33FF66;
					close;
				case 3:
					mes "[多佩尔先生]";
					mes "浪费时间啊. 动作要快啊.";
					close;
				}
			}
			else {
				mes "[多佩尔先生]";
				mes "好的,你是队长吧.";
				mes "队伍注册好了没?";
				mes "你必须在一个已经注册过的队伍里!";
				next;
				mes "[多佩尔先生]";
				mes "队伍名称是 ";
				mes " " + .@d_tname$ + " ";
				mes "请, 确认.";
				close;
			}
		}else{
			mes "[多佩尔先生]";
			mes "如果你不是队伍的注册成员, 你是无法参加战斗的.";
			next;
			mes "[多佩尔先生]";
			mes "我在检查一次, 之后我将送你们去比赛场地.";
			mes "我们必须在一起, 这是一个团队游戏. 不然我们会有麻烦的.";
			close;
		}
	}

OnInit:
	disablenpc "多佩尔先生#wop_team_d";
	end;

OnEnable:
	enablenpc "多佩尔先生#wop_team_d";
	end;

OnDisable:
	disablenpc "多佩尔先生#wop_team_d";
	end;
}

poring_w02,1,1,0	script	#wop_master	-1,{
	end;

OnReset:
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	mapwarp "poring_w02","poring_w01",112,138;
	donpcevent "#wop_angellium1::OnReset";
	donpcevent "#wop_angellium2::OnReset";
	donpcevent "#wop_devillium1::OnReset";
	donpcevent "#wop_devillium2::OnReset";
	set $@wop_team_a,0;
	set $@wop_team_d,0;
	set $@wop_deadcount_a,0;
	set $@wop_deadcount_d,0;
	set $@wop_teamcount,0;
	set $@wop_doorcount_a,0;
	set $@wop_doorcount_d,0;
	donpcevent "#wop_warp_a::OnDisable";
	donpcevent "#wop_warp_d::OnDisable";
	donpcevent "#wop_warp_rtry::OnDisable";
	donpcevent "Poring#wop_door_a::OnEnable";
	donpcevent "Poring#wop_door_d::OnEnable";
	donpcevent "Poring#wop_door_all::OnEnable";
	stopnpctimer;
	end;

OnStart:
	initnpctimer;
	end;

OnAngelingWarn:
	mapannounce "poring_w02","恶魔阵营招募完成. 如果天使阵营不在1分钟内完成队员招募, 将自动取消这场战斗.",0,0xf08080;
	end;

OnDevilingWarn:
	mapannounce "poring_w02","天使阵营招募完成. 如果恶魔阵营不在1分钟内完成队员招募, 将自动取消这场战斗.",0,0xf08080;
	end;

OnDevilingEnd:
	mapannounce "poring_w02","恶魔阵营招募失败. 这场战斗将马上取消.",0,0xf08080;
	end;

OnAngelingEnd:
	mapannounce "poring_w02","天使阵营招募失败. 这场战斗将马上取消.",0,0xf08080;
	end;

OnStop:
	stopnpctimer;
	end;

OnAngelingWin:
	set .@i,1;
OnDevilingWin:
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	if (.@i)
		mapannounce "poring_w02","多佩尔先生: 天使阵营队伍 " + getpartyname($@wop_team_a) + " 赢得了比赛的胜利!",0,0xf08080;
	else
		mapannounce "poring_w02","多佩尔先生: 恶魔阵营队伍 " + getpartyname($@wop_team_d) + " 赢得了比赛的胜利!",0,0xf08080;
	donpcevent "小恶魔#wop_endmaster::OnEnable";
	stopnpctimer;
	// Reset Barriers
	donpcevent "#aroom_ingate_wop::OnDisable";
	donpcevent "#aroom_outgate_wop::OnDisable";
	donpcevent "#droom_ingate_wop::OnDisable";
	donpcevent "#droom_outgate_wop::OnDisable";
	end;

OnTimer5000:
	mapannounce "poring_w02","多佩尔先生: 欢迎所有参加战斗的勇士们.",0,0xf08080;
	donpcevent "多佩尔先生#wop_team_a::OnEnable";
	donpcevent "多佩尔先生#wop_team_d::OnEnable";
	end;

OnTimer8000:
	mapannounce "poring_w02","多佩尔先生: 每个队伍成员必须加入团队, 队长会注册队伍的名字.",0,0xf08080;
	end;

OnTimer12000:
	mapannounce "poring_w02","多佩尔先生: 你还有50秒. 加入该小组并注册它的名称然后进入战斗地图.",0,0xf08080;
	end;

OnTimer32000:
	mapannounce "poring_w02","多佩尔先生: 你还有30秒. 加入该小组并注册它的名称然后进入战斗地图.",0,0xf08080;
	end;

OnTimer62000:
	mapannounce "poring_w02","多佩尔先生: 时间到了. 战斗开始前将解释相关规则.",0,0xf08080;
	end;

OnTimer70000:
	mapannounce "poring_w02","多佩尔先生: 准备好了没? 让我们来解释下规则.",0,0xf08080;
	end;

OnTimer75000:
	mapannounce "poring_w02","多佩尔先生: 天使战队必须在基地保护2只天使波利, 于此同时, 消灭恶魔战队基地中的恶魔波利, 反之亦然.",0,0xf08080;
	end;

OnTimer80000:
	mapannounce "poring_w02","多佩尔先生: 规则很简单,如果你消灭敌人的团队 2 怪物, 则获胜.",0,0xf08080;
	end;

OnTimer85000:
	mapannounce "poring_w02","多佩尔先生: 不过天使波利和恶魔波利会在一定时间内复活, 所以动作要快.",0,0xf08080;
	end;

OnTimer90000:
	mapannounce "poring_w02","多佩尔先生: 还有一些不属于任何团队的波利, 会无差别的攻击任何人.",0,0xf08080;
	end;

OnTimer95000:
	mapannounce "poring_w02","多佩尔先生: 因为波利视人类为天地, 这时你们所有人都需要合作击溃它们.",0,0xf08080;
	end;

OnTimer100000:
	mapannounce "poring_w02","多佩尔先生: 在某些领域, 你们将需要通过消除其他团队成员获得的战争徽章. 好吧, 让我们开始!",0,0xf08080;
	setmapflag "poring_w02",mf_partylock;
	setmapflag "poring_w02",mf_pvp;
	setmapflag "poring_w02",mf_pvp_noguild;
	setmapflag "poring_w02",mf_pvp_nocalcrank;
	donpcevent "#wop_warp_a::OnEnable";
	donpcevent "#wop_warp_d::OnEnable";
	donpcevent "#wop_angellium1::OnAngelingSpawn";
	donpcevent "#wop_angellium2::OnAngelingSpawn";
	donpcevent "#wop_devillium1::OnDevilingSpawn";
	donpcevent "#wop_devillium2::OnDevilingSpawn";
	end;

OnTimer700000:
	mapannounce "poring_w02","多佩尔先生: 时间到了. 你们马上可以参与到颁奖了.",0,0xf08080;
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	donpcevent "#wop_angellium1::OnReset";
	donpcevent "#wop_angellium2::OnReset";
	donpcevent "#wop_devillium1::OnReset";
	donpcevent "#wop_devillium2::OnReset";
	end;

OnTimer703000:
	donpcevent "小恶魔#wop_endmaster::OnEnable";
	stopnpctimer;
	end;
}

poring_w02,51,82,0	script	#wop_warp_a	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#wop_warp_a";
	end;

OnEnable:
	enablenpc "#wop_warp_a";
	specialeffect EF_MAPPILLAR2,AREA,"#wop_warp_a";
	end;

OnDisable:
	disablenpc "#wop_warp_a";
	end;

OnTouch:
	set wop_team,1;
	warp "poring_w02",57,82;
	end;
}

poring_w02,146,82,0	script	#wop_warp_d	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#wop_warp_d";
	end;

OnEnable:
	enablenpc "#wop_warp_d";
	specialeffect EF_MAPPILLAR2,AREA,"#wop_warp_d";
	end;

OnDisable:
	disablenpc "#wop_warp_d";
	end;

OnTouch:
	set wop_team,2;
	warp "poring_w02",140,82;
	end;
}

// Poring Spawn
//============================================================
poring_w02,80,82,0	script	#wop_angellium1	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_angellium1::OnMyMobDead";
	stopnpctimer;
	end;

OnAngelingSpawn:
	monster "poring_w02",80,82,"天使波利",1766,1,"#wop_angellium1::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_a,$@wop_deadcount_a + 1;
	if($@wop_deadcount_a == 1) {
		mapannounce "poring_w02","多佩尔先生: 在1st阵地的天使波利被杀了! 丢1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_angellium1::OnReset";
		donpcevent "#wop_angellium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnDevilingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_a,$@wop_deadcount_a - 1;
	donpcevent "#wop_angellium1::OnAngelingSpawn";
	mapannounce "poring_w02","多佩尔先生: 天使波利在1st阵地复活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
}

poring_w02,98,41,0	script	#wop_angellium2	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_angellium2::OnMyMobDead";
	stopnpctimer;
	end;

OnAngelingSpawn:
	monster "poring_w02",98,41,"天使波利",1766,1,"#wop_angellium2::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_a,$@wop_deadcount_a + 1;
	if ($@wop_deadcount_a == 1) {
		mapannounce "poring_w02","多佩尔先生: 在2nd阵地的天使波利被杀了! 丢1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_angellium1::OnReset";
		donpcevent "#wop_angellium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnDevilingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_a,$@wop_deadcount_a - 1;
	donpcevent "#wop_angellium2::OnAngelingSpawn";
	mapannounce "poring_w02","多佩尔先生: 天使波利在2nd阵地复活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
	end;
}

poring_w02,116,82,0	script	#wop_devillium1	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_devillium1::OnMyMobDead";
	stopnpctimer;
	end;

OnDevilingSpawn:
	monster "poring_w02",116,82,"恶魔波利",1767,1,"#wop_devillium1::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_d,$@wop_deadcount_d + 1;
	if($@wop_deadcount_d == 1) {
		mapannounce "poring_w02","多佩尔先生: 在1st阵地的恶魔波利被杀了! 丢1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnAngelingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_d,$@wop_deadcount_d - 1;
	donpcevent "#wop_devillium1::OnDevilingSpawn";
	mapannounce "poring_w02","多佩尔先生: 恶魔波利在1st阵地复活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
	end;
}

poring_w02,98,124,0	script	#wop_devillium2	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_devillium2::OnMyMobDead";
	stopnpctimer;
	end;

OnDevilingSpawn:
	monster "poring_w02",98,124,"恶魔波利",1767,1,"#wop_devillium2::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_d,$@wop_deadcount_d + 1;
	if ($@wop_deadcount_d == 1) {
		mapannounce "poring_w02","多佩尔先生: 在2nd阵地的恶魔波利被杀了! 丢1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnAngelingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_d,$@wop_deadcount_d - 1;
	donpcevent "#wop_devillium2::OnDevilingSpawn";
	mapannounce "poring_w02","多佩尔先生: 恶魔波利在2nd阵地复活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
}

// Deviruchi
//============================================================
poring_w02,99,201,3	script	小恶魔#wop_endmaster	4_DEVIRUCHI,{
	if (MaxWeight-Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- 稍等!! -";
		mes "- 检查你的负重 -";
		mes "- 你带的东西太多啦. -";
		mes "- 等你清理下负重后 -";
		mes "- 再来找我吧. -";
		close;
	}
	set .@a_point,$@wop_deadcount_a;
	set .@d_point,$@wop_deadcount_d;
	if(getcharid(1) == $@wop_team_a) {
		if(.@a_point < .@d_point) {
			mes "[小恶魔]";
			mes "干的好!";
			mes "多亏了你,增加了我们的胜利的机会.";
			mes "请接受这些硬币作为我们的心意.";
			next;
			if(wop_team == 1) {
				mes "[小恶魔]";
				mes "再见了, 我的人类朋友.";
				close2;
				getitem 7539,3; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小恶魔]";
				mes "稍等, 你看起来很可疑!!";
				mes "啊, 我知道了! 你是天使战队的人?!";
				mes "你来做间谍的吧!";
				mes "我不会忘记你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else if(.@a_point == .@d_point) {
			mes "[小恶魔]";
			mes "你没有尽力啊.";
			mes "太耻辱了...";
			mes "不过还是感谢你为我们而战, 收下这些硬币吧.";
			next;
			if(wop_team == 1) {
				mes "[小恶魔]";
				mes "再见了, 我的人类朋友.";
				close2;
				getitem 7539,1; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小恶魔]";
				mes "稍等, 你看起来很可疑!!";
				mes "啊, 我知道了! 你是天使战队的人?!";
				mes "你来做间谍的吧!";
				mes "肯定是你配合我们战斗的!!";
				mes "我不会忘记你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else {
			if(wop_team == 1) {
				mes "[小恶魔]";
				mes "我猜我们运气不好...";
				mes "我什么都不能给你, 因为我失去了所有...";
				next;
				mes "[小恶魔]";
				mes "再见了, 我的人类朋友.";
				close2;
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小恶魔]";
				mes "稍等... 这里有个叛徒!";
				mes "我知道了! 你是天使阵营的人?!";
				mes "你来做间谍的吧, 天使阵营!";
				next;
				mes "[小恶魔]";
				mes "你应该感到羞愧!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
	}
	else if(getcharid(1) == $@wop_team_d) {
		if (.@a_point > .@d_point) {
			mes "[小恶魔]";
			mes "干的好!";
			mes "谢谢你, 让我们获得胜利.";
			mes "请接受这些硬币作为我们的心意.";
			next;
			if (wop_team == 2) {
				mes "[小恶魔]";
				mes "再见了, 我的人类朋友.";
				close2;
				getitem 7539,3; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小恶魔]";
				mes "稍等, 你看起来很可疑!!";
				mes "啊, 我知道了! 你是天使阵营的人?!";
				mes "你来做间谍的吧!!";
				mes "我不会忘记你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else if (.@a_point == .@d_point) {
			mes "[小恶魔]";
			mes "你尽力了.";
			mes "多亏了你,增加了我们的胜利的机会...";
			mes "请接受这些硬币作为我们的心意.";
			next;
			if(wop_team == 2) {
				mes "[小恶魔]";
				mes "再见了, 我的人类朋友.";
				close2;
				getitem 7539,1; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[Devi]";
				mes "[小恶魔]";
				mes "稍等, 你看起来很可疑!!";
				mes "啊, 我知道了! 你是天使阵营的人?!";
				mes "你来做间谍的吧!";
				mes "肯定是你配合我们战斗的!!";
				mes "我不会忘记你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else {
			if (wop_team == 2) {
				mes "[小恶魔]";
				mes "我猜我们运气不好...";
				mes "我什么都不能给你, 因为我失去了所有...";
				next;
				mes "[小恶魔]";
				mes "我将传送你回人类世界.";
				close2;
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[Devi]";
				mes "稍等, 你看起来很可疑!!";
				mes "啊, 我知道了! 你是天使阵营的人?!";
				mes "你来做间谍的吧, 天使阵营!";
				next;
				mes "[小恶魔]";
				mes "你应该感到羞愧!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
	}
	else {
		mes "[小恶魔]";
		mes "这很奇怪......你的名字不被注册.";
		mes "非常不好意思啦, 规定就是规定. 如果你的队伍没注册我可帮不了你.";
		close;
	}
OnInit:
	disablenpc "小恶魔#wop_endmaster";
	end;

OnEnable:
	enablenpc "小恶魔#wop_endmaster";
	initnpctimer;
	end;

OnDisable:
	disablenpc "小恶魔#wop_endmaster";
	end;

OnTimer3000:
	mapwarp "poring_w02","poring_w02",99,196;
	end;

OnTimer5000:
	mapannounce "poring_w02","多佩尔先生: Allow me to offer a souvenir to the humans that fought bravely for the Porings! Please, speak to Devi.",0,0x00ff00;
	end;

OnTimer65000:
	mapannounce "poring_w02","多佩尔先生: Time is up! Let me teleport you.",0,0x00ff00;
	end;

OnTimer68000:
	mapwarp "poring_w02","poring_w01",112,138;
	end;

OnTimer68100:
	donpcevent "#wop_master::OnStop";
	donpcevent "#wop_devillium1::OnReset";
	donpcevent "#wop_devillium2::OnReset";
	donpcevent "#wop_angellium1::OnReset";
	donpcevent "#wop_angellium2::OnReset";
	set $@wop_team_a,0;
	set $@wop_team_d,0;
	set $@wop_deadcount_a,0;
	set $@wop_deadcount_d,0;
	set $@wop_teamcount,0;
	set $@wop_doorcount_a,0;
	set $@wop_doorcount_d,0;
	donpcevent "#wop_warp_a::OnDisable";
	donpcevent "#wop_warp_d::OnDisable";
	donpcevent "多佩尔先生#wop_team_a::OnDisable";
	donpcevent "多佩尔先生#wop_team_d::OnDisable";
	donpcevent "#wop_warp_rtry::OnDisable";
	donpcevent "波利#wop_door_a::OnEnable";
	donpcevent "波利#wop_door_d::OnEnable";
	donpcevent "波利#wop_door_all::OnEnable";
	stopnpctimer;
	end;
}

// Gate Guardian
//============================================================
poring_w02,99,52,0	script	天使守卫#wop_da	HIDDEN_NPC,{
	cutin "wop_emb0" + $@wop_doorcount_a,1;
	if ($@wop_doorcount_a < 4) {
		if (wop_team == 2) {
			mes "那里有个使用战斗徽章的设备.";
			mes "我可以看到装备徽章的空插槽.";
			next;
			switch(select("- 装备战斗徽章.:- 取消.")) {
			case 1:
				if (countitem(7773)) {
					if ($@wop_doorcount_a < 4) {
						mes "战斗徽章装备完成.";
						delitem 7773,1; // War_Badge
						set $@wop_doorcount_a,$@wop_doorcount_a + 1;
						cutin "wop_emb0" + $@wop_doorcount_a,1;
						if($@wop_doorcount_a == 4) {
							next;
							mes "大门已激活.";
							donpcevent "#aroom_ingate_wop::OnEnable";
							donpcevent "#aroom_outgate_wop::OnEnable";
						}
						close2;
					}
					else {
						mes "所有的战斗徽章都已经装备完成.";
						mes "不需要更多了.";
						close2;
					}
				}
				else {
					mes "^4d4dff - 缺少战斗徽章. 你可以在战斗中通过击杀对方成员来获得. - ^000000";
					close2;
				}
				break;
			case 2:
				mes "取消.";
				close2;
			}
		}
		else {
			mes "这里有一台设备需要装备战斗徽章.";
			mes "要小心, 别让你的敌人获得徽章.";
			close2;
		}
	}
	else {
		mes "所有的战斗徽章均已装备完成.";
		close2;
	}
	cutin "",255;
	end;

OnInit:
	set $@wop_doorcount_a,0;
	end;
}

poring_w02,103,52,0	script	#aroom_ingate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#aroom_ingate_wop";
	end;

OnEnable:
	enablenpc "#aroom_ingate_wop";
	end;

OnDisable:
	disablenpc "#aroom_ingate_wop";
	end;

OnTouch:
	warp "poring_w02",99,49;
	end;
}

poring_w02,93,51,0	script	#aroom_outgate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#aroom_outgate_wop";
	end;

OnEnable:
	enablenpc "#aroom_outgate_wop";
	end;

OnDisable:
	disablenpc "#aroom_outgate_wop";
	end;

OnTouch:
	warp "poring_w02",99,54;
	end;
}

poring_w02,98,113,0	script	恶魔守卫#wop_dd	HIDDEN_NPC,{
	cutin "wop_emb0" + $@wop_doorcount_d,1;
	if ($@wop_doorcount_d < 4) {
		if (wop_team == 1) {
			mes "那里有个使用战斗徽章的东西.";
			mes "我可以看到装备徽章的空插槽.";
			next;
			switch(select("- 装备战斗徽章.:- 取消.")) {
			case 1:
				if (countitem(7773)) {
					if ($@wop_doorcount_d < 4) {
						mes "战斗徽章装备完成.";
						delitem 7773,1; // War_Badge
						set $@wop_doorcount_d,$@wop_doorcount_d + 1;
						cutin "wop_emb0" + $@wop_doorcount_d,1;
						if ($@wop_doorcount_d == 4) {
							next;
							mes "大门已激活.";
							donpcevent "#droom_ingate_wop::OnEnable";
							donpcevent "#droom_outgate_wop::OnEnable";
						}
						close2;
					}
					else {
						mes "所有的战斗徽章都已经装备完成.";
						mes "不需要更多了.";
						close2;
					}
				}
				else {
					mes "^4d4dff - 缺少战斗徽章. 你可以在战斗中通过击杀对方成员来获得. - ^000000";
					close2;
				}
				break;
			case 2:
				mes "取消.";
				close2;
			}
		}
		else {
			mes "这里有一台设备需要装备战斗徽章.";
			mes "要小心, 别让你的敌人获得徽章.";
			close2;
		}
	}
	else {
		mes "所有的战斗徽章均已装备完成.";
		close2;
	}
	cutin "",255;
	end;

OnInit:
	set $@wop_doorcount_d,0;
	end;
}

poring_w02,93,113,0	script	#droom_ingate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#droom_ingate_wop";
	end;

OnEnable:
	enablenpc "#droom_ingate_wop";
	end;

OnDisable:
	disablenpc "#droom_ingate_wop";
	end;

OnTouch:
	warp "poring_w02",98,116;
	end;
}

poring_w02,103,114,0	script	#droom_outgate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#droom_outgate_wop";
	end;

OnEnable:
	enablenpc "#droom_outgate_wop";
	end;

OnDisable:
	disablenpc "#droom_outgate_wop";
	end;

OnTouch:
	warp "poring_w02",98,111;
	end;
}

// Other NPCs
//============================================================
-	script	Angeling Side Poring#wpa	-1,{
	end;
}

-	script	Deviling Side Marin#wpd	-1,{
	end;
}

poring_w01,79,102,5	duplicate(Angeling Side Poring#wpa)	天使阵营波利#wa1	1002
poring_w01,80,107,5	duplicate(Angeling Side Poring#wpa)	天使阵营波利#wa2	1002
poring_w01,84,108,5	duplicate(Angeling Side Poring#wpa)	天使阵营波利#wa3	1002
poring_w01,84,112,5	duplicate(Angeling Side Poring#wpa)	天使阵营波利#wa4	1002
poring_w01,88,112,5	duplicate(Angeling Side Poring#wpa)	天使阵营波利#wa5	1002

poring_w01,114,112,3	duplicate(Deviling Side Marin#wpd)	恶魔阵营波利#wd1	1242
poring_w01,117,111,3	duplicate(Deviling Side Marin#wpd)	恶魔阵营波利#wd2	1242
poring_w01,117,108,3	duplicate(Deviling Side Marin#wpd)	恶魔阵营波利#wd3	1242
poring_w01,122,108,3	duplicate(Deviling Side Marin#wpd)	恶魔阵营波利#wd4	1242
poring_w01,121,105,3	duplicate(Deviling Side Marin#wpd)	恶魔阵营波利#wd5	1242

poring_w01,84,108,3	script	#wop_ex_1	HIDDEN_WARP_NPC,3,3,{
	end;

OnTouch:
	mes "[波利]";
	mes "波~波波波 -!!!";
	mes "让我们给他们上一课, 呤!";
	mes "为了垃圾, 赌上我们的生命, 呤呤!!!";
	next;
	mes "[波利]";
	mes "出发! 战斗! 必胜, 必胜, 必胜!!!";
	emotion ET_GO, getnpcid(0, "天使阵营波利#wa1");
	emotion ET_GO, getnpcid(0, "天使阵营波利#wa2");
	emotion ET_GO, getnpcid(0, "天使阵营波利#wa3");
	emotion ET_GO, getnpcid(0, "天使阵营波利#wa4");
	emotion ET_GO, getnpcid(0, "天使阵营波利#wa5");
	next;
	if(wop_team == 1) {
		mes "[波利]";
		mes "噢啊啊, 这是个为我们战斗的战士!!";
		mes "我的天... 太好了, 呤!";
		mes "波利~ 波利~ 波波波波波~";
		mes "天使阵营~ 波波, 波利!";
	}
	else if(wop_team == 2) {
		mes "[波利]";
		mes "站住, 你! 你不是恶魔阵营的人吗?!";
		mes "快滚! 离开这里!!";
		mes "波波波波波~ 波波~ 波波波~";
		emotion ET_HUK, getnpcid(0, "天使阵营波利#wa1");
		emotion ET_FRET, getnpcid(0, "天使阵营波利#wa2");
		emotion ET_HUK, getnpcid(0, "天使阵营波利#wa3");
		emotion ET_FRET, getnpcid(0, "天使阵营波利#wa4");
		emotion ET_SURPRISE, getnpcid(0, "天使阵营波利#wa5");
	}
	else {
		mes "[波利]";
		mes "嘿, 人类. 和天使阵营一起感觉如何??!";
		emotion ET_HELP, getnpcid(0, "天使阵营波利#wa1");
		emotion ET_GO, getnpcid(0, "天使阵营波利#wa2");
		emotion ET_HELP, getnpcid(0, "天使阵营波利#wa3");
		emotion ET_GO, getnpcid(0, "天使阵营波利#wa4");
		emotion ET_GO, getnpcid(0, "天使阵营波利#wa5");
	}
	next;
	mes "[波利]";
	mes "天使阵营~ 波波波, 波利!";
	mes "胜利~ 胜利~ 波波波波波波利~";
	next;
	mes "- 波利们高高兴兴的庆祝胜利. -";
	close;
}

poring_w01,119,107,3	script	#wop_ex_2	HIDDEN_WARP_NPC,3,3,{
	end;

OnTouch:
	mes "[恶魔波利]";
	mes "唔啊啊啊啊!";
	mes "最后, 是时候给那帮蠢猪点教训了!!";
	mes "那些波利都是低等的白痴!!";
	next;
	mes "[恶魔波利]";
	mes "出发! 战斗! 为了胜利!!!!";
	emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd1");
	emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd2");
	emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd3");
	emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd4");
	emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd5");
	next;
	if (wop_team == 2) {
		mes "[恶魔波利]";
		mes "噢... 这里有个为我们而战的战士!!";
		mes "太神奇了!";
		mes "这证明了人类认识到我们是真正的怪物!!";
	}
	else if (wop_team == 1) {
		mes "[恶魔波利]";
		mes "站住, 你! 你不是天使阵营的人吗?!";
		mes "快滚! 离开这里!!";
		mes "波波波波波~ 波~ 波波~";
		emotion ET_HUK, getnpcid(0, "恶魔阵营波利#wd1");
		emotion ET_FRET, getnpcid(0, "恶魔阵营波利#wd2");
		emotion ET_HUK, getnpcid(0, "恶魔阵营波利#wd3");
		emotion ET_FRET, getnpcid(0, "恶魔阵营波利#wd4");
		emotion ET_SURPRISE, getnpcid(0, "恶魔阵营波利#wd5");
	}
	else {
		mes "[恶魔波利]";
		mes "嘿, 人类. 要为恶魔战队战斗么, 为了黑暗? 想说什么?!";
		emotion ET_HELP, getnpcid(0, "恶魔阵营波利#wd1");
		emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd2");
		emotion ET_HELP, getnpcid(0, "恶魔阵营波利#wd3");
		emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd4");
		emotion ET_GO, getnpcid(0, "恶魔阵营波利#wd5");
	}
	next;
	mes "[恶魔波利]";
	mes "世界上最神奇的波利! 都在恶魔战队! 出发, 恶魔战队, Go-!!";
	next;
	mes "- 波利们高高兴兴地庆祝胜利. -";
	close;
}
