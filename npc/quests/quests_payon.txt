//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================  
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//= 1.6 Updated Dialogs from "Oxygen Mask Quest" [Samuray22]
//= 1.7 Updated headgear quests. [L0ne_W0lf]
//============================================================ 

// 'Pretend Murdered' Quest
//============================================================
payon_in01,99,72,4	script	老太太	8_F_GRANDMOTHER,{
	if(countitem(1049) > 3 ) {
		mes "[老太太]";
		mes "我希望在下个月, 为我的孩子们缝制一套新衣服";
		mes "你能不能将你的^3355FF'韩服'^000000送给我?....我需要四件";
		mes "求求你了, 年轻人!";
		next;
		if(select( "没问题:不行")==1) {
			delitem 1049,4;
			mes "[老太太]";
			mes "唉唷, 真是太感谢你了~我可以帮小孩子做韩服了~";
			mes "呵呵呵....啊!对了, 我年轻时候参加过话剧社团的演出....";
			mes "这是我结婚后离开剧团时带出来的道具....";
			next;
			mes "[老太太]";
			mes "如果你不嫌弃的话...请收下吧!";
			mes "请慢走~";
			getitem 2293,1;
			close;
		}
		mes "[老太太]";
		mes "唉唷唉唷....我这样一个老妇人要到哪去找^3355FF'韩服'^000000呢....";
		mes "这社会真是一点人情味都没有啊....";
		close;
	}
	mes "[老太太]";
	mes "每天都一样努力的上斐扬后山去摘香菇过活, 这个月的生活不知道又要怎么过了";
	mes "....不过, 下个月就是节日了啊";
	mes "今年的过节小孩子们嚷嚷着一定要穿韩服, ";
	mes "要做韩服最少得要有四件^3355FF'韩服'^000000 啊....";
	next;
	mes "[老太太]";
	mes "年轻人, 我拜托你一件事情如果你在旅行的途中找到^0000FF'韩服'^000000 的话....请你一定要再来一趟";
	mes "虽然我没有什么东西可以给你, 但是你的恩惠我老人家一定不会忘记的....";
	close;
}

// 耳罩
payon_in01,18,10,4	script	神秘的姑娘	75,{
	mes "[神秘的姑娘]";
	mes "....";
	mes "我们家族历代都在做寒冬中具有保暖功用的^3355FF'耳罩'^000000";
	mes "因为这附近都是属于暖和的地区, 这里的人几乎都没有体验过寒冬, 加上一般人的收入只够糊口, 负担不起从其他地方支出的费用";
	mes "如果你打算到寒冷地区去的话, 你可以考虑让我帮你做个^3355FF'耳罩'^000000让你带去....";
	next;
	mes "[神秘的姑娘]";
	mes "耳罩是我们家世待流传的手制名品请带^FF3355'诅咒红宝石'^000000与^3311AA'耳机'^000000各一个来给我...";
	mes "另外还要准备^3355FF'柔毛 200根'^000000以及^DDDD00'5000 金币'^000000, 我就帮你做";
	next;
	if (select("好的, 麻烦你了:谢谢, 不用了")==2) {
		mes "[神秘的姑娘]";
		mes "....";
		mes "如果有缘的话, 将来一定还有机会碰面的....";
		mes "....";
		close;
	}
	if(countitem(724)<1 || countitem(949)<200 || countitem(5001)<1 || Zeny<5000) {
		mes "[神秘的姑娘]";
		mes "....顾客, ";
		mes "你所准备的材料不足喔....";
		mes "我也想给你低廉的价格和合理的条件, 但是....";
		mes "我们也有困难....";
		close;
	}
	mes "[神秘的姑娘]";
	mes "....";
	mes "感谢您这是做好的耳罩, 请您收下...";
	delitem 724,1;
	delitem 949,200;
	delitem 5001,1;
	set Zeny, Zeny - 5000;
	getitem 2283,1;
	next;
	mes "[神秘的姑娘]";
	mes "....";
	mes "感谢您购买我们家所制造的物品, 您一定会满意它的品质的...";
	close;
}

// 'Oxygen Mask' Quest
//============================================================
payon_in02,25,71,4	script	青年	4_M_04,{
	if(countitem(701) > 4) {
		mes "[青年]";
		mes "你说什么! 啊..不行, 怎么说都不行...";
		mes "哼.. 居然说那些家伙很可爱... 绝对..不行";
		next;
		switch(select("给他看欧拉殴拉:给他欧拉殴拉:取消")) {
		case 1:
			mes "[青年]";
			mes "喔喔喔喔喔";
			mes "这个..是 欧拉欧拉...!";
			mes "我就长话短说吧! 你要是..把这个给我的话, 我就把我最珍贵的神秘宝物给你!";
			mes "如..如何啊?你不想要吗?";
			close;
		case 2:
			delitem 701, 5;
			mes "[青年]";
			mes "呵呵呵呵呵..终于到手了! 欧拉殴拉!";
			mes "令人讨厌的盗虫,现在可以一扫而空了";
			next;
			mes "[青年]";
			mes "哈哈...哈哈...";
			mes "不好意思, 我太过兴奋了.. 嗯.. 我就照约定把我说的那个神秘宝物给你吧!";
			next;
			getitem 5004,1;
			mes "[青年]";
			mes "把这个罩在嘴上深呼吸的话... 随时随地都可以呼吸到令人神轻气爽的新鲜空气";
			mes "以前我曾经到海边工作一段时间, 这是那时候带回来的";
			mes "这个现在我用不着了, 为了答谢你, 这个就送给你吧!";
			next;
			mes "[青年]";
			mes "呵呵呵呵呵! 就这样吧!我得要赶快试用一下欧拉欧拉了!";
			close;
		case 3:
			mes "[青年]";
			mes "呵呵呵呵呵... 如果不能消除盗虫的话...";
			mes "那我也无计可施了, 只好试着去喜欢盗虫.. 呵呵呵呵呵...";
			close;
		}
	}
	mes "[青年]";
	mes "呃啊啊啊...真是.. 烦.. 烦死了.";
	mes "这些可恨的盗虫";
	next;
	if( select( "继续对话:取消") == 1) {
		mes "[青年]";
		mes "说起我的人生啊, 那真可以波澜万丈来形容";
		mes "我还小的时候父母亲就去世了... 孤零零的我每天不眠不休打杂过活";
		next;
		mes "[青年]";
		mes "至今也已经十年了.. 现在我的手边有一点存款...我梦想着能拥有一间属于自己的房子";
		next;
		mes "[青年]";
		mes "但是我的存款还是不够买一间比较像样的房子...";
		mes "再加上.. 我现在已经有一间大却不值钱的房子... 那是我不得已在没有选择的情况下买的...";
		next;
		mes "[青年]";
		mes "就是这样, 所以整个房子几乎都被该死的盗虫给完全占据作为巢穴了";
		next;
		mes "[青年]";
		mes "我也试过补杀它们..驱感它们.. 所有可以用的方法都用尽了..但是那也只是一时的方法, 不久后它们还是一样会出现!";
		mes "我甚至还一度将自己辛辛苦苦好不容易买下的房子一把火给烧了!";
		next;
		mes "[青年]";
		mes "啊啊..我真的很想... 将它们消灭殆尽...是不是用了 ^3355FF欧拉欧拉^000000 就可以了呢?";
		mes "听说这是在其他国家偶尔可以求得的相当珍贵稀少的东西";
		mes "如果有五个的话.. 我就可以完全消灭那些该死的鬼东西了...";
		close;
	}
	mes "[青年]";
	mes "怎么杀也杀不死杀不尽...我已经感到....厌烦了";
	mes "我不管了..随便吧...";
	close;
}

// 'Helm of Angel' and 'Deviruchi Cap' Quest
//============================================================
payon_in01,56,12,7	script	年轻人#12	4_M_ORIENT02,{
	mes "[年轻人]";
	mes "......有什么事吗?";
	next;
	mes "[年轻人]";
	mes "....你应该也大概听过情况了吧..";
	mes "长话短说. 讲重点吧.";
	next;
	mes "[年轻人]";
	mes "等一下!";
	mes "你准备的物品";
	mes "不管插着卡的, 冶炼的";
	next;
	mes "[年轻人]";
	mes "我都同一取代";
	mes "交给我的时候, 一定要注意.";
	mes "物品准备好了吗? 拿给我吧.";
	next;
	switch (select("天使头盔:小恶魔帽:下次再来找你")) {
	case1:
		mes "[年轻人]";
		mes "^3355FF金属头盔^000000 1 个(有洞)";
		mes "^3355FF天使发圈^000000 1 个.";
		mes "^3355FF卡伦之牙^000000 5 个.";
		next;
		mes "[年轻人]";
		mes "物品准备好了吗? 拿给我吧.";
		next;
		if(countitem(2254) < 1 || countitem(2229) < 1 || countitem(7036) < 5) break;
		mes "[年轻人]";
		mes "拿去吧.. 这是你的天使头盔.";
		delitem 2254,1;
		delitem 2229,1;
		delitem 7036,5;
		getitem 5025,1;
		close;
	case2:
		mes "[年轻人]";
		mes "^3355FF小恶魔角^000000 600 个.";
		mes "^3355FF狮鹫兽的指甲^000000 40 个.";
		next;
		mes "[年轻人]";
		mes "物品准备好了吗? 拿给我吧.";
		next;
		if(countitem(1038) < 600 || countitem(7048) < 40) break;
		mes "[年轻人]";
		mes "拿去吧.. 这是你的小恶魔帽子.";
		delitem 1038,600;
		delitem 7048,40;
		getitem 5038,1;
		close;
	case3:
		mes "[年轻人]";
		mes "...... 慢走啊.";
		close;
	}
	mes "[年轻人]";
	mes "..... 物品数量不对啊.";
	mes "确认一下再来吧.";
	close;
}
