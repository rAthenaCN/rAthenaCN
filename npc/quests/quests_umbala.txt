//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================  
//= Quest NPCs related to Umbala
//===== By: ================================================== 
//= sabernet09 & rAthena Team
//===== Current Version: ===================================== 
//= 2.2a
//===== Compatible With: ===================================== 
//= rAthena 7.15 +
//===== Description: ========================================= 
//= [Official Conversion]
//= Umbalian Language Quest, Umbala Skeletal Gate Quest,
//= Umbala Event, Create Essence/Dismantle Stone
//	Phase 1. Learning the Language
//	  (Elder) First discussion
//	  (Elder) Asking about learning the language
//	  (Elder) Final step (understanding NPC speech)
//	------------------------------------------
//	Phase 2. Create Essence/Dismantle Stone
//	 (Shaman) Get permission to speak with her from the chief
//	  (Elder) Ask about conditions needed to be fulfilled to get permission
//	  (Elder) Get permission
//	 (Shaman) Use the shaman to create essences and dismantle elemental stones.
//= Umbala Domestic Dispute?
//= - This quest is only avaliable if you have not done language quest.
//= - Variables in use: um_wind, MISC_QUEST (Bit 32768)
//===== Additional Comments: ================================= 
//= fixed by x[tsk], Lupus, PoW
//= Quest completion is now registered
//= 1.1 fixed Poporing Egg ID -> Poring Egg, fixed condition
//= 1.2-1.6 fixed some Umbalian Language quests [Lupus]
//= 1.7 Moved Umbala Event, Create Essence/Dismantle Stone, and Skeletal Gate from npc/cities/umbala.txt [Evera]
//= 1.7a Removed all custom quests to custom folder [Lupus]
//= 1.8 Added official chance of failure to Create Essence/Dismantle Stone [Lupus]
//= 1.9 Added functionality to allow players to dismantle more
//=     than a stone at once, up to 10. [SinSloth]
//= 2.0 Updated hack check and added a weight check to dismantling. [SinSloth]
//= 2.1 Rescripted to Aegis 10.3 standards. Added strange quest. [L0ne_W0lf]
//= 2.2 Readded the ability to dismantle multiple stones at once. [L0ne_W0lf]
//= 2.2a Fixed weight check from previous update. [L0ne_w0lf]
//============================================================ 

// Umbalian Language Quest :: um_trans
//============================================================
um_in,39,122,5	script	雨檀族 酋长	4_M_UMCHIEF,{
	switch(event_umbala) {
	case 0:
		mes "[卡乐喀坦]";
		mes "呵呵, 是人类啊, 应该是第一次";
		mes "来到雨檀族村落吧? 不管是谁";
		mes "只要是第一次来的人类族, ";
		mes "大部分都是那种脸";
		next;
		mes "[卡乐喀坦]";
		mes "我是卡乐喀坦, 是雨檀族的酋长";
		mes "村子里的所有事情都由我来决定, ";
		mes "能够与我沟通都是因为, ";
		mes "有一次进入这村子里的";
		mes "一个人类族探险家的功劳";
		next;
		mes "[卡乐喀坦]";
		mes "很久之前的事了... 不知道";
		mes "现在又跑去哪探险了, ";
		mes "托他的福学了很多人类的语言和文化, ";
		mes "之外也学了各种";
		mes "东西...";
		next;
		mes "[卡乐喀坦]";
		mes "在那之后我也教人类, ";
		mes "学我们雨檀族的语言, ";
		mes "不过, 不会随便告诉";
		mes "任何人... 如果是坏人";
		mes "学会我们的语言, ";
		mes "可能会对我们种族";
		mes "有很大的危险";
		next;
		mes "[卡乐喀坦]";
		mes "如果你也想学的话, ";
		mes "先去努力的了解些雨檀族";
		mes "的文化吧, 就算是语言不通, ";
		mes "也可以充分的感觉到";
		mes "服装或面貌, 以及生活之类的";
		next;
		mes "[卡乐喀坦]";
		mes "有了想要学习的心里打算之后";
		mes "再来找我吧, 虽然外表也很重要, ";
		mes "不过不要忘记, ";
		mes "心里的决心才是更重要的...";
		set event_umbala,1;
		close;
	case 1:
		mes "[卡乐喀坦]";
		mes "欧, 人类的后代啊, 村子里";
		mes "都逛过了吗? 很好奇";
		mes "你的感想是如何呢, ";
		mes "也有第一次看过人类的雨檀族, ";
		mes "所以也许会吓到";
		mes "而躲起来";
		next;
		mes "[卡乐喀坦]";
		mes "不过又有什么事情来找";
		mes "我了呢? 逛村子看起来";
		mes "应该没什么问题, 有什么";
		mes "困难的事情吗?";
		next;
		switch (select("想要学习雨檀族的语言:汶巴巴 汶巴巴?:没什么事情")) {
		case 1:
			if(getequipid(1) != getequipid(9) && getequipid(1) != getequipid(10) && getequipid(9)==getequipid(10)) {
				mes "[卡乐喀坦]";
				mes "帅气的面具啊, 我们雨檀族";
				mes "都喜欢面具, 隐藏着自己";
				mes "原来的脸和表情, 我们";
				mes "对于显露自己脸给别人看, ";
				mes "都有着很多的想法";
				next;
				mes "[卡乐喀坦]";
				mes "所以呢, 平时都戴着面具";
				mes "跟别的朋友们聊天, ";
				mes "带着面具反而";
				mes "更能够传达真实的心, ";
				mes "这是我们所相信的";
				next;
				mes "[卡乐喀坦]";
				mes "如果是你的话蛮有学";
				mes "我们语言的价值, 好吧, ";
				mes "已经答应过的教你说跟写, ";
				mes "不过人类族听了 '雨檀语' 之后";
				mes "为了学习必须要准备";
				mes "几种东西...";
				next;
				mes "[卡乐喀坦]";
				mes "对了, 需要纸, ";
				mes "我也是学习人类族的语言时, ";
				mes "跟人类拿了张纸才开始";
				mes "学语言的, 去准备吧, ";
				mes "^3377FF油纸^000000 10张, ";
				mes "^3377FF光滑的纸^000000 5张";
				next;
				mes "[卡乐喀坦]";
				mes "再来, 还有两种, 需要";
				mes "用什么写在纸上... 对了, ";
				mes "^3377FF墨汁^000000 1个, ";
				mes "^3377FF羽毛^000000 1个";
				mes "等全部准备好了再教你";
				set event_umbala,2;
				close;
			}
			mes "[卡乐喀坦]";
			mes "你好像还不太了解, ";
			mes "我们雨檀族啊...";
			mes "不能因为文化不同及语言不通";
			mes "的理由, 而放弃想要努力";
			mes "学习的心";
			next;
			mes "[卡乐喀坦]";
			mes "对, 连那种表情也会变成";
			mes "雨檀族所害怕的对象, ";
			mes "真正重要的并没有传达, ";
			mes "而有时候以显露的外表";
			mes "来判断所有的一切";
			next;
			mes "[卡乐喀坦]";
			mes "再去逛逛吧, 还有, ";
			mes "想学语言的话要知道, ";
			mes "用什么方式说跟听, ";
			mes "还有差不多准备好了的话, ";
			mes "再来找我吧";
			next;
			mes "[卡乐喀坦]";
			mes "总之我的面具怎么样呢? ";
			mes "是今年雨檀族最流行的...";
			mes "有没有很酷呢? 嗯? 嗯?";
			close;
		case 2:
			mes "[卡乐喀坦]";
			mes "哈哈, 只是模仿声音";
			mes "是不能传达任何意义的, ";
			mes "所谓的语言不就是为了传达想法, ";
			mes "而大家所定的约定吗?";
			mes "你不这样觉得吗?";
			next;
			mes "[卡乐喀坦]";
			mes "有时人类族好像忘了";
			mes "那种约定的意义而";
			mes "过生活, 真是遗憾, ";
			mes "尽管是同类的人种, ";
			mes "竟然还需要翻译啊...";
			next;
			mes "[卡乐喀坦]";
			mes "如果对我们的雨檀族还有";
			mes "兴趣的话, 再努力的试着去了解吧, ";
			mes "还有等你定下心之后";
			mes "再来找我吧, ";
			mes "不是声音, 我会教你真正的意义";
			close;
		case 3:
			mes "[卡乐喀坦]";
			mes "无所适事的也不错啊, ";
			mes "不过定下一个目标, ";
			mes "再去旅行会更有意义, ";
			mes "而且会牢牢记在心里的";
			close;
		}
	case 2:
		if(countitem(7151)>=10 && countitem(7111)>=5 && countitem(1024)>=1 && countitem(916)>=1) {
			mes "[卡乐喀坦]";
			mes "好吧, 看来都准备好了啊...";
			mes "那么现在开始教你, ";
			mes "通过这过程之后你也可以, ";
			mes "跟我们雨檀族对话了";
			next;
			mes "[卡乐喀坦]";
			mes "...........";
			next;
			mes "[卡乐喀坦]";
			mes "...........";
			mes "................";
			next;
			mes "[卡乐喀坦]";
			mes "...........";
			mes "................";
			mes ".....................";
			next;
			mes "[卡乐喀坦]";
			mes "好, 结束了, 把目前烦闷";
			mes "的心情甩掉, 偶而也跟";
			mes "其他雨檀族人们聊聊天吧, ";
			mes "聊天是很重要的";
			delitem 7151,10;
			delitem 7111,5;
			delitem 1024,1;
			delitem 916,1;
			set event_umbala,3;
			next;
			mes "[卡乐喀坦]";
			mes "那么, 如果有别的事情";
			mes "再来找我吧, ";
			mes "我会全心全意的帮你";
		}
		else {
			mes "[卡乐喀坦]";
			mes "看来还没准备好啊...";
			mes "是不是忘记需要";
			mes "的东西了呢? 再讲一次, ";
			mes "不要忘记一定要带过来喔";
			next;
			mes "[卡乐喀坦]";
			mes "^3377FF油纸^000000 10张, ";
			mes "^3377FF光滑的纸^000000 5张, ";
			mes "^3377FF墨汁^000000 1个, ";
			mes "^3377FF羽毛^000000 1个, ";
			mes "等全部准备好了再教你";
		}
		close;
	default:	//3,6,7
		mes "[卡乐喀坦]";
		mes "还有什么事情要找我吗?";
		mes "需要的话我也想为你介绍, ";
		mes "村子里的环境, 不过以我的立场, ";
		mes "是绝对不能离开这位子的";
		next;
		mes "[卡乐喀坦]";
		mes "领导一个部落, ";
		mes "并不是那么简单的事, ";
		mes "如果你站在我的立场的话, ";
		mes "多多少少会谅解的, ";
		mes "自己去逛逛吧";
		close;
	case 4:
		mes "[卡乐喀坦]";
		mes "看来对普初恰乐坦";
		mes "有什么要求啊, 不一定要";
		mes "有我的允许也可以啊... ";
		mes "如果他有那样说的话, ";
		mes "我也得为他准备些什么了";
		next;
		mes "[卡乐喀坦]";
		mes "不需要很多东西, ";
		mes "只要有一个你们";
		mes "人类族的面具就好了...";
		mes "面具的话普初恰乐坦也应该会";
		mes "相当的满足, 对吧?";
		next;
		mes "[卡乐喀坦]";
		mes "^3377FF笑脸面具^000000 1个, ";
		mes "对雨檀族来说收到面具";
		mes "可以说是最大的光荣, ";
		mes "我代替收下这就叫做, ";
		mes "萨满教僧的特殊情况, 请你了解";
		set event_umbala,5;
		close;
	case 5:
		if(countitem(2278)) {
			mes "[卡乐喀坦]";
			mes "呵呵, 好像带来了";
			delitem 2278,1;
			set event_umbala,6;
			next;
			mes "[卡乐喀坦]";
			mes "我会通知他说答应帮你了, ";
			mes "去找他聊聊吧, ";
			mes "我也不知道他会给你人类族, ";
			mes "什么样的帮助, 不过那能力";
			mes "肯定会是个很大的帮助";
		}
		else {
			mes "[卡乐喀坦]";
			mes "^3377FF笑脸面具^000000 1个, ";
			mes "对雨檀族来说收到面具";
			mes "可以说是最大的光荣, ";
			mes "我代替收下这就叫做, ";
			mes "萨满教僧的特殊情况, 请你谅解";
		}
		close;
	}
}

//====================================
// 骷髅门
//====================================
umbala,221,193,1	script	 #骷髅门	111,{
	if (event_umbala == 7) {
		warp "um_in",32,71;
		end;
	}
	mes "^3355FF门旁有着长相看起来";
	mes "令人不悦的骷髅, 门锁的";
	mes "紧紧的所以无法进去, ";
	mes "感觉到里面有人的动静, ";
	mes "看来好像有人在^000000";
	next;
	if(select("详细的观察骷髅:结束")==2) {
		mes "^3355FF要敲打一个打不开的门, ";
		mes "认为是一件很难的事情, 所以";
		mes "就决定要放弃走过去了^000000";
		close;
	}
	mes "^3355FF白骷髅的两眼空空的, ";
	mes "仔细看看可以看到有个, ";
	mes "魔力矿石般大的";
	mes "磨碎痕迹, 对面的柱子上";
	mes "也有骷髅, 那种痕迹";
	mes "不太容易找得到^000000";
	next;
	setarray .@id,0,717,715,716;
	for(set .@i,0; .@i<2; set .@i,.@i+1) {
		set .@eye$,(.@i==0)? "左": "右";
		mes "看到^3355FF" +.@eye$+ "眼孔了, ";
		mes "要怎么办呢?^000000";
		next;
		set .@j,select("不动它:放入蓝色魔力矿石:放入黄色魔力矿石:放入红色魔力矿石")-1;
		if(.@j==0) {
			mes "^3355FF把" +.@eye$+ "眼孔放着不动^000000";
		}
		else {
			if(countitem(.@id[.@j])<1) {
				mes "现在所携带的物品里一个^3355FF" +getitemname(.@id[.@j])+ "";
				mes "都没有, 没办法";
				mes "只好把" +.@eye$+ "眼孔放着不动^000000";
			}
			else {
				mes "把持有的^3355FF" +getitemname(.@id[.@j])+ "";
				mes "插在骷髅的" +.@eye$+ "眼孔里^000000";
				delitem .@id[.@j],1;
				next;
				set .@result,(.@i==0)? .@j: .@result+.@j*10;
				mes "^3355FF魔力矿石发出滚动的声音, ";
				mes "透过骷髅的嘴里出来了^000000";
				getitem .@id[.@j],1;
			}
		}
		next;
	}
	mes "^3355FF................^000000";
	next;
	mes "^3355FF.......................";
	mes ".......................^000000";
	next;
	mes "^3355FF.......................";
	mes ".......................";
	mes ".......................^000000";
	next;
	if((event_umbala<6 && .@result==33) || (event_umbala>=6 && .@result==13)) {
		mes "^3355FF在骷髅的空眼孔里";
		mes "有着吱喀的声音, 门就";
		mes "打开了, 在门打开来的同时, ";
		mes "不知不觉中我的脚步已经";
		mes "走向建筑物里面了^000000";
		close2;
		warp "um_in",32,71;
		end;
	}
	mes "^3355FF没有发生任何事情, ";
	mes "感觉上骷髅在笑, ";
	mes "不过认为就是心情上的关系吧^000000";
	close;
}

//================================
// 属性石任务
//================================
um_in,44,71,2	script	雨檀族 萨满	782,{
	switch(event_umbala) {
	default:
		mes "[?????]";
		mes "巴汶巴汶巴巴汶巴巴巴";
		mes "汶巴汶巴…";
		mes "汶巴巴汶巴巴巴汶";
		mes "巴巴汶巴汶巴汶汶汶";
		mes "汶巴汶巴巴汶...";
		close2;
		warp "umbala",217,186;
		end;
	case 3:
		mes "[普初恰乐坦]";
		mes "没想到人类会来到, ";
		mes "我们雨檀族的村落..";
		mes "还不知道大自然会对这样的变化, ";
		mes "会下达什么样的话, ";
		mes "还是一个未知数啊";
		next;
		mes "[普初恰乐坦]";
		mes "你们人类是个可怕的种族...";
		mes "只要是接触到的, ";
		mes "就不择手段的想要拥有, ";
		mes "为了去想要到达的场所, ";
		mes "会付出任何的代价";
		next;
		mes "[普初恰乐坦]";
		mes "你们人类会带给我们";
		mes "怎样的影响... 不过呢";
		mes "我来找你, 我也是对你";
		mes "有所要求啊";
		next;
		mes "[普初恰乐坦]";
		mes "不过呢, 我的能力是留给";
		mes "雨檀族的, 需要帮忙的话, ";
		mes "去得到酋长的允许后再过来吧, ";
		mes "还有, 不要对村民";
		mes "讲太多的话...";
		mes "我们都喜欢安安静静的过生活";
		set event_umbala,4;
		close;
	case 4:
	case 5:
		mes "[普初恰乐坦]";
		mes "我说过要得到酋长的允许, ";
		mes "没得到允许的人, ";
		mes "我不会给任何帮助的, ";
		mes "虽然很辛苦的进来了... 出去吧";
		close;
	case 6:
		mes "[普初恰乐坦]";
		mes "看来得到允许了啊... 虽然有点不甘心, ";
		mes "不过我还是帮你好了, 可是呢";
		mes "是不是你需要的东西, ";
		mes "你自己判断之后再做决定吧";
		next;
		mes "[普初恰乐坦]";
		mes "搜集自然物里隐藏的力量, ";
		mes "分享牢牢结合的力量, ";
		mes "是我可以帮忙的能力, ";
		mes "指的就是你们所说的, ";
		mes "属性原石那些东西...";
		next;
		mes "[普初恰乐坦]";
		mes "暂时需要准备一下, ";
		mes "所以说觉得有需要时, ";
		mes "再找我讲话吧";
		set event_umbala,7;
		close;
	case 7:
		break;
	}
	mes "[普初恰乐坦]";
	mes "需要帮助的人类啊...";
	mes "虽然不知道我的能力对你";
	mes "有没有帮助, 不过我还是会帮你的";
	next;
	mes "[普初恰乐坦]";
	mes "那么, 你需要什么呢?";
	mes "属性的结合, 属性的分解...";
	mes "结合的话需要自然物, ";
	mes "分解的话需要属性石";
	next;
	switch (select("属性的结合:属性的分解:结束")) {
	case 1:
		mes "[普初恰乐坦]";
		mes "属性的结合...";
		mes "你需要哪种属性的结合呢?";
		mes "地, 水, 火, 风... 讲出";
		mes "你现在所需要的属性吧";
		next;
		switch (select("地属性:水属性:火属性:风属性")) {
		case 1:
			if(countitem(947)<15) {
				mes "[普初恰乐坦]";
				mes "地属性... 需要充满着地的";
				mes "力量的自然物, 活在森林里";
				mes "充满着大地与树木力量的锹形虫...";
				mes "需要那锹形虫的 ^3377FF硬角 15个^000000 ";
			}
			else {
				set .@need,947;
				set .@amount,15;
				set .@gain,993;
			}
			break;
		case 2:
			if(countitem(946)<20) {
				mes "[普初恰乐坦]";
				mes "水属性... 需要充满着";
				mes "水的力量的自然物, 全身含着";
				mes "水分, 并牢牢的遮住身体的蜗牛, ";
				mes "需要那 ^3377FF蜗??20个^000000";
			}
			else {
				set .@need,946;
				set .@amount,20;
				set .@gain,991;
			}
			break;
		case 3:
			if(countitem(904)<20) {
				mes "[普初恰乐坦]";
				mes "火属性... 需要充满着";
				mes "火的力量的自然物, 用全身";
				mes "吸受着沙漠的热气而生活的";
				mes "蝎子, 需要那^3377FF蝎子尾巴^000000";
				mes "^3377FF20个^000000 ";
			}
			else {
				set .@need,904;
				set .@amount,20;
				set .@gain,990;
			}
			break;
		case 4:
			if(countitem(1013)<25) {
				mes "[普初恰乐坦]";
				mes "风属性... 需要充满着";
				mes "风的力量的自然物, 用小小的翅膀";
				mes "轻松的飞行的瓢虫, ";
				mes "需要瓢虫的 ^3377FF彩色昆虫外壳^000000";
				mes "^3377FF25个^000000";
			}
			else {
				set .@need,1013;
				set .@amount,25;
				set .@gain,992;
			}
			break;
		}
		if(.@need == 0) {
			next;
			mes "[普初恰乐坦]";
			mes "需要的自然物只有那个...";
			mes "准备好之后再来吧, ";
			mes "我总是会在这里的";
			close;
		}
		mes "[普初恰乐坦]";
		mes "随着大自然的能力, ";
		mes "把那隐藏的力量之泉源集合在一起, ";
		mes "从1到9中选一个";
		mes "你喜欢的数位, ";
		mes "如果不想要属性结合的话, ";
		mes "就选择0 ";
		while(1) {
			next;
			input .@num;
			if(.@num<=0) {
				mes "[普初恰乐坦]";
				mes "好像没希望了";
				close;
			}
			if(.@num>9) {
				mes "[普初恰乐坦]";
				mes "数字选择错误";
				continue;
			}
			break;
		}
		mes "[普初恰乐坦]";
		switch(.@need) {
			case 946: mes "把那些蜗?欠旁谡夥刑诘墓? "; break;
			case 904: mes "把那些蝎子尾巴放在这沸腾的锅里, "; break;
			case 947: mes "把那些硬角放在这沸腾的锅里, "; break;
			default: mes "把那些彩色昆虫外壳放在这沸腾的锅里, "; break;
		}
		mes "开始念神圣的咒语, ";
		mes "要记住你所说的数字, ";
		mes "否则, 也许不会起作用";
		next;
		mes "[普初恰乐坦]";
		mes "Amba Omba Zatumba! Umba! Ti!";
		mes "Umputaun Eulukaba! Umba! Ha!";
		mes "Julu Humba Rulala! Umba! La!";
		mes "Datuha Ombabalaka! Umba! Si!";
		mes "Sunutaba Abulumba! Umba! Si!";
		next;
		delitem .@need,.@amount;
		getitem .@gain,1;
		mes "[普初恰乐坦]";
		mes "你想要的属性原石在这里, ";
		mes "只是从原本存在的东西";
		mes "中所制成的, 好好的";
		mes "用在需要的目的上吧";
		close;
	case 2:
		mes "[普初恰乐坦]";
		mes "属性的分解...";
		mes "你需要哪种属性的分解呢?";
		mes "地, 水, 火, 风...";
		mes "讲出你现在所需要的属性吧";
		next;
		switch (select("地属性:水属性:火属性:风属性")) {
		case 1:
			if(countitem(997)==0) {
				mes "[普初恰乐坦]";
				mes "你想要地属性的分解吗...";
				mes "需要一个盛满着大地之";
				mes "天意的地属性石, ";
				mes "^3377FF土灵原石, 1个^000000";
			}
			else {
				set .@need,997;
				set .@gain,993;
			}
			break;
		case 2:
			if(countitem(995)==0) {
				mes "[普初恰乐坦]";
				mes "你想要水属性的分解啊...";
				mes "需要一个奔流的大洋神秘, ";
				mes "结冰的水属性石, ";
				mes "^3377水灵原石,  1个^000000";
			}
			else {
				set .@need,995;
				set .@gain,991;
			}
			break;
		case 3:
			if(countitem(994)==0) {
				mes "[普初恰乐坦]";
				mes "你想要火属性的分解啊...";
				mes "需要一个有着燃烧的, ";
				mes "火焰之心的火属性石, ";
				mes "^3377FF火灵原石, 1个^000000";
			}
			else {
				set .@need,994;
				set .@gain,990;
			}
			break;
		case 4:
			if(countitem(996)==0) {
				mes "[普初恰乐坦]";
				mes "你想要风属性的分解啊...";
				mes "需要一个隐藏着粗暴狂风";
				mes "样子的风属性石, ";
				mes "^3377FF风灵原石, 1个^000000";
			}
			else {
				set .@need,996;
				set .@gain,992;
			}
			break;
		}
		next;
		if(.@need == 0) {
			mes "[普初恰乐坦]";
			mes "需要的属性石只有那个...";
			mes "准备好之后再来吧, ";
			mes "我总是会在这里的";
			close;
		}
		mes "[普初恰乐坦]";
		switch(.@need) {
			case 997: mes "将为你所带来的大地之天意, "; 	break;
			case 995: mes "将为你所带来的大洋神秘, "; 	break;
			case 994: mes "将为你所带来的火焰之心, "; 	break;
			case 996: mes "将为你所带来的粗暴狂风, "; 	break;
		}
		mes "转变成在结合之前的样子, ";
		mes "从1到9中选一个, ";
		mes "你喜欢的数位, ";
		mes "如果不想要属性的分解的话";
		mes "就选择0";
		while(1) {
			next;
			input .@num;
			if(.@num<=0) {
				mes "[普初恰乐坦]";
				mes "好像没希望了";
				close;
			}
			if(.@num>9) {
				mes "[普初恰乐坦]";
				mes "要从1到 9之中选择, ";
				mes "这很重要请选择, ";
				mes "真正喜欢的数位吧";
				continue;
			}
			break;
		}
		set .@num,.@num+rand(5);
		if(.@num>10)
			set .@num,.@num-10;
		if(.@num<5)
			set .@num,.@num+5;
		mes "[普初恰乐坦]";
		mes "把这属性石放在地上, ";
		mes "开始念神圣的咒语, ";
		mes "要记住你所说的数字, ";
		mes "否则, 也许无法";
		mes "完全分解";
		next;
		mes "[普初恰乐坦]";
		mes "Umba Umba Kalapum! Umba! Ta!";
		mes "Lukura Ukulele Um! Umba! Ka!";
		mes "Abulaka Tabulakan! Umba! La!";
		mes "Ombaludu Zan Kunu! Umba! Ku!";
		mes "Kum Tum Lakulakun! Umba! Ha!";
		next;
		delitem .@need,1;
		getitem .@gain,.@num;
		mes "[普初恰乐坦]";
		mes "你想要的属性原石在这里, ";
		mes "只是从原本存在的东西";
		mes "中所制成的, 好好的";
		mes "用在需要的目的上吧";
		close;
	case 3:
		mes "[普初恰乐坦]";
		mes "不想要吗, 也许那";
		mes "也会成为一个选择, ";
		mes "如果以后有需要的话, ";
		mes "再来找我吧";
		close;
	}
}