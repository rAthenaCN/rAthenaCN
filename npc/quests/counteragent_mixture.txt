//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================  
//= Counteragent and Mixture Quest(Morgenstein)
//===== By: ================================================== 
//= kobra_k88
//= added some dialogs for Morgenstein by Komurka
//===== Current Version: ===================================== 
//= 2.2b
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Counteragent and Mixtures for making dyes & Alchemist Job Quest
//===== Additional Comments: ================================= 
//= 2.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 2.2a A few minor fixes. (bugreport:1122) [L0ne_W0lf]
//= 2.2b Follow up fixes to bugreport:1122 [L0ne_W0lf]
//============================================================ 

alberta_in,133,55,3	script	雷赐	4_M_02,{
	if (molgenstain == 2) {
		mes "[商人 雷赐]";
		mes "啊...就是你吗.";
		mes "那你有去看过吉芬的化学家吗?";
		next;
		if (select("询问莫基斯坦事迹:放弃") == 1) {
			mes "[商人 雷赐]";
			mes "啊, 有听过吉芬化学家事迹, 可没有机会去是吧?";
			mes "恩恩... 为何不去啊?快点去吧!说不定制作非常奇特的物品 ...";
		}
		else {
			mes "[商人 雷赐]";
			mes "有话老实说吧, 当然这不是勉强的...";
		}
		close;
	}
	mes "[商人 雷赐]";
	mes "有事情吗?";
	next;
	setarray .@talk$,"对话","叙述溶液","询问有关卡勒波迪药水","询问有关帝特立米药水","放弃";
	if(countitem(971) && countitem(972)) {
		set .@word$,"那个溶液";
		cleararray .@talk$[2],"",2;
	}
	else if(countitem(972)) {
		set .@word$,"卡勒波迪";
		set .@talk$[1],"";
		set .@talk$[3],"";
	}
	else if(countitem(971)) {
		set .@word$,"帝特立米";
		cleararray .@talk$[1],"",2;
	}
	else {
		cleararray .@talk$[1],"",3;
	}
	switch (select(.@talk$[0],.@talk$[1],.@talk$[2],.@talk$[3],.@talk$[4])) {
	case 1:
		mes "[商人 雷赐]";
		mes "为了寻找魔法道具, 曾在吉芬待过";
		mes "那时有听到一位疯子化 学家的传闻";
		mes "对化学家很有兴趣";
		mes "所以访问他问过一些事但";
		mes "没有得到他的 回应.";
		mes "他非常投入自己研究";
		next;
		mes "[商人 雷赐]";
		mes "只能听化学家的自言自语而已";
		mes "'卡勒波迪药水... 帝特立米药水...'";
		mes "那时候我不知道到这是与水不同的创新液体";
		close;
	default:
		mes "[商人 雷赐]";
		mes "哇!这就是" +.@word$+ "!";
		mes "连我也没有亲自看过的, 您真是厉害";
		mes "真遗憾, 对这液体没有清楚了解, 可能那位疯子化学家才能 知道...";
		next;
		mes "[商人 雷赐]";
		mes "您想去问他解决这个疑问吧?";
		mes "好久以前的事了, 所以不能确定他还在";
		mes "告诉您他住在吉芬, 他名字叫 ^0000ff莫基斯坦^000000.";
		next;
		mes "[商人 雷赐]";
		mes "详细的事情是去吉芬问问看吧.";
		mes "祝您好运.";
		set DYE_EVE,1;
		close;
	case 5:
		mes "[商人 雷赐]";
		mes "哈... 真是无聊的人耶.";
		close;
	}
}

geffen,184,114,6	script	阿欧鲁的朋#cm	2_M_MOLGENSTEIN,{
	if(DYE_EVE == 2) {
		mes "[吉芬居民 阿欧鲁的朋]";
		mes "老天爷又让我们在一起. 我在等您来这";
		mes "对啊...您有没有找到您要找的人?";
		next;
		if(select("叙述莫基斯坦事迹:放弃")==1) {
			mes "[吉芬居民 阿欧鲁的朋]";
			mes "您还没见过面了.";
			mes "他住在打铁铺的 2楼. 虽然跟您认识不是很久算是个缘份. 我给您一个意见, 不要太靠近他 会对您不好...";
		}
		else {
			mes "[吉芬居民 阿欧鲁的朋]";
			mes "看您的脸看不出来任何斗志. 可是您不会讲出来心里话.";
			mes "我当您 是好朋友... 这是我一个人的错觉吗? ";
		}
		close;
	}
	mes "[吉芬居民 阿欧鲁的朋]";
	mes "虽然靠伟大的魔法力量也不能时间倒转. 我这样讲话的一瞬间时间还是过去.";
	mes "为什叫我站着?";
	next;
	if(DYE_EVE > 0)
		set .@talk$,"叙述莫基斯坦事迹";
	switch (select("对话",.@talk$,"取消")) {
	case 1:
		mes "[吉芬居民 阿欧鲁的朋]";
		mes "研究和崇拜魔法的我不算是普通人. 不过这个世界有很多怪人 ...";
		mes "您会做到把全身的力量投入一个事情?  放弃一切... 好像跟疯子 一样...";
		close;
	case 2:
		set DYE_EVE,2;
		mes "[吉芬居民 阿欧鲁的朋]";
		mes "您知道哪个疯子您也不算正常人. 您很想知道他不过...";
		mes "我不能告诉您, 他是什么样的人. 我做不到 对不起... 不过您要了解我. 我只能告诉您他住在哪.";
		next;
		mes "[吉芬居民 阿欧鲁的朋]";
		mes "他住在打铁铺的2楼. 虽然跟您认识不是很久算是个缘份. 我给您一个意见, 不要太靠近他 会对您不好...";
		close;
	case 3:
		mes "[吉芬居民 阿欧鲁的朋]";
		mes "看您的脸看不出来任何斗志. 可是您不会讲出来心里话.";
		mes "可惜那我也没办法了... 人也是大自然的一部分... 就是说人的 意志... 像个大自然的流动.";
		close;
	}
}

geffen_in,141,140,3	script	莫基斯坦#cm	2_M_MOLGENSTEIN,{
	switch(DYE_EVE) {
	case 0:
	case 1:
		mes "[化学者莫基斯坦]";
		mes "哈哈哈哈哈... 哈哈哈哈哈...";
		mes "人, 我看到人拉..哈哈哈哈哈...";
		close;
	case 2:
		set DYE_EVE,3;
		mes "[化学者莫基斯坦]";
		mes "哈哈哈哈 ... 就是这样.";
		mes "哈哈哈哈 ... 哈哈哈哈哈哈 ...";
		next;
		mes "[化学者莫基斯坦]";
		mes "...我很久没有见过活人啦";
		mes "...这是个坏习惯...哈哈";
		mes "好吧 ... 有什么事呢?";
		break;
	case 3:
		mes "[化学者莫基斯坦]";
		mes "哈哈哈哈...请进来吧.";
		mes " 哈哈哈哈哈... 哈哈哈哈哈...";
		break;
	}
	next;
	switch (select("打听研究的内容:制作新的物品:取消")) {
	case 1:
		mes "[化学者莫基斯坦]";
		mes "哈哈哈哈...您好像在哪打听研究的传闻...";
		mes "那么.. 那我没隐瞒您直接都讲... ";
		mes "不用隐瞒...哈哈哈哈...";
		next;
		mes "[化学者莫基斯坦]";
		mes "说实话...我的研究已经进入完成的阶段...";
		mes "以我的天才头脑什么东西都能够做出来...";
		mes "哈哈哈...";
		mes "只是时间的问题...";
		mes "就是时间...哈哈哈...";
		next;
		mes "[化学者莫基斯坦]";
		mes "... 您想知道研究什么吗";
		mes "... 那很简单... ";
		mes "是研究用水溶化不了的东西";
		mes "怎么让它溶化";
		next;
		mes "[化学者莫基斯坦]";
		mes "是很有趣吗? ";
		mes "让石头和钢铁都变液体混在一起的办法!!!";
		mes "所有的东西都变液体~~~~ 都溶化掉!! ";
		next;
		mes "[化学者莫基斯坦]";
		mes "哈...哈...嘿...嘿...";
		mes "不管怎么样...";
		mes "到目前为止不能溶化所有的东西.";
		mes "不过有几个新的发现.";
		next;
		mes "[化学者莫基斯坦]";
		mes "用这种办法做出更多的东西, ";
		mes "比您想像中的还要多... ";
		mes "只是您想要的话!!";
		next;
		mes "[化学者莫基斯坦]";
		mes "...噢噢...";
		mes "就像脑袋会分泌使人兴奋和快乐的激素...";
		mes "...啊啊啊啊...";
		mes "您想要得话...";
		close;
	case 2:
		mes "[化学者莫基斯坦]";
		mes "嘻嘻嘻嘻...果然您也有兴趣.";
		mes "是的...是的...世界上任何人都会有兴趣...";
		next;
		mes "[化学者莫基斯坦]";
		mes "这么兴奋和有有趣 的事情 很少见...呵呵呵呵";
		mes "好... 您想做什? ...";
		mes "嗯? 嘻嘻嘻嘻";
		next;
		if(select("中和剂:混合剂")==1) {
			set .@need,971;
			set .@price,3000;
			set .@gain,973;
		}
		else {
			set .@need,972;
			set .@price,4000;
			set .@gain,974;
		}
		mes "[化学者莫基斯坦]";
		mes "噢...你想要做" +getitemname(.@gain)+ "吗...";
		mes "呵呵呵呵...好让我告诉你需要什么东西.";
		mes "...看看...噢...噢... 啊啊...啊哈...";
		next;
		mes "[化学者莫基斯坦]";
		mes "噢...我想起来了...";
		mes "好...酒精...." +getitemname(.@need);
		mes "..还有空瓶子...";
		next;
		mes "[化学者莫基斯坦]";
		mes "-- 你只要有这3个物品就可以做出来.";
		mes "还有参加这次活动你要对我有所表示...";
		mes "嘻嘻嘻嘻...不要感到太大的压力";
		next;
		mes "[化学者莫基斯坦]";
		mes "...我没有要什么..呵呵...";
		mes "听好..只要" +.@price+ " Zeny";
		next;
		mes "[化学者莫基斯坦]";
		mes "噢?你不会觉得很贵吧?";
		mes "...我感觉得到...你和我一样";
		mes "心里有实验和创造得欲望!";
		mes "嘻嘻嘻嘻嘻嘻嘻嘻";
		next;
		mes "[化学者莫基斯坦]";
		mes "好...开始吧~";
		next;
		if(select("开始:取消")==2) {
			mes "[化学者莫基斯坦]";
			mes "哈... 哈哈哈...";
			close;
		}
		if(countitem(970)<1 || countitem(.@need)<1 || countitem(713)<1 || Zeny<.@price) {
			mes "[化学者莫基斯坦]";
			mes "好像还少东西啊!!";
			close;
		}
		mes "[化学者莫基斯坦]";
		mes "呼呼呼呼... 嘻嘻嘻嘻... 好... 就这样...好...继续...还要... 哈哈哈哈";
		mes "...啊啊... 啊哈... 哈... 啊哈... 噢噢... 噢! ...噢噢噢噢噢噢啊!!! ...!!!!!!";
		next;
		delitem 970,1;
		delitem .@need,1;
		delitem 713,1;
		set Zeny, Zeny - .@price;
		getitem .@gain,1;
		mes "[化学者莫基斯坦]";
		mes "...啊哈...";
		close;
	case 3:
		mes "[化学者莫基斯坦]";
		mes "恩....恩恩恩恩恩....";
		close;
	}
}

//============================================================
// Old changelog
//============================================================
//= 1.0 Fully working
//= 1.1 Update for Alchemist Quest.
//= 1.2 added check for Empty Bottle [Lupus]
//= 1.3 Fixed some lil thingys [Darkchild]
//= 1.4 More li'l thing, added comments for items IDs [Lupus]
//= 1.5 Fixed Spelling mistakes. [Nexon]
//= 1.6 Fixed some bugs, also related to Alch Job quest [Lupus]
//= 1,7 Fixed exploit
//= 1.8 Got rid of 'al_morgen' var [Lupus]
//= 2.1 now uses (MISC_QUEST & 4) bit instead of al_morgen [Lupus]
//============================================================
