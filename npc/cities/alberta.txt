//============================================================
//= rAthenaCN [城镇人员]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena 1.0
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//============================================================

alberta,97,51,0	script	法比安	84,{
	mes "[法比安]";
	mes "嘿... 环游世界的时候我可听到了好多好玩的事儿.";
	next;
	mes "[法比安]";
	mes "我听说过有些蕴含有魔物力量的卡片呢. 如果有人使用卡片的话, 那么就可以使用魔物的力量.";
	next;
	mes "[法比安]";
	mes "我想啊, 这种让你收集所有卡片的狂热说不定是个什么骗局呢. 我的意思是啊, 一张卡片怎么能够拥有魔物的力量呢?!";
	next;
	mes "[法比安]";
	mes "我可是很严肃的对待这件事情的啊...";
	close;
}

alberta,53,39,0	script	斯泰纳	100,{
	mes "[斯泰纳]";
	mes "哦...!";
	mes "欢迎来到艾尔贝塔,";
	mes "年轻的冒险者!";
	next;
	mes "[斯泰纳]";
	mes "如果我看起来很烦躁请不要责怪我. 我在这儿转来转去是想做个计划的. 我听说吉芬有一家店出售增加魔抗的装备.";
	next;
	mes "[斯泰纳]";
	mes "如果我把那些货进来, 然后高价出售...";
	close;
}

alberta_in,20,183,0	script	乍得	49,{
	mes "[乍得]";
	mes "人们说传说中的刚尼尔长矛带有必中的攻击效果. 不知道是不是真的...";
	next;
	mes "[乍得]";
	mes "人们也说婴儿在出生前是由鹤送过来的, 也说女孩子喜欢戳那些蠢蛋男孩子, 还有人说圣诞老人真的存在的! 当然仅仅在姜饼城.";
	next;
	mes "[乍得]";
	mes "我不敢相信...";
	mes "即使这些东西之中";
	mes "只有一个是真的,";
	mes "我也不相信...";
	close;
}

alberta,131,139,2	script	醉醺醺的老头	54,{
	mes "[蒂亚戈]";
	mes "^666666*嗝*^000000";
	mes "你...你...";
	mes "看什么呀你? 滚蛋!!";
	next;
	switch(select("一言不发.","让这个家伙一个人呆着去吧.")) {
		case 1:
			mes "[蒂亚戈]";
			mes "哈哈哈哈 ^666666*嗝*^000000... 你还蛮聪明的嘛. 虽然我现在看起来很没用噶, 但是我以前是舰 '玛丽之光'的一名水手";
			next;
			switch(select("没听说过.","真的吗? 别开玩笑了!")) {
				case 1:
					mes "[蒂亚戈]";
					mes "没听说过吗? 哪有一个人没听说过臭名昭著的 '玛丽之光!' ^666666*嗝~*^000000";
					next;
					mes "[蒂亚戈]";
					mes "啊~ 那些旧时光啊. 如果... 如果没有那场风暴...^666666*嗝*^000000";
					next;
					mes "[蒂亚戈]";
					mes "啊~ 船长, 好想念我们的船长啊... 没有一个人可以接下来我们船长的剑的.";
					close;
				case 2:
					mes "[蒂亚戈]";
					mes "是啊! 没有人敢和我们 '玛丽之光!'作伴的! 也没有人可以在剑术对决中打败我们船长的!";
					next;
					mes "[蒂亚戈]";
					mes "船长啊~!!! ^666666*嗝~*^000000 他会这样挥剑, 这样, 这样!!";
					next;
					mes "[蒂亚戈]";
					mes "船长所打的混蛋身旁所有的人都被火焰所围绕啊";
					next;
					mes "[蒂亚戈]";
					mes "要不是那把大剑有神秘力量, 要不就是船长技术过人...!";
					next;
					mes "[蒂亚戈]";
					mes "呼呼~~ ^666666*呜呜* *呜呜...*^000000 上帝啊, 真的好想念大家啊! 唉,我现在很郁闷, 你走吧.";
					close;
			}
		case 2:
			mes "[蒂亚戈]";
			mes "对了!";
			mes "走吧你~";
			close;
	}
}

alberta,58,80,2	script	沙启尔	99,{
	mes "[沙启尔]";
	if (rand(2)) {
		mes "我们这些商人在买卖的时候有特别的讨价还价的技巧. 这样买别人的东西的时候可以赚得更多点.";
		next;
		mes "[沙启尔]";
		mes "当然不是叫 '请便宜点吧!' 你要有魄力, 还要精通修辞!";
		next;
		mes "[沙启尔]";
		mes "用这种神奇的能力我们可以得到省下 24 % 的钱. 但是这种技能是需要努力才学得会的!";
	} else {
		mes "作为我们商人来说.";
		mes "可以在路边开设自己的露天商店.";
		mes "来做些生意.";
		next;
		mes "[沙启尔]";
		mes "我们可以用打折技巧从镇上的店里面低价买进货物, 然后装进我们租来的手推车里.";
		next;
		mes "[沙启尔]";
		mes "之后呢, 我们就可以到处走喇, 然后把货物卖出, 赚些钱!";
		next;
		mes "[沙启尔]";
		mes "这样做生意方便又安全. 虽然卖东西感觉很简单, 但是可不要睡着...";
	}
	close;
}

alberta,62,156,2	script	宋雅	102,{
	mes "[宋雅]";
	switch(rand(3)){
	case 0:
		mes "这次我经过森林的时候看到了这个绿绿的, 在地上动来动去的茎.";
		next;
		mes "[宋雅]";
		mes "我就上前去看看这到底是什么, 但是我到的时候啊, 这个茎居然从开始猛击我的手了!!";
		next;
		mes "[宋雅]";
		mes "真心吓人...所以我就跳了回去...然后我发现那个不是茎, 是一个小小的动物.";
		next;
		mes "[宋雅]";
		mes "真幸运我没把它弄生气. 如果激怒了他的话, 这个动物不管有多小都是很危险的.";
		close;
	case 1:
		mes "你知道那些在通往斐扬森林种住着的看上去懒洋洋的熊吗?";
		next;
		mes "[宋雅]";
		mes "有一次觉得好玩, 我就朝一头熊扔了一块石头, 然后突然他就冲向了我啊! 我好怕怕的当时! 然后就跑啊跑啊, 然后只听见一声巨响!!!";
		next;
		mes "[宋雅]";
		mes "他被一根很低的树枝给撞晕倒了! 我自此发誓, 我以后再也不会随便为了乐子激怒动物了!";
		close;
	case 2:
		mes "记得有一次我看见一堆狼和一些大笨熊在较量!";
		next;
		mes "[宋雅]";
		mes "那些狼啊, 比他们看上去的可要团结合作多了. 如果他们其中的一头被攻击的话, 那么其他所有周围的狼都会跑来帮助.";
		next;
		mes "[宋雅]";
		mes "如果旁边还有其他这种类型的魔物的话, 你面前这个还是别打了吧, 小心他们联合起来对付你啊!";
		close;
	}
}

alberta,93,174,2	script	艾玛奶奶	103,{
	mes "[艾玛奶奶]";
	mes "不久以前,";
	mes "一艘废弃的船只";
	mes "顺着潮水";
	mes "飘进了艾尔贝塔的港口.";
	next;
	mes "[艾玛奶奶]";
	mes "本想来看看有没有生还者的, 所以一些镇里人就去船只里头冒险看看. 结果他们都吓坏了一样的跑出来, 说一堆尸体在船只里面行走啊.";
	next;
	mes "[艾玛奶奶]";
	mes "那艘船里头也有很多危险的水生生物. 镇里人即使是想走进去也走不进去啊.";
	next;
	mes "[艾玛奶奶]";
	mes "我们都拿那个不吉利的船只没办法, 那就让那艘船这么呆着吧. 现在有些远征团已经开始进入船只, 尝试清除那些魔物.";
	next;
	mes "[艾玛奶奶]";
	mes "所以像你这种年轻人, 又是新兵蛋子的, 去那边冒冒险也不错. 但是如果你比较弱小的话, 我建议你啊, 还是别冒着生命危险去的为妙.";
	close;
}

alberta,189,151,5	script	菲斯克	100,{
	mes "[菲斯克]";
	mes "嘿朋友,";
	mes "要去哪儿";
	mes "玩玩啊?";
	next;
	switch(select("沉船 -> 250 zeny.","依斯鲁得海滨 -> 500 zeny.","没事.")) {
		case 1:
			if (Zeny < 250) {
				mes "[菲斯克]";
				mes "嘿别骗我啊! 说了收 250 zeny的!";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[菲斯克]";
				mes "没有 500 zeny 我才不会给你过去呢!";
				close;
			}
			set Zeny,Zeny - 500;
			if(checkre(0))
				warp "izlude",195,212;
			else
				warp "izlude",176,182;
			end;
		case 3:
			mes "[菲斯克]";
			mes "好吧...";
			mes "估计是航海菜鸟一个.";
			close;
	}
}

alb2trea,39,50,6	script	菲斯克#a2t	100,{
	mes "[菲斯克]";
	mes "想要返回艾尔贝塔对吧?";
	next;
	if(select("是的.","变了主意了.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	保罗	86,{
	mes "[保罗]";
	mes "真好的一天啊~";
	mes "想要加入我们沉船冒险团吗?";
	mes "一起去那艘船上";
	mes "碰碰运气";
	mes "看看能不能搞到点什么?";
	next;
	mes "[保罗]";
	mes "哦! 我有件事情要先声明一下, 如果你不够强的话还是别去了.";
	next;
	mes "[保罗]";
	mes "想要加入吗?";
	mes "申请费只要 200 Zeny.";
	mes "只要你服了 200 Zeny 之后.";
	mes "我们就是一伙的了.";
	mes "只要 200 Zeny.";
	next;
	switch(select("加入!","啊, 不用了.")) {
		case 1:
			if (Zeny < 200) {
				mes "[保罗]";
				mes "朋友, 你的钱不够啊. 等你钱够了来吧.";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[保罗]";
			mes "好吧...";
			mes "如果你想通了就来找我吧.";
			mes "可别太久";
			mes "我们就要动身去冒险了.";
			close;
	}
}

alberta,190,173,4	script	菲利克斯 	85,{
	set .@weight,MaxWeight-Weight;
	mes "[菲利克斯]";
	if ((.@weight) < 10000) {
		mes "等等啊!!";
		mes "你东西带的太多了!";
		mes "你不能在拿东西了啊!!";
		mes "减轻点份量再来吧!";
		mes "我会在这里等你.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "你在这里干什么?";
		mes "这艘船上不提供免费的东西的!!";
		next;
		mes "[菲利克斯]";
		mes "啊啊, 要不你给我 10 个杰勒比结晶, 我给你一个红水, 好伐?";
		mes "如果你实在那么娘儿们的话, 3 个杰勒比, 换一个胡萝卜.";
		next;
		mes "[菲利克斯]";
		mes "如果感兴趣的话就把东西拿来吧.";
		set @event_zelopy,1;
		close;
	} else {
		mes "嗯, 你要换红水 还是胡萝卜? 哪一个?";
		next;
		switch(select("红水.","胡萝卜.")) {
		case 1:
			mes "[菲利克斯]";
			mes "那好...";
			mes "看看你带来了几个...";
			mes "有没有达到要求...";
			next;
			mes "[菲利克斯]";
			if (countitem(909) < 10) {
				mes "你聋子啊? 说了 10 个杰勒比结晶换一个红水的!";
				close;
			} else {
				set .@max,countitem(909)/10;
				mes "嗯, 不错嘛...";
				mes "你要多少红水?";
				mes "尽管开口.";
				next;
				switch(select("能换多少就换多少.","我来说要换多少吧.","唔, 我的结晶比你的东西要好多了, 才不换呢.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;
				case 2:
					mes "[菲利克斯]";
					mes "一次不超过 100 个所以别乱搞三七的好伐? 如果不要就输入 '0'.";
					mes "你现在最多可以换 " + .@max + " 但是一定要记住, 一次最多 100 个?.";
					input .@amount;
					next;
					mes "[菲利克斯]";
					if (.@amount <= 0) {
						mes "多谢了, 要就来我地方吧.";
						close;
					}
					if (.@amount > 100) {
						mes "我不是说过了么?一次最多 100 个, 你是想把我逼疯是么!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "嗯啊, 你好像带的东西不够. 如果你要那么多的话再去拿杰勒比结晶来我地方吧.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[菲利克斯]";
					mes "没问题,";
					mes "下次再见.";
					close;
				}
				mes "[菲利克斯]";
				mes "得了吧, 走吧你! 记得吃那些狗屎货色的时候别噎着啊.";
				close;
			}
		case 2:
			mes "[菲利克斯]";
			mes "看看你有什么多少...";
			next;
			mes "[菲利克斯]";
			if (countitem(909) < 3) {
				mes "死基佬, 滚粗去? 说了 3 个杰勒比结晶换一个胡萝卜的!";
				close;
			} else {
				set .@max,countitem(909)/3;
				mes "也不是特别娘儿们啊...";
				mes "要多少?";
				next;
				switch(select("能换多少换多少","数量我来定吧.","唔, 我的结晶比你的东西要好多了, 才不换呢.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[菲利克斯]";
					mes "一次不超过 100 个所以别乱搞三七的好伐? 如果不要就输入 '0'.";
					input .@amount;
					next;
					mes "[菲利克斯]";
					if (.@amount == 0) {
						mes "那么, 下回再见.";
						close;
					}
					if (.@amount > 100) {
						mes "死基佬, 一次最多 100 个, 我是不会和你这种人断背的!";
						close;
					}
					if (countitem(909) < .@amount*3) {
						mes "貌似你没那么多...你再去搞点来吧.";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[菲利克斯]";
					mes "哼, 小样, 你的菊花早晚是我的~";
					close;
				}
				mes "[菲利克斯]";
				mes "走吧走吧~! 别自己打肿脸撑胖子.";
				close;
			}
		}
	}
}
