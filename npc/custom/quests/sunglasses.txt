//============================================================
//= rAthenaCN [太阳眼镜制作/打洞]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= Slotted Sunglasses Quest
//===== By: ==================================================
//= amichan
//===== Current Version: =====================================
//= 1.8
//===== Description: =========================================
//= Quest to get Slotted Sunglasses.
//= Will reset after quest completion.
//===== Additional Comments: =================================
//= 1.0 by Aegis 1.1 by aichan 1.2 by x[tsk] 1.3 by Darkchild
//= 1.5 Fixed Exploit [Lupus]
//= 1.6 Fixed Exploit [KarLaeda]
//= 1.7 Relocated 'Maseph' to moc_fild07. [Kisuka]
//= 1.8 Cleaning. [Euphy]
//============================================================

alberta,88,193,5	script	太阳眼镜商人	1_F_MERCHANT_01,{
	mes "[太阳眼镜商人]";
	if(SG_QUEST1 == 1) {
		mes "我再也没有什么可以告诉你的.";
		next;
		mes "[太阳眼镜商人]";
		mes "去找玛瑟菲. 她在梦洛克北部.";
		close; }
	mes "您好.有什么可以帮你?";
	next;
	if(select("- 我听说你可以制作 ^0000FF洞墨镜^000000.:- 没什么, 抱歉打扰你.")==2) {
		mes "[太阳眼镜商人]";
		mes "你准备好了回来找我.";
		close; }
	mes "[太阳眼镜商人]";
	mes "我不能制作它, 但我可以告诉你在哪里可以找到制作的人. 一小笔费用...";
	next;
	if(select("- 多少钱?:- 没门, 我会自己找到她!")==2) {
		mes "[太阳眼镜商人]";
		mes "随你便, 制造者将不会给你制造 ^0000FF洞墨镜^000000 除非她知道你来了.";
		mes "只有我可以告诉她你来了.";
		next;
		mes "[太阳眼镜商人]";
		mes "当你放弃了回来找我. 哈哈哈...";
		close; }
	mes "[太阳眼镜商人]";
	mes "我要告诉你有关 ^0000FF洞墨镜^000000 你需要我:";
	mes " - ^0000881克拉钻石^000000";
	mes " - ^00008850 柔毛^000000";
	mes " - ^000088100000z^000000";
	next;
	if(select("- 好吧, 在这里.:- 那太多了!")==2) {
		mes "[太阳眼镜商人]";
		mes "随你便.";
		close; }
	mes "[太阳眼镜商人]";
	if (countitem(730) < 1 || countitem(949) < 50 || Zeny < 100000) {
		mes "你少了什么东西. 当你已经收集了所有的材料再回来.";
		close; }
	delitem 730,1;
	delitem 949,50;
	set Zeny, Zeny - 1000000;
	set SG_QUEST1,1;
	mes "[太阳眼镜商人]";
	mes "太好了. 现在,仔细听.";
	next;
	mes "[太阳眼镜商人]";
	mes "找一个在梦洛克北部名叫玛瑟菲的人.";
	mes "我会送她一个消息让她知道你来了.";
	close;
}

moc_fild07,334,186,5	script	玛瑟菲	4_F_GON,{
	mes "[玛瑟菲]";
	if(SG_QUEST1 != 1) {
		mes "好天气,不是么 ?";
		close; }
	mes "你好.";
	mes "你来这里为了 ^000088洞墨镜^000000, 对么?";
	next;
	if(select("- 是的:- 不,抱歉打扰你.")==2) {
		mes "[玛瑟菲]";
		mes "那你可以走了.";
		close; }
	mes "[玛瑟菲]";
	mes "做一个, 我需要一对 ^000088太阳眼镜^000000 和 40000000z.";
	next;
	if(select("- 给你.:- 不了,谢谢.")==2) {
		mes "[玛瑟菲]";
		mes "随便你.";
		close; }
	mes "[玛瑟菲]";
	if (countitem(2201) < 1) {
		mes "我需要一副 ^000088太阳眼镜^000000. 当你有了再来.";
		close; }
	if (Zeny < 40000000) {
		mes "你没有足够的钱. 当你有了再来.";
		close; }
	delitem 2201,1;
	set Zeny, Zeny - 40000000;
	mes "谢谢. 我马上就做...";
	next;
	mes "[玛瑟菲]";
	mes "拿去吧, 我的朋友.";
	mes "享受你的 ^000088洞墨镜^000000.";
	getitem 2202,1;
	set SG_QUEST1,0;
	close;
}
