//============================================================
//= rAthenaCN [头巾大盗]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= Sphinx Mask Quest (custom)
//===== By: ==================================================
//= sabernet09
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= This quest is related to Umbalian Chief.
//= 1.1 Removed it from official Umbala quests [Lupus]
//= 1.2 Added a missing variable (sphmask_q) [erKURITA]
//= 1.3 Optimized, and coordinates moved. [Euphy]
//============================================================

morocc,208,90,6	script	头巾大盗	1_M_MERCHANT,{
	mes "[头巾大盗]";
	if(event_umbala < 2) {
		mes "还想从我这里得到什么? 去去!";
		close; }
	if(sphmask_q) {
		mes "你和我没有更多的生意来往, 走开!";
		close; }
	mes "你好, 我的朋友, 你对这种罕见的面具感兴趣? 它的价值, 我向你保证你值得拥有. 狮身人面具";
	next;
	callsub L_Menu,1,"1,000,000",1000000;
	callsub L_Menu,2,"750,000",750000;
	callsub L_Menu,3,"500,000",500000;
	next;
	mes "[头巾大盗]";
	mes "啊哈! 算了吧! 我可以在别处卖的更好!!";
	set sphmask_q,1;
	close;

L_Menu:
	mes "[头巾大盗]";
	switch(getarg(0)) {
		case 1: mes "哇? 喔喔喔, 你知道那是我从别人那里偷来的? 我可不会还回去. 但可以开个价钱, 怎么样?"; break;
		case 2: mes "啊哈 你是个生意人,好吧 那么..."; break;
		case 3: mes "额... 你真会砍价, 好吧... 我最终报价..."; break; }
	next;
	if(select("- 支付"+getarg(1)+"z:- 不买")==2) return;
	mes "[头巾大盗]";
	if (Zeny < getarg(2)) {
		mes "你在拿我开玩笑?你没有足够的钱!";
		close; }
	set Zeny, Zeny-getarg(2);
	mes "喔喔, 成交!";
	getitem 7114,1;
	set sphmask_q,1;
	close;
}
