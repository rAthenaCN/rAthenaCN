//============================================================
//= rAthenaCN [国王奖励任务]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= Event King Items
//===== By: ==================================================
//= $ephiroth
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Grand Circlet Quest combined with Excalibur Quest.
//===== Additional Comments: =================================
//= 2006/09/27: 1.0 Release and fully working. [$ephiroth]
//= 1.1 Optimized. [Euphy]
//============================================================

prt_castle,80,170,4	script	普隆德拉国王	1_M_PRON_KING,{
	mes "[普隆德拉国王]";
	mes "你好, 请问你能不能帮我?";
	next;
	if(select("- 当然可以, 我可以为你做些什么?:- 对不起, 我很忙")==2) {
		mes "[普隆德拉国王]";
		mes "好的, 这太好了, 我敢肯定一定会有人帮到我的.";
		close; }
	mes "[普隆德拉国王]";
	mes "谢谢你. 好的, 在我旁边的是我的女儿她有点吵闹..";
	mes "如果你能给她这些东西, 我肯定她就会平静下来.";
	mes "我将会送给你我的皇冠或者我的宝剑来当奖励.";
	next;
	setarray .@items1[0],5007,742,30,741,50,753,20,732,1,710,20,629,20;
	setarray .@items2[0],1137,2254,1,2282,1,714,1;
	switch(select("- 你需要些什么?:- 我把东西都带齐了!:- 算了")) {
		case 1:
	for(set .@i,1; .@i<3; set .@i,.@i+1) {
				mes "[普隆德拉国王]";
				mes "如果你能给我：";
				for(set .@j,1; .@j<getarraysize(getd(".@items"+.@i)); set .@j,.@j+2)
					mes " ~ ^0055FF"+getd(".@items"+.@i+"["+(.@j+1)+"]")+"^000000x "+getitemname(getd(".@items"+.@i+"["+.@j+"]"));
				mes "我将送出我的 "+((.@i)?"- 宝剑.":"- 皇冠.");
				if (.@i==2) close;
				next; }
		case 2:
			mes "[普隆德拉国王]";
			mes "你想要哪个物品?";
			next;
			set .@i, select("宝剑:皇冠");
			for(set .@j,1; .@j<getarraysize(getd(".@items"+.@i)); set .@j,.@j+2)
				if (countitem(getd(".@items"+.@i+"["+.@j+"]")) < getd(".@items"+.@i+"["+(.@j+1)+"]")) set .@nr,1;
			mes "[普隆德拉国王]";
			if (.@nr) { 
				mes "你有些东西缺少了.";
				mes "等你收集好所有东西再回来吧.";
				close; }
			for(set .@j,1; .@j<getarraysize(getd(".@items"+.@i)); set .@j,.@j+2)
				delitem getd(".@items"+.@i+"["+.@j+"]"), getd(".@items"+.@i+"["+(.@j+1)+"]");
			getitem getd(".@items"+.@i+"[0]"),1;
			mes "非常感谢你!!";
			mes "这次肯定可以让她冷静下来的.";
			mes "作为承诺, 这是我的 "+((.@i==1)?"宝剑.":"皇冠.");
			close;
		case 3:
			mes "[普隆德拉国王]";
			mes "好的, 没关系, 我确信一定会有其他人能帮到我的.";
			close;
	}
}

prt_castle,84,170,4	script	国王的女儿	1_M_SIZ,{
	mes "[国王的女儿]";
	mes "哼, 我现在讨厌死我父王了!!";
	next;
	if(select("- 为什么?:- 无所谓..")==2) {
		mes "[国王的女儿]";
		mes "那好吧! 哼!!";
		close; }
	mes "[国王的女儿]";
	mes "好吧, 他给不了我想要的!!";
	mes "他是普隆德拉王国的国王. 他应该能够给我所有的东西!!!";
	close;
}
