//============================================================
//= rAthenaCN [二十一点庄家]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== Hercules Script ======================================
//= 二十一点（二十一点）
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= 二十一点纸牌游戏.  基于非标准赌场的游戏
//= 二十一点规则.  庄家必须至少拥有17点才能停牌, 不然会
//= 自动拿牌到17点或以上, 之后停牌.  玩家必须至少拥有
//= 13点才能停牌.  A牌可以是11点或1点.  可以加倍下注.
//= 目前不允许进行买保险操作或分成2副牌操作.
//===== Additional Comments: =================================
//= 
//============================================================

cmd_in02,188,89,1	script	二十一点庄家	1_M_LIBRARYMASTER,{
	mes "[庄家]";
	mes "你好! 你想玩二十一点游戏吗?";
M_Menu:
	next;
	menu "- 规则是什么?",M_0, "- 我想玩.",L_Play, "- 不, 谢谢.",M_End;

M_0:
	mes "[庄家]";
	mes "你想知道些什么?";
sM_Menu0:
	next;
	menu "- 基本说明.",sM_0a, "- 输赢方式.",sM_0b, "- '加倍下注'.",sM_0c,
	"- A牌的数值.",sM_0d, "- 不用了.",M_Menu;

sM_0a:
	mes "[庄家]";
	mes "二十一点是以目标接近或达到21点的一种纸牌游戏";
	mes "尽可能的接近21点但不要超过.";
	next;
	mes "[庄家]";
	mes "游戏开始前, 玩家必须下注.";
	mes "一旦下注, 庄家和玩家分别分给2张牌.";
	mes "根据你拥有的牌, 你可以选择 ^5533FF'停牌'^000000,";
	mes "或者你可以选择 ^5533FF'分牌'^000000.";
	next;
	mes "[庄家]";
	mes "当你选择 '停牌', 你就是告诉了庄家你不再需要牌了.";
	mes "这样如果庄家想分牌的话, 就可以分牌.";
	mes "要想停牌, 你必须 ^FF3355至少有13点^000000. ";
	mes "庄家只能在他拥有^FF3355 17点或更多时^000000才可停牌.";
	next;
	mes "[庄家]";
	mes "当你选择 '分牌', 你就是告诉了庄家你想再要一张牌了";
	mes "通过分得更多的牌你会增加自己的总点数.";
	mes "只要你 ^FF3355少于21^000000 点,";
	mes "你就可以分牌.";
	goto sM_Menu0;
sM_0b:
	mes "[庄家]";
	mes "有3种方式可以在玩二十一点时产生输赢.";
	next;
	mes "[庄家]";
	mes "1.) 在二十一点最后一轮, 如果你的点数";
	mes "比庄家的多你就赢得了这局.  反过来如果你";
	mes "比庄家的点数少你就输了.";
	next;
	mes "[庄家]";
	mes "2.) 如果你分牌时使得你的点数超过了21点你";
	mes "自动输的这局.  这个叫 ^5533FF'爆牌'^000000.  ";
	mes "如果庄家爆牌那么你就赢得了这局.";
	next;
	mes "[庄家]";
	mes "3.) 如果你在最初的2张牌的点数达到了21点, ";
	mes "你就自动赢得了这局.  这个叫做 ^5533FF'二十一点'^000000";
	mes "而这发生在你得到了A牌和10牌. ";
	mes "如果庄家得到了二十一点他就赢得了这局.";
	next;
	mes "[庄家]";
	mes "4.) 除了输和赢, 你可以和庄家打平.  ";
	mes "如果你和庄家在最后一轮的总点数相同,";
	mes "这就导致没有输赢的平局";
	next;
	mes "[庄家]";
	mes "这个被称作和庄家 ^5533FF'平手'^000000 .  这也";
	mes "适用于你和庄家同时拿到二十一点.";
	goto sM_Menu0;
sM_0c:
	mes "[庄家]";
	mes "'加倍下注' 选择允许你加倍当前的赌注,";
	mes "但缺点是你将只能够多拿一张牌.";
	mes ".  这个选择只能在每局的开始可以选择.";
	next;
	mes "[庄家]";
	mes "这是一个加倍下注有用的例子, ";
	mes "当你前2张牌的总点数是11.  ";
	mes "你就有机会从下一张牌得到21点或20点.  ";
	mes "这将会是加倍下注的好手法.";
	goto sM_Menu0;
sM_0d:
	mes "[庄家]";
	mes "A牌是二十一点里面的特殊牌, ";
	mes "因为他有2个点数.  一张A牌可以作为11点,";
	mes "或者当做1点.  例如你有一张A牌和4牌, 那么";
	mes "你可以拥有15点或5点.";
	next;
	mes "[庄家]";
	mes "如果你决定停牌, A牌自动计算为11点";
	mes "使你拥有15点.";
	next;
	mes "[庄家]";
	mes "如果你决定分牌并且得到了9牌, ";
	mes "A牌自动计算为1点使得你总点数为14.";
	mes "如果把A牌当做11点,";
	mes "你就会使总点数超过21点而输掉比赛.";
	next;
	mes "[庄家]";
	mes "正是因为A牌的灵活性";
	mes "使其成为了游戏中最强大的牌.";
	goto sM_Menu0;
M_End:
	mes "[庄家]";
	mes "欢迎随时回来";
	close;

//================
L_Play:
	mes "[庄家]";
	mes "请下注...";
	next;
	menu "- 2z",M_1a, "- 100z",M_1b, "- 20000z",M_1c, "- 1000000z",M_1d, "- 承受不了....",M_End;

M_1a:
	if(Zeny < 2) goto sL_NotEnuf;
	set @bet, 2;
	goto L_Cont0;
M_1b:
	if(Zeny < 100) goto sL_NotEnuf;
	set @bet, 100;
	goto L_Cont0;
M_1c:
	if(Zeny < 20000) goto sL_NotEnuf;
	set @bet, 20000;
	goto L_Cont0;
M_1d:
	if(Zeny < 1000000) goto sL_NotEnuf;
	set @bet, 1000000;
	goto L_Cont0;

sL_NotEnuf:
	mes "[庄家]";
	mes "很抱歉你没有足够的Zeny下注.";
	close;

L_Cont0:
	mes "(正在洗牌)";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- 这是 ^FF5533庄家的^000000 的牌:";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "    庄家拥有: ^FF5533"+@dealTot+"^000000";
	mes " ";
	mes "- 这是 ^5533FF你的^000000 牌:";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "    你有: ^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "    你有: ^5533FF" +@playTot+ "^000000, or ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "- 分牌",M_Hit, "- 停牌",M_Stay, "- 加倍下注",M_Double;
	if(@dealerTurn == 0) menu "- 分牌",M_Hit, "- 停牌",M_Stay;

M_Stay:
	mes "[庄家]";
	if(@playTot < 13) goto sL_PlayToLow;
	if(@dealTot > 16) mes "庄家停牌.";
	if(@dealTot > 16 || @numD == 5) goto L_Check;
	mes "庄家正在分牌";
	next;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	set @dealerTurn, 1;
	goto L_Start;

sL_PlayToLow:
	mes "抱歉你没有达到停牌的足够的点数.  你必须分牌.";
	next;
	goto M_Hit;

M_Hit:
	if(@numP == 5) goto M_Stay;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;
M_Double:
	mes "[庄家]";
	mes "玩家选择加倍下注.  你当前的赌注加倍,";
	mes "你可以额外得到一张牌.";
	next;
	set @dealerTurn, 1;
	set @bet, @bet*2;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;

//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

sL_Win:
	mes "[庄家]";
	mes "恭喜, 你赢了!";
	next;
	set Zeny, Zeny + @bet;
	goto L_Play;
sL_Lose:
	mes "[庄家]";
	mes "抱歉你输了.";
	set Zeny, Zeny - @bet;
	next;
	goto L_Play;
sL_Push:
	mes "[庄家]";
	mes "平手.  你与庄家打平.";
	next;
	goto L_Play;


//==================================
	// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
	// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

GetDealTot:
	set @dealTot, @dealTot + @dealCard[@i];
	set @i, @i + 1;
	if(@i < @numD) goto GetDealTot;
	if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
	set @i, 0;
GetPlayTot:
	set @playTot, @playTot + @playCard[@i];
	set @i, @i + 1;
	if(@i < @numP) goto GetPlayTot;
	if(@pAce == 1 && @playTot > 21) set @pAce, 2;
	if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
	// Sub function for displaying the Cards
sF_D2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_D3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_P3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_D4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@dealCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_P4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@playCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_D5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
}
