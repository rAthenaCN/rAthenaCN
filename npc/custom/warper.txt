//============================================================
//= rAthenaCN [传送员]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Warper
//===== By: ================================================== 
//= Euphy
//===== Current Version: =====================================
//= 1.4b
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: ========================================= 
//= A complete - but very condensed - warper script.
//= Some coordinates written by Tekno-Kanix and ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Added missing duplicates and fixed coordinates.
//= 1.2 Added new episodes and simplified functions.
//= 1.3 Added Renewal checks and Instances menu.
//=     Aligned coordinates with @go.
//= 1.4 Added new Guild Dungeons.
//= 1.4a Slight edits.
//= 1.4b Added Wolfchev's Laboratory warp.
//============================================================

-	script	Warper	-1,{
function Go; function Disp; function Pick; function Restrict;

// --------------------------------------------------
//	Main Menu:
// --------------------------------------------------

menu	"- 最后一次传送点 ^777777["+lastwarp$+"]^000000",-,
    	"- 城镇",Towns,
    	"- 野外",Fields,
    	"- 洞穴",Dungeons,
    	"- 公会城堡",Castles,
    	"- 地下工会密穴",Guild_Dungeons,
    	"- 副本",Instances,
    	"- 特殊地区",Special;

	if (lastwarp$ == "")
		message strcharinfo(0),"You haven't warped anywhere yet.";
	else
		warp lastwarp$,lastwarpx,lastwarpy;
	end;

// ------------------- Functions -------------------
// * Go("<map>",<x>,<y>);
//	~ Warps directly to a map.
//
// * Disp("<Menu Option>",<first option>,<last option>);
// * Pick("<map_prefix>"{,<index offset>});
//	~ Dynamic menu and map selection (auto-numbered).
//
// * Disp("<Option 1>:<Option 2>:<etc.>");
// * Pick("","<map1>","<map2>","<etc.>");
//	~ Manual menu and map selection (listed).
//
// * Restrict("<RE | Pre-RE>"{,<menu option numbers>});
//	~ Only allows map for Renewal or Pre-Renewal modes.
//     If menu option numbers are given, only those maps
//     will be restricted (i.e. not for "Go").
//
// Other notes:
//   ~ Array @c[] holds all (x,y) coordinates.
//   ~ Use @c[2] EXCEPT when maps begin dynamically
//	  at 0: use @c[0] and Pick() offset 1.
// --------------------------------------------------

function Go {
	set lastwarp$, getarg(0);
	set lastwarpx, getarg(1,0);
	set lastwarpy, getarg(2,0);
	warp getarg(0),getarg(1,0),getarg(2,0);
	end;
}
function Disp {
	if (getargcount() < 3)
		set @menu$, getarg(0);
	else {
		set @menu$,"";
		for (set .@i,getarg(1); .@i<=getarg(2); set .@i,.@i+1)
			set @menu$, @menu$+getarg(0)+" "+.@i+":";
	}
	return;
}
function Pick {
	set .@warp_block,@warp_block;
	set @warp_block,0;
	set .@select, select(@menu$);
	if (getarg(0) == "") {
		set .@i, .@select;
		set .@map$, getarg(.@i);
	} else {
		set .@i, .@select-getarg(1,0);
		set .@map$, getarg(0)+((.@i<10)?"0":"")+.@i;
	}
	if (.@warp_block & (1<<.@select)) {
		message strcharinfo(0),"This map is not enabled in "+((checkre(0))?"":"Pre-")+"Renewal.";
		end;
	}
	set .@x, @c[.@i*2];
	set .@y, @c[.@i*2+1];
	deletearray @c[0],getarraysize(@c);
	Go(.@map$,.@x,.@y);
}
function Restrict {
	if ((getarg(0) == "RE" && !checkre(0)) || (getarg(0) == "Pre-RE" && checkre(0))) {
		if (getarg(1,0)) {
			set @warp_block,0;
			for (set .@i,1; .@i<getargcount(); set .@i,.@i+1)
				set @warp_block, @warp_block | (1<<getarg(.@i));
		} else {
			message strcharinfo(0),"This map is not enabled in "+((checkre(0))?"":"Pre-")+"Renewal.";
			end;
		}
	}
	return;
}

// --------------------------------------------------
	Towns:
// --------------------------------------------------
menu	"普隆德拉",T1, "艾尔贝塔",T2, "艾尔帕兰",T3, "天津",T4, "哎哟泰雅",T5,
    	"巴西里斯",T6, "克魔岛",T7, "德瓦他",T8, "艾卡拉奇",T9, "艾因贝赫",T10, 
    	"艾因布罗克",T11, "埃迪斯特斯",T12, "吉芬",T13, "昆仑",T14, "毁葛",T15,
    	"依斯鲁得",T16, "爪哇咿",T17, "里希塔乐镇",T18, "洛阳",T19, "姜饼城",T20,
    	"绵绵岛",T21, "马来港",T22, "马努克",T23, "米德加尔特联合驻地",T24,
    	"穆拉",T25, "梦罗克",T26, "莫斯科比亚",T27, "无名岛",T28,
    	"尼芙菲姆",T29, "斐扬",T30, "拉赫",T31, "辉煌领域",T32, "托尔火山",T33,
    	"汶巴拉",T34, "伯仁斯",T35, "朱诺",T36;

T1: Go("prontera",155,183);
T2: Go("alberta",28,234);
T3: Go("aldebaran",140,131);
T4: Go("amatsu",198,84);
T5: Go("ayothaya",208,166);
T6: Restrict("RE");
	Go("brasilis",196,217);
T7: Go("comodo",209,143);
T8: Restrict("RE");
	Go("dewata",200,180);
T9: Restrict("RE");
	Go("ecl_in01",48,53);
T10: Go("einbech",63,35);
T11: Go("einbroch",64,200);
T12: Restrict("RE");
	 Go("dicastes01",198,187);
T13: Go("geffen",119,59);
T14: Go("gonryun",160,120);
T15: Go("hugel",96,145);
T16: Go("izlude",128,(checkre(3)?146:114));
T17: Go("jawaii",251,132);
T18: Go("lighthalzen",158,92);
T19: Go("louyang",217,100);
T20: Go("xmas",147,134);
T21: Restrict("RE");
	 Go("malangdo",140,114);
T22: Restrict("RE");
	 Go("malaya",231,200);
T23: Go("manuk",282,138);
T24: Go("mid_camp",210,288);
T25: Restrict("RE");
	 Go("mora",55,146);
T26: Go("morocc",156,93);
T27: Go("moscovia",223,184);
T28: Go("nameless_n",256,215);
T29: Go("niflheim",202,174);
T30: Go("payon",179,100);
T31: Go("rachel",130,110);
T32: Go("splendide",201,147);
T33: Go("thor_camp",246,68);
T34: Go("umbala",97,153);
T35: Go("veins",216,123);
T36: Go("yuno",157,51);

// --------------------------------------------------
	Fields:
// --------------------------------------------------
menu	"天津 原野",F1, "哎哟泰雅 原野",F2, "彩虹桥 领域", F3,
    	"巴西平原",F4, "克魔岛 森林/海边 区域",F5, "德瓦他 原野",F6,
    	"艾卡拉奇 平原",F7, "艾音布罗克 原野",F8, "埃迪斯特斯 区域",F9,
    	"吉芬 区域",F10, "昆仑原野",F11, "毁葛 原野",F12,
    	"里希塔乐镇 原野",F13, "洛阳 原野",F14, "姜饼城 原野",F15,
    	"马来亚 森林",F16, "马努克旷野",F17, "妙勒尼 山脉 区域",F18,
    	"莫斯科比亚草原",F19, "尼芙菲姆 区域",F20, "斐扬 树林",F21,
    	"普隆德拉 区域",F22, "拉赫 原野",F23, "苏克拉特 沙漠 区域",F24,
    	"辉煌领域旷野",F25, "汶巴拉 森林区域",F26, "伯仁斯 旷野",F27,
    	"朱诺 原野",F28;

F1: setarray @c[2],190,197;
	Disp("天津 原野",1,1); Pick("ama_fild");
F2: setarray @c[2],173,134,212,150;
	Disp("哎哟泰雅 原野",1,2); Pick("ayo_fild");
F3: Restrict("RE");
	setarray @c[2],193,220,220,187;
	Disp("彩虹桥 领域",1,2); Pick("bif_fild");
F4: Restrict("RE");
	setarray @c[2],74,32;
	Disp("巴西平原",1,1); Pick("bra_fild");
F5: Restrict("Pre-RE",5);
	setarray @c[2],180,178,231,160,191,172,228,194,224,203,190,223,234,177,194,175,172,172;
	Disp("克魔岛 森林/海边 区域",1,9); Pick("cmd_fild");
F6: Restrict("RE");
	setarray @c[2],371,212;
	Disp("德瓦他 原野",1,1); Pick("dew_fild");
F7: Restrict("RE");
	setarray @c[2],97,314;
	Disp("艾卡拉奇 平原",1,1); Pick("ecl_fild");
F8: Restrict("Pre-RE",2,10);
	setarray @c[2],142,225,182,141,187,228,185,173,216,173,195,148,272,220,173,214,207,174,196,200;
	Disp("艾音布罗克 原野",1,10); Pick("ein_fild");
F9: Restrict("RE");
	setarray @c[2],143,132,143,217;
	Disp("埃迪斯特斯 区域",1,2); Pick("dic_fild");
F10: Restrict("Pre-RE",13,15);
	 setarray @c[0],46,199,213,204,195,212,257,192,188,171,166,263,248,158,195,191,186,183,221,117,178,218,136,328,240,181,235,235,211,185;
	 Disp("吉芬 区域",0,14); Pick("gef_fild",1);
F11: setarray @c[2],220,227;
	 Disp("昆仑原野",1,1); Pick("gon_fild");
F12: Restrict("Pre-RE",3,7);
	 setarray @c[2],268,101,222,193,232,185,252,189,196,106,216,220,227,197;
	 Disp("毁葛 原野",1,7); Pick("hu_fild");
F13: setarray @c[2],240,179,185,235,240,226;
	 Disp("里希塔乐镇 原野",1,3); Pick("lhz_fild");
F14: setarray @c[2],229,187;
	 Disp("洛阳 原野",1,1); Pick("lou_fild");
F15: setarray @c[2],115,145;
	 Disp("姜饼城 原野",1,1); Pick("xmas_fild");
F16: Restrict("RE");
	 setarray @c[2],40,272,207,180;
	 Disp("马来亚 森林",1,2); Pick("ma_fild");
F17: setarray @c[2],35,236,35,262,84,365;
	 Disp("马努克旷野",1,3); Pick("man_fild");
F18: setarray @c[2],204,120,175,193,208,213,179,180,181,240,195,270,235,202,188,215,205,144,245,223,180,206,196,208;
	 Disp("妙勒尼 山脉 区域",1,12); Pick("mjolnir_");
F19: setarray @c[2],82,104,131,147;
	 Disp("莫斯科比亚草原",1,2); Pick("mosk_fild");
F20: setarray @c[2],215,229,167,234;
	 Disp("尼芙菲姆 区域",1,2); Pick("nif_fild");
F21: Restrict("Pre-RE",5,11);
	 setarray @c[2],158,206,151,219,205,148,186,247,134,204,193,235,200,177,137,189,201,224,160,205,194,150;
	 Disp("斐扬 树林",1,11); Pick("pay_fild");
F22: setarray @c[0],208,227,190,206,240,206,190,143,307,252,239,213,185,188,193,194,187,218,210,183,195,149,198,164;
	 Disp("普隆德拉 区域",0,11); Pick("prt_fild",1);
F23: Restrict("Pre-RE",2,7,9,10,11,13);
	 setarray @c[2],192,162,235,166,202,206,202,208,225,202,202,214,263,196,217,201,87,121,277,181,221,185,175,200,174,197;
	 Disp("Rachel Field",1,13); Pick("ra_fild");
F24: setarray @c[2],219,205,177,206,194,182,224,170,198,216,156,187,185,263,206,228,208,238,209,223,85,97,207,202,31,195,38,195;
	 Disp("苏克拉特 沙漠 区域 1:苏克拉特 沙漠 区域 2:苏克拉特 沙漠 区域 3:苏克拉特 沙漠 区域 7:苏克拉特 沙漠 区域 11:苏克拉特 沙漠 区域 12:苏克拉特 沙漠 区域 13:苏克拉特 沙漠 区域 16:苏克拉特 沙漠 区域 17:苏克拉特 沙漠 区域 18:苏克拉特 沙漠 区域 19:苏克拉特 沙漠 区域 20:苏克拉特 沙漠 区域 21:苏克拉特 沙漠 区域 22");
	 Pick("","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","moc_fild20","moc_fild21","moc_fild22");
F25: setarray @c[2],175,186,236,184,188,204;
	 Disp("辉煌领域旷野",1,3); Pick("spl_fild");
F26: setarray @c[2],217,206,223,221,237,215,202,197;
	 Disp("汶巴拉 森林区域",1,4); Pick("um_fild");
F27: Restrict("Pre-RE",5);
	 setarray @c[2],186,175,196,370,222,45,51,250,202,324,150,223,149,307;
	 Disp("伯仁斯 旷野",1,7); Pick("ve_fild");
F28: Restrict("Pre-RE",5,10);
	 setarray @c[2],189,224,192,207,221,157,226,199,223,177,187,232,231,174,196,203,183,214,200,124,195,226,210,304;
	 Disp("朱诺 原野",1,12); Pick("yuno_fild");

// --------------------------------------------------
	Dungeons:
// --------------------------------------------------
menu	"深渊湖水 地下洞穴",D1, "樱花城迷宫",D2, "蚂蚁地狱 密穴",D3,
    	"古代神殿 迷宫",D4, "克魔岛 海边",D5, "彩虹桥高塔",D41,
    	"生体试验 研究所",D6, "巴西瀑布洞穴",D7, "海底 洞窟",D8, "艾尔帕兰 钟塔",D9,
    	"妙勒尼 废弃矿场",D10, "普隆德拉 地下水道",D11, "无名岛修道院",D12, "德瓦他 火山洞穴",D13,
    	"艾音布罗克 矿山洞穴",D14, "葛帔尼亚",D15, "吉芬塔 地下 密穴",D16,
    	"克雷斯特汉姆古城",D17, "昆仑洞穴",D18, "迷藏森林",D19,
    	"冰洞穴",D20, "优配擂斯 废墟",D21, "机械娃娃工厂",D22, "洛阳 古都王陵",D23,
    	"诺可罗德溶洞",D24, "绵绵岛洞穴",D25, "莫斯科森林",D26,
    	"尼德霍格的巢穴",D27, "奥丁神殿",D28, "兽人地下洞窟",D29,
    	"裴扬迷宫",D30, "金字塔 迷宫",D31, "拉赫 神殿圣域",D32,
    	"甲虫洞穴",D33, "史芬克斯 密穴",D34, "沉没之船",D35, "达纳托斯之塔",D36,
    	"伯仁斯 火山洞窟",D37, "玩具工厂",D38, "乌龟岛",D39, "汶巴拉森林",D40;

D1: setarray @c[2],261,272,275,270,116,27;
	Disp("深渊湖水 地下洞穴",1,3); Pick("abyss_");
D2: setarray @c[2],228,11,34,41,119,14;
	Disp("樱花城迷宫",1,3); Pick("ama_dun");
D3: setarray @c[2],35,262,168,170;
	Disp("蚂蚁地狱 密穴",1,2); Pick("anthell");
D4: setarray @c[2],275,19,24,26;
	Disp("古代神殿 迷宫:迷宫内部"); Pick("ayo_dun");
D5: setarray @c[2],266,67,255,244,23,260;
	Disp("克魔岛 海边",1,3); Pick("","beach_dun","beach_dun2","beach_dun3");
D6: Restrict("RE",4);
	setarray @c[2],150,288,150,18,140,134,244,52;
	Disp("生体试验 研究所",1,4); Pick("lhz_dun");
D7: Restrict("RE");
	setarray @c[2],87,47,262,262;
	Disp("巴西瀑布洞穴",1,2); Pick("bra_dun");
D8: Restrict("RE",6);
	setarray @c[0],168,168,253,252,236,204,32,63,26,27,141,187;
	Disp("海底 洞窟",1,6); Pick("iz_dun",1);
D9: setarray @c[2],199,159,148,283,65,147,56,155,297,25,127,169,277,178,268,74;
	Disp("艾尔帕兰 钟塔 1层:艾尔帕兰 钟塔 2层:艾尔帕兰 钟塔 3层:艾尔帕兰 钟塔 4层:钟塔地下 1层:钟塔地下 2层:钟塔地下 3层:钟塔地下 4层");
	Pick("","c_tower1","c_tower2","c_tower3","c_tower4","alde_dun01","alde_dun02","alde_dun03","alde_dun04");
D10: setarray @c[2],52,17,381,343,302,262;
	 Disp("妙勒尼 废弃矿场",1,3); Pick("mjo_dun");
D11: setarray @c[2],131,247,19,19,180,169,100,92;
	 Disp("普隆德拉 地下水道",1,4); Pick("","prt_sewb1","prt_sewb2","prt_sewb3","prt_sewb4");
D12: setarray @c[2],51,14,150,11,120,10;
	 Disp("无名岛修道院",1,3); Pick("abbey");
D13: Restrict("RE");
	 setarray @c[2],285,160,299,29;
	 Disp("德瓦他 火山洞穴",1,2); Pick("dew_dun");
D14: setarray @c[2],22,14,292,290;
	 Disp("艾音布罗克 矿山洞穴",1,2); Pick("ein_dun");
D15: setarray @c[2],40,103,203,34,266,168,130,272;
	 Disp("葛帔尼亚",1,4); Pick("gefenia",0);
D16: setarray @c[0],104,99,115,236,106,132,203,200;
	 Disp("吉芬塔 地下 密穴",1,4); Pick("gef_dun",1);
D17: setarray @c[2],370,304,199,29,104,25,150,15,157,287,147,15,258,255,108,291,171,283,68,277,156,7,12,7,133,271,224,274,14,70,150,14;
	 Disp("古城入口:地上楼层 1层:地上楼层 2层:骑士团 1层:骑士团 2层:地下公墓:地下水道 1层:地下水道 2层:地下水道 3层:地下水道 4层:修道院:楼梯密穴:地下洞穴 1层:地下洞穴 2层:地下监狱 1层:地下监狱 2层");
	 Pick("","glast_01","gl_cas01","gl_cas02","gl_knt01","gl_knt02","gl_chyard","gl_sew01","gl_sew02","gl_sew03","gl_sew04","gl_church","gl_step","gl_dun01","gl_dun02","gl_prison","gl_prison1");
D18: setarray @c[2],153,53,28,113,68,16;
	 Disp("昆仑洞穴",1,3); Pick("gon_dun");
D19: setarray @c[2],176,7,93,20,23,8;
	 Disp("迷藏森林",1,3); Pick("prt_maze");
D20: setarray @c[2],157,14,151,155,149,22,33,158;
	 Disp("冰洞穴",1,4); Pick("ice_dun");
D21: setarray @c[2],140,51,53,247,37,63,150,285;
	 Disp("废墟入口:废墟 1层:废墟 2层:废墟核心");
	 Pick("","jupe_cave","juperos_01","juperos_02","jupe_core");
D22: setarray @c[2],28,226,41,198;
	 Disp("机械娃娃工厂",1,2); Pick("kh_dun");
D23: setarray @c[2],218,196,282,20,165,38;
	 Disp("古都王陵:古都王陵内部:审良宫"); Pick("lou_dun");
D24: setarray @c[2],126,68,47,30;
	 Disp("诺可罗德溶洞",1,2); Pick("mag_dun");
D25: Restrict("RE");
	 setarray @c[2],33,230;
	 Disp("绵绵岛洞穴",1,1); Pick("mal_dun");
D26: setarray @c[2],189,48,165,30,32,135;
	 Disp("莫斯科森林",1,3); Pick("mosk_dun");
D27: setarray @c[2],61,239,60,271;
	 Disp("尼德霍格的巢穴",1,2); Pick("nyd_dun");
D28: setarray @c[2],298,167,224,149,266,280;
	 Disp("奥丁神殿",1,3); Pick("odin_tem");
D29: setarray @c[2],32,170,21,185;
	 Disp("兽人地下洞窟",1,2); Pick("orcsdun");
D30: setarray @c[0],21,183,19,33,19,63,155,159,201,204;
	 Disp("裴扬迷宫",1,5); Pick("pay_dun",1);
D31: Restrict("RE",7,8);
	 setarray @c[2],192,9,10,192,100,92,181,11,94,96,192,8,94,96,192,8;
	 Disp("金字塔 1层:金字塔 2层:金字塔 3层:金字塔 4层:金字塔地下 1层:金字塔地下 2层:金字塔地下 1层 - 梦魇:金字塔地下 2层 - 梦魇");
	 Pick("","moc_pryd01","moc_pryd02","moc_pryd03","moc_pryd04","moc_pryd05","moc_pryd06","moc_prydn1","moc_prydn2");
D32: setarray @c[2],140,11,32,21,8,149,204,218,150,9;
	 Disp("拉赫 神殿圣域",1,5); Pick("ra_san");
D33: Restrict("RE");
	 setarray @c[2],364,44,101,141;
	 Disp("甲虫洞穴",1,2); Pick("dic_dun");
D34: setarray @c[2],288,9,149,81,210,54,10,222,100,99;
	 Disp("史芬克斯 密穴",1,5); Pick("","in_sphinx1","in_sphinx2","in_sphinx3","in_sphinx4","in_sphinx5");
D35: setarray @c[2],69,24,102,27;
	 Disp("沉没之船",1,2); Pick("treasure");
D36: setarray @c[2],150,39,150,136,220,158,59,143,62,11,89,221,35,166,93,148,29,107,159,138,19,20,130,52;
	 Disp("达纳托斯之塔",1,12); Pick("tha_t");
D37: setarray @c[2],21,228,75,205,34,272;
	 Disp("伯仁斯 火山洞窟",1,3); Pick("thor_v");
D38: setarray @c[2],205,15,129,133;
	 Disp("玩具工厂",1,2); Pick("xmas_dun");
D39: setarray @c[2],154,49,148,261,132,189,100,192;
	 Disp("入口:乌龟岛 1层:乌龟岛 2层:乌龟岛 3层"); Pick("tur_dun");
D40: Restrict("Pre-RE",1,2);
	 setarray @c[2],42,31,48,30,204,78;
	 Disp("树木里的木工所:树木里的异界之路:天地树叶树干");
	 Pick("","um_dun01","um_dun02","yggdrasil01");
D41: Restrict("RE");
	 setarray @c[2],57,13,64,88,45,14,26,23;
	 Disp("Bifrost Tower",1,4); Pick("ecl_tdun");

// --------------------------------------------------
	Castles:
// --------------------------------------------------
menu	"艾尔帕兰城堡",C1, "吉芬城堡",C2, "斐扬城堡",C3,
    	"普隆德拉城堡",C4, "阿卢纳贝兹城堡",C5, "秀发兹发德城堡",C6;

C1: setarray @c[2],48,83,95,249,142,85,239,242,264,90;
	Disp("娜易修番斯塔引:乎汗修番佳乌:纽蓝贝勒克:布勒刺布勒克:罗探布勒克");
	Pick("","alde_gld","alde_gld","alde_gld","alde_gld","alde_gld");
C2: setarray @c[2],214,75,308,240,143,240,193,278,305,87;
	Disp("雷普立温:耀布力格:易司那普:培勒格:眉撒丝达刺");
	Pick("","gef_fild13","gef_fild13","gef_fild13","gef_fild13","gef_fild13");
C3: setarray @c[2],121,233,295,116,317,293,140,160,204,266;
	Disp("明亭:天坛:佛影:红楼:竹林岘");
	Pick("","pay_gld","pay_gld","pay_gld","pay_gld","pay_gld");
C4: setarray @c[2],134,65,240,128,153,137,111,240,208,240;
	Disp("克林希特:斯般希特:拉兹立克斯:司葵古:关德");
	Pick("","prt_gld","prt_gld","prt_gld","prt_gld","prt_gld");
C5: setarray @c[2],158,272,83,47,68,155,299,345,292,107;
	Disp("马勒杜:希乐:霍伦:吉本:巴拿迪斯");
	Pick("","aru_gld","aru_gld","aru_gld","aru_gld","aru_gld");
C6: setarray @c[2],293,100,288,252,97,196,137,90,71,315;
	Disp("喜民:安得郎克:毕得布莱因:何料德:思凯提乐尼");
	Pick("","sch_gld","sch_gld","sch_gld","sch_gld","sch_gld");

// --------------------------------------------------
	Guild_Dungeons:
// --------------------------------------------------
menu	"青林湖泊",G1, "露依纳",G2, "巴基力雷恩",G3, "布哩妥利亚",G4,
    	"发菲依雅",G5, "尼它堆",G6;

G1: Restrict("RE",2,3);
	setarray @c[2],119,93,119,93,120,130;
	Disp("公会地下密穴 F1:公会地下密穴 F2:深渊回廊 太古之风");
	Pick("","gld_dun01","gld_dun01_2","gld2_pay");
G2: Restrict("RE",2,3);
	setarray @c[2],39,161,39,161,147,155;
	Disp("公会地下密穴 F1:公会地下密穴 F2:深渊回廊 英雄之泪");
	Pick("","gld_dun02","gld_dun02_2","gld2_ald");
G3: Restrict("RE",2,3);
	setarray @c[2],50,44,50,44,140,132;
	Disp("公会地下密穴 F1:公会地下密穴 F2:深渊回廊 战士之路");
	Pick("","gld_dun03","gld_dun03_2","gld2_prt");
G4: Restrict("RE",2,3);
	setarray @c[2],116,45,116,45,152,118;
	Disp("公会地下密穴 F1:公会地下密穴 F2:深渊回廊 王者之丘");
	Pick("","gld_dun04","gld_dun04_2","gld2_gef");
G5: Go("arug_dun01",199,195);
G6: Go("schg_dun01",200,124);

// --------------------------------------------------
	Instances:
// --------------------------------------------------
menu	"食月暴龙湖",I1, "噩梦医院 2F",I2, "宝箱巨鳄 洞穴",I3,
    	"无尽之塔",I4, "迷雾森林",I5, "绵绵岛 海底洞穴",I6, "尼德霍格的巢穴",I7,
    	"章鱼洞穴",I8, "旧克雷斯特汉姆古城",I9, "兽人的回忆",I10, "封印神殿",I11,
    	"狼骑士的实验室",I12;

I1: Restrict("RE");
	Go("ma_scene01",172,175);
I2: Restrict("RE");
	Go("ma_dun01",151,8);
I3: Restrict("RE");
	Go("ma_fild02",316,317);
I4: Go("e_tower",72,112);
I5: Restrict("RE");
	Go("bif_fild01",161,334);
I6: Restrict("RE");
	Go("mal_in01",164,21);
I7: Go("nyd_dun02",95,193);
I8: Restrict("RE");
	Go("mal_dun01",152,230);
I9: Restrict("RE");
	Go("glast_01",204,268);
I10: Go("gef_fild10",240,198);
I11: Go("monk_test",306,143);
I12: Restrict("RE");
	Go("lhz_dun04",148,269);

// --------------------------------------------------
	Special:
// --------------------------------------------------
menu	"精炼房",S1, "战场",S2, "赌场",S3, "乐园团总部",S4,
    	"昆仑战斗场",S5, "依斯鲁得战斗场",S6, "魔物赛跑竞赛场",S7, "竞速场",S8;

S1: Go("auction_01",22,68);
S2: Go("bat_room",154,150);
S3: Go("cmd_in02",179,129);
S4: Restrict("RE");
	Go("moc_para01",31,14);
S5: Go("gon_test",48,10);
S6: Go("arena_room",100,88);
S7: Go("p_track01",62,41);
S8: Go("turbo_room",99,114);
}

// --------------------------------------------------
//	Duplicates:
// --------------------------------------------------
alb2trea,57,70,6	duplicate(Warper)	传送员#tre	759
alberta,28,240,4	duplicate(Warper)	传送员#alb	759
aldebaran,145,118,4	duplicate(Warper)	传送员#alde	759
amatsu,203,87,4	duplicate(Warper)	传送员#ama	759
ayothaya,209,169,6	duplicate(Warper)	传送员#ayo	759
comodo,194,158,4	duplicate(Warper)	传送员#com	759
einbech,59,38,6	duplicate(Warper)	传送员#einbe	759
einbroch,69,202,4	duplicate(Warper)	传送员#einbr	759
gef_fild10,71,339,4	duplicate(Warper)	传送员#orc	759
geffen,124,72,4	duplicate(Warper)	传送员#gef	759
glast_01,372,308,4	duplicate(Warper)	传送员#glh	759
gonryun,162,122,4	duplicate(Warper)	传送员#gon	759
hugel,101,151,4	duplicate(Warper)	传送员#hug	759
izlu2dun,110,92,4	duplicate(Warper)	传送员#izd	759
izlude,134,150,4	duplicate(Warper)	传送员#izl	759	//Pre-RE: (132,120)
jawaii,253,138,4	duplicate(Warper)	传送员#jaw	759
lighthalzen,162,102,4	duplicate(Warper)	传送员#lhz	759
louyang,208,103,6	duplicate(Warper)	传送员#lou	759
manuk,274,146,6	duplicate(Warper)	传送员#man	759
mid_camp,216,288,4	duplicate(Warper)	传送员#mid	759
mjolnir_02,85,364,4	duplicate(Warper)	传送员#mjo	759
moc_ruins,64,164,4	duplicate(Warper)	传送员#moc	759
morocc,159,97,4	duplicate(Warper)	传送员#mor	759
moscovia,229,191,4	duplicate(Warper)	传送员#mos	759
nameless_n,259,213,4	duplicate(Warper)	传送员#nam	759
niflheim,205,179,4	duplicate(Warper)	传送员#nif	759
pay_arche,42,134,4	duplicate(Warper)	传送员#arc	759
payon,182,108,4	duplicate(Warper)	传送员#pay	759
prontera,159,192,4	duplicate(Warper)	传送员#prt	759
prt_fild05,279,223,6	duplicate(Warper)	传送员#cul	759
rachel,135,116,4	duplicate(Warper)	传送员#rac	759
splendide,205,153,4	duplicate(Warper)	传送员#spl	759
thor_camp,249,76,4	duplicate(Warper)	传送员#thor	759
umbala,106,150,3	duplicate(Warper)	传送员#umb	759
veins,214,123,4	duplicate(Warper)	传送员#ve	759
xmas,150,136,6	duplicate(Warper)	传送员#xmas	759
yuno,162,47,4	duplicate(Warper)	传送员#yuno	759

// --------------------------------------------------
//	Duplicates (Renewal):
// --------------------------------------------------
brasilis,201,222,4	duplicate(Warper)	传送员#bra	759
dewata,204,186,6	duplicate(Warper)	传送员#dew	759
dicastes01,194,194,6	duplicate(Warper)	传送员#dic	759
ecl_in01,51,60,4	duplicate(Warper)	传送员#ecl	759
malangdo,134,117,6	duplicate(Warper)	传送员#mal	759
malaya,231,204,4	duplicate(Warper)	传送员#ma	759
mora,57,152,4	duplicate(Warper)	传送员#mora	759
