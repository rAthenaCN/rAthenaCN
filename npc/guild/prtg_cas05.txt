//============================================================
//= rAthenaCN [公会战GVG]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================
//= War of Emperium (Prontera)
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= NPCs that relate to Prontera Guild Castle 5
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Corrected treasure room switch coordinates. [L0ne_W0lf]
//= 1.3 Removed dialog from interior flags. [L0ne_W0lf]
//= 1.4 Partially rewrote Script. [Masao]
//============================================================

// Guild Flags (Outside Castle)
//============================================================
-	script	关德#1::OutsideFlagsPR5	722,{

	set .@GID, GetCastleData("prtg_cas05",1);

	if (.@GID == 0) {
		mes "[ 神圣卢恩・米德加兹王国法令 ]";
		mes " ";
		mes "1. 遵循神圣卢恩・米德加兹王国法令, ";
		mes "我们正式声明";
		mes "此城堡没有正式拥有者.";
		mes " ";
		mes "2. 经过所有试炼,";
		mes "并且击碎华丽金属的勇士.";
		mes "将会得到国王殿下的诏书";
		mes "被授予该城堡拥有权.";
		mes "并一同守护卢恩・米德加兹王国!";
		close;
	}
	if (getcharid(2) == .@GID) {
		mes "[ 回声 ]";
		mes "勇士们...";
		mes "想要回到你们的荣耀之地吗?";
		next;
		if(select("返回公会城堡.:取消.") == 1) {
			close2;
			if (getcharid(2) == GetCastleData("prtg_cas05",1)) {
				warp "prtg_cas05",52,41;
				end;
			}
			end;
		}
		close;
	}
	mes "[ 神圣卢恩・米德加兹王国法令 ]";
	mes " ";
	mes "1. 遵循神圣卢恩・米德加兹王国法令,";
	mes "我们认可此处为.";
	mes " ^ff0000"+GetGuildName(.@GID)+"^000000公会的私人领地.";
	mes " ";
	mes "2. 公会 ^ff0000"+GetGuildName(.@GID)+"^000000 的领袖为:";
	mes "^ff0000"+GetGuildMaster(.@GID)+"^000000";
	mes "如果任何人对此有异议.";
	mes "请用手中的武器来证明自己的勇气和荣耀!";
	close;

OnRecvCastlePt05:
	FlagEmblem GetCastleData("prtg_cas05",1);
	end;
}

prt_gld,199,243,2	duplicate(OutsideFlagsPR5)	关德#1-2	722
prt_gld,199,236,2	duplicate(OutsideFlagsPR5)	关德#2	722
prt_gld,197,243,2	duplicate(OutsideFlagsPR5)	关德#3	722
prt_gld,197,236,2	duplicate(OutsideFlagsPR5)	关德#4	722

// Guild Flags (Inside Castle)
//============================================================
-	script	关德#5::InsideFlagsPR5	722,{
	end;

OnRecvCastlePt05:
	FlagEmblem GetCastleData("prtg_cas05",1);
	end;
}

prtg_cas05,19,247,4	duplicate(InsideFlagsPR5)	关德#5-2	722
prtg_cas05,19,243,4	duplicate(InsideFlagsPR5)	关德#6	722
prtg_cas05,26,247,4	duplicate(InsideFlagsPR5)	关德#7	722
prtg_cas05,26,243,4	duplicate(InsideFlagsPR5)	关德#8	722
prtg_cas05,249,289,4	duplicate(InsideFlagsPR5)	关德#9	722
prtg_cas05,256,289,4	duplicate(InsideFlagsPR5)	关德#10	722
prtg_cas05,253,271,4	duplicate(InsideFlagsPR5)	关德#11	722
prtg_cas05,273,257,4	duplicate(InsideFlagsPR5)	关德#12	722

// Guild Flag (Town)
//============================================================
prontera,165,194,5	duplicate(InsideFlagsPR5)	关德#13	722

// Kafra Employee
//============================================================
prtg_cas05,52,41,4	duplicate(guildkafra)	卡普拉 职员#prtg_cas05	117

// Steward
//============================================================
prtg_cas05,36,37,4	duplicate(cm)	Raynor#prtg_cas05	55

// Treasure Room Exit
//============================================================
prtg_cas05,281,176,0	script	#Lever_PR05	111,{

	mes " ";
	mes "这里有一个小小的控制杆, 去拉一下吗?";
	next;
	if(select("上前拉杆.:算了.") == 1) {
		close2;
		warp "prtg_cas05",36,38;
		end;
	}
	close;
}

// Guild Dungeon Switch
//============================================================
prtg_cas05,212,95,0	duplicate(gdlever)	#prtg_cas05	111

// Guild Treasure Spawns
//============================================================
prtg_cas05,276,178,0	duplicate(Gld_Trea_Spawn)	宝箱#prtg_cas05	-1

// AGIT Manager
//============================================================
prtg_cas05,266,266,0	duplicate(Gld_Agit_Manager)	Agit#prtg_cas05	-1

// Guardians
//============================================================
prtg_cas05,36,37,4	duplicate(Gld_Guard_Template)	监护人#prtg_cas05	-1
