//============================================================
//= rAthenaCN [战场]汉化脚本 [ www.rAthenaCN.com ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= BattleGround System - KvM Item Dealer
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena 1.0
//===== Description: ========================================= 
//= [Official Conversion]
//= Kreiger Von Midgard Battleground Point redemption NPC
//= * NOTE * 
//= KvM Does NOT use items, but a character variable to
//= keep track of points. This NPC when talked to will
//= convert already-given tokens and convert them to points.
//= under the player variable kvm_point.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//============================================================

bat_room,151,144,3	script	KVM 战场后勤官#a	734,{
	if (countitem(7773)) {
		set .@pointstoadd,countitem(7773);
		delitem 7773,.@pointstoadd;
		set kvm_point,kvm_point+.@pointstoadd;
		mes "[战场后勤官]";
		mes "我看到的那些是 "+getitemname(7773)+"?";
		mes "我现在不会接受那种货币了,";
		mes "但是我能帮你把哪些货币换掉,";
		mes "你有 "+.@pointstoadd+" "+getitemname(7773)+"s?";
		mes "好了，都弄好了 ^580080"+ kvm_point +"^000000 KVM 点数.";
		next;
	}
	if (checkweight(1201,1) == 0) {
		mes "- 等等 !! -";
		mes "- 现在你已经 -";
		mes "- 拿了太多东西了. -";
		mes "- 请把多余的东西放入仓库 -";
		mes "- 再找我对话. -";
		close;
	}
	mes "[战场后勤官]";
	mes "你好";
	mes "我是负责发放";
	mes "用KVM 点数的奖励的.";
	mes "如果你选错了奖励";
	mes "那我将无能为力，所以请慎重.";
	mes "请选择以下选项.";
	next;
	set .@name$,strcharinfo(0);
	switch(select("看看 KVM 奖励目录.:购买 KVM 奖励物品.:查询 KVM 点数.:介绍下 KVM 奖励.:介绍下 KVM 点数.")) {
	case 1:
		mes "[战场后勤官]";
		mes "这里是KVM 奖励目录.";
		mes "每一件武器需要 2,000 点数";
		mes "每一件盔甲需要 10 到 1200 点点数";
		mes "所以选择奖励时";
		mes "请慎重.";
		close2;
		readbook 11017,1;
		end;
	case 2:
		mes "[战场后勤官]";
		mes "你想看哪一件物品";
		mes "如果想看详细的物品介绍";
		mes "请查看";
		mes "^3131FFKVM 奖励目录^000000.";
		next;
		switch(select("武器:盔甲/装饰品:量产型盔甲/装饰品:普及型盔甲/装饰品")) {
		case 1:
			mes "[战场后勤官]";
			mes "你选择了武器.";
			mes "请选择你要哪种武器.";
			next;
			switch(select("短剑/剑/矛:杖/锤/斧/手里剑:弓/拳刃/乐器/鞭子:书/拳套:左轮手枪/来福/格林机关枪/猎枪/发射器")) {
			case 1:
				mes "[战场后勤官]";
				mes "你选择了 短剑/剑/矛.";
				mes "请选择你要哪种武器.";
				next;
				switch(select("短剑:单手剑:双手剑:单手矛:双手矛")) {
				case 1: callsub PurchaseItem,13042,0,1; //Krieger_Dagger1 
				case 2:
					mes "[战场后勤官]";
					mes "你选择了 '单手剑' .";
					mes "这里有3把  单手剑: 荣光之刃, 荣光细剑 和 荣光复仇者.";
					mes "如果你想知道每把剑详细情况，请查看 KVM 奖励目录.";
					mes "请选择.";
					next;
					switch(select("荣光之刃:荣光细剑:荣光复仇者")) {
					case 1: callsub PurchaseItem,13416,0,0; //Krieger_Onehand_Sword1
					case 2: callsub PurchaseItem,13417,0,0; //Krieger_Onehand_Sword2
					case 3: callsub PurchaseItem,13418,0,0; //Krieger_Onehand_Sword3
					}
				case 3: callsub PurchaseItem,1187,0,1; //Krieger_Twohand_Sword1
				case 4: callsub PurchaseItem,1426,0,1; //Krieger_Onehand_Spear1
				case 5: callsub PurchaseItem,1486,0,1; //Krieger_Twohand_Spear1
				}
			case 2:
				mes "[战场后勤官]";
				mes "你选择了 '杖/锤/斧/手里剑'.";
				mes "请选择你要哪种武器.";
				next;
				switch(select("杖:锤:单手斧:双手斧:手里剑")) {
				case 1:
					mes "[战场后勤官]";
					mes "你选择了 '杖' .";
					mes "这里有3把 杖: 荣光法杖, 荣光之锤 和 荣光权杖.";
					mes "如果你想知道每把杖详细情况，请查看 KVM 奖励目录.";
					mes "请选择你要哪种武器.";
					next;
					switch(select("荣光法杖:荣光之锤:荣光权杖")) {
					case 1: callsub PurchaseItem,2002,0,0; //Krieger_Twohand_Staff1
					case 2: callsub PurchaseItem,1640,0,0; //Krieger_Onehand_Staff1
					case 3: callsub PurchaseItem,1641,0,0; //Krieger_Onehand_Staff2
					}
				case 2: callsub PurchaseItem,1546,0,1; //Krieger_Onehand_Mace1
				case 3: callsub PurchaseItem,1310,0,1; //Krieger_Onehand_Axe1
				case 4: callsub PurchaseItem,1382,0,1; //Krieger_Twohand_Axe1
				case 5: callsub PurchaseItem,13307,0,1; //Krieger_Huuma_Shuriken1
				}
			case 3:
				mes "[战场后勤官]";
				mes "你选择了 弓/拳刃/乐器/鞭子.";
				mes "请选择你要哪种武器.";
				next;
				switch(select("弓:拳刃:乐器:鞭子")) {
				case 1: callsub PurchaseItem,1743,0,1; //Krieger_Bow1
				case 2:
					mes "[战场后勤官]";
					mes "你选择了 拳刃.";
					mes "这个有2把拳刃: 荣光咆哮 和 荣光拳刃.";
					mes "如果你想知道每把拳刃详细情况，请查看 KVM 奖励目录.";
					mes "请选择你想要的武器.";
					next;
					switch(select("荣光咆哮:荣光拳刃")) {
					case 1: callsub PurchaseItem,1281,0,0; //Krieger_Katar1
					case 2: callsub PurchaseItem,1282,0,0; //Krieger_Katar2
					}
				case 3: callsub PurchaseItem,1927,0,1; //Krieger_Instrument1
				case 4: callsub PurchaseItem,1981,0,1; //Krieger_Whip1
				}
			case 4:
				mes "[战场后勤官]";
				mes "你选择了 书/拳套 .";
				mes "请选择你想要的武器.";
				next;
				switch(select("书:拳套")) {
				case 1:
					mes "[战场后勤官]";
					mes "你选择了 '书'.";
					mes "这里有2本书: 荣光之碑 and 荣光启示录.";
					mes "如果你想知道每本书详细情况，请查看 KVM 奖励目录.";
					mes "请选择你想要的武器.";
					next;
					switch(select("荣光之碑:荣光启示录")) {
					case 1: callsub PurchaseItem,1576,0,0; //Krieger_Book1
					case 2: callsub PurchaseItem,1577,0,0; //Krieger_Book2
					}
				case 2:
					mes "[战场后勤官]";
					mes "你选择了 '拳套' .";
					mes "这里有2个拳套: 荣光爪套 and 荣光拳套.";
					mes "如果你想知道个拳套详细情况，请查看 KVM 奖励目录.";
					mes "请选择你想要的武器.";
					next;
					switch(select("荣光爪套:荣光拳套")) {
					case 1: callsub PurchaseItem,1826,0,0; //Krieger_Knuckle1
					case 2: callsub PurchaseItem,1827,0,0; //Krieger_Knuckle2
					}
				}
			case 5:
				mes "[战场后勤官]";
				mes "你选择了 '左轮手枪/来福/格林机关枪/猎枪/发射器.";
				mes "请选择你想要的武器.";
				next;
				switch(select("左轮手枪:来福:格林机关枪:猎枪:发射器")) {
				case 1: callsub PurchaseItem,13110,0,1; //Krieger_Pistol1
				case 2: callsub PurchaseItem,13176,0,1; //Krieger_Rifle1
				case 3: callsub PurchaseItem,13177,0,1; //Krieger_Gatling1
				case 4: callsub PurchaseItem,13178,0,1; //Krieger_Shotgun1
				case 5: callsub PurchaseItem,13179,0,1; //Krieger_Launcher1
				}
			}
		case 2:
			mes "[战场后勤官]";
			mes "你选择了 装备/装饰品.";
			mes "请选择你想要的武器.";
			next;
			switch(select("盔甲:披肩:鞋子:装饰品")) {
			case 1: callsub PurchaseItem,2394,1,2; //Krieger_Suit1
			case 2: callsub PurchaseItem,2549,2,2; //Krieger_Muffler1
			case 3: callsub PurchaseItem,2444,3,2; //Krieger_Shoes1
			case 4: callsub PurchaseItem,2772,4,2; //Krieger_Ring1
			}
		case 3:
			mes "[战场后勤官]";
			mes "你选择了 '量产型装备/装饰品' .";
			mes "请选择你想要的武器.";
			next;
			switch(select("量产型盔甲:量产型鞋子:量产型装饰品")) {
			case 1: callsub PurchaseItem,2395,5,3; //Krieger_Suit2
			case 2: callsub PurchaseItem,2445,6,3; //Krieger_Shoes2
			case 3: callsub PurchaseItem,2773,7,3; //Krieger_Ring2
			}
		case 4:
			mes "[战场后勤官]";
			mes "你选择了 '普及型 装备/装饰品' .";
			mes "请选择你想要的武器.";
			next;
			switch(select("普及型盔甲:普及型鞋子:普及型装饰品")) {
			case 1: callsub PurchaseItem,2396,8,4; //Krieger_Suit3
			case 2: callsub PurchaseItem,2446,9,4; //Krieger_Shoes3
			case 3: callsub PurchaseItem,2774,10,4; //Krieger_Ring3
			}
		}
	case 3:
		mes "[战场后勤官]";
		mes ""+ .@name$ +", 你目前的点数是 ^580080"+ kvm_point +"^000000 KVM 点.";
		close;
	case 4:
		mes "[战场后勤官]";
		mes "KVM 奖励分三类: 武器/装备/装饰品.";
		next;
		mes "[战场后勤官]";
		mes "每把武器需要 2,000 KVM 点数.";
		mes "每件装备分别需要 840/630/580 KVM 点数.";
		mes "每件装饰品需要 1,200 KVM 点数.";
		next;
		mes "[战场后勤官]";
		mes "KVM武器 are LV 4 武器, 他们可以被升级并有特殊效果: 屠杀, 破灭 and 天使之赐福.";
		next;
		mes "[战场后勤官]";
		mes "如果你凑齐了一套装备, 你可以有一个特殊效果.";
		mes "KVM装饰品也有特殊效果.";
		next;
		mes "[战场后勤官]";
		mes "最后我们有量产型的装备.";
		mes "lv 60 以上的玩家可以装备 这些装备 普及型装备也有等级限制.";
		close;
	case 5:
		mes "[战场后勤官]";
		mes "KVM 点数 是你在KVM战场上得到的奖励 (KVM).";
		mes "你能通过这个点数来购得装备.";
		close;
	}

PurchaseItem:
	// Pricing for Weapon, Suit1, Muffler1, Shoes1, Ring1m Suit2, Shoes2, Ring2, Suit3, Shoes3, Ring3
	setarray .@prices[0],2000,840,630,580,1200,120,70,200,15,10,30;

	mes "[战场后勤官]";

	if (!getarg(2))
		mes "你已经选择了";
	else if (getarg(2) == 1)
		mes "这里只有一件: "+getitemname(getarg(0))+".";
	else if (getarg(2) == 2) {
		mes "你已经选择了";
		mes "对于等级高于80的冒险者，装备是: ^ff0000"+getitemname(getarg(0))+"^000000.";
	}
	else if (getarg(2) == 3) {
		mes "你已经选择了量产型的";
		mes "这里只有一件量产型的: ^ff0000"+getitemname(getarg(0))+"^000000.";
	}
	else if (getarg(2) == 4) {
		mes "你选择了普及型的";
		mes "这里只有一件普及型的: ^ff0000"+getitemname(getarg(0))+"^000000.";
	}

	if (!getarg(1))
		mes "兑换要求为 ^0000ff2,000^000000 KVM点， "+ .@name$ +", 你的点数有: ^580080"+ kvm_point +"^000000.";
	else if (getarg(1) == 4)
		mes "兑换要求为 ^0000ff1,200^000000 KVM点， "+ .@name$ +", 你的点数有: ^580080"+ kvm_point +"^000000.";
	else
		mes "兑换要求为 ^0000ff"+.@prices[getarg(1)]+"^000000 KVM点， "+ .@name$ +", 你的点数有: ^580080"+ kvm_point +"^000000.";

	mes "你确定你想要这个物品吗？";
	next;
	switch(select("不，我不买这个。:好吧，我就买这个。")) {
	case 1:
		mes "[战场后勤官]";
		mes "你选择了“我不买这个。";
		mes "购买物品时，请注意有没有退款。";
		break;
	case 2:
		if (kvm_point >= .@prices[getarg(1)]) {
			set kvm_point,kvm_point-.@prices[getarg(1)];
			getitem getarg(0),1;
			mes "[战场后勤官]";
			mes "You have purchased a "+getitemname(getarg(0))+".";
			if (!getarg(1))
				mes "你的KVM点降低 ^0000ff"+getarg(1)+"^000000 点，你现在的KVM点 ^580080"+kvm_point+"^000000.";
			if (getarg(1) == 4)
				mes "你的KVM点降低 ^0000ff1,200^000000 点，你现在的KVM点 ^580080"+kvm_point+"^000000.";
			else
				mes "你的KVM点降低 ^0000ff"+.@prices[getarg(1)]+"^000000 点，你现在的KVM点 ^580080"+kvm_point+"^000000.";
		}
		else {
			mes "[战场后勤官]";
			if (!getarg(1))
				mes "你需要 ^0000ff2,000^000000 KVM点购买这个物品";
			else if (getarg(1) == 4)
				mes "你需要 ^0000ff1,200^000000 KVM点购买这个物品。";
			else
				mes "你需要 ^0000ff"+.@prices[getarg(1)]+"^000000 KVM点购买这个物品。";
			mes "However, 你现在的KVM点 ^580080"+ kvm_point +"^000000.";
			mes "你还不够分数购买";
			mes "当你获得足够的分数，请再来。";
		}
	}
	close;
}
